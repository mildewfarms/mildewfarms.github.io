
<html>
<head>
<title>December, 2003: Programming Graphical Applications with Gtk2-Perl, Part 2</title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; The Perl Journal-->

<h1>Programming Graphical Applications with Gtk2-Perl, Part 2</h1>
<p><i>The Perl Journal</i> December, 2003</p>
<h2></h2>


<h3>By Gavin Brown</h3>


<I>Gavin works for the domain registry CentralNic, and was a coauthor of Professional Perl Programming (Wrox Press, 2001). He can be contacted at gavin.brown@uk.com.</I>

<hr>





<p>Gtk+ is a toolkit for creating graphical applications on X11 systems. It has also been ported to Windows and BeOS, and runs on Mac OS X using Apple's X11 server. It was originally created for the GIMP, a popular image-manipulation program, and later became the toolkit for GNOME, the GNU Network Object Model Environment.</p>

<p>In Part 1 of this article, published in <i>TPJ</i> last month, I gave a brief overview of the event-based programming model and discussed some of the most common widgets available. This month, I'll complete the exploration of the Gtk+ widget set and also explore some of the handy time-saving features of Gtk2-Perl.</p>
<h3>Container Widgets Continued: Panes</h3>

<p>Panes in Gtk+ are analogous to frames in HTML&#151;they break the window into two separate parts, with a bar between them that can be dragged to resize the two parts.</p>

<p>Pane widgets come in two flavors: horizontal and vertical. They are both created in the same way:</p>

<blockquote>
<pre>
my $hpaned = Gtk2::HPaned-&gt;new;
my $vpaned = Gtk2::VPaned-&gt;new;

</pre>
</blockquote>

<p>Panes can contain precisely two widgets&#151;to reflect this, you add widgets to it using the <i>add1()</i> and <i>add2()</i> methods:</p>

<blockquote>
<pre>
$hpaned-&gt;add1($left_widget);
$hpaned-&gt;add2($right_widget);

</pre>
</blockquote>

<p>or</p>

<blockquote>
<pre>
$vpaned-&gt;add1($top_widget);
$vpaned-&gt;add2($bottom_widget);

</pre>
</blockquote>

<p><a name="rl8"></a><a href="#l8">Listing 8</a> shows how the paned widgets can be used. The resulting widgets are shown in <a name="rf8"></a><a href="0312af8.html">Figure 8</a>.</p>
<h3>Scrolled Windows</h3>

<p>Scrolled windows are handy when you want to place a widget inside a scrollable area. Almost all applications have something like this because inevitably, the data the program uses will grow so that it can't all be displayed at once.</p>

<p>Scrolled windows are often used in conjunction with another kind of container called a <i>viewport</i>. Viewports are rarely used without a scrolled window, so I won't talk about them, but you do need to know about them.</p>

<p><a name="rl9"></a><a href="#l9">Listing 9</a> is a simple example of scrolled window usage, which results in the window shown in <a name="rf9"></a><a href="0312af9.html">Figure 9</a>. The scrolled window allows us to view the butterfly, even though it's much larger than the window size.</p>

<p>Some widgets have their own scrollbars and don't need a viewport. These include the <i>Gtk2::TextView</i> and <i>Gtk2::TreeView</i> widgets. For these, you just need to use the <i>add() </i>method.</p>

<p>The <i>add_with_viewport()</i> method creates a new viewport, adds the image to it, and then adds it to the scrolled window. The <i>set_policy()</i> method tells the widget under what conditions it should display horizontal and vertical scrollbars, in that order. When set to "automatic," it will display a scrollbar when the child widget is larger than the scrolled window. Other values are "always" and "never."</p>
<h3>Event Boxes</h3>

<p>Event boxes allow you to add callbacks to widgets for events and signals that they don't support. For example, a <i>Gtk2::Image</i> cannot have any signals connected to it. However, if you place the image inside an event box, you can connect a signal to the event box, as shown in <a name="rl10"></a><a href="#l10">Listing 10</a> and the corresponding <a name="rf10"></a><a href="0312af10.html">Figure 10</a>.</p>
<h3>Manipulating Graphics with <i>Gtk2::Gdk::Pixbuf</i></h3>

<p>The <i>gdk-pixbuf</i> libraries (namespaced as <i>Gtk2::Gdk::Pixbuf</i> in Perl) provide a flexible and convenient way to handle graphics in Gtk+. <i>gdk-pixbuf</i> supports a wide range of image formats, including PNG, JPG, GIF, and XPM. It also has support for animations.</p>

<p>The <i>GdkPixbuf</i> provides some methods for basic image manipulation. However, you may find that libraries such as <i>GD</i> and <i>Image::Magick </i>will be better for more complicated tasks.</p>
<h3>Resizing an Image</h3>

<p>To resize a pixbuf, use the <i>scale_simple()</i> method:</p>

<blockquote>
<pre>
$pixbuf-&gt;scale_simple(
        $destination_pixbuf,
        $new_width,
        $new_height,
        $scaling_type
);

</pre>
</blockquote>

<p>This will resize <i>$pixbuf</i> and place the resulting image into <i>$destination_pixbuf</i>. If you don't want to create a new object, just replace <i>$destination_pixbuf</i> with <i>$pixbuf</i>. <i>$new_width</i> and <i>$new_height</i> are the dimensions desired, in pixels. <i>$scaling_type</i> determines the algorithm to be used: "bilinear" produces better quality images, but is slower than "nearest."</p>

<p>If you want to resize the pixbuf into a new pixbuf object, you must make sure that the destination pixbuf has the same properties as the source. To do this, use the various <i>get_*() </i>methods as arguments to the constructor:</p>

<blockquote>
<pre>
my $destination_pixbuf = Gtk2::Gdk::Pixbuf-&gt;new(
        $source_pixbuf-&gt;get_colorspace,
        $source_pixbuf-&gt;get_has_alpha,
        $source_pixbuf-&gt;get_bits_per_sample,
        $source_pixbuf-&gt;get_width,
        $source_pixbuf-&gt;get_height,
);

</pre>
</blockquote>
<h3>Changing the Brightness of a pixbuf</h3>

<p>To change the brightness of an image, use the <i>saturate_and_pixelate()</i> method:</p>

<blockquote>
<pre>
$pixbuf-&gt;saturate_and_pixelate(
        $destination_pixbuf,
        2,
        0
);

</pre>
</blockquote>

<p>The second argument is the factor by which to brighten or darken the image: If it's less than 1.0, the image is darkened; if it's higher, then the image is brightened. The last argument determines whether the image is "pixelated." This draws a checkerboard over the image as though it were disabled. In this case, we don't want that, so it's set to 0.</p>

<p>As with the example above, the destination pixbuf must have the same properties as the source.</p>
<h3>Copying a Region from a pixbuf</h3>

<p>You can copy a region of the image out of a pixbuf using the <i>copy_area()</i> method:</p>

<blockquote>
<pre>
$source_pixbuf-&gt;copy_area(
        $src_x,
        $src_y,
        $width,
        $height,
        $destination_pixbuf,
        $dest_x,
        $dest_y
);

</pre>
</blockquote>

<p><i>$src_x</i> and <i>$src_y</i> are the horiztonal and vertical coordinates of the top left corner of the region to be copied. <i>$width</i> and <i>$height</i> define its dimensions. <i>$dest_x</i> and <i>$dest_y</i> are the coordinates in <i>$destination_pixbuf</i> where the copied area will be positioned.</p>
<h3>Writing pixbuf Data to Disk</h3>

<p>You can write a pixbuf to disk in any format that <i>gdk-pixbuf</i> supports. For example:</p>

<blockquote>
<pre>
$pixbuf-&gt;save('image.png' 'png');

</pre>
</blockquote>



<p>writes the pixbuf in PNG format.</p>
<h3>Getting the pixbuf of a Stock Icon</h3>

<p>Should you need the pixbuf of a stock icon, you can use the <i>render_icon()</i> method of any available widget:</p>

<blockquote>
<pre>
my $stock_pixbuf = $window-&gt;render_icon($stock_id, $stock_size);

</pre>
</blockquote>



<p>See "More Information" for references to the allowed values for <i>$stock_id</i> and <i>$stock_size</i>.</p>
<h3>Glade</h3>

<p>The Rapid Application Development (RAD) tool Glade is a WYSIWYG graphical interface designer. Using it, you can construct an interface in minutes that would take hours to hand craft.</p>

<p>The Glade program produces UI description files in XML format, and the <i>Gtk2::GladeXML</i> module is capable of reading these XML files and constructing your interface at runtime. The advantage in this is immediate&#151;it provides for a separation between logic and presentation that will make your job a lot easier in the future. And if you happen to work with a Human Computer Interface (HCI) expert, they can have complete control over the interface without ever having to touch a line of code, and you can get on with the job of making the program work without having to constantly revise the interface.</p>

<p>Here's a simplified example of how to use a Glade XML file in your program:</p>

<blockquote>
<pre>
#!/usr/bin/perl
use Gtk2 -init;
use Gtk2::GladeXML;
use strict;

my $gladexml = Gtk2::GladeXML-&gt;new('example.glade');

$gladexml-&gt;signal_autoconnect_from_package('main');

Gtk2-&gt;main;

exit;

# this is called when the main window is closed:
sub on_main_window_delete_event {
        Gtk2-&gt;main_quit;
}

[snip]

</pre>
</blockquote>

<p>This program takes a Glade file called "example.glade" and creates an interface. When designing your interface, you can define the names of subroutines to handle widget signals, and the <i>signal_autoconnect_from_package()</i> method tells <i>Gtk2::GladeXML</i> which package name the handlers can be found in.</p>

<p>Each widget in the Glade file has a unique name, for example, <i>my_main_window</i> or <i>label5</i>. You can get the widget object for a specific object using the <i>get_widget()</i> method:</p>

<blockquote>
<pre>
my $label = $gladexml-&gt;get_widget('label5');
$label-&gt;set_text('Hello World!');

</pre>
</blockquote>

<p>You may find that just experimenting with Glade will be a good learning exercise. Glade is also an excellent prototyping utility, so that even if you don't plan to make use of <i>Gtk2::GladeXML</i>, you will find it invaluable for planning how your program will be organized.</p>
<h3><i>Gtk2::SimpleList</i></h3>

<p>As well as providing Perl bindings for the Gtk+ widget set, Gtk2-Perl comes with a couple of extra modules that provide a much more Perlish way of working with some of the more complex widgets. An example is the Model, View, Controller (MVC) system for list and tree widgets. While being extremely powerful, they're a pain to work with, especially since the average application will rarely make use of the full features available.</p>

<p><i>Gtk2::SimpleList</i> is designed to make working with lists easy. <a name="rl11"></a><a href="#l11">Listing 11</a> is an example of its usage. You can see what this program looks like in <a name="rf11"></a><a href="0312af11.html">Figure 11</a>.</p>

<p><i>Gtk2::SimpleList</i> acts as a wrapper around the standard <i>Gtk2::TreeView</i> and inherits all of the latter's methods, so that once your list is created you can manipulate it in the usual way. Data is represented as a Perl list of lists (an array of array references), and can be modified directly without having to redraw the entire list.</p>

<p>In the constructor, we are required to define the columns in the list we will use. We do this using a pseudohash where the column titles are the keys, and their types are the values. The allowed values are "text" for a text string, "int" for an integer, "double" for a double-precision floating-point value and "pixbuf" for a <i>Gtk2::Gdk::Pixbuf</i> object.</p>

<p>We can populate the data in the list either by writing to the <i>$list-&gt;{data}</i> value or using a subroutine.</p>

<p>In addition to the methods it inherits from <i>Gtk2::TreeView</i>, <i>Gtk2::SimpleList</i> also provides:</p>

<blockquote>
<pre>
$list = Gtk2::SimpleList-&gt;new_from_treeview($treeview, [column_data]);

</pre>
</blockquote>



<p>This creates a list using an already created treeview widget. This is handy for when you're using Glade and such a widget has already been created for you.</p>

<blockquote>
<pre>
@indices = $list-&gt;get_selected_indices;

</pre>
</blockquote>



<p>This returns an list of numbers representing the currently selected rows in the list.</p>

<blockquote>
<pre>
$list-&gt;set_data_array($arrayref);

</pre>
</blockquote>



<p>This method populates the list data array. <i>$arrayref </i>is a reference to an array of array references.</p>

<blockquote>
<pre>
$list-&gt;select(@rows);
$list-&gt;unselect(@rows);

</pre>
</blockquote>

<p>These methods select and unselect the rows identified in the <i>@rows</i> array.</p>

<blockquote>
<pre>
$list-&gt;set_column_editable($column, $editable);

</pre>
</blockquote>



<p>Cells in the list can be edited by the user&#151;this method can be used to enable or disable this feature. <i>$column</i> is the index of the column, and <i>$editable</i> should be a true value to enable editing, or false to disable it.</p>
<h3><i>Gtk2::SimpleMenu</i></h3>

<p>This module makes it easy to create hierarchical application menus (the File, Edit, View menu at the top of each window). The constructor takes as an argument a reference to an array containing a tree of menus, submenus, and items. (See <a name="rl12"></a><a href="#l12">Listing 12</a>; you can see the resulting program in <a name="rf12"></a><a href="0312af12.html">Figure 12</a>.)</p>

<p>Each item and submenu is represented by an anonymous hash, containing data needed to set up the menu. The various <i>item_type</i>s include <i>Branch</i> for a submenu, <i>LastBranch</i> for a right-justified menu, <i>Item</i> for a standard item, <i>StockItem</i> for an item with a stock item (which is then specified in the <i>extra_data</i> key), and <i>RadioItem</i> and <i>CheckItem,</i> which create items with radio buttons and checkboxes, respectively.</p>

<p>You can define a callback for when the item is clicked using the <i>callback</i> key, but you can also specify a <i>callback_action, </i>which is then passed as an argument to the function defined by the <i>default_callback</i> argument in the constructor.</p>
<h3>Creating Your Own Widgets</h3>

<p>It is very easy to create custom widgets with Gtk2-Perl. All you need to do is create a package that inherits from an existing Gtk+ widget, and extend the functionality. <a name="rl13"></a><a href="#l13">Listing 13</a> is an example of a simple extension to <i>Gtk2::Button</i>. The resulting program is shown in <a name="rf13"></a><a href="0312af13.html">Figure 13</a>.</p>

<p>The first thing we need to do is declare what package we want to inherit from. This is as easy as</p>

<blockquote>
<pre>
use base 'Gtk2::SomeWidget';

</pre>
</blockquote>



<p>Then we need to override the constructor. But we need to get hold of a <i>Gtk2::SomeWidget</i> object so we can play with it. The <i>$package-&gt;SUPER::new</i> expression does this for us&#151;then we can re-<i>bless() </i>it into our own package and start playing.</p>

<p>The <i>Gtk2::SimpleList</i> and <i>Gtk2::SimpleMenu</i> widgets are examples of this kind of custom widget, which is derived from an existing Gtk+ widget. There's also the <i>Gtk2::PodViewer</i> widget, available from CPAN, which provides a Perl POD rendering widget based on <i>Gtk2::TextView</i>.</p>
<h3>More Information</h3>

<p>This article cannot really cover every aspect of Gtk2-Perl programming, but I hope it has given you enough of an introduction that you will want to find out more. There is certainly a steep learning curve, but once you've reached the top I'm sure you'll find that it was worth it!</p>

<p>To help you on your way, I have compiled a reasonably complete list of references that should make your Gtk2-Perl programming much less painful. </p>
<h3>Gtk+ Documentation</h3>

<p>There is no direct POD documentation for Gtk2-Perl. This is because the Perl bindings adhere as closely as possible to the original C implementation. The C API is fully documented and is available in two forms:</p>



<p><b>Online. </b>You can read the full Gtk+ documentation at http://www .gtk.org/api/. This includes documentation for Gtk+, the Gtk+ stock icon set, the <i>Glib</i> general-purpose utility library,<i> GdkPixbuf</i>, and <i>Pango</i>.</p>

<p><b>Devhelp. <i>Devhelp</b></i> is an API documentation browser that works natively with the Gtk+ documentation system. As well as having a full index of the documentation, you can also search the function reference. It is available from http://www.imendio.com/projects/ devhelp/, and I thoroughly recommend it.</p>



<p>If you're not familiar with C you may have trouble at first translating the examples from C to Perl. Fear not! The Gtk2-Perl team has documented the C to Perl mapping in the <i>Gtk2::api</i> POD document.</p>
<h3>Tutorials</h3>

<p>There are a number of tutorials that will help the learning process. Stephen Wilhelm's tutorial on the old Gtk-Perl 1.x series still works with Gtk2-Perl in 90 percent of the examples, and is a pretty complete guide to the available widgets. The URL for his tutorial is http://jodrell.net/files/gtk-perl-tutorial/.</p>

<p>Ross McFarland, one of the Gtk2-Perl developers, has also written a tutorial that is available at http://gtk2-perl.sourceforge.net/ doc/intro/.</p>
<h3>General Information</h3>

<p>The main Gtk+ web site is at http://www.gtk.org/ and the Gtk2-Perl site is http://gtk2-perl.sf.net/. The Gtk2-Perl site has a great deal of extra information including documentation for <i>Glib</i>, as well as build utilities like <i>ExtUtils::PkgConfig</i> (the perl bindings pkg-config), and information on developing XS bindings and the internals of Gtk2-Perl.</p>
<h3>Asking Questions</h3>

<p>Gtk2-Perl has a mailing list at http://mail.gnome.org/mailman/ listinfo/gtk-perl-list. There is also an active IRC channel&#151;join #gtk-perl on irc.gnome.org and ask your question.</p>
<h3>Installing Gtk2-Perl</h3>

<p>If you like what you see here and want to get started with Gtk2-Perl, the first thing you'll need to do is install the Gtk2-Perl modules. You can do this by downloading and compiling the source libraries from the Gtk2-Perl web site, but you can also use the CPAN bundle. With this, installaton is as simple as issuing this command:</p>

<blockquote>
<pre>
perl -MCPAN -e 'install Bundle::Gnome2'

</pre>
</blockquote>



<p>This will install the latest versions of all the available Gtk2-Perl libraries.</p>
<p><b></b></p>
<p><b>TPJ</b></p>



	
	
<br>
<br>
<b><a name="l8"></a>Listing 8</b><br>
<pre>#!/usr/bin/perl
use Gtk2 -init;
use strict;

my $window = Gtk2::Window-&gt;new;
$window-&gt;set_title('Paned Example');
$window-&gt;signal_connect('delete_event', sub { Gtk2-&gt;main_quit });
$window-&gt;set_border_width(8);

my $vpaned = Gtk2::VPaned-&gt;new;
$vpaned-&gt;set_border_width(8);

$vpaned-&gt;add1(Gtk2::Button-&gt;new('Top Pane'));
$vpaned-&gt;add2(Gtk2::Button-&gt;new('bottom Pane'));

my $frame = Gtk2::Frame-&gt;new('Right Pane');
$frame-&gt;add($vpaned);

my $hpaned = Gtk2::HPaned-&gt;new;
$hpaned-&gt;set_border_width(8);

$hpaned-&gt;add1(Gtk2::Button-&gt;new('Left Pane'));
$hpaned-&gt;add2($frame);

$window-&gt;add($hpaned);

$window-&gt;show_all;

Gtk2-&gt;main;</pre>
<a href="#rl8">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l9"></a>Listing 9</b><br>
<pre>#!/usr/bin/perl
use Gtk2 -init;
use strict;

my $window = Gtk2::Window-&gt;new;
$window-&gt;set_title('Scrolled Window Example');
$window-&gt;signal_connect('delete_event', sub { Gtk2-&gt;main_quit });
$window-&gt;set_border_width(8);

my $image = Gtk2::Image-&gt;new_from_file('butterfly.jpg');

my $scrwin = Gtk2::ScrolledWindow-&gt;new;
$scrwin-&gt;set_policy('automatic', 'automatic');

$scrwin-&gt;add_with_viewport($image);

$window-&gt;add($scrwin);

$window-&gt;show_all;

Gtk2-&gt;main;</pre>
<a href="#rl9">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l10"></a>Listing 10</b><br>
<pre>#!/usr/bin/perl
use Gtk2 -init;
use strict;

my $window = Gtk2::Window-&gt;new;
$window-&gt;set_title('Event Box Example');
$window-&gt;signal_connect('delete_event', sub { Gtk2-&gt;main_quit });
$window-&gt;set_border_width(8);

my $image = Gtk2::Image-&gt;new_from_stock('gtk-dialog-info', 'dialog');

# a Gtk2::Image widget can't have the 'clicked' signal, so this causes
# an error:
$image-&gt;signal_connect('clicked', \&amp;clicked);

my $box = Gtk2::EventBox-&gt;new;

# but this works!
$box-&gt;signal_connect('button_release_event', \&amp;clicked);

$box-&gt;add($image);

$window-&gt;add($box);

$window-&gt;show_all;

Gtk2-&gt;main;

sub clicked {
        print "someone clicked me!\n";
}</pre>
<a href="#rl10">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l11"></a>Listing 11</b><br>
<pre>#!/usr/bin/perl
use Gtk2 -init;
use Gtk2::SimpleList;
use strict;

my $window = Gtk2::Window-&gt;new;
$window-&gt;set_title('List Example');
$window-&gt;set_default_size(300, 200);
$window-&gt;signal_connect('delete_event', sub { Gtk2-&gt;main_quit });
$window-&gt;set_border_width(8);

my $list = Gtk2::SimpleList-&gt;new(
        'Browser Name'  =&gt; 'text',
        'Version'       =&gt; 'text',
        'Uses Gecko?'   =&gt; 'bool',
        'Icon'          =&gt; 'pixbuf'
);

$list-&gt;set_column_editable(0, 1);

@{$list-&gt;{data}} = (
        [ 'Epiphany',   '1.0.4', 1, Gtk2::Gdk::Pixbuf-&gt;new_from_file('epiphany.png') ],
        [ 'Galeon',     '1.3.9', 1, Gtk2::Gdk::Pixbuf-&gt;new_from_file('galeon.png') ],
        [ 'Konqueror',  '3.1.4', 0, Gtk2::Gdk::Pixbuf-&gt;new_from_file('konqueror.png') ],
);

my $scrwin = Gtk2::ScrolledWindow-&gt;new;
$scrwin-&gt;set_policy('automatic', 'automatic');

$scrwin-&gt;add_with_viewport($list);

my $button = Gtk2::Button-&gt;new_from_stock('gtk-ok');
$button-&gt;signal_connect('clicked', \&amp;clicked);

my $vbox = Gtk2::VBox-&gt;new;
$vbox-&gt;set_spacing(8);

$vbox-&gt;pack_start($scrwin, 1, 1, 0);
$vbox-&gt;pack_start($button, 0, 1, 0);

$window-&gt;add($vbox);

$window-&gt;show_all;

Gtk2-&gt;main;

sub clicked {
        my $selection = ($list-&gt;get_selected_indices)[0];
        my @row = @{(@{$list-&gt;{data}})[$selection]};
        printf(
                "You selected %s, which is at version %s and %s Gecko.\n",
                $row[0],
                $row[1],
                ($row[2] == 1 ? "uses" : "doesn't use")
        );
        Gtk2-&gt;main_quit;
}</pre>
<a href="#rl11">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l12"></a>Listing 12</b><br>
<pre>#!/usr/bin/perl
use Gtk2 -init;
use Gtk2::SimpleMenu;
use strict;

my $window = Gtk2::Window-&gt;new;
$window-&gt;set_title('Menu Example');
$window-&gt;set_default_size(300, 200);
$window-&gt;signal_connect('delete_event', sub { Gtk2-&gt;main_quit });

my $vbox = Gtk2::VBox-&gt;new;

my $menu_tree = [
        _File =&gt; {
                item_type =&gt; '&lt;Branch&gt;',
                children =&gt; [
                        _New =&gt; {
                                item_type       =&gt; '&lt;StockItem&gt;',
                                callback        =&gt; \&amp;new_document,
                                accelerator     =&gt; '&lt;ctrl&gt;N',
                                extra_data      =&gt; 'gtk-new',
                        },
                        _Save =&gt; {
                                item_type       =&gt; '&lt;StockItem&gt;',
                                callback        =&gt; \&amp;save_document,
                                accelerator     =&gt; '&lt;ctrl&gt;S',
                                extra_data      =&gt; 'gtk-save',
                        },
                        _Quit =&gt; {
                                item_type       =&gt; '&lt;StockItem&gt;',
                                callback        =&gt; sub { Gtk2-&gt;main_quit },
                                accelerator     =&gt; '&lt;ctrl&gt;Q',
                                extra_data      =&gt; 'gtk-quit',
                        },
                ],
        },
        _Edit =&gt; {
                item_type =&gt; '&lt;Branch&gt;',
                children =&gt; [
                        _Cut =&gt; {
                                item_type       =&gt; '&lt;StockItem&gt;',
                                callback_action =&gt; 0,
                                accelerator     =&gt; '&lt;ctrl&gt;X',
                                extra_data      =&gt; 'gtk-cut',
                        },
                        _Copy =&gt; {
                                item_type       =&gt; '&lt;StockItem&gt;',
                                callback_action =&gt; 1,
                                accelerator     =&gt; '&lt;ctrl&gt;C',
                                extra_data      =&gt; 'gtk-copy',
                        },
                        _Paste =&gt; {
                                item_type       =&gt; '&lt;StockItem&gt;',
                                callback_action =&gt; 2,
                                accelerator     =&gt; '&lt;ctrl&gt;V',
                                extra_data      =&gt; 'gtk-paste',
                        },
                ],
        },
        _Help =&gt; {
                item_type =&gt; '&lt;LastBranch&gt;',
                children =&gt; [
                        _Help =&gt; {
                                item_type       =&gt; '&lt;StockItem&gt;',
                                callback_action =&gt; 3,
                                accelerator     =&gt; '&lt;ctrl&gt;H',
                                extra_data      =&gt; 'gtk-help',
                        }
                ],
        },
];

my $menu = Gtk2::SimpleMenu-&gt;new (
                menu_tree               =&gt; $menu_tree,
                default_callback        =&gt; \&amp;default_callback,
        );

$vbox-&gt;pack_start($menu-&gt;{widget}, 0, 0, 0);
$vbox-&gt;pack_start(Gtk2::Label-&gt;new('Rest of application  here'), 1, 1, 0);

$window-&gt;add($vbox);

$window-&gt;show_all;

Gtk2-&gt;main;

sub new_document {
        print "user wants a new document.\n";
}

sub save_document {
        print "user wants to save.\n";
}

sub default_callback {
        my (undef, $callback_action, $menu_item) = @_;
        print "callback action number $callback_action\n";
}</pre>
<a href="#rl12">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l13"></a>Listing 13</b><br>
<pre>#!/usr/bin/perl
use Gtk2 -init;
use strict;

my $window = Gtk2::Window-&gt;new;
$window-&gt;set_title('Custom Widget Example');
$window-&gt;set_default_size(150, 100);
$window-&gt;signal_connect('delete_event', sub { Gtk2-&gt;main_quit });
$window-&gt;set_border_width(8);

my $button = Gtk2::FunnyButton-&gt;new(
        normal  =&gt; 'Click me!',
        over    =&gt; 'Go on, click me!',
        clicked =&gt; 'Click me harder!',
);

$window-&gt;add($button);

$window-&gt;show_all;

Gtk2-&gt;main;

package Gtk2::FunnyButton;
use base 'Gtk2::Button';
use strict;

sub new {
        my ($package, %args) = @_;
        my $self = $package-&gt;SUPER::new;
        bless($self, $package);
        $self-&gt;{labels} = \%args;
        $self-&gt;add(Gtk2::Label-&gt;new($self-&gt;{labels}{'normal'}));
        $self-&gt;signal_connect('enter_notify_event', sub { $self-&gt;child-&gt;set_text($self-&gt;{labels}{'over'}) });
        $self-&gt;signal_connect('leave_notify_event', sub { $self-&gt;child-&gt;set_text($self-&gt;{labels}{'normal'}) });
        $self-&gt;signal_connect('clicked', sub { $self-&gt;child-&gt;set_text($self-&gt;{labels}{'clicked'}) });
        return $self;
}</pre>
<a href="#rl13">Back to article</a><br>
	
	
	


</body>
</html>