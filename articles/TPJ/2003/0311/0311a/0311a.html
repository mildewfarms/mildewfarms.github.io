
<html>
<head>
<title>November, 2003: Programming Graphical Applications with Gtk2-Perl, Part 1</title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; The Perl Journal-->

<h1>Programming Graphical Applications with Gtk2-Perl, Part 1</h1>
<p><i>The Perl Journal</i> November, 2003</p>
<h2></h2>


<h3>By Gavin Brown</h3>


<I>Gavin works for the domain registry CentralNic, and was a coauthor of Professional Perl Programming (Wrox Press, 2001). He can be contacted at gavin.brown@uk.com.</I>

<hr>





<p>Gtk+ is a toolkit for creating graphical applications on X11 systems. It has also been ported to Windows, BeOS, and runs on Mac OS X using Apple's X11 server. It was originally created for the GIMP, a popular image-manipulation program, and later became the toolkit for GNOME (GNU Network Object Model Environment).</p>

<p>Perl already has mature, stable, and popular bindings for the 1.x series of Gtk+ and for the GNOME libraries. However, bindings for the 2.x series of Gtk+ reached the 1.0 version in October, so now is a good time to talk about the things that you can do with the new libraries.</p>

<p>This article will be comprised of two parts: This month, I'll discuss some basics of event-based programming and introduce you to widgets and containers in Gtk+. Next month, I'll discuss manipulating graphics, introduce the Rapid Application Development (RAD) tool Glade, and show you how to create your own widgets. </p>
<h3>What's New in Gtk+ 2</h3>

<p>Gtk+ 2.x builds on the old 1.x series with a number of improvements. These include support for font antialiasing using Xft, better font management using Fontconfig, integrated and themeable stock icons, and an advanced Model-View-Controller (MVC) model for list and tree widgets. All of these new features are available to Gtk2-Perl.</p>
<h3>Some Basic Principles</h3>

<p>Gtk+ employs some programming paradigms that can be a bit confusing to people who've never done GUI programming before. So before we go too far, I think it's a good idea to cover them, if only briefly.</p>
<h3>Event-Based Programming</h3>

<p>At the heart of GUI programming is the notion of event-based programming. Unlike writing programs for the console or the Web, your program does not run sequentially&#151;some parts of the code will be run when the program starts, but some may not be executed until an event occurs or a signal is emitted by a widget. This may be the user clicking on a button or pressing a key or moving the mouse. Events can also be triggered by messages from the window manager telling the application to quit. Or the user could be performing a drag-and-drop action from another application.</p>

<p>As a result, applications using Gtk2-Perl tend to follow this structure:</p>



<ul>
  <li>Build the basic interface.</li>
  <li>Set up callbacks for various different events and signals.</li>
  <li>Enter a main loop during which the application listens for events and signals and executes the defined callbacks.</li>
</ul>



<p>The main loop itself is provided by the Gtk+ libraries, so as a rule, there's very little need to create one yourself.</p>
<h3>Widgets and Containers</h3>

<p>Widgets are graphical objects that the user can interact with. A widget might be a button or an icon, a menu or a scrollbar. Gtk+ includes a large palette of widgets that perform all kinds of tasks.</p>

<p>In order to organize these widgets on screen, and to establish the relationships between them, containers are used to group widgets together. For example, most applications will have a toolbar with icons for different functions. These icons are widgets and are grouped together by a container widget; namely, the toolbar itself. The toolbar is also contained within the top-level window. A large number of widgets are also containers; for example, a button can contain a simple text label or an icon.</p>

<p>In Gtk2-Perl, all widgets are represented as Perl objects. In fact, they're also blessed hash references so that you can do inheritance and extensions and create your own custom widgets (more on that next month).</p>
<h3>Hello World</h3>

<p><a name="rl1"></a><a href="#l1">Listing 1</a> is a simple "Hello World" program, using the Gtk2-Perl modules. The application that this program produces is shown in <a name="rf1"></a><a href="0311af1.html">Figure 1</a>.</p>

<p>The first line after the shebang loads the Gtk2 module. The <i>-init</i> argument after the module name is a convenience&#151;it calls the <i>Gtk2-&gt;init</i> function so that you don't have to later on.</p>

<p>The <i>$quit</i> variable contains a reference to an anonymous subroutine. These references are used extensively in Gtk2-Perl as callbacks that are executed when an interface event occurs. Line 5 in  <a name="rl1"></a><a href="#l1">Listing 1</a> is equivalent to</p>

<blockquote>
<pre>
sub quit {
        exit;
}

my $quit = \&amp;quit;

</pre>
</blockquote>



<p>Similarly, line 8 could be changed to</p>

<blockquote>
<pre>
$window-&gt;signal_connect('delete_event', \&amp;quit);

</pre>
</blockquote>



<p>The rest of the program is involved with the creation of widgets, setting their properties, building up interface by placing widgets inside other widgets, and finally displaying the application on the screen.</p>

<p>Lines 7-12 create a top-level window widget that will contain all the other widgets, and then sets various properties of that widget. Line 14 creates a simple text label. Lines 16 and 17 create a button, and set a callback to use when the button is clicked. Lines 19-23 create a special container called a "packing box" that is used to group widgets. In this case, it is a vertical packing box, so the widgets are stacked one below the other (a horizontal packing box is also available).</p>

<p>Finally, on lines 27 and 29, the widgets are rendered on screen and the main Gtk+ loop is called. Without this first line, nothing would appear on screen. Without the second, the UI would be unresponsive, as it would not respond to any events that occur.</p>
<h3>Controlling Program Flow</h3>

<p>A vital component of event-based programming is the concept of a program loop. All GUI systems have such a loop&#151;during each iteration, the program checks for any events and responds to them accordingly.</p>

<p>You can control the Gtk+ main loop using the following functions:</p>

<blockquote>
<pre>
Gtk2-&gt;main;

</pre>
</blockquote>



<p>This starts the main loop. Sequentially, your program pauses at this point in the program until the main loop ends.</p>

<blockquote>
<pre>
Gtk2-&gt;main_quit;

</pre>
</blockquote>



<p>This tells the main loop to end. Your program will then continue as before.</p>

<p>There are some situations where you might want to perform an iteration outside of the main loop&#151;for example, while your program is reading or writing a filehandle or socket, or some other process that would otherwise freeze your user interface. In such situations, you can use this code to "catch up" on any unhandled events:</p>

<blockquote>
<pre>
Gtk2-&gt;main_iteration while (Gtk2-&gt;events_pending);

</pre>
</blockquote>

<p>The <i>Gtk2-&gt;main_iteration</i> function does the job of checking for and responding to any events that might have cropped up since the last check. Unsurprisingly, <i>Gtk2-&gt;events_pending</i> returns a true value if there are any events that haven't been handled.</p>
<h3>Basic Widgets and Their Properties</h3>

<p>Widgets all have a common set of methods that they inherit from their base class, <i>Gtk2::Widget</i>. There are far too many widgets and properties to discuss here, but the most important ones are worth taking a look at. Full information about the available widgets can be found in the Gtk+ API documentation and the Gtk-Perl tutorial; see the the "More Information" section at the end of the article.</p>
<h3>Common Widget Methods</h3>

<blockquote>
<pre>
my $widget = Gtk2::Widget-&gt;new;

</pre>
</blockquote>



<p>Since all Gtk+ widgets are Perl objects, they need to be constructed and, as expected, the <i>new()</i> method is the constructor. Most widgets can be created without any arguments, but a few will require various parameters to be set when they're constructed. If you aren't sure, use the <i>new()</i> method without arguments and run your program&#151;if an argument is missing, you will be told.</p>

<blockquote>
<pre>
$widget-&gt;show;
$widget-&gt;show_all;
$widget-&gt;hide;
$widget-&gt;hide_all;

</pre>
</blockquote>

<p>A widget has to be rendered onto the display before it can be used&#151;the <i>show()</i> method does this rendering. Every widget has to be shown, so the <i>show_all()</i> method is handy since it calls <i>show()</i> on the widget and all the widget's children. The <i>hide()</i> and <i>hide_all()</i> methods do the reverse&#151;they remove a widget from the screen.</p>

<blockquote>
<pre>
my $handler_id = $widget-&gt;signal_connect($signal, $callback, \@callback_data);
$widget-&gt;signal_disconnect($handler_id);

</pre>
</blockquote>

<p>This method attaches the supplied callback reference to the given signal for the widget. There are a wide range of signals and events available&#151;again, too many to discuss in this article. The <i>signal_disconnect()</i> method detaches the callback identified by the handler ID.</p>

<blockquote>
<pre>
my $container = $widget-&gt;get_parent;

</pre>
</blockquote>



<p>This method returns the parent widget of the widget&#151;for example, in <a name="rl1"></a><a href="#l1">Listing 1</a>, using <i>get_parent()</i> on the <i>$vbox</i> container would return <i>$window</i>.</p>
<h3>Windows</h3>

<p>Window widgets come in two types: top-level windows (<i>Gtk2::Window</i>), which contain the application's main interface; and dialogs (<i>Gtk2::Dialog</i>) that appear occasionally; for example, to ask the user a question or display configuration settings.</p>

<p>To create a top-level window, use the following:</p>

<blockquote>
<pre>
my $window = Gtk2::Window-&gt;new;

</pre>
</blockquote>



<p>A top-level window is a container, so you can use the <i>add()</i> method to place another widget inside it. This method is common to all container widgets (more on this in the "Containers" section).</p>

<p>Dialogs are similar to normal windows, but come with some extra bits and pieces to save time. <a name="rl2"></a><a href="#l2">Listing 2</a> is a simple example of <i>Gtk2::Dialog</i> usage. The resulting window is shown in <a name="rf2"></a><a href="0311af2.html">Figure 2</a>.</p>

<p>Dialog windows come prepackaged with an <i>action area</i> for buttons, and a vertical packing box for widgets (the <i>vbox()</i> method of the dialog returns the packing box). You can add buttons to the action area using the <i>add_buttons()</i> method, using a hash containing the label or stock ID for each button, and the response code. The <i>response</i> signal can be used to determine what to do when the dialog is closed or when one of the buttons is clicked. For example, if the user presses the <i>close</i> button on the window frame, the response code is <i>delete-event</i>, and we've told the dialog to use <i>cancel</i> for the "cancel" button and <i>ok</i> for "OK."</p>
<h3>Labels</h3>

<blockquote>
<pre>
my $label = Gtk2::Label-&gt;new('This is a label.');
$label-&gt;set_text("Ain't it neat?!");
my $text = $label-&gt;get_text;

</pre>
</blockquote>

<p>Labels are simple, rectangular widgets containing text. This text can be formatted in a number of ways. Gtk+ uses a system called "Pango" to format text, and you can use a simple HTML-like markup language to add formatting to your text labels (see <a name="rl3"></a><a href="#l3">Listing 3</a>). The resulting window is shown in <a name="rf3"></a><a href="0311af3.html">Figure 3</a>.</p>
<h3>Images</h3>

<p>A real headache with the old Gtk+ 1.x series was the complexity involved in displaying images. In version 2.x, image support has been vastly improved. Gtk+ now directly supports a wide range of image formats and features, including GIF animation and PNG's alpha channels.</p>

<blockquote>
<pre>
my $image = Gtk2::Image-&gt;new_from_file('icon2.png');
$image-&gt;set_from_file('icon2.png');
$image-&gt;set_from_animation(Gtk2::Gdk::PixbufAnimation 	-&gt;new_from_file('animation.gif', my $error));

</pre>
</blockquote>

<p>Additionally, you can access Gtk+'s wide range of stock icons:</p>

<blockquote>
<pre>
my $image = Gtk2::Image-&gt;new_from_stock('gtk-quit');
$image-&gt;set_from_stock('gtk-ok');

</pre>
</blockquote>

<p>A complete list of all the stock icons can be found in the Gtk+ reference (see "More Information" for links).</p>
<h3>Buttons</h3>

<p>Buttons can be created in a number of ways. First, you can call the constructor with a string argument as a text label:</p>

<blockquote>
<pre>
my $button = Gtk2::Button-&gt;new('Click Me!');

</pre>
</blockquote>



<p>or you can request a stock button:</p>

<blockquote>
<pre>
my $button = Gtk2::Button-&gt;new_from_stock('gtk-ok');

</pre>
</blockquote>



<p>The button will use the appropriate stock icon and use the standard label for that icon (e.g., "OK" or "Quit"). This label is internationalized automatically based on the user's language settings.</p>

<p>To use your own child widget, simply call the constructor without arguments:</p>

<blockquote>
<pre>
my $button = Gtk2::Button-&gt;new;

$button-&gt;add($widget);

</pre>
</blockquote>

<p>Most of the time, you'll want to use the <i>clicked</i> signal to connect a callback to a button, like so:</p>

<blockquote>
<pre>
$button-&gt;signal_connect('clicked', \&amp;clicked);

sub clicked {
        my $button = shift;
        print "button was clicked\n";
}

</pre>
</blockquote>
<h3>Text Inputs</h3>

<p>This creates a simple, single-line text input:</p>

<blockquote>
<pre>
my $entry = Gtk2::Entry-&gt;new;
$entry-&gt;set_text('Enter your name here.');
my $text = $entry-&gt;get_text;

</pre>
</blockquote>

<p>You can connect the <i>activate</i> signal to a <i>Gtk2::Entry</i> widget to catch when the user presses the Enter key.</p>
<h3>Containers</h3>

<p>Containers are widgets that can hold other widgets. They are vital for creating an organized interface because they define the relationships between elements on screen.</p>
<h3>Common Container Methods</h3>

<blockquote>
<pre>
$container-&gt;add($widget);
$container-&gt;remove($widget);

</pre>
</blockquote>

<p>Most containers support the <i>add()</i> and <i>remove()</i> methods. They simply add a widget to a container or remove it. In general, there are two kinds of containers: Those that can hold just a single widget (for example, a button) and are derived from the <i>Gtk2Bin</i> base widget; and those that can hold more than one. The <i>add()</i> and <i>remove()</i> methods are common to this first kind. Multiple containers usually have their own methods for adding widgets.</p>

<blockquote>
<pre>
my @child_widgets = $container-&gt;get_children;

</pre>
</blockquote>



<p>This returns an array of all the widgets that are held by a widget. Single-widget containers also have the <i>child()</i> method.</p>
<h3>Packing Boxes</h3>

<p>Packing boxes allow you to align widgets in horizontal rows or vertical columns. They are among the most useful containers&#151;you will probably find that your applications will make heavy use of them. <a name="rl4"></a><a href="#l4">Listing 4</a> demonstrates the use of packing boxes and produces the output in <a name="rf4"></a><a href="0311af4.html">Figure 4</a>.</p>

<p>When the user clicks the button, the horizontal packing box is removed from the window and is replaced with a vertical packing box.</p>

<blockquote>
<pre>
$box-&gt;pack_start($widget, $expand, $fill, $padding);
$box-&gt;pack_end($widget, $expand, $fill, $padding);

</pre>
</blockquote>

<p>To add a widget to a packing box, use either <i>pack_start()</i> or <i>pack_end()</i>. The former places the widget in the next slot to the left (or top, in the case of a vertical packing box); the latter places the widget into the next slot to the right (or bottom).</p>

<p>The remaining three arguments define the widget's appearance. Setting <i>$expand</i> to 1 (or any true value) will cause the packing box to grow to fill all the space available. If <i>$fill</i> is true, then the widgets inside the packing box are expanded to fill all the space available. The <i>$padding</i> variable determines the padding (in pixels) between widgets.</p>

<p><a name="rl5"></a><a href="#l5">Listing 5</a> demonstrates the different packing modes and generates the layout seen in <a name="rf5"></a><a href="0311af5.html">Figure 5</a>. In the first row, the packing box is shrunk to fit the size of the buttons. In the second, the packing box is expanded but the buttons are spread out. In the third, the buttons are expanded to fill the available size.</p>
<h3>Tables</h3>

<p>Tables are similar to packing boxes, but they add a second dimension. Widgets can be arranged in rows and in columns. The table size, however, is fixed&#151;while you can add as many widgets to a packing box as you like, with a table, you must define the number of rows and columns in the table when you create it.</p>

<p><a name="rl6"></a><a href="#l6">Listing 6</a> shows you how tables work. Since they are two-dimensional, you can attach widgets in many more ways. The corresponding <a name="rf6"></a><a href="0311af6.html">Figure 6</a> shows widgets that span multiple rows and columns.</p>

<p>When you create a table, you must specify its size:</p>

<blockquote>
<pre>
my $table = Gtk2::Table-&gt;new(6, 5);

</pre>
</blockquote>

<p>The above code creates a table six rows high and five columns wide. If you try to insert a widget outside this grid, you will get an error.</p>

<p>The <i>attach_defaults()</i> method is used to add widgets to a table. There is also <i>attach()</i>, which gives you more fine control over the way widgets appear within the table&#151;see the "More Information" section for details.</p>

<blockquote>
<pre>
$table-&gt;attach_defaults($widget, $left_coord, $right_coord, $top_coord, 
                       $bottom_coord);

</pre>
</blockquote>

<p>The four arguments to <i>attach_defaults()</i> after the widget are the coordinates of the left-, right-, top-, and bottom-most edges of the widget. The table grid starts in the top left at "0, 0" and extends to <i>$x-1</i>, <i>$y-1</i>, where <i>$x</i> and <i>$y</i> are the dimensions specified when you create the table.</p>
<h3>Frames</h3>

<p>Frames create a border around a widget with a text label as a title. Frames can only contain a single widget. An example of frame usage is shown in <a name="rl7"></a><a href="#l7">Listing 7</a>, and the resulting window is shown in <a name="rf7"></a><a href="0311af7.html">Figure 7</a>.</p>

<p>The frame style can be controlled with the <i>set_shadow_type()</i> method. Allowed values are "in," "etched_in," "out," and "etched_out."</p>
<h3>More Information</h3>

<p>This article cannot really cover every aspect of Gtk2-Perl programming, but I hope it has given you enough of an introduction that you will want to find out more. There is certainly a steep learning curve, but once you've reached the top, I'm sure you'll find that it was worth it!</p>

<p>To help you on your way, I have compiled a reasonably complete list of references that should make your Gtk2-Perl programming much less painful. Also, check out Part 2 of this article next month for more on creating your own widgets.</p>
<h3>Gtk+ Documentation</h3>

<p>There is currently no POD documentation for Gtk2-Perl. This is because the Perl bindings adhere as closely as possible to the original C implementation. The C API is fully documented and is available in two forms:</p>



<p><b>Online. </b>You can read the full Gtk+ documentation at http://www .gtk.org/api/. This includes documentation for the Gtk+ widget set, the <i>Glib</i> general purpose utility library,<i> GdkPixbuf</i>, and Pango.</p>

<p><b>Devhelp. <i>Devhelp</b></i> is an API documentation browser that works natively with the Gtk+ documentation system. As well as having a full index of the documentation, you can also search the function reference. It is available at http://www.imendio.com/projects/ devhelp/, and I thoroughly recommend it.</p>

<p>If you're not familiar with C, you may have trouble at first translating the examples from C to Perl. Fear not! The Gtk2-Perl team has documented the C to Perl mapping in the <i>Gtk2::api</i> POD document.</p>
<h3>Tutorials</h3>

<p>There are a number of tutorials that will help the learning process. Stephen Wilhelm's tutorial on the old Gtk-Perl 1.x series still works with Gtk2-Perl in 90 percent of the examples and is a pretty complete guide to the available widgets. The URL for his tutorial is http://jodrell.net/files/gtk-perl-tutorial/.</p>

<p>Ross McFarland, one of the Gtk2-Perl developers, has also written a tutorial that is available at http://gtk2-perl.sourceforge.net/ doc/intro/.</p>
<h3>General Information</h3>

<p>The main Gtk+ web site is at http://www.gtk.org/ and the Gtk2-Perl site is http://gtk2-perl.sf.net/. The Gtk2-Perl site has a great deal of extra information including documentation for <i>Glib</i>, as well as build utilities like <i>ExtUtils::PkgConfig</i> (the Perl bindings pkg-config), and information on developing XS bindings and the internals of Gtk2-Perl.</p>
<h3>Asking Questions</h3>

<p>Gtk2-Perl has a mailing list at http://mail.gnome.org/mailman/ listinfo/gtk-perl-list/. There is also an active IRC channel&#151;join #gtk-perl on irc.gnome.org and ask your question.</p>


<p><b></b></p>
<p><b>TPJ</b></p>



	
	
<br>
<br>
<b><a name="l1"></a>Listing 1</b><br>
<pre>#!/usr/bin/perl
use Gtk2 -init;
use strict;

my $quit = sub { exit };

my $window = Gtk2::Window-&gt;new;
$window-&gt;signal_connect('delete_event', $quit);
$window-&gt;set_position('center');
$window-&gt;set_border_width(8);
$window-&gt;set_title('Hello World!');
$window-&gt;set_default_size(200, 100);

my $label = Gtk2::Label-&gt;new('Hello World!');

my $button = Gtk2::Button-&gt;new_from_stock('gtk-quit');
$button-&gt;signal_connect('clicked', $quit);

my $vbox = Gtk2::VBox-&gt;new;
$vbox-&gt;set_spacing(8);

$vbox-&gt;pack_start($label,  1, 1, 0);
$vbox-&gt;pack_start($button, 0, 0, 0);

$window-&gt;add($vbox);

$window-&gt;show_all;

Gtk2-&gt;main;</pre>
<a href="#rl1">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l2"></a>Listing 2</b><br>
<pre>#!/usr/bin/perl
use Gtk2 -init;
use strict;

my $dialog = Gtk2::Dialog-&gt;new;

$dialog-&gt;set_title('Example Dialog');
$dialog-&gt;set_border_width(8);
$dialog-&gt;vbox-&gt;set_spacing(8);
$dialog-&gt;set_position('center');
$dialog-&gt;set_default_size(200, 100);

$dialog-&gt;add_buttons(
        'gtk-cancel' =&gt; 'cancel',
        'gtk-ok' =&gt; 'ok'
);

$dialog-&gt;signal_connect('response', \&amp;response);

my $label = Gtk2::Label-&gt;new('Example dialog');

$dialog-&gt;vbox-&gt;pack_start($label, 1, 1, 0);

$dialog-&gt;show_all;

$dialog-&gt;run;

sub response {
        my ($button, $response) = @_;
        print "response code is $response\n";
        exit;
}</pre>
<a href="#rl2">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l3"></a>Listing 3</b><br>
<pre>#!/usr/bin/perl
use Gtk2 -init;
use strict;

my $window = Gtk2::Window-&gt;new;
$window-&gt;signal_connect('delete_event', sub { Gtk2-&gt;main_quit });
$window-&gt;set_border_width(8);
$window-&gt;set_title('Label Example');

my $label = Gtk2::Label-&gt;new;

my $markup = &lt;&lt;"END";
This is some plain text.

&lt;b&gt;This is some bold text.&lt;/b&gt;

&lt;span size="small"&gt;This is some small text.&lt;/span&gt;

&lt;span size="large"&gt;This is some large text.&lt;/span&gt;

&lt;span foreground="blue"&gt;This is blue.&lt;/span&gt; &lt;span style="italic"&gt;This is italic.&lt;/span&gt; &lt;tt&gt;This is monospace.&lt;/tt&gt;
END

$label-&gt;set_markup($markup);

$window-&gt;add($label);

$window-&gt;show_all;

Gtk2-&gt;main;

exit;</pre>
<a href="#rl3">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l4"></a>Listing 4</b><br>
<pre>#!/usr/bin/perl
use Gtk2 -init;
use strict;

my $window = Gtk2::Window-&gt;new;
$window-&gt;set_title('Packing Box Example');
$window-&gt;signal_connect('delete_event', sub { Gtk2-&gt;main_quit });
$window-&gt;set_border_width(8);

my $label  = Gtk2::Label-&gt;new("Here's a label");
my $button = Gtk2::Button-&gt;new("Here's a button");
my $image  = Gtk2::Image-&gt;new_from_stock('gtk-dialog-info', 'dialog');

$button-&gt;signal_connect('clicked', \&amp;clicked);

my $box = Gtk2::HBox-&gt;new;
$box-&gt;set_spacing(8);

$box-&gt;pack_start($label,  0, 0, 0);
$box-&gt;pack_start($button, 0, 0, 0);
$box-&gt;pack_start($image,  0, 0, 0);

$window-&gt;add($box);

$window-&gt;show_all;

Gtk2-&gt;main;

exit;

sub clicked {

    $window-&gt;remove($box);          # remove from the window
    foreach my $child ($box-&gt;get_children) {
            $box-&gt;remove($child);   # remove all the children
    }
    $box-&gt;destroy;                  # destroy

    # create a new box:
    my $new_box = (ref($box) eq 'Gtk2::VBox' ? Gtk2::HBox-&gt;new : 
                   Gtk2::VBox-&gt;new);

    # re-pack the widgets:
    $new_box-&gt;pack_start($label,  0, 0, 0);
    $new_box-&gt;pack_start($button, 0, 0, 0);
    $new_box-&gt;pack_start($image,  0, 0, 0);
    $new_box-&gt;show_all;

    $box = $new_box;
    $window-&gt;add($box);             # add the new box
    return 1;
}</pre>
<a href="#rl4">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l5"></a>Listing 5</b><br>
<pre>#!/usr/bin/perl
use Gtk2 -init;
use strict;

my $window = Gtk2::Window-&gt;new;
$window-&gt;set_title('Packing Box Example');
$window-&gt;signal_connect('delete_event', sub { Gtk2-&gt;main_quit });
$window-&gt;set_default_size(300, 200);
$window-&gt;set_border_width(8);

my $vbox = Gtk2::VBox-&gt;new;
$vbox-&gt;set_spacing(8);

my @values = (
        [ 0, 0 ],
        [ 1, 0 ],
        [ 1, 1 ],
);

foreach my $args (@values) {
        my $hbox = Gtk2::HBox-&gt;new;
        $hbox-&gt;set_spacing(8);
        $hbox-&gt;pack_start(Gtk2::Button-&gt;new('First Item'), @{$args}, 0);
        $hbox-&gt;pack_start(Gtk2::Button-&gt;new('Second'),     @{$args}, 0);
        $hbox-&gt;pack_start(Gtk2::Button-&gt;new('Third'),      @{$args}, 0);
        $vbox-&gt;pack_start($hbox, 0, 0, 0);
}

$window-&gt;add($vbox);

$window-&gt;show_all;

Gtk2-&gt;main;

exit;</pre>
<a href="#rl5">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l6"></a>Listing 6</b><br>
<pre>#!/usr/bin/perl
use Gtk2 -init;
use strict;

my $window = Gtk2::Window-&gt;new;
$window-&gt;set_title('Table Example');
$window-&gt;signal_connect('delete_event', sub { Gtk2-&gt;main_quit });
$window-&gt;set_border_width(8);

my $table = Gtk2::Table-&gt;new(4, 2);
$table-&gt;set_col_spacings(8);
$table-&gt;set_row_spacings(8);

# two buttons side-by-side:
$table-&gt;attach_defaults(Gtk2::Button-&gt;new('0,0 -&gt; 1, 0'), 0, 1, 0, 1);
$table-&gt;attach_defaults(Gtk2::Button-&gt;new('1,0 -&gt; 2, 0'), 1, 2, 0, 1);

# one button spanning two columns:
$table-&gt;attach_defaults(Gtk2::Button-&gt;new('0,1 -&gt; 2, 1'), 0, 2, 1, 2);

# one button spanning two rows:
$table-&gt;attach_defaults(Gtk2::Button-&gt;new('0,3 -&gt; 1, 4'), 0, 1, 2, 4);

# two more buttons to fill the space:
$table-&gt;attach_defaults(Gtk2::Button-&gt;new('1,3 -&gt; 2, 3'), 1, 2, 2, 3);
$table-&gt;attach_defaults(Gtk2::Button-&gt;new('1,4 -&gt; 2, 4'), 1, 2, 3, 4);

$window-&gt;add($table);
$window-&gt;show_all;

Gtk2-&gt;main;

exit;</pre>
<a href="#rl6">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l7"></a>Listing 7</b><br>
<pre>#!/usr/bin/perl
use Gtk2 -init;
use strict;

my $window = Gtk2::Window-&gt;new;
$window-&gt;set_title('Frame Example');
$window-&gt;signal_connect('delete_event', sub { Gtk2-&gt;main_quit });
$window-&gt;set_border_width(8);

my $frame = Gtk2::Frame-&gt;new('Frame Title');

$frame-&gt;set_shadow_type('etched_in');

my $button = Gtk2::Button-&gt;new('Frame Contents');
$button-&gt;signal_connect('clicked', sub { Gtk2-&gt;main_quit });
$button-&gt;set_border_width(8);

$frame-&gt;add($button);

$window-&gt;add($frame);

$window-&gt;show_all;

Gtk2-&gt;main;</pre>
<a href="#rl7">Back to article</a><br>
	
	
	


</body>
</html>