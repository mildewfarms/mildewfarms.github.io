
<html>
<head>
<title>November, 2003: Perl Poker</title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; The Perl Journal-->

<h1>Perl Poker</h1>
<p><i>The Perl Journal</i> November, 2003</p>
<h2></h2>


<h3>By Simon Cozens</h3>


<I>Simon is a freelance programmer and author, whose titles include Beginning Perl (Wrox Press, 2000) and Extending and Embedding Perl (Manning Publications, 2002). He's the creator of over 30 CPAN modules and a former Parrot pumpking. Simon can be reached at simon@ simon-cozens.org.</I>

<hr>





<p>It's not all work and no play at my company. In fact, the Northern Ireland Perl Mongers scene has been swept with somewhat of a poker craze recently. As well as trying to improve our own poker play, we've naturally had a look around at the state of computer poker research.</p>

<p>We found a great number of interesting things: online poker servers where humans and poker-playing robots could play against each other, papers on "pseudo-optimum strategy," evaluation algorithms, and examples in C and in Java&#151;but nothing in Perl.</p>

<p>This needed to be fixed.</p>
<h3>Getting Straight to Work</h3>

<p>Thankfully, quite a few of the tools out there can be integrated pretty quickly into Perl without much effort. For starters, we can take <i>Games::Cards</i> as our card library. We'll make some revisions to it later so that it can handle more of the areas we need.</p>

<p>Another thing we'll need to do is have some means of evaluating what's a winning hand. The good news is, there's a GNU poker library (no, really), which does that for us, at http://pokersource .sourceforge.net/; the bad news is, it's in C.</p>

<p>Well, not a problem. It's time to dive into some XS as a simple way of linking these things together. We'll start by creating the framework for our module with <i>h2xs</i>:</p>

<blockquote>
<pre>
% h2xs -A -n Games::Poker::HandEvaluator

</pre>
</blockquote>



<p>and now we need to make sure that <i>libpoker</i> gets linked in, by telling the Makefile.PL about the library:</p>

<blockquote>
<pre>
LIBS =&gt; '-lpoker'

</pre>
</blockquote>



<p>Assuming that the poker library is correctly installed, this will be enough to link the library into our Perl module. We run Makefile.PL and do a <i>make test</i> to ensure everything's OK. If the library isn't correctly installed, you might see something like this:</p>

<blockquote>
<pre>
Note (probably harmless): No library found for -lpoker

</pre>
</blockquote>



<p>The "probably harmless" is a complete lie.</p>

<p>Now we have to write a little bit of glue code to expose the parts of the library's functionality that we're interested in. The hand-evaluation function for an ordinary deck of cards in standard (nonlowball) poker is called <i>StdDeck_StdRules_EVAL_N</i>. However, it expects to see a bitmask of the deck, and we don't really want to construct that bitmask ourselves. Instead, we borrow a function from the examples that ship with <i>libpoker</i> for turning a string into a bitmask. This function goes above the <i>XS MODULE</i> line in HandEvaluator.xs, as we're not going to need to call it from Perl.</p>

<blockquote>
<pre>
int parse_cards(char *handstr, StdDeck_CardMask* cards) {
  char *p;
  int c = 0;
  int ncards = 0;
  char str[80];

  StdDeck_CardMask_RESET(*cards);
  strcpy(str, handstr);
  p = strtok(str, " ");

  do {
    if (DstringToCard(StdDeck, p, &amp;c) == 0)
      return 0;
    if (!StdDeck_CardMask_CARD_IS_SET(*cards, c)) {
      StdDeck_CardMask_SET(*cards, c);
      ++ncards;
    };
  } while ((p = strtok(NULL, " ")) != NULL);
  return ncards;
}

</pre>
</blockquote>

<p>Now we can wrap our evaluation function. This will take a string representing a hand of cards&#151;for instance, <i>Ah 7c Jd As 7h </i>for two pair aces and sevens&#151;and return an integer. The higher the integer, the better the hand:</p>

<blockquote>
<pre>
int
_evaluate( hand );
    char* hand;
PREINIT:
    StdDeck_CardMask cards;
    int ncards;
CODE:
    ncards = parse_cards(hand, &amp;cards);
    if (ncards) 
        RETVAL = StdDeck_StdRules_EVAL_N(cards, ncards);
    else
        RETVAL = 0;
OUTPUT:
    RETVAL

</pre>
</blockquote>

<p>Of course, we often want an explanation of that integer (such as "<i>Two Pair (A 7 J)</i>"). The <i>libpoker</i> function <i>StdRules_HandVal_toString</i> fills a buffer with such an explanation, so we'll wrap that in an XS function called <i>handval</i>:</p>

<blockquote>
<pre>
char*
handval( hval )
    int hval;
PREINIT:
  char buf[80];
  int n;
CODE:
  StdRules_HandVal_toString(hval, buf);
  RETVAL = buf; 
OUTPUT:
  RETVAL

</pre>
</blockquote>

<p>We'll now write a wrapper function that can handle both an ordinary string and a <i>Games::Card::CardSet</i> object, so the module will not need changes when we integrate it with the rest of our poker modules. <i>Games::Card::CardSet</i> (and hence its derived classes) has a <i>print</i> method that nearly does what we want, but not quite. It prints out the hand like so:</p>

<blockquote>
<pre>
Player 1:  2S   2C   3S   4D   6D   9C  10S

</pre>
</blockquote>



<p>whereas we want</p>

<blockquote>
<pre>
2S 2C 3S 4D 6D 9C TS

</pre>
</blockquote>



<p>(Notice that <i>libpoker</i> expects "<i>T</i>" for ten)</p>

<p>This is nothing that a few regular expressions can't fix:</p>

<blockquote>
<pre>
sub evaluate {
    my $hand = shift;
    if (UNIVERSAL::isa($hand, "Games::Cards::CardSet")) {
        $hand = $hand-&gt;print;
        $hand =~ s/.*://; $hand =~ s/\s+/ /g;
        $hand =~ s/10/T/g;
    }
    return 0 unless $hand;
    _evaluate($hand);
}

</pre>
</blockquote>

<p>The mistake everyone makes at least once is to say <i>$hand-&gt;isa</i>, which fails badly if <i>$hand</i> is just an ordinary string&#151;<i>UNIVERSAL::isa</i> is a good way of getting around the problem.</p>
<h3>For My Next Trick...</h3>

<p>And that's basically all we need to do for our hand-evaluation library. Now we'll turn our attention to the Online Poker Protocol, a mechanism designed by the folks at Alberta University, who have been doing a lot of work on computer poker.</p>

<p>Their protocol and servers allow computer players to pit their wits against humans. They play Texas Hold'em, a variant of poker where each player receives two cards of their own ("hold cards"), and then five community cards are dealt face down before all players ("the board"). The board is revealed in three stages, with a round of betting before each stage. The first stage is the "flop," where three of the board cards are revealed. Then comes a round of betting and the "turn," where another board card is turned over. After another round of betting, the final card is revealed on the "river." Then there is a final round of betting before all players' cards are turned over in the "showdown."</p>

<p>We'll begin by writing a simple client to allow us to play on the server, with a view to developing a computer player in the future.</p>

<p>The protocol is documented at the section on http://games.cs .ualberta.ca/webgames/poker/bots.html in the http: manpage, and is a binary TCP/IP communication protocol. Debugging binary protocols is not terribly easy and I needed some more information about how the protocol dealt with certain conditions, so I wrote a rather nice protocol analyzer, which I'm sure I'll tell you about next time.</p>

<p>We start by defining the protocol in terms Perl can understand. For instance, it makes sense to have the command names linked to their byte equivalent as constants, like so:</p>

<blockquote>
<pre>
use constant JOIN_GAME =&gt; 20;

</pre>
</blockquote>



<p>And we also maintain an array that specifies how the arguments are to be formatted. The protocol uses 4-byte integers and zero-terminated strings, so it seems reasonable to use the <i>pack</i> function to prepare data for transit. For instance, the <i>JOIN_GAME</i> command takes two strings (username and password) followed by an integer (protocol version) and another string (client identifier). The <i>pack</i> encoding of this would be <i>Z*Z*NZ*:</i></p>

<blockquote>
<pre>
$protocol[JOIN_GAME] = "Z*Z*NZ*";

</pre>
</blockquote>



<p>Now we can write a function that can be called with the right arguments:</p>

<blockquote>
<pre>
$self-&gt;send_packet(JOIN_GAME, 
                   "perlkibot", 
                   "sekrit", 
                   1,
                   "Games::Poker::OPP");

</pre>
</blockquote>

<p>which packs this into a packet as specified by the protocol and sends it down to the server. We start by making sure that the message number is a valid part of the protocol:</p>

<blockquote>
<pre>
sub send_packet {
    my ($self, $message_id, @data) = @_;
    croak sprintf "Protocol error: command 0x%x not recognised", 
        $message_id unless exists $protocol[$message_id];

</pre>
</blockquote>

<p>and now we can simply use <i>pack</i> to transform the arguments:</p>

<blockquote>
<pre>
if ($protocol[$message_id]) {
    eval { $packed_data = pack($protocol[$message_id], @data); };
    croak sprintf "Problem packing data for %d command", 
        $message_id if $@;
}

</pre>
</blockquote>

<p>Now we have the arguments packed into<i> $packed_data</i>. The protocol specifies that we first send the message number as a 4-byte integer, followed by the length of the arguments (that's <i>$packed_data</i>) as a 4-byte integer:</p>

<blockquote>
<pre>
my $packet = pack "NN", $message_id, length $packed_data;

</pre>
</blockquote>



<p>Finally, we add on the arguments and send the packet out:</p>

<blockquote>
<pre>

$packet .= $packed_data;
$self-&gt;put($packet);
return $packet;

</pre>
</blockquote>

<p>Notice that we use another method, <i>put</i>, to actually put the data "on the wire." This gives us abstraction about how we do this&#151;we might want to use <i>IO::Socket::INET</i>, or a <i>POE</i> wheel, or something else entirely. Similarly, we can write a helper function to retrieve a packet from the server, unpack the arguments in the same way, and return a message number and unpacked arguments.</p>

<p>After creating a standard constructor to hold the username, password, and server connection details, we can write an <i>IO::Socket::INET</i> implementation of the module like so:</p>

<blockquote>
<pre>
sub connect {
    my $self = shift;
    $self-&gt;{socket} = IO::Socket::INET-&gt;new(
        PeerHost =&gt; $self-&gt;{server},
        PeerPort =&gt; $self-&gt;{port},
    );
}
sub put { my ($self, $what) = @_; $self-&gt;{socket}-&gt;syswrite($what); }
sub get {
    my ($self, $len) = @_;
    my $buf = " "x$len;
    my $newlen = $self-&gt;{socket}-&gt;sysread($buf, $len);
    return substr($buf,0,$newlen);
}

</pre>
</blockquote>

<p>Now all the groundwork is finally done! Writing functions to actually speak the protocol is now a lot more straightforward:</p>

<blockquote>
<pre>
sub joingame {
    my $self = shift;
    $self-&gt;send_packet(JOIN_GAME, $self-&gt;{username}, $self-&gt;{password},
        1, "Games::Poker::OPP");
    my ($status) = $self-&gt;get_packet();
    if ($status == GOODPASS) { return 1; } 
    elsif ($status == BADPASS) { return 0; } 
    else {
        croak sprintf "Protocol error: got %i from server", $status;
    }
}

</pre>
</blockquote>

<p>Now we need to think about how to handle the game play itself. There are many ways to do this, but the way I decided upon was to provide a main loop function <i>playgame</i>, which calls user-defined callbacks to display status information and decide how to bet. To help the user writing these callbacks, we export some of the protocol constants, particularly those that represent actions that the player can make (e.g., <i>FOLD</i>, <i>CHECK</i>, <i>RAISE</i>, <i>CALL</i>).</p>

<p>I also decided to create an object to hold the game state. Since this is generic to Texas Hold'em and not specific to our protocol, I created a separate module called <i>Games::Poker::TexasHold'em</i>. (Yes, this is a gratuitous abuse of apostrophe-as-package-separator.) In the future, this module will be extended with functions to analyze hand potential, but for now, it just keeps track of the game.</p>

<p>I won't go into much detail about the <i>TexasHold'em</i> module, except to say that it can be used quite independently of the Online Poker Protocol module:</p>

<blockquote>
<pre>
use Games::Poker::TexasHold'em;
my $game = Games::Poker::TexasHold'em-&gt;new(
    players =&gt; [
        { name =&gt; "lathos", bankroll =&gt; 500 },
        { name =&gt; "MarcBeth", bankroll =&gt; 500 },
        { name =&gt; "Hectate", bankroll =&gt; 500 },
        { name =&gt; "RichardIII", bankroll =&gt; 500 },
    ],
    button =&gt; "Hectate",
    bet =&gt; 10,
    limit =&gt; 50
);

$game-&gt;blinds;
$game-&gt;check; $game-&gt;bet(10); $game-&gt;call; $game-&gt;fold;
$game-&gt;fold;
$game-&gt;next_stage;
$game-&gt;check; $game-&gt;bet(20); $game-&gt;raise(40); $game-&gt;call;
$game-&gt;next_stage;
print $game-&gt;status;

This will print out:

Pot: 80 Stage: turn
?                 Name Bankroll  InPot
.               lathos $  470 $   30
F             MarcBeth $  490 $   10
F              Hectate $  490 $   10
            RichardIII $  470 $   30

</pre>
</blockquote>

<p>Showing that all but <i>lathos</i> and <i>RichardIII</i> have folded, these two started madly throwing cash into the pot. (Which is, for those in the know, what usually happens in games...) We're currently in the turn, it's <i>lathos</i>' bet, and there's $80 in the pot.</p>

<p><i>playgame</i> sits in a loop, looking for messages from the server and determining how to respond to them:</p>

<blockquote>
<pre>
sub playgame {
    my $self = shift;
    $self-&gt;{game} = undef;

    while (my ($cmd, @data) = $self-&gt;get_packet()) {

</pre>
</blockquote>

<p>A <i>PING</i> packet (which doesn't often get sent) needs to be replied immediately with a <i>PONG</i>, and that's all we need to do with it:</p>

<blockquote>
<pre>
if ($cmd == PING) { $self-&gt;send_packet(PONG); next; }


</pre>
</blockquote>

<p>And a <i>GOODBYE</i> packet should end the loop:</p>

<blockquote>
<pre>

if ($cmd == GOODBYE) { last }

</pre>
</blockquote>

<p>Anything that is purely advisory, such as chatter from other players or information from the server, gets handed to a user-defined status routine:</p>

<blockquote>
<pre>
if ($cmd == CHATTER ||
    $cmd == INFORMATION) {
        $self-&gt;{status}-&gt;($self, $cmd, @data); next;
    }

</pre>
</blockquote>

<p><i>playgame</i> has a concept of the current game. If we join the room in the middle of an existing game, we may get sent messages that are not actually for our consumption. If we don't have anything in <i>$self-&gt;{game}</i>, then we ignore the message, unless, of course, it's the start of a new game:</p>

<blockquote>
<pre>
next unless $self-&gt;{game} or $cmd == START_NEW_GAME;

</pre>
</blockquote>

<p>Now we dispatch any command off to its appropriate handler, and also call the <i>status</i> routine so that the client gets the opportunity to display some message to the user:</p>

<blockquote>
<pre>
if (exists $handlers[$cmd]) {
    $handlers[$cmd]-&gt;($self, $cmd, @data);
}
$self-&gt;{status}-&gt;($self, $cmd, @data);

</pre>
</blockquote>

<p>These handlers are responsible for updating the <i>Games::Poker::TexasHold'em</i> object, and also for making the all-important callback to determine how the client should play!</p>

<p>But where do these handlers come from? Well, when we fill the <i>@protocol</i> array with the <i>pack</i> formats of the expected arguments, we also fill the <i>@handlers</i> array:</p>

<blockquote>
<pre>
map {
     $protocol[$_-&gt;[0]] = $_-&gt;[1];
     $handlers[$_-&gt;[0]] = $_-&gt;[2] if $_-&gt;[2];
} (
[ START_NEW_GAME , "N5(Z*NN)*", \&amp;new_game_handler ],
[ HOLE_CARDS , "NZ*", \&amp;hole_card_handler ],
[ NEW_STAGE , "NZ*", \&amp;next_stage_handler ],
[ NEXT_TO_ACT , "N4", \&amp;next_turn_handler ],
[ FOLD , "NN", \&amp;fold_handler ],
[ CALL , "NN", \&amp;call_handler ],
...

</pre>
</blockquote>

<p>This ensures that, for instance, when a player folds, the <i>fold-handler</i> routine is called:</p>

<blockquote>
<pre>
sub fold_handler { shift-&gt;{game}-&gt;fold() }

</pre>
</blockquote>



<p>And when it's someone's turn to play, the <i>next_turn_handler</i> gets called:</p>

<blockquote>
<pre>
sub next_turn_handler {
    my ($self, $cmd, $who, $to_call, $min_bet, $max_bet) = @_;
    my $game = $self-&gt;{game};

</pre>
</blockquote>

<p>It might be our turn to play&#151;<i>$who</i> is set to a seat number, and if that seat number matches ours, then we need to call the callback and do what it tells us to:</p>

<blockquote>
<pre>
# If it's me, make the callback
if ($who == $game-&gt;{seats}-&gt;{$self-&gt;{username}}) {
    my $action = $self-&gt;{callback}-&gt;($self, $to_call, $min_bet, $max_bet);
    return $self-&gt;send_packet(ACTION, $action);
}

</pre>
</blockquote>

<p>And there is a slight discrepancy between how <i>Games::Poker::TexasHold'em</i> works and how the game is played on the servers&#151;after a round of betting, the servers want to start the next round by making the player who was to the left of the dealer in the last round into the dealer for this round, whereas my module handles it by continuing betting from the player after the one who last bet. Of course, the servers are correct, and you might call this discrepancy a bug that I haven't fixed yet, but we use this opportunity to ensure that the status object and the server both agree on who's to play next:</p>

<blockquote>
<pre>
$game-&gt;{next} = $who;

</pre>
</blockquote>



<p>And that's basically all there is to the game play. Let's now turn to building a client with this module.</p>
<h3>Upping the Ante</h3>

<p>There's a simple, text-based poker client that ships with the module. The bulk of it is made up of the two callbacks that are sent to <i>Games::Poker::OPP</i>, the status callback and the main action callback.</p>

<p>This is a very simple callback&#151;it displays the status table, shows your hole cards and the cards on the board, and prompts for what you want to do:</p>

<blockquote>
<pre>
callback =&gt; sub {
    my $game = shift-&gt;state();
    print $game-&gt;status;
    print "Hole cards: ", $game-&gt;hole, "\n";
    print "Board cards: ", $game-&gt;board, "\n";
    print "[F]old, [C]all/check [B]et/[R]aise\n";
    print "Your turn: ";

</pre>
</blockquote>

<p>And then it reads your reaction, and sends it back:</p>

<blockquote>
<pre>
    my $action = &lt;STDIN&gt;;
    if ($action =~ /f/i) { $action = FOLD; }
    elsif ($action =~ /[br]/i) { $action = RAISE; }
    else { $action = CALL; }
    return $action;
 }

</pre>
</blockquote>

<p>(The <i>FOLD</i>, <i>RAISE,</i> and <i>CALL</i> constants are exported by the <i>Games::Poker::OPP</i> module.)</p>

<p>The status callback is equally simple. It gets a command:</p>

<blockquote>
<pre>
status =&gt; sub {
    my ($self, $cmd, @stuff) = @_;

</pre>
</blockquote>

<p>If that's informational, it just prints out the arguments:</p>

<blockquote>
<pre>
if ($cmd == CHATTER || $cmd == INFORMATION) {
    print @stuff, "\n";
    return;
}

</pre>
</blockquote>

<p>The server information messages tell you a good deal of what's happening in the game (such as <i>lathos has folded</i>), so we don't need to display any specific status information for these commands:</p>

<blockquote>
<pre>
return if $cmd == FOLD || $cmd == RAISE || $cmd == CALL
       || $cmd == BLIND;

</pre>
</blockquote>

<p>If we were writing a graphical client, though, these would be good opportunities to update the display.</p>

<p>And in all other circumstances, we just print out the game state:</p>

<blockquote>
<pre>
    my $game = $self-&gt;state;
    return unless $game;
    print "\n&#151;-\n";
    print $game-&gt;status;
    print "Hole cards: ", $game-&gt;hole, "\n";
    print "Board cards: ", $game-&gt;board, "\n";
    print "&#151;-\n";
}

</pre>
</blockquote>

<p>That's all it takes to create a poker-playing robot in Perl with <i>Games::Poker::OPP</i>&#151;now all that's left to do is add some analysis, a little artificial intelligence, test it out on the servers for a few weeks, and then...Las Vegas, here we come.</p>
<p><b></b></p>
<p><b></b></p>
<p><b>TPJ</b></p>




</body>
</html>