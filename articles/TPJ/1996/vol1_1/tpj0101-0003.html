<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">

<html>
<head>
  <title>Perl and the Tk Extension - The Perl Journal, Spring 1996</title><!-- end head -->
  <meta name="generator" content=
  "HTML Tidy for Linux/x86 (vers 12 April 2005), see www.w3.org">
  <meta name="vscategory" content="Perl">
  <meta name="vsisbn" content="">
  <meta name="vstitle" content="Perl and the Tk Extension">
  <meta name="vsauthor" content="Steve Lidie">
  <meta name="searchdescription" content=
  "The Tk extension for Perl is referred to as Perl/Tk, and runs under the X Window System found on most Unix computers. X uses a client/server model, where clients (such as the one you are about to see) communicate with a server that manages the computer's display, keyboard, and mouse. For every display there is a window manager that provides a consistent 'look and feel', at least at a high level, for all clients sharing the machine's display. There are many different window managers, but they all provide similar facilities, such as iconifying, moving, and resizing windows, and framing them in decorative borders. You'll see window manager commands in later columns.">
  <meta name="vsimprint" content="The Perl Journal">
  <meta name="vspublisher" content="Earthweb">
  <meta name="vspubdate" content="Spring 1996">
  <!-- always update the article title and issue -->

</head>

<body bgcolor="#FFFFFF">
  <font face="verdana" size="1">Issue 1, Spring 1996</font>

  <h2 align="center">Perl and the Tk Extension</h2>

  <h4><i>Steve Lidie</i></h4>
  <!-- packages described, if necessary -->

  <h3>Using X from Perl</h3>

  <p>Well, hello! This is the first of several articles about the
  Tk toolkit, a marvelous object-oriented Perl extension that
  provides a comprehensive widget collection for all kinds of
  spiffy graphical applications. Tk was developed by John K.
  Ousterhout and adapted and extended for Perl by Nick
  Ing-Simmons.</p>

  <p>The Tk extension for Perl is referred to as Perl/Tk, and runs
  under the X Window System found on most Unix computers. X uses a
  client/server model, where clients (such as the one you are about
  to see) communicate with a server that manages the computer's
  display, keyboard, and mouse. For every display there is a window
  manager that provides a consistent "look and feel", at least at a
  high level, for all clients sharing the machine's display. There
  are many different window managers, but they all provide similar
  facilities, such as iconifying, moving, and resizing windows, and
  framing them in decorative borders. You'll see window manager
  commands in later columns.</p>

  <p>This article contains a gentle introduction to the
  fundamentals of Perl/Tk, after which it develops, step by step, a
  real application. As is typical of my writing style I often
  generate more questions than I answer, so you'll want to keep
  this URL handy:</p>

  <p><a href="http://w4.lns.cornell.edu/~pvhp/ptk/ptkFAQ.html"
  target=
  "resource window">http://w4.lns.cornel.edu/~pvhp/ptk/ptkFAQ.html</a></p>

  <p>This is the location of the Perl/Tk FAQ, the repository of
  Almost Everything Ever Written About Perl/Tk, thoughtfully
  maintained by Peter Prymmer.</p>

  <p>The current version of Perl/Tk is Tk-b9.01 and builds
  successfully against Perl 5.002. Check out a <a href=
  "http://www.perl.com/CPAN/SITES.html" target=
  "resource window">CPAN</a> site close to you to obtain the latest
  Tk distribution, which at the moment is:</p>

  <p>CPAN/authors/id/NI-S/Tk-b9.01.tar.gz</p>

  <p>Perl/Tk programs are written using the object-oriented syntax
  <tt>$object-&gt;method</tt>, where <tt>$object</tt> refers to a
  Tk widget (such as a Button or Menu), and method names an action
  to be performed. We'll learn more about objects and such in the
  next column, but now, without further ado, here is your
  prototypical "Hello world" program written in Perl/Tk, swiped
  from the Tk distribution:</p>
  <pre>
#!/usr/bin/perl -w 
&#13;# A simple Tk script that creates a button that prints 
# "Hello, world". 
# Clicking on the button terminates the program. 
# 
# The first line imports the Tk objects into the application,
# the second line creates the main window, the third through
# fifth lines create the button and define the code to be 
# executed when the button is pressed, the sixth line asks the 
# packer to shrink-wrap the application's main window around the 
# button, and the seventh line starts the event loop. 
&#13;use Tk; 
$MW = MainWindow-&gt;new; 
$hello = $MW-&gt;Button(
    -text =&gt; 'Hello, world', 
    -command =&gt; sub {print STDOUT "Hello, world\n"; exit;} ); 
$hello-&gt;pack; 
MainLoop;  
</pre>

  <p>When the program is executed, this window appears:</p>

  <div align="center">
    <img src="images/lidie-fig1.gif" width="144" height="60"
    border="0" alt="Hello World Graphic">
  </div>

  <p>The program may or may not be self-explanatory! The main
  window, <tt>$MW</tt>, is the program's first toplevel window -
  the primary "container" for most, if not all, descendant widgets,
  which form a hierarchy (each widget always has a parent, and
  might have children as well).</p>

  <p>This particular toplevel widget has a single child object
  belonging to the Button class. All widgets are objects derived
  from some base class, inheriting its characteristics. You might
  have several instances of button objects that look quite
  different, but share the distinguishing characteristics of class
  Button: they display a text label or bitmap, and "do something"
  when pressed. When the button in the example is pressed, the
  anonymous subroutine is executed, which prints "Hello, world" and
  exits. The subroutine is called because it is bound to the button
  click. Almost all widget classes have default button and keypress
  bindings established by Perl/Tk, and you can add, delete or
  modify bindings on a class or per-widget basis as you see
  fit.</p>

  <p>The statement:</p>
  <pre>
$hello = $MW-&gt;Button( ... ); 
</pre>

  <p>is a widget creation command: an object of class Button is
  constructed and configured with the specified options, which
  becomes a descendant of widget <tt>$MW</tt>, the main window. The
  variable $hello is initialized with an object reference to the
  newly created button widget. In Perl, an object reference is just
  an ordinary reference that points to something that has been
  "blessed" (using the Perl <tt>bless()</tt> function) into a
  certain class. The "something" is typically a hash or a list, and
  the act of blessing an object ties it to that particular class.
  Perl/Tk widget objects are hashes, as shown in this debug
  run:</p>
  <pre>
Dandy:/home/bug/ perl -de 0 
&#13;Loading DB routines from $RCSfile: perl5db.pl,v 
$$Revision: 4.1 $$Date: 92/08/07 18:24:07 $ Emacs 
support available. 
&#13;Enter h for help. 
&#13;main::(-e:1): 0 
&#13;DB&lt;1&gt; use Tk 
DB&lt;2&gt; $ref = {} 
DB&lt;3&gt; $MW = MainWindow-&gt;new 
DB&lt;4&gt; $oref = $MW-&gt;Button 
DB&lt;5&gt; print $ref 
HASH(0x200f78c8) 
DB&lt;6&gt; print $oref 
Tk::Button=HASH(0x2021c780) 
</pre>

  <p>The variable <tt>$ref</tt> is a plain reference to an
  anonymous hash, whereas <tt>$oref</tt> is an object reference to
  a hash of class Tk::Button. But from now on, I'll refer to
  variables like <tt>$hello</tt> and <tt>$oref</tt> simply as
  objects or widgets. (If you're not familiar with the Perl
  debugger, the idiom perl <tt>-de 0</tt> starts an interactive
  instance of Perl where you can debug, or simply enter Perl
  commands - a great prototyping environment.)</p>

  <p>The statement:</p>
  <pre>
$hello-&gt;pack;
</pre>

  <p>is a <i>method invocation</i> command: the Tk geometry manager
  known as the packer is invoked to assign a size and position to
  the <tt>$hello</tt> object, and then to "map" it. A widget must
  be mapped (or realized) before it becomes visible on the display.
  By default widgets are always packed inside their parent, and if
  you don't specify otherwise, the packer aligns them in a column,
  from top to bottom.</p>

  <p>Perl/Tk programs are <i>event driven</i>, meaning that you
  don't write a "main loop" in the standard sense, but rather
  delegate that job to Tk. Instead, you write small code sections,
  referred to as <i>callbacks</i>, a fancy name for a subroutine,
  to process those events and which Tk invokes as required. There
  are many Tk events that need to be processed in a timely fashion:
  timers, file input and output, and motion and button events
  generated by your mouse. You activate the Tk event loop with a
  <tt>MainLoop()</tt> statement, which should be the last line
  called by your program.</p>

  <p>In summary, most Perl/Tk applications share these common
  features:</p>

  <ul>
    <li>A <i>use Tk</i> statement at the beginning of the program
    which imports the base Tk definitions.</li>

    <li>A primary <i>MainWindow</i> as the root of the widget
    hierarchy.</li>

    <li>A series of widget creation commands.</li>

    <li>Optional binding and callback creation and registration
    commands. (More about those soon.)</li>

    <li>A series of geometry commands to pack widgets in a pleasing
    and user friendly manner.</li>

    <li>A <tt>MainLoop()</tt> command to begin program execution.
    (Actually, there are times when you must control event
    processing yourself; we'll see an example of this in a later
    column.)</li>
  </ul>

  <p>Tk provides 15 standard widgets, see <a href=
  "tpj0101-0003a.html">Listing 1</a>; Perl/Tk provides additional
  <i>derived</i> widgets, as well as <i>composite</i> widgets like
  ColorEditor, Dial, FileSelect, LabEntry and Table. Composite
  widgets, also called <i>megawidgets</i>, are complex objects
  built from these standard widgets.</p>

  <p>The Perl/Tk application that I am going to develop is called
  <i>Plot Program</i>, or <i>plop</i> for short, featuring Button,
  Canvas, Dialog, Frame, Label, LabEntry, Menu, Menubutton,
  Scrollbar and Text widgets. <i>Plop</i> plots a list of
  mathematical functions of the form <tt>y = f($x)</tt>, where
  <tt>$x</tt> iterates from the graph's X-minimum to X-maximum.
  Each function is evaluated in turn for a particular value of
  <tt>$x;</tt> the <tt>y</tt> value is then computed and a point is
  painted on the canvas. (<i>Plop</i> emphasizes the canvas widget
  because I've noticed that new Tk users, after watching around
  2000 lines of canvas documentation roll by, tend to place
  "exploring the canvas widget" at the end of their to-do list! But
  I think you'll find that feature-rich does not mean difficult to
  use.)</p>

  <p>Remembering that a canvas widget can be thought of as an
  artist's canvas for freehand drawing of graphics and text, we'll
  treat it as a classical Cartesian coordinate system. A key
  difference is that the canvas origin, position (0,0), is defined
  to be the top <i>left</i> corner of the canvas window, and that
  canvas X coordinates increase when moving right (as you'd expect)
  and Y coordinates increase when moving down (as you wouldn't).
  Also, canvas coordinates can't have negative values. For these
  reasons, we'll use and equation to transform between canvas and
  Cartesian coordinates.</p>

  <p>Here's the very first version of the program:</p>
  <pre>
#!/usr/bin/perl -w 
use strict; 
use Tk; 
my($o, $s) = (250, 20);
my($pi, $x, $y) = (3.1415926, 0); 
my $mw = MainWindow-&gt;new; 
my $c = $mw-&gt;Canvas(-width =&gt; 500, -height =&gt; 500); 
$c-&gt;pack; 
$c-&gt;create('line', 50, 250, 450, 250); 
$c-&gt;create('text', 10, 250, -fill =&gt; 'blue', -text =&gt; 'X'); 
$c-&gt;create('line', 250, 50, 250, 450); 
$c-&gt;create('text', 250, 10, -fill =&gt; 'blue', -text =&gt; 'Y'); 
&#13;for ($x = -(3*$pi); $x &lt;= +(3*$pi); $x += 0.1) { 
    $y = sin($x); 
    $c-&gt;create('text', $x*$s+$o, $y*$s+$o, -fill =&gt; 'red',   -text =&gt; '.'); 
    $y = cos($x); 
    $c-&gt;create('text', $x*$s+$o, $y*$s+$o, -fill =&gt; 'green', -text =&gt; '.'); 
} 
MainLoop; 
</pre>

  <p>Granted, this is really ugly code, lacking in style, but it's
  a proof of concept. As you'll see, I'll whip this code into
  proper shape pronto! Before I explain it, <a href=
  "images/lidie-fig2.gif">Figure 2</a> displays what it looks like
  when executed:</p>

  <p align="center"><a href="images/lidie-fig2.gif"><img src=
  "images/lidie-fig2_small.gif" width="100" height="102" border="0"
  alt=""></a></p>

  <p align="center"><font size="-1"><b>Figure 2:</b> Plot
  Program</font></p>

  <p>Some global variables are initialized, the main window
  <tt>($mw)</tt> and a canvas widget <tt>($c)</tt> are created, and
  the canvas is realized. The next four statements create two
  canvas line items (for the graph axes) and two text items (for
  the axis labels). Other canvas item types are arcs, bitmaps,
  images, ovals, polygons, rectangles and windows.</p>

  <p>The statements:</p>
  <pre>
$c-&gt;create('line', 50, 250, 450, 250); 
$c-&gt;create('text', 10, 250, 
            -fill =&gt; 'blue', 
            -text =&gt; 'X'); 
</pre>

  <p>draw and annotate the X axis. The canvas method
  <tt>create()</tt> expects as its first argument the type of item
  to create: here, I'm creating one line item and one text item.
  Since the canvas is 500x500 pixels, I deliberately arranged for
  canvas coordinate position (250,250) to coincide with the
  Cartesian origin (0,0). I also wanted to have 50-pixel wide
  top/bottom and left/right margins. Given these constraints, the X
  axis line starts at (50,250) and extends horizontally to
  (450,250), with a blue letter "X" painted in the left margin at
  (10,250). Similarly, the Y axis is stroked vertically from top to
  bottom and labeled with a blue "Y". Now all that remains is to
  graph some functions.</p>

  <p>The <i>for</i> statement varies from -3p to +3p radians, and
  even old biology-types like myself know that sine and cosine
  return values in the range [-1,1]. Such tiny values aren't
  especially useful unless you're looking for a graph one pixel
  high, so a transform is required:</p>
  <pre>
$y = sin($x); 
$c-&gt;create('text', $x*$s+$o, $y*$s+$o, 
            -fill =&gt; 'red', 
            -text =&gt; '.'); 
</pre>

  <p>We want to scale our <tt>$y</tt> values, which is what the
  expression <tt>$y*$s+$o</tt> does: the Y value is enlarged 20
  times and translated to the canvas origin. Then a red dot is
  deposited on the canvas. (There's actually a bug is the transform
  equation. Can you spot it? Hint: try graphing the <tt>exp()</tt>
  function.)</p>

  <p>So much for the ugly <i>plop</i> prototype; with a lot of work
  I can turn this code into a first-rate Perl/Tk application. For
  starters I want to eliminate every single hardcoded value and use
  variables instead. Then I'll add these features:</p>

  <ul>
    <li>A menu across the top. Like all respectable applications,
    it'll have File and Help menubuttons.</li>

    <li>A title for the graph.</li>

    <li>Adjustable minimum and maximum X and Y values.</li>

    <li>An editable list of functions.</li>

    <li>The option to read in functions from a file. Heck, let's
    just do it: eval {require "<i>plop.pl</i>";}. Just store your
    private functions in the file <i>plop.pl</i> and they'll be
    available for plotting. For instance, <i>plop.pl</i> might
    contain these lines if you wanted to graph the hyperbolic
    arctangent:</li>
  </ul>
  <pre>
sub atanh { 
    return undef if ($ARG[0] &lt; -1 or $ARG[0] &gt; 1); 
    .5 * log((1 + $ARG[0])/(1-$ARG[0]));
} 
&#13;1; 
</pre>

  <p>On the next page you'll see a sample run of the new
  <i>plop</i>. The complete program is available at the TPJ web
  site.</p>

  <p>The main window is divided into three major regions: a top
  frame with menubuttons (containing the File and Help menus), the
  canvas in the middle (including the title and boundary values),
  and a bottom area containing a series of other widgets (including
  a scrollable text widget with the list of functions).</p>

  <p>The Perl code has been modularized and looks something like
  this:</p>
  <pre>
 
    my $MW = MainWindow-&gt;new; 
    initialize_dialogs; 
    initialize_menus; 
    initialize_canvas; 
    initialize_functions;
</pre>

  <p>Subroutine <tt>initialize_dialogs()</tt> creates dialog
  widgets that aren't part of the main window proper - they pop up
  at certain times, wait for the user to respond, and then go away.
  Typically they persist for the lifetime of the application: thus,
  they are created once during program initialization and are then
  hidden until it's time to "Show" them; Show is a dialog method
  that deiconifies the widget, waits for the user to select a
  dialog button, and then returns the label of the selected button
  to the program. Here is how <i>plop's</i> "About" dialog widget
  is created:</p>
  <pre>
$DIALOG_ABOUT = $MW-&gt;Dialog(
      -title =&gt; 'About', 
       -text =&gt; "plot_program $VERSION\n\n" .                  
                                ' 95/12/04', 
     -bitmap =&gt; 'info', 
    -buttons =&gt; ['Dismiss']
);
</pre>

  <p>Like all widget creation commands, <tt>$MW-&gt;Dialog()</tt>
  returns a reference to an object. The <i>buttons</i> attribute is
  a list of strings that specify the button labels. In this case,
  there's only one button, "Dismiss", which hides the dialog after
  you've read the really informative "About" message!</p>

  <p align="center"><a href="images/lidie-fig3.gif"><img src=
  "images/lidie-fig3_small.gif" width="100" height="107" border="0"
  alt=""></a></p>

  <p align="center"><font size="-1"><b>Figure 3:</b> Plot
  Continuous Functions of the Form y=f[$x]</font></p>

  <p>To create the <i>plop</i> menus, <tt>initialize_menus()</tt>
  reuses some old code that generates menubuttons from a data
  structure, mainly because I'm lazy and menus always take time to
  get just right. My next column goes into details on menus,
  cascades, and so on, but for now examine this code:</p>
  <pre>
$MBF = $MW-&gt;Frame(-relief =&gt; 'raised', 
             -borderwidth =&gt; 1); 
$MBF-&gt;pack(-fill =&gt; 'x'); 
&#13;make_menubutton($MBF, 'File', 0, 'left',
                [['Quit', \&amp;exit, 0]]); 
&#13;make_menubutton($MBF, 'Help', 0, 'right', 
                [ 
                 ['About', [$DIALOG_ABOUT =&gt; 'Show'], 0],
                 ['',       undef,                    0],
                 ['Usage', [$DIALOG_USAGE =&gt; 'Show'], 0]
                ]
               ); 
</pre>

  <p>The first statement creates the container frame to hold the
  menubuttons, with a relief of "raised" and a borderwidth of one.
  The <i>relief</i> attribute specifies the widget's 3D look, but
  you need a non-zero <i>borderwidth</i> to see it. Notice that the
  frame is packed with its fill attribute set to "x", which makes
  the packer geometry manager expand the frame in the X direction
  to fill all available space. Otherwise, the File and Help
  menubuttons would be mapped side-by-side and centered in the
  frame. Creating the menubuttons and their corresponding menu
  items entails calls to <tt>make_menubutton()</tt> with these 5
  parameters:</p>

  <ul>
    <li><b>The parent widget</b></li>

    <li><b>The menubutton label</b></li>

    <li><b>The shortcut character index</b>. All our menubuttons
    have a shortcut character index of 0. For example, the 0th
    (first) character of "File" is 'f', which means that users can
    type <i>Alt-f</i> to activate the File menu.</li>

    <li><b>The side of the menu frame</b> to pack the
    menubutton.</li>

    <li><b>A list of lists describing the menu items</b>. Each
    inner list has three components: a label, a callback that is
    executed when the menu item is invoked, and a shortcut
    underline character. Null labels are treated as separators -
    do-nothing menu items that appear as lines.</li>
  </ul>

  <p>Callbacks come in various flavors, and we'll see more of them
  in later columns. But in <i>plop's</i> case there are just two:
  an explicit reference to a subroutine (also called a code
  reference), and a reference to an array. An example of the first
  form is the Quit menu item, which calls <tt>exit()</tt>. The Help
  menu items use the second form, where the first array element is
  an object (widget reference) and the second is the name of the
  method to invoke. Thus, when the user selects "About", the about
  dialog widget appears. Note that widgets used in callbacks must
  exist before they are referred to - that's precisely why the
  dialog widgets were created first.</p>

  <p>Subroutine <tt>initialize_canvas()</tt> generates the middle
  area of <i>plop's</i> main window, but is slightly different than
  the first version because it has a title, embedded widgets with
  editable X and Y values, and axes moved to the borders of the
  area to reduce visual clutter.</p>
  <pre>
$CANV = $MW-&gt;Canvas( 
              -width =&gt; $MAX_PXL + $MARGIN * 2,
             -height =&gt; $MAX_PXL, 
             -relief =&gt; 'sunken');
$CANV-&gt;pack; 
$CANV-&gt;Tk::bind('&lt;Button-1&gt;' =&gt; 
                \&amp;display_coordinates);
</pre>

  <p>The above code creates the canvas but uses global "constants"
  rather than hardcoded values: <tt>$MAX_PXL</tt> is obviously the
  size of the canvas, in pixels. Here's our first callback, which
  binds the subroutine display_coordinates() to mouse button 1.</p>
  <pre>
$CANV-&gt;create('text', 325, 25, 
     -text =&gt; 'Plot Continuous Functions Of The Form y=f($x)',  
     -fill =&gt; 'blue');
</pre>

  <p>Nothing new there, eh? But wait, something new follows, the
  canvas item type called a window!</p>
  <pre>
# Create the X axis and label it. Then label the minimum
# and maximum X values, and draw tick marks to 
# indicate where they fall. The axis limits are LabEntry
# widgets embedded in Canvas windows. 
&#13;$CANV-&gt;create('line', 
        $MIN_PXL + $MARGIN, $MAX_PXL - $MARGIN,
        $MAX_PXL - $MARGIN, $MAX_PXL - $MARGIN); 
&#13;$CANV-&gt;create('window', 
        $MIN_PXL + $MARGIN, $MAX_PXL - $label_offset,
        -window =&gt; $MW-&gt;LabEntry( 
                   -textvariable =&gt; \$X_MIN,
                   -label =&gt; 'X Minimum'));
&#13;$CANV-&gt;create('line', 
               $MIN_PXL + $MARGIN, 
               $MAX_PXL - $MARGIN - $tick_length,
               $MIN_PXL + $MARGIN, 
               $MAX_PXL - $MARGIN + $tick_length); 
&#13;
$CANV-&gt;create('window', 
   $MAX_PXL - $MARGIN, $MAX_PXL - $label_offset, 
   -window =&gt; $MW-&gt;LabEntry(
                   -textvariable =&gt; \$X_MAX, 
                          -label =&gt; 'X Maximum')); 
&#13;$CANV-&gt;create('line', 
               $MAX_PXL - $MARGIN, 
               $MAX_PXL - $MARGIN - $tick_length,
               $MAX_PXL - $MARGIN, 
               $MAX_PXL - $MARGIN + $tick_length); 
</pre>

  <p>The first canvas line item is simply the horizontal X axis,
  and the two remaining lines are the tick marks at each end. The
  two window items are containers where other objects can be
  stuffed, in this case two composite LabEntry widgets, which, as
  you might guess, combine the features of label and entry widgets.
  Their textvariable attributes are references to scalars
  <tt>$X_MIN</tt> and <tt>$X_MAX;</tt> when the program changes the
  variable's value, it's reflected on the display, and when the
  user edits a LabEntry, the associated textvariable is updated.
  The Y axis is handled in a similar manner.</p>

  <p>Subroutine <tt>initialize_functions()</tt> creates
  <i>plop's</i> remaining widgets, which are, in top-to-bottom
  packing order, a spacer frame, a label providing rudimentary
  instructions, a text widget with an attached scrollbar, and
  finally another container frame to hold a button or so.</p>
  <pre>
$MW-&gt;Frame(-height =&gt; 20)-&gt;pack; 
$MW-&gt;Label(  -text =&gt; 'Enter your functions here',        
       -foreground =&gt; 'blue')-&gt;pack; 
&#13;# Create a Frame with a scrollable Text widget that
# displays the function list, and a Button to 
# initiate plot activities. 
&#13;my $functions_frame = $MW-&gt;Frame; 
$functions_frame-&gt;pack; 
$TEXT = $functions_frame-&gt;Text(-height =&gt; 6); 
$TEXT-&gt;pack; 
$functions_frame-&gt;AddScrollbars($TEXT); 
$functions_frame-&gt;configure(-scrollbars =&gt; 'e'); update_functions; 
&#13;my $buttons_frame = $MW-&gt;Frame; 
$buttons_frame-&gt;pack(-padx =&gt; 10, -pady =&gt; 5,
                   -expand =&gt; 1,  -fill =&gt; 'x'); 
my @pack_attributes=qw(-side left -fill x -expand 1); 
$buttons_frame-&gt;Button( -text =&gt; 'Plot', 
                     -command =&gt; \&amp;plot_functions
                      )-&gt;pack(@pack_attributes); 
</pre>

  <p>Ho hum, a 20 pixel high frame (so much for the ban on
  hardcoded constants!) to occupy space, and some instructional
  text inked in blue. (But did you know that anywhere you can give
  a dimension as an integer pixel value you can also append the
  characters <i>i, c, m</i> or <i>p</i>, to indicate inches,
  centimeters, millimeters or points?) Then there's the text widget
  with a scrollbar anchored "east", and lastly a large "Plot"
  button. Notice the convenient method <tt>AddScrollbars()</tt> for
  attaching scrollbars to the text widget. The text widget contains
  the function list, which is particularly appropriate since each
  line can be tagged and assigned a different color. The function
  values are then plotted in that color.</p>

  <p>The graphical interface is now complete, and when the user
  invokes the "Plot" button, the callback <tt>plot_functions()</tt>
  is executed. Before actually plotting the function list,
  <i>plop</i> tidies up the text window and ensures that each
  function is assigned its proper color. <i>Plop</i> provides for
  up to nine simultaneous functions before the colors cycle. Here's
  the code:</p>
  <pre>
$TEXT-&gt;delete('0.0', 'end'); 
my $i = 0; 
foreach (@FUNCTIONS) { 
    $TEXT-&gt;insert('end', "$::ARG\n", [$i]);     
        $TEXT-&gt;tagConfigure($i, 
     -foreground =&gt; $COLORS[$i % $NUM_COLORS],
           -font =&gt; 'fixed'); 
    $i++; 
} 
$TEXT-&gt;yview('end');
</pre>

  <p>First, everything is deleted, from line zero, character zero,
  to the end of the text widget. Then, each function from the
  <i>@FUNCTIONS</i> array is inserted and assigned a tag, which
  just happens to be its order in the text widget. A tag is simply
  an identifying string used for reference in other widget
  commands. In this case, the tagged text items are configured with
  their unique foreground color and assigned a fixed space
  font.</p>

  <p>Now that the text widget is in synch with the function list,
  let's plot some functions:</p>
  <pre>
$CANV-&gt;delete('plot'); 
$canv_x = $MIN_PXL + $MARGIN; # X minimum 
$DX = $X_MAX - $X_MIN;        # update delta X 
$DY = $Y_MAX - $Y_MIN;        # update delta Y 
&#13;ALL_X_VALUES: 
for ($x=$X_MIN; $x&lt;=$X_MAX; $x += ($X_MAX-$X_MIN)/$ALEN) { 
    ALL_FUNCTIONS: 
    foreach (0 .. $#FUNCTIONS) { 
        $y = eval $FUNCTIONS[$::ARG]; 
        $canv_y = (($Y_MAX - $y) / $DY) * $ALEN + $MARGIN;
        if ($canv_y &gt; $MIN_PXL + $MARGIN and 
               $canv_y &lt; $MAX_PXL + $MARGIN) {
              $CANV-&gt;create('text', $canv_x, $canv_y, 
              -fill =&gt; $COLORS[$::ARG % $NUM_COLORS], 
              -tags =&gt; ['plot'], -text =&gt; '.', )
        }    
    }                         # end of ALL_FUNCTIONS 
    $canv_x++;                # next X pixel 
}                             # end of ALL_X_VALUES 
</pre>

  <p>After all this we're back to where we started, except that the
  code has been made more general and the transform equation has
  been fixed. <tt>$X_MIN</tt> and <tt>$X_MAX</tt> are dynamically
  assigned because they're part of the LabEntry widgets, and the X
  increment is calculated dynamically based on those values and the
  axis length. Y points painted on the canvas are automatically
  assigned their proper colors. And each point is tagged with the
  string "plot", so all current graphs can be easily deleted the
  next time the "Plot" button is pushed; that's what the
  <tt>$CANV-&gt;delete</tt>('<i>plot</i>') is for.</p>

  <p>But there's one stone left unturned: the button binding
  established during canvas creation. Since we already know how to
  convert a Cartesian coordinate to a canvas coordinate, I thought
  it would be fun to do the opposite: click anywhere on the canvas
  to display the Cartesian coordinates. The following code
  demonstrates how to handle an X event structure, in this case a
  button press:</p>
  <pre>
sub display_coordinates { 
    my($canvas) = @::ARG; 
    my $e = $canvas-&gt;XEvent; 
    my($canv_x, $canv_y) = ($e-&gt;x, $e-&gt;y); 
    my($x, $y); 
    $x = $X_MIN + $DX * (($canv_x - $MARGIN) / $ALEN);
    $y = $Y_MAX - $DY * (($canv_y - $MARGIN) / $ALEN); 
    print "\nCanvas x = $canv_x, Canvas y = $canv_y.\n";
    print "Plot x = $x, Plot y = $y.\n";
} 
</pre>

  <p>When a binding callback is executed, the subroutine is
  implicitly passed a reference to its widget - here, the canvas.
  Using <tt>XEvent()</tt>, the variable <tt>$e</tt> is now assigned
  a reference to the event structure. Two of <tt>$e's</tt> methods,
  <tt>x()</tt> and <tt>y()</tt>, return the relative position of
  the mouse when button 1 was pressed. Once the coordinates are
  known, it's a simple matter of using the existing transform
  equation, solving for X and Y, and printing the results.</p>

  <p>That's it for this time. If you have questions about this
  article, or ideas for upcoming ones, feel free to let me know.
  Next time we'll look more into objects, build a composite widget,
  and examine menus in greater detail.</p>

  <p>__END__</p>
  <hr>

  <p><i>Steve Lidie is a Systems Programmer at Lehigh
  University.</i> <!-- end of article -->
   <!-- end of file --></p>
</body>
</html>
