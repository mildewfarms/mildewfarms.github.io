<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
<html>
<head>
  <title>Perl/Tk: Another Wild Widget Tour - The Perl Journal, Summer 1997</title>
  <meta name="generator" content=
  "HTML Tidy for Linux/x86 (vers 12 April 2005), see www.w3.org">
  <meta name="vscategory" content="Perl">
  <meta name="vsisbn" content="">
  <meta name="vstitle" content="Perl/Tk: Another Wild Widget Tour">
  <meta name="vsauthor" content="Steve Lidie">
  <meta name="searchdescription" content=
  "Besides the standard Tk widgets described in TPJ #1, the Perl/Tk distribution contains numerous lesser-known widgets, such as the Tix and user contributed widgets, which we'll look at now. This is just an overview; don't expect to see every option of every widget itemized. That's what online documentation is for. ( Most of these widgets have embedded pod documentation, so you can run the perldoc program to read it: perldoc Tk::FileDialog. If perldoc can't find the man page, you can find it yourself in your lib directory and then read the pod yourself or convert it to HTML with pod2html.)">
  <meta name="vsimprint" content="The Perl Journal">
  <meta name="vspublisher" content="Earthweb">
  <meta name="vspubdate" content="Summer 1997">
  <!-- always update the article title and issue -->

  <!-- end head -->
</head>

<body bgcolor="#FFFFFF">
  <font face="verdana" size="1">Issue 6, Summer 1997</font>

  <h2 align="center">Perl/Tk: Another Wild Widget Tour</h2>

  <h4><i>Steve Lidie</i></h4>
  <!-- packages described, if necessary -->
  <font size="+2">B</font>esides the standard Tk widgets described
  in <a href="../vol1_1/tpj0101-0003.html">TPJ #1</a>, the Perl/Tk
  distribution contains numerous lesser-known widgets, such as the
  Tix and user contributed widgets, which we'll look at now. This
  is just an overview; don't expect to see every option of every
  widget itemized. That's what online documentation is for. ( Most
  of these widgets have embedded pod documentation, so you can run
  the perldoc program to read it: <tt>perldoc Tk::FileDialog</tt>.
  If perldoc can't find the man page, you can find it yourself in
  your lib directory and then read the pod yourself or convert it
  to HTML with <i>pod2html</i>.)

  <p>To keep matters simple, this widget tour conforms to the
  Perl/Tk widget demonstration standard. <i>widget</i> is a program
  (bundled in the Perl/Tk distribution) that highlights all the
  standard Tk widgets and many features of the Tk toolkit. You can
  extend <i>widget's</i> demonstrations with your own if you follow
  a few simple rules.</p>

  <p>To run these demonstrations all you need to do is save the
  sample scripts in an empty directory, ensuring that they all end
  with a <tt>.pl</tt> suffix. Then simply change to that directory
  and execute the <i>widget</i> program with a single argument: the
  directory name containing the demonstrations. (If you're lazy,
  you can just set the environment variable <tt>WIDTRIB</tt> to
  that directory and run <i>widget</i> without a command line
  parameter.)</p>

  <h3>Font</h3>

  <p><img src="images/widget1.gif" width="338" height="113" alt=
  "Font"></p>

  <p>Our first stop on the tour is Font, which isn't really a
  widget at all. Rather, it's a Tk method you invoke to help
  configure real widgets. You supply Font with an approximate font
  specification, and it queries the X server and returns the name
  of the closest font available on your system.</p>

  <p>In X, a fully qualified font name consists of 14 attributes,
  of which these are most important:</p>

  <p>family: e.g. Times, Helvetica, or <tt>Courier</tt><br>
  weight: e.g. <b>bold</b> or normal<br>
  slant: i, r, o (<i>italic</i>, roman, <i>oblique</i>)<br>
  size: measured in pixels or points<br></p>

  <p>Different X servers have different fonts available, so don't
  specify attributes that aren't strictly necessary. (Actually, the
  only guaranteed font name is "fixed".) Here's a program that
  identifies which fonts are on your system.</p>
  <pre>
# Font - find X fonts.
use Tk::Font;
use vars qw/$top/;
sub Font { 
my($demo) = @ARG; 
	
my $demo_widget = $MW-&gt;WidgetDemo(   
-name =&gt; $demo,      
-text =&gt; 'Font - find X fonts.',     
-geometry_manager =&gt; 'grid'  
);
	
$top = $demo_widget-&gt;top;     # get grid master      
my $font = $top-&gt;Font(qw/foundry adobe family times point    
120/);  
$top-&gt;Label(-text =&gt; $font,-font =&gt; $font)-&gt;grid;   
$font=$top-&gt;Font('*-courier-medium-r-normal-*-*');   
$top-&gt;Label(-text =&gt; $font, -font =&gt;   
$font)-&gt;grid; 
}
</pre>

  <p>Note that all widget demonstrations have their own use
  statement to incorporate the widget; to use the Font widget, you
  need to use <tt>Tk::Font</tt>. Furthermore, every demonstration
  is a subroutine that uses the <i>WidgetDemo</i> widget to create
  its top level window and user interface. The top level window is
  divided into three regions. The upper region has the
  demonstration title, the bottom region contains the standard
  Dismiss and See Code buttons. The middle region is where your
  demonstration code is placed, and <i>WidgetDemo</i> lets you
  reference that code with its <tt>top()</tt> method. All standard
  widget demonstrations follow this format.</p>

  <p>The <tt>Font()</tt> subroutine shown above creates two labels,
  each with a different font, which are gridded and displayed.
  Notice that Font accepts its input either in standard X notation
  shown in the second <tt>$top-&gt;Font</tt> statement above, or in
  the more intuitive keyword <tt>=&gt;</tt> value form shown in the
  first statement.</p>

  <h3>Adjuster</h3>

  <p><img src="images/widget4.gif" width="200" height="201" alt=
  "Adjuster"></p>

  <p>An <i>Adjuster</i> widget is a tiny handle that you can grab
  with your mouse and use to interactively resize a packed widget.
  Here's how to create one:</p>
  <pre>
my(@pack) = qw/-fill both -expand 1/;
&#13;my $t1 = $top-&gt;Text(qw/-width 20
                           -height 5/)-&gt;packAdjust(@pack);   
	                                          
foreach (1 .. 10) {$t1-&gt;insert('end', "Text 1, line $ARG\n")}        
&#13;my $t2 = $top-&gt;Text(qw/-width 20 -height 5/)-&gt;pack(@pack);
&#13;foreach (1 .. 10) {$t2-&gt;insert('end', "Text 2, line $ARG\n")
}
</pre>

  <p>Here two text widgets are packed top to bottom, but the
  topmost one has an adjuster below it. Dragging the adjuster down
  with button 1 pressed enlarges the top text widget and shrinks
  the bottom one. If the adjuster is packed left or right then the
  widget's width is changed.</p>

  <h3>Balloon</h3>

  <p><img src="images/widget2.gif" width="200" height="128" alt=
  "Balloon"></p>

  <p>A Balloon object provides the framework to create and attach
  help balloons to various widgets. These balloons pop up when the
  mouse pauses over the widget for more than a specified amount of
  time. It's another one of those widgets that isn't really a
  widget - it's more a classical object with methods that let you
  define and manipulate other widgets.</p>

  <p>Balloon help can appear in a balloon that pops up over a
  widget, in a status area, or both. The following code makes
  balloon help appear in both places.</p>
  <pre>
my $s = $top-&gt;Label(qw/-width 20 -borderwidth 2
-relief sunken/)-&gt;grid;
	
my $t = $top-&gt;Text(qw/-width 20 -height 5
	
-background azure/)-&gt;grid;
	
my $balloon = $top-&gt;Balloon(-statusbar =&gt; $s);
	
$balloon-&gt;attach($s,
	
-balloonmsg =&gt; 'See Balloon help here',
	
-statusmsg  =&gt; 'This is a Label widget');
	
$balloon-&gt;attach($t,
	
-balloonmsg =&gt; 'Type Text Here',
	
-statusmsg  =&gt; 'This is a Text widget');
	
</pre>

  <p>The status bar <tt>$s</tt> is typically a label, but can be
  any widget that accepts <tt>-text</tt>, such as Button,
  CheckButton, Radiobutton, or Message. The balloon system is
  initialized with a call to the <tt>Balloon()</tt> constructor,
  where the status bar is registered. The <tt>attach()</tt> method
  associates widgets with their corresponding help messages. Then,
  when the cursor pauses over a registered widget for more than 100
  milliseconds (this delay can be changed), the status bar message
  is updated and a help balloon pops up over the widget, like the
  "Type Text Here" window below.</p>

  <h3>BrowseEntry</h3>

  <p><img src="images/widget3.gif" width="200" height="201" alt=
  "BrowseEntry"></p>

  <p>A BrowseEntry widget is an enhanced version of the LabEntry
  (labelled entry) widget. It provides a button to pop up a list of
  all possible values that the Entry might take. BrowseEntry
  supports all LabEntry options except <tt>-textvariable</tt>,
  which is replaced by <tt>-variable</tt>.</p>
  <pre>
my $var;
my $be = $top-&gt;BrowseEntry(-label =&gt; "Label",
-variable =&gt; \$var)-&gt;grid;
	
foreach my $opt (1 .. 15) {
	
$be-&gt;insert('end', "opt$opt");
	
	}
</pre>

  <p>We see that BrowseEntry has an <tt>insert()</tt> method that
  lets you populate it with attributes, just like the ListBox and
  Text widgets. Indeed, the popup widget looks just like a Listbox,
  and after making a selection the value is stored in the
  BrowseEntry variable <tt>$var</tt>.</p>

  <h3>DialogBox</h3>

  <div align="center">
    <img src="images/widget6.gif" width="200" height="279" alt=
    "DialogBox with populated frame">
  </div>

  <p>The DialogBox widget is a spiffier version of the Dialog
  widget. It defines two vertical frames, placing all the dialog
  buttons in the lower frame and letting you pack a single Tk
  widget in the upper frame.</p>
  <pre>
my $d = $top-&gt;DialogBox(qw/-title DialogBox
-buttons/ =&gt; [qw/OK Cancel/]);
	
my $c = $d-&gt;add(qw/Canvas -width 160
	
-height 40/)-&gt;grid;
	
$c-&gt;createPolygon(qw/10 10 20 5 10 30 15 30
	
20 20 10 15 10 10 -fill red/);
	
$c-&gt;createText(qw/100 20
	
-text/ =&gt; 'A strange polygon');
	
$top-&gt;Button(qw/-text DialogBox -command/ =&gt;
	
sub{ print "You selected button ", $d-&gt;Show, ".\n";
	
	})-&gt;grid;
	
</pre>

  <p>The window produced by this code is shown below:</p>

  <p><img src="images/widget5.gif" width="184" height="114" alt=
  "A strange Polygon"></p>

  <p>DialogBox has an <tt>add()</tt> method that populates the top
  frame, and a <tt>Show()</tt> method that pops up the dialog and
  waits for user input. Here the DialogBox contains a Canvas, which
  itself contains a strange red polygon. The last statement creates
  a Button that pops up the DialogBox and prints the name of the
  selected button.</p>

  <p>The first argument to <tt>add()</tt> is a string - <i>not</i>
  a widget reference - naming the type of widget to place in the
  top portion of the DialogBox. This means that you can't create
  your own frame, populate it as you'd like, and use that in the
  DialogBox. What you do instead is let <tt>add()</tt> create the
  empty frame, and fetch a reference to the frame using
  <tt>Subwidget()</tt>:</p>
  <pre>
&#13;my $frame = $d-&gt;Subwidget('frame');
&#13;
</pre>

  <p>Notice that DialogBox advertises the widget as a lowercased
  name. Once you have the frame's handle you can fill it as
  usual:</p>
  <pre>
$frame-&gt;Message(-text =&gt;
'This DialogBox has a populated frame.')-&gt;grid;
	
$frame-&gt;Label(-image =&gt; $d-&gt;Photo(-file =&gt;
Tk-&gt;findINC('demos/images/teapot.ppm')))-&gt;grid;
	
</pre>

  <h3>FileDialog and FileSelect</h3>

  <p>FileDialog and FileSelect are widgets that let you select a
  file, or possibly a directory, from a dialog window. Although
  each has an extensive set of options, far too numerous to detail
  here, both operate in essentially the same way: they present a
  list of files from which you can choose, and a list of
  directories for you to navigate (and possibly choose).</p>
  <pre>
my $fselect = $top-&gt;FileSelect(-directory =&gt; $ENV{HOME});
$top-&gt;Button(qw/-text FileSelect -command/ =&gt;
	sub {
	
	my $fname = $fselect-&gt;Show;
	
	print "You selected $fname.\n" if defined $fname;
	
	print "You canceled.\n" unless defined $fname;
	
	})-&gt;grid;
	
</pre>

  <p>To create a basic FileSelect widget, just pass
  <tt>-directory</tt> to the constructor and, as usual, use
  <tt>Show()</tt> to pop up the dialog for user interaction.
  Similarly, create a FileDialog like this:</p>
  <pre>
my $fdialog = $top-&gt;FileDialog('Path' =&gt; $ENV{HOME});
</pre>

  <p>FileDialog also has a <tt>Show(</tt>) method. Of the two
  widgets, FileDialog is more customizable. Here's what the
  FileSelect widget looks like:</p>

  <p><img src="images/widget7.gif" width="210" height="250" alt=
  "FileSelect"></p>

  <p>Here's what the FileDialog widget looks like:</p>

  <p><img src="images/widget8.gif" width="286" height="266" alt=
  "FileDialog"></p>

  <h3>HList</h3>

  <p>The most complicated widget we'll look at is HList, the
  hierarchical list widget. It's used to display any data with a
  hierarchical structure, like a family tree or Unix file system.
  Since I don't have my family's genealogy handy, let's display a
  simple Unix directory tree.</p>

  <p>Each HList list element is uniquely identified by a string
  called the <i>entry path</i>, consisting of one or more names
  delimited by a separator character (like the <tt>/</tt> in a Unix
  pathname). List entries are normally connected by lines, but you
  can disable them. List entries can be annotated with simple text,
  a text/image combination, or a Perl/Tk widget.</p>
  <pre>
my $h = $top-&gt;Scrolled(qw\HList -separator /
-itemtype imagetext\)-&gt;grid; $h-&gt;{file_image} =
	
$top-&gt;Bitmap(-file =&gt;
	       
Tk-&gt;findINC('file.xbm'));
	       
$h-&gt;{fold_image} = $top-&gt;Bitmap(-file =&gt;
	       
Tk-&gt;findINC('folder.xbm'));
	       
$h-&gt;configure(-command =&gt; sub {
print "Selected $ARG[0], size=",
	
$h-&gt;info('data', $ARG[0]), ".\n"; 
});
my $text = Tk-&gt;findINC('demos');
chdir $text; 
show_dir '.', $text, $h;
</pre>

  <p>The demonstration starts by creating a scrolled HList instance
  with <tt>/</tt> as the entry path separator - convenient since
  the Unix pathname can then double as the HList entry path. Each
  list entry is adorned with a file or directory name and a
  file-to-folder bitmap - that's what <tt>-itemtype</tt> does.
  (Other legal <tt>-itemtype</tt> values are text and window.) The
  bitmaps are needed at various places in the code, so to avoid
  declaring them as global variables they're stored as instance
  variables in the HList widget. (There's a danger here of a hash
  key clash here [<i>see page 8, top right.</i> -ed.], but in a
  later issue of TPJ we'll see a relatively safe way to store
  private instance data.) In the <tt>configure()</tt> method, the
  <tt>-command</tt> callback is bound to double-button-1 and is
  invoked with a list entry's entry path as the first argument.
  More on that later.</p>

  <p>Displaying the file hierarchy is a simple exercise in
  recursion.</p>
  <pre>
sub show_dir {
my($entry_path, $text, $h) = @ARG;
opendir H, $entry_path;
my(@dirent) = grep ! /^\.\.?$/, sort(readdir H);
closedir H;
my($file_img, $fold_img) = ($h-&gt;{file_image},
	
	$h-&gt;{fold_image});
	
	$h-&gt;add($entry_path, -text =&gt; $text,
	
	-image =&gt; $fold_img, -data =&gt; 'DIR');
	
	 while ($ARG = shift @dirent) {
	 
	 my $file = "$entry_path/$ARG";
	 
	 if (-d $file) {
	 
	 show_dir $file, $ARG, $h;
	 
	 } else { 
	 
	 my $size = -s $file;
	 
	 $h-&gt;add($file, -text =&gt; $ARG,
	 
	 -image =&gt; $file_img, -data =&gt; $size);
	 
	 }      
    }
	
} # end show_dir
</pre>

  <p><tt>show_dir()</tt> initializes <tt>@dirent</tt> with the
  contents of the current directory and adds the current folder to
  the HList hierarchy. Note that <tt>add()</tt> expects an entry
  path, and since <tt>-itemtype</tt> is "imagetext", it expects
  values for both <tt>-text</tt> and <tt>-image</tt> options. The
  <tt>-data</tt> option associates a string with a list entry, in
  this case 'DIR'.</p>

  <p>The <i>while</i> loop simply iterates through
  <tt>@dirent</tt>, recursively calling <tt>show_dir()</tt> on
  directories. New files are added to the hierarchy, and their
  sizes are stored in the <tt>-data</tt> strings.</p>

  <p>Now the callback shown earlier should make sense. A double
  click invokes <tt>info()</tt> to retrieve and print an item's
  <tt>-data</tt> attribute - either a file size or, for
  directories, the word 'DIR'.</p>

  <p><img src="images/widget9.gif" width="200" height="219" alt=
  "Hlist"></p>

  <h3>More Menus</h3>

  <p>The following demonstration illustrates pulldown, popup and
  option menus. But there's something new here - menus created via
  a supported data structure, something I've been hinting at since
  <a href="../vol1_1/tpj0101-0003.html">TPJ #1</a>! All the magic
  lies in the <tt>-menuitem</tt> construct. Here's how it works</p>

  <p><b>Pulldown menus.</b> An application's <i>menu bar</i> is
  typically a frame laid out across the top of the primary window,
  with one or more <i>menu buttons</i> that, when selected, display
  a window of <i>menu items</i> such as buttons and cascades. The
  <tt>-menuitems</tt> structure lets you tailor the appearance of a
  menu button's window by describing individual menu items in a
  concise manner.</p>

  <p><tt>-menuitems</tt> is a list of lists, but don't let that
  scare you; the outer list is just there so that
  <tt>-menuitems</tt> can be passed as a scalar. It's the inner
  lists that you care about.</p>
  <pre>
my $menubar = $top-&gt;Frame(qw/-bd 5 -relief ridge
-bg orange/)-&gt;grid;
my $aircraft = 'B-52'; 
my $file = $menubar-&gt;Menubutton(qw/-text File
	               
	-relief raised -menuitems/ =&gt;
	[                      
    [Button =&gt; '~Open', -command =&gt; [\&amp;puts, 'Open']],
	
	[Separator =&gt; ''], 
	
	[qw/Cascade ~Aircraft -menuitems/ =&gt;                         
    [ 
	  
	[qw/Radiobutton B-52  -variable/ =&gt; \$aircraft],
	       
	[qw/Radiobutton B-2   -variable/ =&gt; \$aircraft],
	       
	[qw/Radiobutton F-117 -variable/ =&gt; \$aircraft],
	       
	[qw/Radiobutton F-22  -variable/ =&gt; \$aircraft],             
    ]
], 
	[Separator =&gt; ''],
	               
	[Button =&gt; '~Popup Menu'],
	               
	[Separator =&gt; ''],
	               
	[Button =&gt; '~Quit', -command =&gt;[\&amp;puts,
	               
	'Quit'] ],              
  ]              
); 
$file-&gt;grid(qw/-row 0 -column 0/);
</pre>

  <p>After gridding the demonstration's menu bar, a single (long!)
  statement creates a cascade, three buttons, and three separator
  menu items. ( To learn more about menus and friends, see <a href=
  "../vol1_2/tpj0102-0003.html">TPJ #2</a>.) For each inner list,
  the first element specifies the type of menu item and the second
  specifies its text label. The tilde character is a shortcut for
  <tt>-underline</tt> and precedes the character to be underlined.
  Remaining values are standard widget options. Of special note is
  the Aircraft cascade - it uses the -menuitems structure too.</p>

  <p><img src="images/widget10.gif" width="200" height="148" alt=
  "menu bar"></p>

  <p><b>Popup menus.</b> Notice that the File menu button has a
  menu item labelled "Popup Menu". Amazingly enough, pressing it
  makes popup menu appear.</p>
  <pre>
my $p = $menubar-&gt;Menu(qw/-tearoff 0 -menuitems/ =&gt;
[
	[qw/Button Hi -command/ =&gt; [\&amp;puts, 'Hi']],
	
	[qw/Checkbutton frog -onvalue 1 -offvalue 0/],
	
]
	
	); 
$file-&gt;entryconfigure('Popup Menu',
	       -command =&gt; [$p =&gt; 'Popup']);
	       
</pre>

  <p>Here we create the popup menu and again use -menuitems to
  populate the menu's window. The File menu item labelled "Popup
  Menu" is then configured to send a <tt>Popup()</tt> message to
  the menu <tt>$p</tt> on a button release event.</p>

  <p><b>Option menus.</b> The Optionmenu isn't a true menu. It's
  more similar to a BrowseEntry. An Optionmenu presents a list of
  values; choosing one changes the Optionmenu text and optionally
  sets a global variable and/or invokes a callback.</p>
  <pre>
my $o = $menubar-&gt;Optionmenu(-textvariable =&gt; \$aircraft,
	-options =&gt; [qw/B-52 B-2 F-117 F-22/],
	
	-command =&gt; sub {print "$aircraft!\n"},
	
	       -relief =&gt; 'raised');
</pre>

  <h3>NoteBook</h3>

  <p>A NoteBook widget displays several pages in a limited space.
  NoteBook pages are stacked so that only the top page is visible.
  To raise a page to the top of the stack, the user clicks on its
  tab.</p>

  <p><img src="images/widget11.gif" width="300" height="141" alt=
  "NoteBook"></p>

  <p>The NoteBook widget derives from the Frame widget, so it
  possesses all of Frame's attributes. It extends Frame by adding a
  visual notebook tab and methods to manipulate text and images on
  the tab.</p>
  <pre>
my $name = "Rajappa Iyer";
my $email = "rsi\@somewhere.com";
my $os = "Linux";
# Create the NoteBook and add 2 pages, which are really
# Frames with tabs. Given that, we create LabEntry
# widgets and pack them as usual.
my $n = $top-&gt;NoteBook(qw/-ipadx 6 -ipady 6/)-&gt;pack;
my $address = $n-&gt;add(qw/address -label Address
                                 -underline 0/);
my $pref = $n-&gt;add(qw/pref -label Preferences
                                 -underline 0/);
	
   $address-&gt;LabEntry(
              -label =&gt; "Name: ", -width =&gt; 20,
	  -labelPack =&gt; [qw/-side left -anchor w/],
       -textvariable =&gt; \$name
	             )-&gt;pack(qw/-anchor nw/);
	       
   $address-&gt;LabEntry(
              -label =&gt; "Email Address:",
	      -width =&gt; 50,
	  -labelPack =&gt; [qw/-side left -anchor w/],
       -textvariable =&gt; \$email
	             )-&gt;pack(qw/-anchor nw/);
	       
$pref-&gt;LabEntry(-label =&gt; "Operating System:",
                 width =&gt; 15,
	    -labelPack =&gt; [qw/-side left/],
         -textvariable =&gt; \$os
	       )-&gt;pack(qw/-anchor nw/);
	       
$top-&gt;Button(qw/-text Update -command/ =&gt;
    sub {
	 print "name = $name, email = $email, os = $os\n";
	}
	        )-&gt;pack;
</pre>

  <p>This demonstration is taken directly from the Perl/Tk
  distribution and starts by generating a NoteBook widget with two
  pages: <tt>$address</tt> and <tt>$pref</tt>. The Address page was
  created first, which is why it's on top of Preferences. Since the
  pages are derived Frame widgets, you can pack them however you'd
  like. I've packed them with some labelled entry widgets just to
  keep things simple.</p>

  <h3>Miscellaneous Widgets</h3>

  <p>The TPJ programs web page provides several widget
  demonstrations I haven't talked about: Axis, Dial, Pod and
  WaitBox. Axis is a derived canvas widget with X and Y axes so you
  can plot simple 2D data. Dial is a scale widget disguised as a
  speedometer. Pod lets you render embedded pod text and display it
  in a window. (I saw this done in <i>ptkmines</i>, a Minesweeper
  game you'll find at
  http://hadron4.phys.rpi.edu/~smithj4/software/ptkmines, and
  thought it was pretty cool.) WaitBox is a modal dialog widget
  with an optional progress bar.</p>

  <p>Finally, I'd like to mention two other derived widgets that
  can make your GUI development easier: ROText and TextUndo. Both
  are text widgets; ROText lets you display read-only text,
  eliminating all bindings that would let users alter the text.
  TextUndo, on the other hand, lets the user edit the text with an
  unlimited undo capability.</p>

  <p>_ _END_ _</p>
  <hr>
  <i>Steve Lidie is a Systems Programmer at Lehigh University.</i> 
  <!-- end of article -->
   <!-- end of file -->
</body>
</html>
