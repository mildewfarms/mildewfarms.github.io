<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
<html>
<head>
  <title>Tktk: a Perl/Tk Solitaire Game - The Perl Journal, Summer 2000</title>
  <meta name="generator" content=
  "HTML Tidy for Linux/x86 (vers 12 April 2005), see www.w3.org">
  <meta name="vscategory" content="Perl">
  <meta name="vsisbn" content="">
  <meta name="vstitle" content="Tktk: a Perl/Tk Solitaire Game">
  <meta name="vsauthor" content="Greg Bacon">
  <meta name="searchdescription" content="T">
  <meta name="vsimprint" content="The Perl Journal">
  <meta name="vspublisher" content="Earthweb">
  <meta name="vspubdate" content="Spring 2000">
  <!-- always update the article title and issue -->

  <!-- link to the previous and next documents relative to the current document -->
</head>

<body>
  <font face="verdana" size="1">Issue 18, Summer 2000</font>

  <h2 align="center">Tktk: a Perl/Tk Solitaire Game</h2>

  <h4 align="left"><i>Greg Bacon</i></h4>
  <!-- packages described, if necessary -->

  <div align="center">
    <table width="475" border="1" cellspacing="0" cellpadding="5">
      <tr>
        <td bgcolor="#CCCC99" align="middle" colspan="2">
        <b>URLs:</b></td>
      </tr>

      <tr>
        <td>Tk
        ...................................................................................................CPAN<br>

        Pyramid rules ....
        http://www.semicolon.com/solitaire/rules/pyramidrules.html<br>

        Card images .................................
        http://www.waste.org/~oxymoron/cards/</td>
      </tr>
    </table>
  </div>

  <p>One evening, I saw my wife playing a solitaire game that I'd
  never seen before, and whose name I still don't know. I asked her
  to explain the rules. The game turns out to be a simpler
  variation of Pyramid, which is described at <a href=
  "http://www.semicolon.com/solitaire/rules/pyramidrules.html"
  target=
  "resource window">http://www.semicolon.com/solitaire/rules/pyramidrules.html</a>.</p>

  <h3>The Rules</h3>

  <p>To play, you lay out the cards in a seven row pyramid. The
  first row contains one card, the second two, and so on. When
  laying subsequent rows, you should cover each card in the
  previous row with two cards (imagine staggered joints in
  brickmasonry), as shown in Figure 1.</p>

  <p><font face="verdana" size="-2"><b>Figure 1:</b> The start of
  the game.</font><br>
  <img src="images/tpj0502-0003-01.gif" alt="The start of the game"
  width="271" height="287"></p>

  <p>If you want to impress your solitaire aficionado buddies, call
  these 28 cards the <i>tableau</i>. The rest of the cards are the
  <i>stock</i>. To play, place the stock's bottom card face up on
  top of the stock. This top card is called the <i>base</i>. Your
  goal is to pick up all the cards in the tableau. You may pick up
  any uncovered card whose denomination or rank is adjacent to the
  denomination of the base card, regardless of suit. For instance,
  if your base card is a six, you can pick up a five or seven. Aces
  are considered adjacent to both deuces and kings.</p>

  <p>When you pick up a card from the tableau, it becomes the new
  base card. When you decide that you want a new base card (usually
  when you can't pick up any more from the tableau), you flip from
  the bottom of the stock. The game is over when you empty the
  tableau (you win) or when you exhaust the stock (you lose).</p>

  <p>It turned out to be a fun game. A solitaire game has to be
  tantalizing to hold the player's interest, and this one
  definitely is. My wife would laugh cruelly when I bemoaned being
  so close to winning (she says she's only won about three times in
  all the years that she's played). I realized that she had
  infected me with an awful meme, so now I'll spread it to you.</p>

  <h3>A First Cut</h3>

  <p>The more I played, the more I became impatient with tedious
  tasks like shuffling the deck and laying out the cards. I kept
  thinking, "I should implement this game in Perl", and eventually
  I did. I had a lot of fun in the process, and this article will
  describe the steps involved.</p>

  <p>The first decision was whether the interface should be
  graphical or character-based. I chose graphical. (Sorry, I'm a
  wuss - I even play the tiled X11 version of <i>nethack</i>.) The
  next step was to find some card images (because I didn't want to
  wake up the Gimp), and a web search found <a href=
  "http://www.waste.org/~oxymoron/cards/" target=
  "resource window">http://www.waste.org/~oxymoron/cards/</a>. The
  images have sensible names: <tt>qs.gif</tt> is the queen of
  spades, <tt>th.gif</tt> is the ten of hearts, and so on.</p>

  <h3>Shuffling</h3>

  <p>Before we can play, we have to shuffle the deck. The
  Fisher-Yates algorithm to the rescue:</p>
  <pre>
    # Fisher-Yates shuffle
    sub shuffle {
        use integer;

        my $array = shift;
        my $i;

        for ($i = @$array; -$i; ) {
            my $j = int rand ($i+1);
            @$array[$i,$j] = @$array[$j,$i];
        }
    }
</pre>

  <p>Of course, we need a deck to shuffle. I decided to build a
  hash whose keys are the card denominations and suits (the
  basenames of the card images) and whose values are Tk::Photo
  objects created from the card images:</p>
  <pre>
    my %card;
    sub init_cards {
        my $c = shift;

        my @denom = ( 2 .. 9, qw/ t j q k a / );
        my @suits = qw/ h d c s /;

        my @dim = ( -width =&gt; CARDWIDTH, -height =&gt; CARDHEIGHT );

        foreach my $d (@denom) {
            foreach my $s (@suits) {
                my $im = $c-&gt;Photo(-file =&gt; "cards/$d$s.gif", @dim);
                $card{"$d$s"} = $im;
            }
        }
    }
</pre>

  <h3>Layout</h3>

  <p>Now that we have a shuffled deck, we need to lay out the
  cards. I considered using Gtk, but I have more experience with Tk
  and wanted to play my game as soon as possible.</p>

  <p>Using Tk, we create a Tk::Canvas object and place Tk::Photo
  image items on it. The layout is pretty simple. The tableau's
  vertical axis of symmetry coincides with the vertical center line
  of the canvas. For each row, we compute half the width of the
  whole row, move left half that distance, and place cards from
  left to right. The code looks like this (note that I called the
  tableau the "field"):</p>
  <pre>
    my %pos;
    sub init_field {
        my $c    = shift;
        my $deck = shift;

        my $width = $c-&gt;cget('width');

        my $y = 7;
        my $x;

        for (1 .. 7) {
            # The 3 + results in a better looking layout
            $x = 3 + $width/2 - int($_/2) * 
                                (CARDWIDTH+CARDSPACE);

            if ($_ &amp; 1) {  # Odd-numbered row?
                $x -= CARDWIDTH/2;
            }

            # Place $_ cards
            my @row = splice @$deck, 0, $_;
            for (@row) {
                $c-&gt;create(
                    'image',
                    int $x, int $y,
                    -anchor =&gt; 'nw', 
                    -image  =&gt; $card{$_},
                    -tags   =&gt; [ 'field', "card=$_" ],
                );

                $x += CARDWIDTH + CARDSPACE;
            }

            $y += CARDHEIGHT/2;
        }
    }
</pre>

  <p>Notice that we didn't have to bother with <i>clipping</i>, the
  process of deciding which pixels should be displayed. We simply
  placed the rows from back to front (that is, from the top of the
  pyramid to the bottom), letting new items cover anything that
  might be under them. This is called the <i>painter's
  algorithm</i> and, conveniently, achieves the arrangement we
  want.</p>

  <h3>Show the Tableau!</h3>

  <p>The heart of the program ties it all together:</p>
  <pre>
    my $mw = MainWindow-&gt;new;
    my $c  = $mw-&gt;Canvas(
        -width  =&gt; 7 * CARDWIDTH  + 22,
        -height =&gt; 5 * CARDHEIGHT + 18,
    );

    init_cards $c;
    my @deck = keys %card;
    shuffle \@deck;
    init_field $c, \@deck;

    $c-&gt;pack;

    MainLoop;
</pre>

  <p>This program is enough to produce a layout similar to Figure
  1.</p>

  <h3>Would You Like To Play A Game?</h3>

  <p>I once had a math professor who obsessively moved radicals out
  of the denominators of fractions because "our teachers like it
  better this way." You'd think he would have been able to cast off
  those chains when he took his Ph.D. Programmers (or at least
  pedants like me) feel the same sort of guilt about rampant use of
  globals. I decided to aggregate all the game state information
  into an anonymous hash and then pass the state to the different
  subroutines. There's a Rob Pike quote that I like to repeat: "The
  O-O languages give you more of course - prettier syntax, derived
  types and so on - but conceptually they provide little extra." I
  bring it up here because too many programmers would have carried
  this aggregation a step further, to define a class implementing
  the entire game. That would be hunting rabbits with a tank,
  because it's not likely that we'll have more than one game
  running at once and because I don't plan on inheriting and
  overriding methods from the game.</p>

  <h3>Making the Moves</h3>

  <p>Our program is about to become at least a little interesting
  as we make it recognize and respond to events. The events that
  we're interested in are when the user clicks on a card in the
  tableau and when the user clicks on the base card.</p>

  <p>Tk operates under an <i>event driven</i> model. This means
  that the programmer says to Tk, "When a certain type of event
  happens (like a mouse click or a key press), execute this code."
  The code that Tk executes in response to an event is known as a
  <i>callback</i>. The way to register callbacks (that is, how you
  ask Tk to execute callbacks when certain events take place), is
  to use Tk's <tt>bind()</tt> method. Here's a simple example:</p>
  <pre>
    $parent-&gt;bind(qw/field &lt;1&gt;/ =&gt; 
                        sub { print "Saw a click!\n" });
</pre>

  <p>This tells Tk to print a message when the user clicks Button-1
  (usually the left mouse button) on an item or widget with the
  <tt>field</tt> tag. See the Tk::bind documentation for
  details.</p>

  <p>Tags are simple strings that we can associate with items and
  widgets. When we laid the cards in the field, we gave them two
  tags: the <tt>field</tt> tag, and a tag indicating which card it
  is.</p>
  <pre>
    $c-&gt;create(
        'image',
        int $x, int $y,
        -anchor =&gt; 'nw',
        -image  =&gt; $card{$_},
        <b>-tags   =&gt; [ 'field', "card=$_" ],</b>
    );
</pre>

  <p>As you can see, tags allow the programmer to create a logical
  association among different widgets.</p>

  <h3>Laying the Base</h3>

  <p>Now that we know how to deal with events, the code to cycle
  through the stock is a SMOP (Simple Matter Of Programming). If
  there's a card on the bottom of the stock, we display and
  remember it. Otherwise, we display the back of a card and tell Tk
  to stop executing callbacks when the user clicks the base
  card.</p>
  <pre>
    sub next_base {
        my $game = shift;
    
        my $c    = $game-&gt;{CANVAS};
        my $deck  = $game-&gt;{DECK};
    
        my $ht = $c-&gt;cget('height');
        my $wd = $c-&gt;cget('width');
    
        my $x = 3 + $wd/2 - CARDWIDTH/2;
        my $y = $ht - 3 - CARDHEIGHT;
    
        # Lose any base card that might be there
        $c-&gt;delete('base');
    
        my $image;
        my $up;
        if ($deck and @$deck) {
            $up = pop @$deck;
            $image = $card{$up};
        } else {
            $image = $card{b};
            $c-&gt;bind(qw/base &lt;1&gt;/ =&gt; '');
            $up = 0;
        }
    
        $up = substr $up, 0, 1;
    
        $c-&gt;create('image',
            int $x, int $y,
            -anchor =&gt; 'nw',
            -image  =&gt; $image,
            -tags   =&gt; [ 'base' ],
        );
    
        $game-&gt;{UP} = $up;
    }
</pre>

  <p>We register this callback with:</p>
  <pre>
    $c-&gt;bind(qw/base &lt;1&gt;/ =&gt; sub { next_base $game });
</pre>

  <h3>Pick Up Lines</h3>

  <p>To legally pick up a card, it must be completely uncovered.
  The Tk::Canvas class provides a method for asking which items
  overlap a certain rectangular area. Before we can do that, we
  have to know where (in terms of canvas coordinates) we laid each
  card. We add this line to <tt>init_field()</tt>:</p>
  <pre>
    @{ $game-&gt;{POS}{$_} }{ qw/ X Y / } = ($x, $y);
</pre>Tk associates the <tt>current</tt> tag with the item or
widget involved in triggering the current callback. We can ask Tk
what other tags the "current" item has which helps us to figure out
what card it is. The code looks like this:
  <pre>
    my ($cur)  = $c-&gt;find('withtag' =&gt; 'current');
    my ($card) = grep /^card=..$/, $c-&gt;gettags($cur);
</pre>

  <p>In the rules, I stated that it is legal to pick up a card if
  it is uncovered and its denomination is adjacent to the base
  card's denomination. We can ask Tk whether any cards are covering
  the selected card:</p>
  <pre>
    $card = substr $card, -2, 2;
    my($x, $y) = @{ $game-&gt;{POS}{$card} }{qw/ X Y /};
    
    my @over = grep $_ &gt; $cur,
               $c-&gt;find('overlapping',
                   $x, $y =&gt; $x+$CARDWIDTH, $y+$CARDHEIGHT
               );
</pre>

  <p>Armed with the knowledge of the denominations of the base card
  and the selected card, we need to be able to decide whether those
  denominations are adjacent. I originally used a big, overly
  verbose hash of hashes where the test for denomination adjacency
  looked like</p>
  <pre>
    if ($rank{$base_denom}{NEXT} eq $field_denom or
        $rank{$base_denom}{PREV} eq $field_denom)
    {
        ...;
    }
</pre>

  <p>Stephen Lidie suggested the following data structure:</p>
  <pre>
    my %rank = (
        a =&gt; '2k',
        2 =&gt; '3a',
        # ...
        q =&gt; 'kj',
        k =&gt; 'aq',
    );
</pre>

  <p>The test for a legal move then becomes:</p>
  <pre>
    if ( index($rank{$base_denom}, $field_denom) &gt;= 0 ) {
        $c-&gt;delete($cur);    # Remove the card from the tableau
        push @$deck, $card;
        next_base $game;     # ...and make it the new base card
    }
</pre>

  <p>If we determine at some point that the insidious user is
  attempting to make an illegal move, we ring the bell with
  <tt>$c-&gt;bell</tt> and return.</p>

  <h3>Oops!</h3>

  <p>As I was testing the game, I would pick up a card and then
  realize that I should have picked up another card. Because of the
  bookkeeping in <tt>init_field()</tt> that kept track of where the
  cards were in the tableau, to undo we need only replace the card
  in the tableau and the last base card on the stock. The code is
  simple:</p>
  <pre>
    sub undo {
        my $c    = shift;
        my $game = shift;
    
        my $up = $game-&gt;{UPCARD};
        return unless $up;
    
        my($x, $y) = map int, @{ $game-&gt;{POS}{$up} }{ qw/ X Y / };
        $c-&gt;create('image',
            $x, $y,
            -anchor =&gt; 'nw',
            -image  =&gt; $card{$up},
            -tags   =&gt; [ 'field', "card=$up" ],
        );
    
        $game-&gt;{UPCARD} = 0;
    
        push @{ $game-&gt;{DECK} }, $game-&gt;{OLDBASE};
        next_base $game;
    }
</pre>

  <p>Notice the <tt>$game-&gt;{UPCARD}</tt> check. This makes it so
  that players can only undo a pickup. (If we also allowed players
  to undo after flipping from the bottom of the stock, they would
  enjoy the luxury of a one-card lookahead, and that would make the
  game too easy.)</p>

  <h3>Finishing touches</h3>

  <p>At this point, I had a playable game, but I wanted to add a
  more polished feel. The first touch was to add a check to make
  sure that the directory of cards was where we expected:</p>
  <pre>
    my $CARDS;
    BEGIN {
        # Where the card images live
        $CARDS = "/your/directory/of/card/images";
    
        unless (-d $CARDS) {
            $0 =~ s!^.*/!!;
            die "$0: cards directory 
                                `$CARDS' does not exist!\n" .
            "(Did you forget to set \$CARDS to the 
                                proper value?)\n";
        }
    }
</pre>

  <p>And what GUI would be complete without menus and menu
  accelerators?</p>
  <pre>
    my $restart = sub { $game = new_game $c };
    my $undo    = sub { undo $c, $game };
    
    my $mod = 'Alt';
    if ($^O eq 'MSWin32') {
        $mod = 'Control';
    } elsif ($^O eq 'MacOS') {
        $mod = 'Command';
    }

    my $menu = $mw-&gt;Menu;
    $menu-&gt;cascade(
        -label     =&gt; '~File',
        -tearoff   =&gt; 0,
        -menuitems =&gt; [
            [
                command      =&gt; '~New game',
                -command     =&gt; $restart,
                -accelerator =&gt; "$mod+N",
            ],
            [
                command      =&gt; '~Undo pickup',
                -command     =&gt; $undo,
                -accelerator =&gt; "$mod+U",
            ],
            '',
            [
                command      =&gt; '~Quit',
                -command     =&gt; [ destroy =&gt; $mw ],
                -accelerator =&gt; "$mod+Q",
            ],
        ],
    );
    
    $mw-&gt;configure(-menu =&gt; $menu);
    $mw-&gt;resizable(0, 0);
    
    $mw-&gt;bind("&lt;$mod-n&gt;" =&gt; $restart);
    $mw-&gt;bind("&lt;$mod-q&gt;" =&gt; [ destroy =&gt; $mw ]);
    $mw-&gt;bind("&lt;$mod-u&gt;" =&gt; $undo);
</pre>

  <p>Any artist will tell you that the work isn't done until it's
  signed:</p>
  <pre>
    my $about = $mw-&gt;Dialog(
        -title =&gt; 'About',
        -bitmap =&gt; 'info',
        -default_button =&gt;   'OK',
        -buttons        =&gt; [ 'OK' ],
        -font           =&gt; $FONT,
        -text           =&gt;
            "tktk (Tk timekiller)\n" .
            "gbacon\@cs.uah.edu\n\n".
            "(updated $UPDATED)\n\n" .
            "Tk version $Tk::VERSION",
    );
</pre>

  <p>The result is shown in Figure 2.</p>

  <p><font face="verdana" size="-2"><b>Figure 2:</b> A Dialog box
  containing information about the game.</font><br>
  <img src="images/tpj0502-0003-02.gif" alt=
  "Dialog Box about the game" width="271" height="287"></p>

  <p>I wish it would do something neat when the player empties the
  tableau, but I haven't come up with anything cool enough. If you
  have any ideas, please mail them to me.</p>

  <p>As you've seen, Tk provides a flexible framework for
  developing graphical user interfaces. This program was easy: it
  only took a couple of days to develop. The result was a fun
  alternative to <tt>sol.exe</tt> for killing time. Grab the code
  from the TPJ web site and give it a try.</p>_ _END_ _<br>
  <br>
  <hr>

  <p><i>Greg Bacon (<a href=
  "mailto:gbacon@cs.uah.edu">gbacon@cs.uah.edu</a>) is a Perl
  evangelist who must receive $1 million in contributions from the
  Faithful. Otherwise, he risks being struck down by Larry Wall.
  Greg would also like to remind you that there's no more important
  safety rule than to wear these safety glasses.</i></p>
  <!-- end of article -->
  <!-- link to the previous and next documents relative to the current document -->
  <!-- end of file -->
</body>
</html>
