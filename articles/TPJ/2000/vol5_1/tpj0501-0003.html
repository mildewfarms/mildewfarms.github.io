<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
<html>
<head>
  <title>Perl News - The Perl Journal, Spring 2000</title>
  <meta name="generator" content=
  "HTML Tidy for Linux/x86 (vers 12 April 2005), see www.w3.org">
  <meta name="vscategory" content="Perl">
  <meta name="vsisbn" content="">
  <meta name="vstitle" content="Parsing HTML with HTML::Parser">
  <meta name="vsauthor" content="Ken MacFarlane">
  <meta name="searchdescription" content=
  "The HTML::Parser is an object oriented Perl module which scans the HTML input and breaks it up into segments by how the HTML will be interpreted by the browser.">
  <meta name="vsimprint" content="The Perl Journal">
  <meta name="vspublisher" content="Earthweb">
  <meta name="vspubdate" content="Spring 2000">
  <!-- always update the article title and issue -->

  <!-- end head -->
  <!-- link to the previous and next documents relative to the current document -->
</head>

<body>
  <font face="verdana" size="1">Issue 17, Spring 2000</font>

  <h2 align="center">Parsing HTML with HTML::PARSER</h2>

  <h4 align="left"><i>Ken MacFarlane</i></h4>
  <!-- packages described, if necessary -->

  <div align="center">
    <table width="275" border="1" cellspacing="0" cellpadding="5">
      <tr>
        <td bgcolor="#CCCC99" align="middle"><b>Packages
        Used:</b></td>
      </tr>

      <tr>
        <td align="center">
        HTML::Parser...............................CPAN</td>
      </tr>
    </table>
  </div>

  <p>Perl is often used to manipulate the HTML files constituting
  web pages. For instance, one common task is removing tags from an
  HTML file to extract the plain text. Many solutions for such
  tasks usually use regular expressions, which often end up
  complicated, unattractive, and incomplete (or wrong). The
  alternative, described here, is to use the HTML::Parser module
  available on the CPAN (<a href="http://www.perl.com/CPAN" target=
  "resource window">http://www.perl.com/CPAN</a>). HTML::Parser is
  an excellent example of what Sean Burke noted earlier in this
  issue: some object-oriented modules require extra explanation for
  casual users.</p>

  <p>HTML::Parser works by scanning HTML input, and breaks it up
  into segments by how the text would be interpreted by a browser.
  For instance, this input:</p>

  <p><tt>&lt;A HREF="index.html"&gt;This is a
  link&lt;/A&gt;</tt></p>

  <p>would be broken up into three segments: a start tag (<tt>&lt;A
  HREF="index.html"&gt;</tt>), text (This is a link), and an end
  tag (<tt>&lt;/A&gt;</tt>). As each segment is detected, the
  parser passes it to an appropriate subroutine. There&rsquo;s a
  subroutine for start tags, one for end tags, and another for
  plain text. There are subroutines for comments and declarations
  as well.</p>

  <p>In this article, I&rsquo;ll first give a simple example on how
  to read and print out all the information found by HTML::Parser.
  Next, I&rsquo;ll demonstrate differences in the <i>events</i>
  triggered by the parser. Finally, I&rsquo;ll show how to access
  specific information passed along by the parser.</p>

  <p>As of this writing, there are two major versions of
  HTML::Parser available. Both version 2 and version 3 work by
  having you <i>subclass</i> the module. For this article, I will
  mostly concentrate on the subclassing method, because it will
  work with both major versions, and is a bit easier to understand
  for those not overly familiar with some of Perl&rsquo;s finer
  details. In version 3, there is more of an emphasis on the use of
  references, anonymous subroutines, and similar topics; advanced
  users who may be interested will see a brief example at the end
  of this article.</p>

  <p><b>Getting Started</b></p>

  <p>The first thing to be aware of when using HTML::Parser is
  that, unlike other modules, it appears to do absolutely nothing.
  When I first attempted to use this module, I used code similar to
  this:</p>
  <pre>
    #!/usr/bin/perl -w

    use strict;
    use HTML::Parser;

    my $p = new HTML::Parser;
    $p-&gt;parse_file("index.html");
</pre>

  <p>No output whatsoever. If you look at the source code to the
  module, you&rsquo;ll see why:</p>
  <pre>
sub text
{
# my($self, $text) = @_;
}

sub declaration
{
# my($self, $decl) = @_;
}

sub comment
{
# my($self, $comment) = @_;
}

sub start
{
# my($self, $tag, $attr, $attrseq, $origtext) = @_;
# $attr is reference to a HASH, $attrseq is reference 
                        to an ARRAY
}

sub end
{
# my($self, $tag, $origtext) = @_;
}
</pre>

  <p>The whole idea of the parser is that as it chugs along through
  the HTML, it calls these subroutines whenever it finds an
  appropriate snippet (start tag, end tag, and so on). However,
  these subroutines do nothing. My program works, and the HTML is
  being parsed &mdash; but I never instructed the program to
  <i>do</i> anything with the parse results.</p>

  <p><b>The Identity Parser</b></p>

  <p>The following is an example of how HTML::Parser can be
  subclassed, and its methods overridden, to produce meaningful
  output. This example simply prints out the original HTML file,
  unmodified:</p>
  <pre>
     1  #!/usr/bin/perl -w
     2  
     3  use strict;
     4  
     5  # define the subclass
     6  package IdentityParse;
     7  use base "HTML::Parser";
     8  
     9  sub text {
    10      my ($self, $text) = @_;
    11      # just print out the original text
    12      print $text;
    13  }
    14  
    15  sub comment {
    16      my ($self, $comment) = @_;
    17      # print out original text with comment marker
    18      print "";
    19  }
    20  
    21  sub start {
    22      my ($self, $tag, $attr, $attrseq, $origtext) = @_;
    23      # print out original text
    24      print $origtext;
    25  }
    26  
    27  sub end {
    28      my ($self, $tag, $origtext) = @_;
    29      # print out original text
    30      print $origtext;
    31  }
    32  
    33  my $p = new IdentityParse;
    34  $p-&gt;parse_file("index.html");
</pre>

  <p>Lines 6 and 7 declare the IdentityParse package, having it
  inherit from HTML::Parser. (Type <tt>perldoc perltoot</tt> for
  more information on inheritance.) We then override the
  <tt>text()</tt>, <tt>comment()</tt>, <tt>start()</tt>, and
  <tt>end()</tt> subroutines so that they print their original
  values. The result is a script which reads an HTML file, parses
  it, and prints it to standard output in its original form.</p>

  <p><b>The HTML Tag Stripper</b></p>

  <p>Our next example strips all the tags from the HTML file and
  prints just the text:</p>
  <pre>
     1  #!/usr/bin/perl -w
     2  
     3  use strict;
     4  
     5  package HTMLStrip;
     6  use base "HTML::Parser";
     7  
     8  sub text {
     9      my ($self, $text) = @_;
    10      print $text;
    11  }
    12  
    13  my $p = new HTMLStrip;
    14  # parse line-by-line, rather than the whole 
                                file at once
    15  while (&lt;&gt;) {
    16      $p-&gt;parse($_);
    17  }
    18  # flush and parse remaining unparsed HTML
    19  $p-&gt;eof;
</pre>

  <p>Since we&rsquo;re only interested in the text and HTML tags,
  we override only the <tt>text()</tt> subroutine. Also note that
  in lines 13-17, we invoke the <tt>parse()</tt> method instead of
  <tt>parse_file()</tt>. This lets us read files provided on the
  command line. When using <tt>parse()</tt> instead of
  <tt>parse_file()</tt>, we must also call the <tt>eof()</tt>
  method (line 19); this is done to check and clear
  HTML::Parser&rsquo;s internal buffer.</p>

  <p><b>Another Example: HTML Summaries</b></p>

  <p>Suppose you&rsquo;ve hand-crafted your own search engine for
  your web site, and you want to be able to generate summaries for
  each hit. You could use the HTML::Summary module described in TPJ
  #16, but we&rsquo;ll describe a simpler solution here.
  We&rsquo;ll assume that some (but not all) of your site&rsquo;s
  pages use a <tt>META</tt> tag to describe the content:</p>
  <pre>
&lt;META NAME="DESCRIPTION" CONTENT="description of file"&gt;
</pre>

  <p>When a page has a <tt>META</tt> tag, your search engine should
  use the <tt>CONTENT</tt> for the summary. Otherwise, the summary
  should be the first <tt>H1</tt> tag if one exists. And if that
  fails, we&rsquo;ll use the <tt>TITLE</tt>. Our third example
  generates such a summary:</p>
  <pre>
 
 1  #!/usr/bin/perl -w
 2  
 3  use strict;
 4  
 5  package GetSummary;
 6  use base "HTML::Parser";
 7  
 8  my $meta_contents;
 9  my $h1 =    "";
10  my $title = "";
11  
12  # set state flags
13  my $h1_flag    = 0;
14  my $title_flag = 0;
15  
16  sub start {
17      my ($self, $tag, $attr, $attrseq, $origtext) = @_;
18  
19      if ($tag =~ /^meta$/i &amp;&amp; 
                $attr-&gt;{'name'} =~ /^description$/i) {
20          # set if we find &lt;META NAME="DESCRIPTION"
21          $meta_contents = $attr-&gt;{'content'};
22      } elsif ($tag =~ /^h1$/i &amp;&amp; ! $h1) {
23          # set state if we find &lt;H1&gt; or &lt;TITLE&gt;
24          $h1_flag = 1;
25      } elsif ($tag =~ /^title$/i &amp;&amp; ! $title) {
26          $title_flag = 1;
27      }
28  }
29  
30  sub text {
31      my ($self, $text) = @_;
32      # If we're in &lt;H1&gt;...&lt;/H1&gt; or 
                        &lt;TITLE&gt;...&lt;/TITLE&gt;, save text    
33      if ($h1_flag)    { $h1    .= $text; } 
34      if ($title_flag) { $title .= $text; }
35  }
36  
37  sub end {
38      my ($self, $tag, $origtext) = @_;
39  
40      # reset appropriate flag if we see &lt;/H1&gt; or &lt;/TITLE&gt;
41      if ($tag =~ /^h1$/i)    { $h1_flag = 0; }
42      if ($tag =~ /^title$/i) { $h1_flag = 0; }
43  }
44   
45  my $p = new GetSummary;
46  while (&lt;&gt;) {
47      $p-&gt;parse($_);
48  }
49  $p-&gt;eof;
50  
51  print "Summary information: ", $meta_contents || 
52      $h1 || $title || "No summary information found.", "\n";
</pre>

  <p>The magic happens in lines 19-27. The variable <tt>$attr</tt>
  contains a reference to a hash where the tag attributes are
  represented with key/value pairs. The keys are lowercased by the
  module, which is a code-saver; otherwise, we&rsquo;d need to
  check for all casing possibilities (<tt>name</tt>, <tt>NAME</tt>,
  <tt>Name</tt>, and so on).</p>

  <p>Lines 19-21 check to see if the current tag is a <tt>META</tt>
  tag and has a field <tt>NAME</tt> set to <tt>DESCRIPTION</tt>; if
  so, the variable <tt>$meta_contents</tt> is set to the value of
  the <tt>CONTENT</tt> field. Lines 22-27 likewise check for an
  <tt>H1</tt> or <tt>TITLE</tt> tag. In these cases, the
  information we want is in the text between the start and end
  tags, and not the tag itself. Furthermore, when the text
  subroutine is called, it has no way of knowing which tags (if
  any) its text is between. This is why we set a flag in
  <tt>start()</tt> (where the tag name is known) and check the flag
  in <tt>text()</tt> (where it isn&rsquo;t). Lines 22 and 25 also
  check whether or not <tt>$h1</tt> and <tt>$title</tt> have been
  set; since we only want the first match, subsequent matches are
  ignored.</p>

  <p><b>Another Fictional Example</b></p>

  <p>Suppose that your company has been running a successful
  product site at http://www.bar.com/foo/. However, the web
  marketing team decides that http://foo.bar.com/ looks better in
  the company&rsquo;s advertising materials, so a redirect is set
  up from the new address to the old.</p>

  <p>Fast forward to Friday, 4:45 in the afternoon, when the phone
  rings. The frantic voice on the other end says, "foo.bar.com just
  crashed! We need to change all the links back to the old
  location!" Just when you though a simple search-and-replace would
  suffice, the voice adds: "And marketing says we can&rsquo;t
  change the text of the web pages, only the links."</p>

  <p>"No problem", you respond, and quickly hack together a program
  that changes the links in <tt>A HREF</tt> tags, and nowhere
  else.</p>
  <pre>
     1  #!/usr/bin/perl -w -i.bak
     2  
     3  use strict;
     4  
     5  package ChangeLinks;
     6  use base "HTML::Parser";
     7  
     8  sub start {
     9      my ($self, $tag, $attr, $attrseq, $origtext) = @_;
    10  
    11      # we're only interested in changing &lt;A ...&gt; tags
    12      unless ($tag =~ /^a$/) {
    13          print $origtext;
    14          return;
    15      }
    16  
    17      if (defined $attr-&gt;{'href'}) {
    18          $attr-&gt;{'href'} =~ 
                s[foo\.bar\.com][www\.bar\.com/foo];
    19      }
    20  
    21      print "&lt;A ";
    22      # print each attribute of the &lt;A ...&gt; tag
    23      foreach my $i (@$attrseq) {
    24          print $i, qq(="$attr-&gt;{$i}" );
    25      }
    26      print "&gt;";
    27  }
    28  
    29  sub text {
    30      my ($self, $text) = @_;
    31      print $text;
    32  }
    33  
    34  sub comment {
    35      my ($self, $comment) = @_;
    36      print "&lt;!&mdash;", $comment, "&mdash;&gt;";
    37  }
    38  
    39  sub end {
    40      my ($self, $tag, $origtext) = @_;
    41      print $origtext;
    42  }
    43  
    44  my $p = new ChangeLinks;
    45  while (&lt;&gt;) {
    46      $p-&gt;parse($_);
    47  }
    48  $p-&gt;eof;
</pre>

  <p>Line 1 specifies that the files will be edited in place, with
  the original files being renamed with a <tt>.bak</tt> extension.
  The real fun is in the <tt>start()</tt> subroutine, lines 8-27.
  First, in lines 12-15, we check for an <tt>A</tt> tag; if
  that&rsquo;s not what we have, we simply return the original tag.
  Lines 17-19 check for the <tt>HREF</tt> and make the desired
  substitution.</p>

  <p><tt>$attrseq</tt> appears in line 23. This variable is a
  reference to an array with the tag attributes in their original
  order of appearance. If the attribute order needs to be
  preserved, this array is necessary to reconstruct the original
  order, since the hash <tt>$attr</tt> will jumble them up. Here,
  we dereference <tt>$attrseq</tt> and then recreate each tag. The
  attribute names will appear lowercase regardless of how they
  originally appeared. If you&rsquo;d prefer uppercase, change the
  first <tt>$i</tt> in line 24 to <tt>uc($i)</tt>.</p>

  <p><b>Using HTML::Parser Version 3</b></p>

  <p>Version 3 of the module provides more flexibility in how the
  handlers are invoked. One big change is that you no longer have
  to use subclassing; rather, <i>event handlers</i> can be
  specified when the HTML::Parser constructor is called. The
  following example is equivalent to the previous program but uses
  some of the version 3 features:</p>
  <pre>
     1  #!/usr/bin/perl -w -i.bak
     2  
     3  use strict;
     4  use HTML::Parser;
     5  
     6  # specify events here rather than in a subclass
     7  my $p = HTML::Parser-&gt;new( api_version =&gt; 3,
     8           start_h     =&gt; [\amp;start,
     9  "tagname, attr, attrseq, text"],
    10           default_h   =&gt; [sub { print shift }, "text"],
    11                             );
    12  sub start {
    13      my ($tag, $attr, $attrseq, $origtext) = @_;
    14  
    15      unless ($tag =~ /^a$/) {
    16          print $origtext;
    17          return;
    18      }
    19  
    20      if (defined $attr-&gt;{'href'}) {
    21          $attr-&gt;{'href'} =~ 
                        s[foo\.bar\.com][www\.bar\.com/foo];
    22      }
    23  
    24      print "&lt;A ";
    25      foreach my $i (@$attrseq) {
    26          print $i, qq(="$attr-&gt;{$i}" );
    27      }
    28      print "&gt;";
    29  }
    30  
    31  while (&lt;&gt;) {
    32      $p-&gt;parse($_);
    33  }
    34  $p-&gt;eof;
</pre>

  <p>The key changes are in lines 7-10. In line 8, we specify that
  the start event is to be handled by the <tt>start()</tt>
  subroutine. Another key change is line 10; version 3 of
  HTML::Parser supports the notion of a default handler. In the
  previous example, we needed to specify separate handlers for
  text, end tags, and comments; here, we use <tt>default_h()</tt>
  as a catch-all. This turns out to be a code saver as well.</p>

  <p>Take a closer look at line 9, and compare it to line 9 of the
  previous example. Note that <tt>$self</tt> hasn&rsquo;t been
  passed. In version 3 of HTML::Parser, the list of attributes
  which can be passed along to the handler subroutine is
  configurable. If our program only needed to use the tag name and
  text, we can change the string <tt>tagname</tt>, <tt>attr</tt>,
  <tt>attrseq</tt>, <tt>text</tt> to <tt>tagname</tt>,
  <tt>text</tt> and then change the <tt>start()</tt> subroutine to
  only use two parameters. Also, handlers are not limited to
  subroutines. If we changed the default handler like this, the
  text that would have been printed is instead pushed onto
  <tt>@lines</tt>.</p>
  <pre>
  my $p = HTML::Parser-&gt;new( api_version =&gt; 3,
                     start_h     =&gt; [\&amp;start,
                     "tagname, attr, attrseq, text"],
                     default_h   =&gt; \@lines, "text"],
                         );
</pre>

  <p>Version 3 of HTML::Parser also adds some new features;
  notably, one can now set options to recognize and act upon XML
  constructs, such as <tt>&lt;TAG/&gt;</tt> and
  <tt>&lt;?TAG?&gt;</tt>. There are also multiple methods of
  accessing tag information, instead of the <tt>$attr</tt> hash.
  Rather than go into further detail, I encourage you to explore
  the flexibility and power of this module on your own.</p>

  <p><b>Acknowledgments</b></p>

  <p>The HTML::Parser module was written by Gisle Aas and Michael
  A. Chase. Excerpts of code and documentation from the module are
  used here with the authors&rsquo; permission.</p>

  <p><b>_ _END_ _</b></p>
  <hr>
  <i>Ken MacFarlane</i> <a href=
  "mailto:ksm+tpj@universal.dca.net">(ksm+tpj@universal.dca.net)</a>
  <i>is a consultant with Info Systems, Inc. in Wilmington,
  Delaware. When not working or doing other things Perl, Ken and
  his wife Amanda spend most of their time chasing around their one
  year old son, Timothy.</i> <!-- end of article -->
  <!-- link to the previous and next documents relative to the current document -->
  <!-- end of file -->
</body>
</html>
