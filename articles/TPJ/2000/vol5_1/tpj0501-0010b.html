<html>
<head>
  <title>Listing 2: A table-based implementation of the receive_event() method. - The Perl Journal, Spring 2000</title>
  <meta name="generator" content=
  "HTML Tidy for Linux/x86 (vers 12 April 2005), see www.w3.org">
  <meta name="vscategory" content="Perl">
  <meta name="vsisbn" content="">
  <meta name="vstitle" content=
  "MULTIPLE DISPATCH IN PERL - Listing 2">
  <meta name="vsauthor" content="Damian Conway">
  <meta name="searchdescription" content=
  "MULTIPLE DISPATCH IN PERL">
  <meta name="vsimprint" content="The Perl Journal">
  <meta name="vspublisher" content="Earthweb">
  <meta name="vspubdate" content="Spring 2000">
  <!-- update listing #, listing title, and issue  -->

</head>

<body bgcolor="#FFFFFF" text="#000000" link="#DD0000" vlink=
"#DD0000" alink="#DD0000" leftmargin="0" topmargin="0" marginwidth=
"0" marginheight="0">
  <!-- update listing #, listing title, and issue throughout the table -->

  <table cellspacing="0" cellpadding="5" width="100%" border="0">
    <tr align="left" valign="top">
      <td bgcolor="#CCCC99">
        <table width="100%">
          <tr>
            <td valign="top" bgcolor="#CCCC99" nowrap><font face=
            "Arial,Helvetica" size="+1">Listing 2.</font><br>
            <font size="-1" face="Arial,Helvetica" color=
            "#663300"><b>A table-based implementation of the
            receive_event() method.</b></font><br></td>

            <td valign="top" bgcolor="#CCCC99"><font size="-1"
            face="Arial,Helvetica" color="#663300">MULTIPLE
            DISPATCH IN PERL<br>
            The Perl Journal, Spring 2000</font></td>
          </tr>
        </table>
      </td>
    </tr>

    <tr>
      <td>
        <table width="100%">
          <tr>
            <td width="5%" bgcolor="#FFFFFF">&nbsp;</td>

            <td bgcolor="#FFFFFF">
              <font color="#000000">
              <!-- insert code listing or table here --></font>
              <pre>
<font color="#000000"> package Window;
   
   my $_id = 1;
   sub new { bless { _id =&gt; $_id++ }, $_[0] }
   
   my %table;
   
   sub init {
       my ($param1,$param2,$param3,$handler) = @_;
       foreach my $p1 ( @$param1 ) {
           foreach my $p2 ( @$param2) {
               foreach my $p3 ( @$param3) {
                   $table{$p1}{$p2}{$p3} = $handler;
               }
           }
       }
   }
   
   my $windows  = [qw(Window ModalWindow MovableWindow ResizableWindow )];
   my $events   = [qw(Event ReshapeEvent AcceptEvent
                     MoveEvent ResizeEvent MoveAndResizeEvent )];
   my $modes    = [qw(Mode OnMode OffMode ModalMode )];
   
   init $windows, $events, $modes                                       #case 0
       =&gt; sub { print "Window $_[0]-&gt;{_id} can't handle a ",
                      ref($_[1]), " event in ", ref($_[2]), " mode\n" };
   
   init $windows, $events, [qw(OffMode)]                                #case 1
       =&gt; sub { print "No window operations available in OffMode\n" };
   
   init [qw(ModalWindow)],                                              #case 2
        [qw(ReshapeEvent ResizeEvent MoveEvent MoveAndResizeEvent)],
        $modes
       =&gt; sub { print "Modal windows can't handle reshape events\n" };
   
   init [qw(ModalWindow)], [qw(AcceptEvent)], $modes                     #case 3
       =&gt; sub { print "Modal window $_[0]-&gt;{_id} accepts!\n" };
   
   init [qw(ModalWindow)], [qw(AcceptEvent)], [qw(OffMode)]               #case 4
       =&gt; sub { print "Modal window $_[0]-&gt;{_id} can't accept in OffMode!\n" };
   
   init [qw(MovableWindow ResizableWindow)],                             #case 5
        [qw(MoveEvent MoveAndResizeEvent)],
        [qw(OnMode)]
       =&gt; sub { print "Moving window $_[0]-&gt;{_id}!\n" };
   
   init [qw(ResizableWindow)], [qw(ResizeEvent)], [qw(OnMode)]            #case 6
       =&gt; sub { print "Resizing window $_[0]-&gt;{_id}!\n" };
   
   init [qw(ResizableWindow)], [qw(MoveAndResizeEvent)], [qw(OnMode)]      #case 7
       =&gt; sub { print "Moving and resizing window $_[0]-&gt;{_id}!\n" };
   
   sub receive_event {
       my ($type1, $type2, $type3) = map {ref} @_;
       my $handler = $table{$type1}{$type2}{$type3};
       die "No suitable handler found" unless $handler;
       $handler-&gt;(@_);
   }
   
   package ModalWindow;     @ISA = qw( Window );
   package MovableWindow;   @ISA = qw( Window );
   package ResizableWindow; @ISA = qw( MovableWindow );
</font>
</pre>
            </td>
          </tr>
        </table>
      </td>
    </tr>
  </table>
</body>
</html>
