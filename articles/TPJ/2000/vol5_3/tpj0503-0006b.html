<html>
<head>
  <title>Listing 1: A sample stock ticker program using Finance::Quote - The Perl Journal, Fall 2000</title>
  <meta name="generator" content=
  "HTML Tidy for Linux/x86 (vers 12 April 2005), see www.w3.org">
  <meta name="vscategory" content="Perl">
  <meta name="vsisbn" content="">
  <meta name="vstitle" content="Finance::Quote - Listing 1">
  <meta name="vsauthor" content="Paul Fenwick">
  <meta name="searchdescription" content=
  "A sample stock ticker program using Finance::Quote">
  <meta name="vsimprint" content="The Perl Journal">
  <meta name="vspublisher" content="Earthweb">
  <meta name="vspubdate" content="Fall 2000">
  <!-- update listing #, listing title, and issue  -->

</head>

<body bgcolor="#FFFFFF" text="#000000" link="#DD0000" vlink=
"#DD0000" alink="#DD0000" leftmargin="0" topmargin="0" marginwidth=
"0" marginheight="0">
  <!-- update listing #, listing title, and issue throughout the table -->

  <table cellspacing="0" cellpadding="5" width="100%" border="0">
    <tr align="left" valign="top">
      <td bgcolor="#CCCC99">
        <table width="100%">
          <tr>
            <td valign="top" bgcolor="#CCCC99" nowrap><font face=
            "Arial,Helvetica" size="+1">Listing 1: A sample stock
            ticker program using Finance::Quote</font><br></td>

            <td valign="top" bgcolor="#CCCC99"><font size="-1"
            face="Arial,Helvetica" color=
            "#663300">Finance::Quote<br>
            The Perl Journal, Fall 2000</font></td>
          </tr>
        </table>
      </td>
    </tr>

    <tr>
      <td>
        <table width="100%">
          <tr>
            <td width="5%" bgcolor="#FFFFFF">&nbsp;</td>

            <td bgcolor="#FFFFFF">
              <!-- insert code listing or table here -->
              <pre>

.

#!/usr/bin/perl -w
#
# Example stock-ticker program.  Can look up stocks from multiple
# markets and return the results in local currency.

use strict;
use Finance::Quote;

my $CURRENCY = "AUD";   # Set preferred currency here, or empty string for
                                # no conversion.

# The @stocks array contains a set of array-references.  Each reference
# has the market as the first element, and a set of stocks thereafter.

my @STOCKS = ([qw/australia CML ITE BHP/],
                 [qw/usa MSFT RHAT LNUX/]
             );

# These define the format.  The first item in each pair is the label,
# the second is the printf-style formatting, the third is the width
# of the field (used in printing headers).

my @labels = (["name",  "%12s",  15],
                 ["date",  "%11s",  11], 
                 ["time",  "%10s",  11],
                 ["last",  "%8.2f",  8],
                 ["high",  "%8.2f",  8], 
                 ["low",   "%8.2f",  8],
                 ["close", "%8.2f",  8], 
                 ["volume","%10d",  10]);

my $REFRESH = 120;      # Seconds between refresh.

# -- END CONFIG SECTION --

my $quoter = Finance::Quote-&gt;new();
my $clear  = `clear`;                   # To clear the screen.

# Build our header.

my $header = "\t\t\t\tSTOCK REPORT" .($CURRENCY ? " ($CURRENCY)" : "") ."\n\n";

foreach my $tuple (@labels) {
    my ($name, undef, $width) = @$tuple;
    $header .= sprintf("%".$width."s", uc($name));
}

$header .= "\n".("-"x79)."\n";

# Header is all built.  Looks beautiful.

$quoter-&gt;set_currency($CURRENCY) if $CURRENCY;       # Set default currency.

for (;;) {      # Loop forever.
    print $clear, $header;
    foreach my $stockset (@STOCKS) {
        my ($exchange, @symbols) = @$stockset;
        my %info = $quoter-&gt;fetch($exchange, @symbols);

        foreach my $symbol (@symbols) {
            next unless $info{$symbol, "success"};  # Skip failures.
            foreach my $tuple (@labels) {
                my ($label, $format) = @$tuple;
                printf $format, $info{$symbol, $label};
            }
            print "\n";
        }
    }

    sleep($REFRESH);
}

</pre>
            </td>
          </tr>
        </table>
      </td>
    </tr>
  </table>
</body>
</html>
