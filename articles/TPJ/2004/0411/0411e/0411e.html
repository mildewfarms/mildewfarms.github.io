
<html>
<head>
<title>November, 2004: Scripts as Modules</title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; The Perl Journal-->

<h1>Scripts as Modules</h1>
<p><i>The Perl Journal</i> November, 2004</p>
<h2></h2>


<h3>By brian d foy</h3>


<I>brian has been a Perl user since 1994. He is founder of the first Perl Users Group, NY.pm, and Perl Mongers, the Perl advocacy organization. He has been teaching Perl through Stonehenge Consulting for the past five years, and has been a featured speaker at The Perl Conference, Perl University, YAPC, COMDEX, and Builder.com. Contact brian at comdog@panix.com.</I>

<hr>





<p>Lately, I've been writing scripts that look more like modules. Some of this comes naturally from writing so many modules, but I've also discovered that these scripts are easier to manage and test. The result is a Perl file that acts like a module when I use it like a module, and acts like a script when I use it like a script.</p>

<p>When I first started writing Perl, I mostly wrote the usual type of script: I started at the top of the page and wrote statements that I expected to execute in order as I moved down the page. I could read it like a movie script, going from one line to the next.</p>

<p>Later, as I learned more Perl and got more programming experience (even with other languages such as C, Java, and Smalltalk), I started using more functions. The code, however, was still very procedural and I could follow it linearly down the page, even if I had to look at a function definition every so often.</p>

<p>In the past couple of years, I've gotten on the testing bandwagon. I want to test everything. The <i>Test::More</i> module made that very easy, and there has been an explosion in the number of <i>Test::*</i> modules to check various things (and I'm responsible for a couple of them).</p>

<p>Scripts are hard to test, though. I can choose the input at the start, such as the environment variables, command-line arguments, and standard input. After that I have to wait for the script's output to see if things went right. If things didn't go right, I have to figure out where, between the first line and the last line, things went wrong.</p>

<p>A module is relatively easy to test. A good module author breaks down everything into methods (or plain old functions) that do one task or small bits of a task. As long as the methods don't use side effects (like looking at global variables, including nonlocal versions of Perl's special variables), I can give each method some arguments and check its return values. Once I test each of the methods, I can confidently use them knowing that they do what I expect. When things go wrong, I have a lot less to search through to find the problem.</p>
<h3>The Core Structure</h3>

<p>To create this sort of script, which I have been calling a "modulino," I take Perl back a step. Remember the <i>main()</i> function of a C program, and how Perl made the whole script file <i>main()</i> and called it <i>main::</i>? I need that back, but I'm going to call it the <i>run()</i> method:</p>

<blockquote>
<pre>
#!/usr/bin/perl
package Local::Modulino;

__PACKAGE__-&gt;run( @ARGV ) unless caller();

sub run { print "I'm a script!\n" }

__END__

</pre>
</blockquote>

<p>My modulino no longer assumes that it is in the <i>main::</i> namespace and that the whole file is the script. I need to put everything that I want to do in the <i>run()</i> method, just like I would do with C's <i>main()</i>. As a start, my modulino just prints a short message.</p>

<p>The script-or-module magic works in the third line, which checks the result of the <i>caller()</i> Perl built-in function. If something else in the Perl script calls the file, the <i>caller()</i> function (in scalar context) returns the calling package name. That's true if another Perl file loads this one with <i>use()</i> or <i>require()</i>. If I run my modulino as a script, there is no other file loading it, so <i>caller()</i> returns <i>undef</i>. If <i>caller()</i> returns a false value, then I execute the <i>__PACKAGE__-&gt;run()</i> method (the Perl compiler replaces the <i>__PACKAGE__</i> token with the current package name).</p>

<p>That's it. That's the core of the dual-duty Perl modulino. Everything else is just programming.</p>

<p>I save this file as Modulino.pm and execute it in a couple of different ways. From the command line, I can call it like a script. The <i>caller()</i> expression returns False, so Perl executes the <i>run()</i>, which prints out my short message:</p>

<blockquote>
<pre>
prompt% perl Modulino.pm
I'm a script!
prompt%

</pre>
</blockquote>

<p>When I load the file as a module using the <i>-M</i> switch (which works like <i>use()</i>), the <i>caller()</i> expression returns a true value (extra credit for knowing what the actual value is), so <i>unless()</i> doesn't evaluate the rest of the statement. The <i>run()</i> never runs, and I don't get any output:</p>

<blockquote>
<pre>
prompt% perl -MModulino.pm -e 1
prompt%

</pre>
</blockquote>

<p>I can still get output though&#151;I just need to call the <i>run()</i> method myself:</p>

<blockquote>
<pre>
prompt% perl -MModulino -e 'Local::Modulino-&gt;run()';
I'm a script!
prompt%

</pre>
</blockquote>
<h3>The Rest of the Story</h3>

<p>Now that I have the basic structure of the modulino, I need to apply it to something useful, but that's probably too long for the space I have left in this article. Well, maybe not. For a while, I've wanted a little tool to download the RSS feed from <i>The Perl Journal</i> and print a table of contents. Unlike the PDF files I get for each issue, I always know where the RSS file is: It's the same URL every time.</p>

<p>I wrote a modulino to download, parse, and display the table of contents of <i>The Perl Journal</i>. I could have written this as a script and gone through each of those steps in sequence, but with a modulino, I have a bit more flexibility; and when I decide to test it, I should be able to find problems easier and faster.</p>

<p>In <i>Local::Modulino::RSS</i> (see <a name="rl1"></a><a href="#l1">Listing 1</a>), I display the table of contents as text, which works just fine for me in my terminal window. However, since I structured the code as a module, I could very easily do something else. Perhaps I want to convert the table of contents into HTML so I can display it on my personal home page. Since only my <i>run()</i> knows anything about the data presentation, I just have to override it, which I show later.</p>

<p>The rest of the modulino is a collection of very short functions doing a very specific task. I can easily write some testing code to make sure each of the small functions does what I think they should. I skip that part here since the topic has been covered so well in other articles.</p>

<p>On line 1, I start with a shebang line. If I want to run this as a Perl script without specifying "perl" on the command line, the operating system needs to know which interpreter I intend to use.</p>

<p>Next, I define the package name and invoke the <i>run()</i> method if I call the file as a script. If I use this file as a module, <i>caller()</i> returns true and I don't call the <i>run()</i> method.</p>

<p>On line 8, I define my <i>run()</i> method. On line 11, I take the first argument, which is the package name, off of the argument list. Each method does this, so I can subclass the task. I call each function as a class method so inheritance works out right. The methods will always know who is calling them, even if it is a derived package.</p>

<p>Most of the complexity of the task is hidden behind functions. The <i>fetch_items()</i> method is composed of the <i>get_url()</i>, <i>get_data()</i>, and <i>get_items()</i> methods that do most of the actual work. My <i>run()</i> method simply gets the parts it needs. This way, when I want to write another <i>run()</i> method, I won't have to do so much work.</p>

<p>On line 13, I go through each item and extract the information for that issue. The <i>get_issue()</i> function returns the title of the item, which turns out to be the month and year of publication, along with the articles in that issue as a list of <i>article title, author</i> anonymous array pairs. It's the data, so up to this point, I can still do just about anything I like, but once I have the text for the title and the articles for the latest issue, I simply print them to the terminal as plain text, as I show in <a name="rl2"></a><a href="#l2">Listing 2</a>.</p>

<p>Some of you might have noticed the start of a model-view-controller (MVC) design (although I don't have much controller going on). The data handling and the presentation don't depend on each other. The MVC design, which may sound fancy or exotic, naturally pops up when I use a lot of small functions to do single tasks. The only part of my script that deals with the presentation of the data is the <i>run()</i> method, and that's easy to override with a subclass.</p>

<p>In fact, it's so easy to subclass that I might as well do it here. In <a name="rl3"></a><a href="#l3">Listing 3</a>, I create the <i>Local::Modulino::RSS::HTML</i> modulino, although it only overrides the <i>run()</i> method by defining its own version. I have to tell it that it is a subclass of <i>Local::Modulino::RSS</i> with the <i>use base</i> declaration so it looks in that class for methods it does not define, such as <i>fetch_items()</i> and <i>get_issue()</i>. I also <i>require</i> "RSS.pm" because I didn't bother to install these files as proper modules, so I don't want my modulino to look in Local/Modulino/RSS.pm to find the file. I show the new output format in <a name="rl4"></a><a href="#l4">Listing 4</a>.</p>
<h3>Code</h3>

<p>By creating a modulino, I get my Perl scripts to do double duty as scripts and as modules. If I structure the code as a module, I can reuse and override it just like a module. Since I broke everything down to small functions instead of using a procedural style, I also make things easier to test.</p>




<p><b>TPJ</b></p>



	
	
<br>
<br>
<b><a name="l1"></a>Listing 1</b><br>
<pre>  1 #!/usr/bin/perl
  2 package Local::Modulino::RSS;
  3 
  4 __PACKAGE__-&gt;run() unless caller();
  5 
  6 use HTML::Entities;
  7 use Data::Dumper;
  8 
  9 sub run
 10   {
 11   my $class = shift;
 12   
 13   foreach my $item ( $class-&gt;fetch_items )
 14     {
 15     my( $title, @articles ) = $class-&gt;get_issue( $item );
 16     
 17     print "\n$title\n------------------\n";
 18     printf "%-45s %-30s\n", @$_ foreach ( @articles );
 19     }
 20     
 21   }
 22 
 23 sub fetch_items
 24   {
 25   my $class    = shift;
 26   
 27   my $url      = $class-&gt;get_url();
 28   my $data     = $class-&gt;get_data( $url );
 29   my @items    = $class-&gt;get_items( $$data ); 
 30   }
 31   
 32 sub get_issue
 33   {
 34   my $class    = shift;
 35   
 36   my $title    = $class-&gt;get_title( $_[0] );
 37   my @articles = $class-&gt;get_articles( $_[0] );
 38   
 39   return ( $title, @articles );
 40   }
 41   
 42 sub get_articles
 43   {
 44   my $class    = shift;
 45   
 46   my $d = $class-&gt;get_description( $_[0] );
 47 
 48   my @b = split /&lt;br&gt;\s*&lt;br&gt;/, $d;
 49   my @articles = ();
 50   
 51   foreach my $b ( @b )
 52     {
 53     my @bits = split /&lt;br&gt;/, $b;
 54     $author  = pop @bits;
 55     
 56     my $title = join " ", @bits;
 57     
 58     $class-&gt;_normalize( $author, $title );
 59     push @articles, [ $title, $author ];
 60     }
 61     
 62   @articles;
 63   }
 64   
 65 sub get_description { $_[0]-&gt;_field( $_[1], 'description' ) }
 66 sub get_title       { $_[0]-&gt;_field( $_[1], 'title'       ) }
 67 sub get_items       { $_[0]-&gt;_field( $_[1], 'item'        ) }
 68 
 69 sub _normalize 
 70   { 
 71   my $class    = shift;
 72   
 73   foreach ( 0 .. $#_ )
 74     {
 75     $_[$_] =~ s/^\s*|\s*$//g;
 76     $_[$_] =~ s|&lt;/?b&gt;||g;
 77     $_[$_] =~ s|\s+| |g;
 78     }
 79   }
 80   
 81 sub _field
 82   {
 83   my $data = $_[1];
 84     
 85   HTML::Entities::decode_entities( $data );
 86   
 87   my @matches = $data =~ m|&lt;\Q$_[2]\E&gt;(.*?)&lt;/\Q$_[2]\E&gt;|sig;
 88     
 89   wantarray ? @matches : $matches[0];
 90   }
 91   
 92 sub get_data 
 93   { 
 94   my $class    = shift;
 95   
 96   require LWP::Simple; 
 97   my $data = LWP::Simple::get( $_[0] );
 98   defined $data ? \$data : $data;
 99   }
100   
101 sub get_url { 
102   "http://syndication.sdmediagroup.com/" .
103     "feeds/public/the_perl_journal.xml"
104     }
    </pre>
<a href="#rl1">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l2"></a>Listing 2</b><br>
<pre>September 2004 PDF
------------------
Objective Perl: Objective-C-Style Syntax And Runtime for Perl Kyle Dawkins                  
Scoping: Letting Perl Do the Work for You     David Oswald                  
Secure Your Code With Taint Checking          Andy Lester                   
Detaching Attachments                         brian d foy                   
Unicode in Perl                               Simon Cozens                  
PLUS Letter from the Editor Perl News         Source Code Appendix          

August 2004 PDF
------------------
Regex Arcana                                  Jeff Pinyan                   
XML Subversion                                Curtis Lee Fulton             
OSCON 2004 Round-Up                           Andy Lester                   
Molecular Biology in Perl                     Simon Cozens                  
Pipelines and E-mail Addresses                brian d foy                   
PLUS Letter from the Editor Perl News         Source Code Appendix          </pre>
<a href="#rl2">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l3"></a>Listing 3</b><br>
<pre>#!/usr/bin/perl
package Local::Modulino::RSS::HTML;

use base qw( Local::Modulino::RSS );

require "RSS.pm";

__PACKAGE__-&gt;run() unless caller();

use HTML::Entities;

sub run
  {
  my $class    = shift;
  
  foreach my $item ( $class-&gt;fetch_items )
    {
    my( $title, @articles ) = $class-&gt;get_issue( $item );
    
    print "\n&lt;h3&gt;$title&lt;/h3&gt;\n\n&lt;ul&gt;\n";
    printf "&lt;li&gt;&lt;b&gt;%s&lt;/b&gt;, %s\n", @$_ foreach ( @articles );
    print "&lt;/ul&gt;\n";
    }
  }</pre>
<a href="#rl3">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l4"></a>Listing 4</b><br>
<pre>&lt;h3&gt;September 2004 PDF&lt;/h3&gt;

&lt;ul&gt;
&lt;li&gt;&lt;b&gt;Objective Perl: Objective-C-Style Syntax And Runtime for Perl&lt;/b&gt;, Kyle Dawkins
&lt;li&gt;&lt;b&gt;Scoping: Letting Perl Do the Work for You&lt;/b&gt;, David Oswald
&lt;li&gt;&lt;b&gt;Secure Your Code With Taint Checking&lt;/b&gt;, Andy Lester
&lt;li&gt;&lt;b&gt;Detaching Attachments&lt;/b&gt;, brian d foy
&lt;li&gt;&lt;b&gt;Unicode in Perl&lt;/b&gt;, Simon Cozens
&lt;li&gt;&lt;b&gt;PLUS Letter from the Editor Perl News&lt;/b&gt;, Source Code Appendix
&lt;/ul&gt;

&lt;h3&gt;August 2004 PDF&lt;/h3&gt;

&lt;ul&gt;
&lt;li&gt;&lt;b&gt;Regex Arcana&lt;/b&gt;, Jeff Pinyan
&lt;li&gt;&lt;b&gt;XML Subversion&lt;/b&gt;, Curtis Lee Fulton
&lt;li&gt;&lt;b&gt;OSCON 2004 Round-Up&lt;/b&gt;, Andy Lester
&lt;li&gt;&lt;b&gt;Molecular Biology in Perl&lt;/b&gt;, Simon Cozens
&lt;li&gt;&lt;b&gt;Pipelines and E-mail Addresses&lt;/b&gt;, brian d foy
&lt;li&gt;&lt;b&gt;PLUS Letter from the Editor Perl News&lt;/b&gt;, Source Code Appendix
&lt;/ul&gt;</pre>
<a href="#rl4">Back to article</a><br>
	
	
	


</body>
</html>