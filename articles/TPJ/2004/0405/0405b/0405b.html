
<html>
<head>
<title>May, 2004: Determining List Relationships with List::Compare</title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; The Perl Journal-->

<h1>Determining List Relationships with List::Compare</h1>
<p><i>The Perl Journal</i> May, 2004</p>
<h2></h2>


<h3>By James E. Keenan</h3>


<I>James founded Perl Seminar New York in 2000, and is Treatment Team Leader for the New York State Office of Mental Health at Kingsboro Psychiatric Center. He can be contacted at jkeenan@cpan.org.</I>

<hr>





<p>Two years ago, I was hired by the continuing education department of a local college to teach a six-week introductory course in Perl. The circumstances of my hiring were a bit abrupt: The person originally hired for the course quit the week before the course began. I was hired two nights before my first session, so I needed to develop course materials, and fast.</p>

<p>Perl, of course, came to my rescue. I decided to use Randal L. Schwartz's <i>Learning Perl</i> (O'Reilly &amp; Associates, 2001) as the course text and to create an HTML-based slideshow as my curriculum. I was somewhat familiar with such slideshows because, at the first meeting of one of our local user groups, Perl Seminar New York, my colleague Mark Miller had demonstrated such a slideshow. But that show displayed photos; I wanted to display text. I did a quick hack on Mark's script so that I could accomplish the following:</p>



<ul>
  <li>Write a separate plaintext file, written in Perl's Plain Old Documentation (POD) format, for each slide in the show. Each such plaintext file's name ended in .slide.txt:</li>
</ul>

<blockquote>
<pre>
chomp.slide.txt
defined.slide.txt
# false.slide.txt  # commented-out for illustrative purposes
hello.slide.txt
intro1.slide.txt
print.slide.txt
</pre>
</blockquote>

<ul>
  <li>Place the name of each slide in a master <i>slidelist</i> file.</li>
  <li>Control the order in which slides appear in the show by simply moving their names up or down within <i>slidelist</i>. For instance, a more logical order for the slides above might be:</li>
</ul>

<blockquote>
<pre>
intro1.slide.txt
hello.slide.txt
print.slide.txt
chomp.slide.txt
defined.slide.txt
</pre>
</blockquote>
<h3>A First Attempt at Using Seen-Hashes</h3>

<p>I had to make certain that, for each slide named in <i>slidelist</i>, the corresponding POD file actually existed in a particular subdirectory, <i>texts</i>, where I was writing such files. Put another way, I had to compare the list of filenames in <i>slidelist</i> with the list of actual files in <i>texts</i> and make sure that the first list was a mathematical subset of the second list. To code this, I first turned to Tom Christiansen and Nat Torkington's <i>Perl Cookbook</i> (O'Reilly &amp; Associates, 2003) and its discussion of what I have since come to call "seen-hashes"; i.e., hashes that record whether certain strings have been "seen" in particular lists. (These can be thought of as a kind of lookup table.)</p>

<p>In <a name="rl1"></a><a href="#l1">Listing 1</a>, I show the code I wrote before refactoring and optimizing. I first read the filenames in <i>slidelist</i> (lines 5-13); next read the POD files in <i>texts</i> (lines 15-17); then construct seen-hashes for each (lines 19-21). To determine whether <i>@selections</i> is a proper subset of <i>@sources</i>, I declare <i>$subset_status</i> and set its initial value to "1." I loop through the elements in <i>@sources</i> and if I encounter one that is not also a key in <i>%seen_selections</i>, I set <i>$subset_status</i> to a false value of "0" and exit the loop (lines 23-29).</p>

<p>At this point, I wanted to be warned if I had included a filename in <i>slidelist</i> that did not actually exist in <i>texts</i>. Lines 31-40 accomplish this and shut down the script early if those files are missing.</p>

<p>Next, I decided it would be nice to get a list of those slides for which I had created files but was not currently including in <i>slidelist</i>. Lines 50-60 accomplish this. So at this point, I had a validated list of slides I was ready to turn over to the slidebuilder mechanism itself (code not shown here).</p>

<p>Along with my slideshow, I was developing some basic Perl scripts that I distributed to my students in a printed handout. As not all these scripts made it into the final version of the handout, I created another master file, this time called <i>scriptslist</i>, with which I could control the order in which the selected scripts appeared in the handout&#151;just as I did for the slides with <i>slidelist</i>. Except for some variable names, the code was exactly the same as in <a name="rl1"></a><a href="#l1">Listing 1</a>.</p>
<h3>Refactoring</h3>

<p>At that point, I heard Mark Jason Dominus's voice crying out, "Repeated code is a mistake!" Beginning with Yet Another Perl Conference::North America in Pittsburgh in 2000, I had attended several of Mark's "Perl Program Repair Shop" talks and had absorbed two key points:</p>



<ul>
  <li>Code that is repeated within a single script should be refactored into a subroutine.</li>
  <li>Code that is used in more than one script should be refactored into a module.</li>
  </ul>

<p>The fact that I was using the same code to manage <i>scriptslist</i> as I was for <i>slidelist</i> meant that I clearly had the makings of a module. But what functions were to go into that module?</p>

<p>I reread the relevant recipes in the <i>Perl Cookbook</i> and noticed that Tom and Nat used seen-hashes to derive several interesting relationships among two lists:</p>



<ul>
  <li>The union of the two lists.</li>
  <li>The intersection of the two lists.</li>
  <li>Items unique to the first of two lists (what Tom and Nat call "simple difference").</li>
  <li>The symmetric difference of the two lists: items found in one or the other of two lists, but not both.</li>
  </ul>

<p>In <a name="rl1"></a><a href="#l1">Listing 1</a>, the list described by keys <i>%seen_selections</i> in lines 33-37 was clearly a list of items unique to the first of two lists. In addition, I had used seen-hashes to derive two relationships not explicitly described in the <i>Cookbook</i>:</p>



<ul>
  <li>Items unique to the second of two lists were found in <i>@unused</i>, derived between lines 42 and 50.</li>
  <li>The "left-to-right subset status" derived between lines 23 and 29.</li>
  </ul>

<p>Wouldn't it be nice, I thought, to have a function that, when provided with two lists as inputs, returned all the most interesting relationships between the lists?</p>
<h3>The Birth of <i>List::Compare</i></h3>

<p>And thus, I had the inspiration for <i>List::Compare</i>. During the next month, when I wasn't busy writing slides for my students, I was creating an object-oriented interface in which:</p>




<p>
 1. References to arrays holding two lists are passed to the constructor:
</p>


<blockquote>
<pre>
use List::Compare;
$lc = List::Compare-&gt;new(\@selections, \@sources);
</pre>
</blockquote>

<p>
2. The constructor's initializer computes all of the most interesting relationships between the two lists (see <a name="rl2"></a><a href="#l2">Listing 2</a>).
</p>
<p>
3. The results of those computations are stored in a hash that is blessed into the <i>List::Compare</i> object.
</p>
<p>
4. <i>List::Compare </i>methods simply return the various relationships:
</p>
<blockquote>
<pre>

@union        = $lc-&gt;get_union;
@intersection = $lc-&gt;get_intersection;
@Lonly        = $lc-&gt;get_unique;
@Ronly        = $lc-&gt;get_complement;
@LorRonly     = $lc-&gt;get_symmetric_difference;
$LR           = $lc-&gt;is_LsubsetR;
</pre>
</blockquote>

<p>(For additional interlist relationships not described here, see the <i>List::Compare</i> documentation.)</p>

<p>Using <i>List::Compare</i>, I can rewrite <a name="rl1"></a><a href="#l1">Listing 1</a>, as shown in <a name="rl3"></a><a href="#l3">Listing 3</a>. What once was a 60-line script is now only 41 lines. Moreover, should I need at this point additional relationships between the two lists (e.g., their symmetric difference), I could simply call the appropriate <i>List::Compare</i> method and get the result without additional computation.</p>
<h3>A More Mature <i>List::Compare</i></h3>

<p>I first showed <i>List::Compare</i> to other Perl hackers at the May 2002 meeting of Perl Seminar New York and its first CPAN version was uploaded the next month just before YAPC::NA::2002 in St. Louis. Since that time, I have worked on both its interface and internals in an attempt to allow potential users maximum flexibility and, where possible, speed improvements. Here are the most significant improvements:</p>



<p><b>Accelerated Mode. </b>Shortly after <i>List::Compare</i>'s first presentation, Perl Seminar NY member Josh Rabinowitz argued that if a user wanted to compute only one comparison between two lists (e.g., just their intersection), <i>List::Compare</i> should not spend time computing any other relationships between the two lists.</p>

<p>In response, I decided to offer the user the option of an "accelerated" mode in which the user passes <i>-a</i> as the first argument to the constructor:</p>

<blockquote>
<pre>
$lca = List::Compare-&gt;new('-a', \@selections, \@sources);
@intersection = $lca-&gt;get_intersection;

</pre>
</blockquote>

<p>Internally, the constructor calls an initializer, which populates the object with references to the lists submitted as arguments rather than with the results of computations of set relationships. In the accelerated mode, it is the individual method called that does the computation, not the initializer.</p>

<p>Preliminary benchmarking showed that if a user indeed wanted to extract only one comparison between two lists, the accelerated mode was faster. However, it was no faster than the regular mode if the user wanted two comparisons, and the regular mode pulled ahead if the user wanted three or more comparisons.</p>



<p><b>Multiple Mode.</b> Not long after <i>List::Compare</i>'s CPAN debut, I wondered: Why should a user be restricted to comparisons between just two lists? Why shouldn't a user be able to compute, say, the intersection of three or more lists at a time? So I set out to develop a "multiple" mode in which a user would simply pass additional lists as arguments to the constructor; see <a name="re1"></a><a href="0405be1.html">Example 1</a>.</p>

<p>Comparing three or more lists at a time, however, requires a more careful specification of certain comparisons. When calling the <i>get_unique()</i> method, for example, I wanted the user to be able to get those items unique to, say, <i>@Carmen</i> and not just to the first list passed by reference to the constructor. Therefore, I designed that method's interface so the user could specify the index position of the targeted list as an argument to the method. Since Perl starts counting at zero, <i>@Carmen</i>'s index position is "2" and a list of items unique to that array is generated like this:</p>

<blockquote>
<pre>
@unique_Carmen = $lcm-&gt;get_unique(2);

</pre>
</blockquote>



<p>Similarly, the items <i>not </i>found in <i>@Don</i> would be generated like so:</p>

<blockquote>
<pre>
@complement_Carmen = $lcm-&gt;get_complement(3);

</pre>
</blockquote>



<p>Enabling <i>List::Compare</i> to handle more than two lists also enables us to define two new relationships among the various lists: the complement of their intersection and the complement of their symmetric difference. But since that's very verbose, I have simplified their names for the purpose of method calls and documentation into <i>nonintersection</i> and <i>shared</i>.</p>

<p><i>List::Compare</i> defines the nonintersection of several lists as those found in any of the lists passed to the constructor that do not appear in all of the lists (i.e., all items except those found in the intersection of the lists):</p>

<blockquote>
<pre>
@nonintersection = $lcm-&gt;get_nonintersection;

</pre>
</blockquote>



<p><i>List::Compare</i> defines items shared among several lists as those which appear in more than one of the lists passed to the constructor (i.e., all items except those found in their symmetric difference):</p>

<blockquote>
<pre>
@shared = $lcm-&gt;get_shared;

</pre>
</blockquote>



<p>At first, <i>List::Compare</i>'s <i>multiple</i> mode did not have an "accelerated" variant for faster calculation of just one relationship among three or more lists, but this limitation has been eliminated in the last year. To choose accelerated calculation, pass the <i>-a </i>option as the first argument to the constructor:</p>

<blockquote>
<pre>
$lcm = List::Compare-&gt;new(
		   '-a', \@Al, \@Bob, \@Carmen, \@Don, \@Ed
	   );
@intersection = $lcm-&gt;get_intersection;

</pre>
</blockquote>
<h3>Returning Array References</h3>

<p>Later in 2002, I had occasion to use <i>List::Compare</i> in some work on my day job for the New York State Office of Mental Health. I was using the <i>get_union</i> method to return a list, but then had to immediately pass a reference to the array holding that list to another function.</p>

<blockquote>
<pre>
@union = $lc-&gt;get_union;
some_other_function(@union);

</pre>
</blockquote>

<p>Since having subroutines receive arguments by&#151;and return results by&#151;references is faster than by receiving/returning whole lists, why not allow <i>List::Compare</i> to return just a reference to a list? This led to a new set of <i>List::Compare</i> <i>_ref</i> methods:</p>

<blockquote>
<pre>
$unionref = $lc-&gt;get_union_ref;
some_other_function($unionref);

</pre>
</blockquote>

<p>After this revision had been uploaded to CPAN, Glenn Maciag of Perl Seminar NY noted that if I had simply rewritten the <i>get_union()</i> method to use Perl's <i>wantarray</i> function to examine the method's calling context, I could have dispensed with a separate <i>get_union_ref()</i> method:</p>

<blockquote>
<pre>
@union    = $lc-&gt;get_union;
$unionref = $lc-&gt;get_union;  # NOT IMPLEMENTED

</pre>
</blockquote>



<p>However, since this approach was slightly less self-documenting and since the <i>_ref</i> methods were already out there on CPAN, I didn't implement his suggestion&#151;though I may in the future.</p>
<h3>Four Membership Methods</h3>

<p>In a subsequent revision, I gave <i>List::Compare</i> the ability to answer these questions:</p>



<p><b>Given a string, determine to which of the lists passed to the constructor the string belongs.</b></p>

<blockquote>
<pre>
@memb_arr = $lcm-&gt;is_member_which('golfer');

</pre>
</blockquote>



<p>The list returned by <i>is_member_which()</i> is a list of the indexes in the constructor's argument list in which <i>golfer</i> is found; i.e.:</p>

<blockquote>
<pre>
( 0, 1, 2 )

</pre>
</blockquote>



<p><b>Given several strings, determine to which of the lists passed to the constructor the various strings belong.</b> Do this by passing to the <i>are_members_which()</i> method a reference to an array holding references to the various lists under examination. Get a hash of array references as the return value:</p>

<blockquote>
<pre>
$memb_hash_ref = $lcm-&gt;are_members_which(
			    [ qw| abel baker fargo hilton zebra | ]
		        );

"$memb_hash_ref" will be:

{
	 abel     =&gt; [ 0             ],
	 baker    =&gt; [ 0, 1          ],
	 fargo    =&gt; [ 0, 1, 2, 3, 4 ],
	 hilton   =&gt; [    1, 2       ],
	 zebra    =&gt; [               ],
};

</pre>
</blockquote>

<p><b>Given a string, determine whether it can be found in any of the lists passed as arguments to the constructor.</b> Return 1 if a specified string can be found in any of the lists and 0 if it cannot:</p>

<blockquote>
<pre>
$found = $lcm-&gt;is_member_any('abel');

</pre>
</blockquote>



<p>In the example above, <i>$found</i> will be <i>1</i> because <i>abel</i> is found in one or more of the lists passed as arguments to <i>new()</i>.</p>

<p><b></p>

<p>Given several strings, determine if each such string can be found in any of the lists passed as arguments to the constructor.</b> Do this by passing to the <i>are_members_any()</i> method a reference to an array holding references to the various lists under examination. Get a hash reference as the return value where the value of each element is either <i>1</i> if the particular string can be found in any of the lists under examination and <i>0</i> if it cannot.</p>

<blockquote>
<pre>
$memb_hash_ref = $lcm-&gt;are_members_any(
			    [ qw| abel baker fargo hilton zebra | ]
		        );

"$memb_hash_ref" will be:

{
	 abel     =&gt; 1,
	 baker    =&gt; 1,
	 fargo    =&gt; 1,
	 hilton   =&gt; 1,
	 zebra    =&gt; 0,
};

</pre>
</blockquote>
<h3>Passing Seen-Hashes to the Constructor</h3>

<p>In the course of a Perl script, if a user has already created two or more lookup tables in the form of seen-hashes and needs to determine set relationships among the lists implied by those seen-hashes, it seemed to me that he or she should be able to pass references to those seen-hashes directly to the constructor. This is now possible; see <a name="re2"></a><a href="0405be2.html">Example 2</a>.</p>

<p>C&lt;@intersection&gt; will contain the following:</p>

<blockquote>
<pre>
qw( baker camera delta edward fargo golfer)

</pre>
</blockquote>
<p>
The constructor figures out for itself whether it has been passed arrays or seen-hashes.
</p>
<h3>Returning Unsorted Lists</h3>

<p>When I first wrote <i>List::Compare</i>, all comparison lists were sorted in ASCII-betical order by default. Since sorting imposes a cost in speed, a later revision enables a user who does not need a presorted list to pass an <i>unsorted</i> option to the constructor:</p>

<blockquote>
<pre>
$lc = List::Compare-&gt;new('-u', \@Llist, \@Rlist);

</pre>
</blockquote>



<p>or</p>

<blockquote>
<pre>
$lc = List::Compare-&gt;new('&#151;unsorted', \@Llist, \@Rlist);

</pre>
</blockquote>



<p>I haven't benchmarked this approach, yet, but my impression is that the speed boost is largely marginal. It's there for you to use; TMTOWTDI.</p>
<h3>A Functional Interface</h3>

<p>In the autumn of 2003, I began to wonder if an object-oriented interface to <i>List::Compare</i> was always necessary. What would I have to do to enable a user to pass references to several lists directly to a function such as <i>get_union()</i> rather than first passing them to a constructor?</p>

<p>This is now possible with <i>List::Compare::Functional</i>:</p>

<blockquote>
<pre>
use List::Compare::Functional qw( get_union get_complement );

# same 5 lists as above:  @Al, @Bob, @Carmen, @Don, @Ed

@union = get_union( [ \@Al, \@Bob, \@Carmen, \@Don, \@Ed ] );

</pre>
</blockquote>

<p>Note that, as with many Perl modules that employ a functional interface rather than an object oriented one, the user must specifically import the function(s) he or she later wishes to call. (Import tag groups are available; see the documentation.)</p>

<p>Note also that with <i>List::Compare::Functional</i>, the first argument passed to the function is a reference to an array (anonymous or named), which, in turn holds a list of references to the lists under examination. This proved necessary to distinguish the arguments representing the lists under examination from particular lists that are passed to certain functions. For example, if we wish to find those items not found in <i>@Don</i>, we need to pass a second argument (also via reference) like this:</p>

<blockquote>
<pre>
@complement_Don = get_complement(
		  	     [ \@Al, \@Bob, \@Carmen, \@Don, \@Ed ],
		   	     [ 3 ]
		 );

</pre>
</blockquote>

<p>Since, on the inside, much of <i>List::Compare::Functional</i>'s code works like that of <i>List::Compare</i>'s accelerated mode, and since it incurs no overhead for object creation, it should provide faster results than <i>List::Compare</i>. But the interface to each <i>List::Compare::Functional</i> function is necessarily less elegant than that to <i>List::Compare</i>'s methods.</p>
<h3>Conclusion</h3>

<p><i>List::Compare</i> offers a variety of ways to compare two or more lists. The Perl code around which it is built is old, well-tested, and certainly not mine. It is, at its best, a sound implementation of a good interface. It is not rocket science.</p>

<p>It should be noted that, with the exception of <i>List::Compare</i>'s <i>get_bag()</i> method (not discussed in this article), all comparisons conducted by <i>List::Compare</i> only ask whether a given string was seen in a given list at all. In general, <i>List::Compare</i> ignores how many times a string was seen in a given list. If you need to make decisions based on how many times a string was seen in a given list, don't use <i>List::Compare</i>; look elsewhere. You will find, however, many situations in which <i>List::Compare</i> or its <i>Functional</i> variants can save you from typing that <i>Perl Cookbook</i> code over again.</p>


<p><b>TPJ</b></p>



	
	
<br>
<br>
<b><a name="l1"></a>Listing 1</b><br>
<pre>#!/usr/bin/perl
use strict;
use warnings;

my ($raw, @selections, @sources);
open LIST, 'slidelist' or die "Can't open slidelist for reading:  $!";
while ($raw = &lt;LIST&gt;) {
        next if ($raw =~ /^\s+$/ or $raw =~ /^#/); # no comments or blanks
        next unless ($raw =~ /\.slide\.txt$/);
        chomp $raw;
        push(@selections, "texts/$raw");
}
close LIST or die "Cannot close slidelist:  $!";

opendir DIR, 'texts' or die "Couldn't open texts directory: $!";
@sources = map {"texts/$_"} grep {/\.slide\.txt$/} readdir DIR;
closedir DIR;

my (%seen_selections, %seen_sources);
$seen_selections{$_}++ foreach @selections;
$seen_sources{$_}++    foreach @sources;

my $subset_status = 1;
foreach (@selections) {
        if (! exists $seen_sources{$_}) {
                        $subset_status = 0;
                        last;
        }
}

unless ($subset_status) {
        my (%selections_only);
        foreach (keys %seen_selections) {
                        $selections_only{$_}++ if (! exists $seen_sources{$_});
        }
        print "These files, though listed in 'slidelist', are not found in 'texts' directory.\n\n";
        print "    $_\n" foreach (keys %selections_only);
        print "\nEdit 'slidelist' as needed and re-run script.\n";
        exit (0);
}

my (%intersection, %difference);
foreach (keys %seen_selections) {
        $intersection{$_}++ if (exists $seen_sources{$_});
}
foreach (keys %seen_sources) {
        $difference{$_}++ unless (exists $intersection{$_});
}

my @unused = keys %difference;
open UNUSED, "&gt;unused" or die "Could not open unused for writing: $!";
print UNUSED "Files currently unused:\n";
if (@unused) {
        print UNUSED "    $_\n" foreach (sort @unused);
        print "There are unused files; see 'unused'.\n";
} else {
        print UNUSED "    [None.]\n";
        print "There are no unused files.\n";
}
close UNUSED or die "Could not close unused: $!";</pre>
<a href="#rl1">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l2"></a>Listing 2</b><br>
<pre># Computation of Relationships between Two Lists in List::Compare's Regular Mode
# Regular Mode =&gt; Compares only two lists; computes all relationships at once;
# stores the results in a hash which is blessed into the List::Compare object.
# Below:  _init(), which is called by List::Compare's constructor and which returns
# a reference to the hash which the constructor will bless.

sub _init {
        my $self = shift;
        my ($unsortflag, $refL, $refR) = @_;
        my (%data, %seenL, %seenR);
        my @bag = $unsortflag ? (@$refL, @$refR) : sort(@$refL, @$refR);

        my (%intersection, %union, %Lonly, %Ronly, %LorRonly);
        my $LsubsetR_status = my $RsubsetL_status = 1;
        my $LequivalentR_status = 0;

        foreach (@$refL) { $seenL{$_}++ } 
        foreach (@$refR) { $seenR{$_}++ } 

        foreach (keys %seenL) {
                $union{$_}++;
                if (exists $seenR{$_}) {
                          $intersection{$_}++;
                } else {
                          $Lonly{$_}++;
                }
}

        foreach (keys %seenR) {
                $union{$_}++;
                $Ronly{$_}++ unless (exists $intersection{$_});
        }

        $LorRonly{$_}++ foreach ( (keys %Lonly), (keys %Ronly) );

        $LequivalentR_status = 1 if ( (keys %LorRonly) == 0);

        foreach (@$refL) {
                if (! exists $seenR{$_}) {
                        $LsubsetR_status = 0;
                        last;
                }
        }
        foreach (@$refR) {
                if (! exists $seenL{$_}) {
                        $RsubsetL_status = 0;
                        last;
                        }
        }

        $data{'seenL'}                = \%seenL; 
        $data{'seenR'}                = \%seenR; 
        $data{'intersection'}        = $unsortflag ? [    keys %intersection ] 
                                                                               : [ sort keys %intersection ];
        $data{'union'}                = $unsortflag ? [    keys %union ]
                                                                                                         : [ sort keys %union ];
        $data{'unique'}        = $unsortflag ? [    keys %Lonly ] 
                                                         : [ sort keys %Lonly ];
        $data{'complement'}        = $unsortflag ? [    keys %Ronly ] 
                                                         : [ sort keys %Ronly ];
        $data{'symmetric_difference'} = $unsortflag ? [    keys %LorRonly ] 
                                                         : [ sort keys %LorRonly ];
        $data{'LsubsetR_status'}        = $LsubsetR_status;
        $data{'RsubsetL_status'}        = $RsubsetL_status;
        $data{'LequivalentR_status'}  = $LequivalentR_status;
        $data{'bag'}                = \@bag;
        return \%data;
}</pre>
<a href="#rl2">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l3"></a>Listing 3</b><br>
<pre>#!/usr/bin/perl
use strict;
use warnings;
use List::Compare;

my ($raw, @selections, @sources);
open LIST, 'slidelist' or die "Can't open slidelist for reading:  $!";
while ($raw = &lt;LIST&gt;) {
        next if ($raw =~ /^\s+$/ or $raw =~ /^#/); # no comments or blanks
        next unless ($raw =~ /\.slide\.txt$/);
        chomp $raw;
        push(@selections, "texts/$raw");
}
close LIST or die "Cannot close slidelist:  $!";

opendir DIR, 'texts' or die "Couldn't open texts directory: $!";
@sources = map {"texts/$_"} grep {/\.slide\.txt$/} readdir DIR;
closedir DIR;

my $lc = List::Compare-&gt;new(\@selections, \@sources);
my $LR = $lc-&gt;is_LsubsetR;
unless ($LR) {
        my @slidelist_only = $lc-&gt;get_unique();
        print "These files, though listed in 'slidelist', are not found in 'texts' directory.\n\n";
        print "    $_\n" foreach (@slidelist_only);
        print "\n";
        print "Edit 'slidelist' as needed and re-run script.\n";
        exit (0);
}

my @unused = $lc-&gt;get_complement;
open(UNUSED, "&gt;unused") or die "Could not open unused for writing: $!";
print UNUSED "Files currently unused:\n";
if (scalar(@unused)) {
        print UNUSED "    $_\n" foreach (sort @unused);
        print "There are unused files; see 'texts/unused'.\n";
} else {
        print UNUSED "    [None.]\n";
        print "There are no unused files.\n";
}
close(UNUSED) or die "Could not close unused: $!";</pre>
<a href="#rl3">Back to article</a><br>
	
	
	


</body>
</html>