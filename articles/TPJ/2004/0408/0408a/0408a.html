
<html>
<head>
<title>August, 2004: Regex Arcana</title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; The Perl Journal-->

<h1>Regex Arcana</h1>
<p><i>The Perl Journal</i> August, 2004</p>
<h2></h2>


<h3>By Jeff Pinyan</h3>


<I>Jeff "japhy" Pinyan, author of Regexp::Parser, is finishing up at RPI in Troy, NY. He can be contacted at japhy@perlmonk.org.</I>

<hr>





<p>Perl's regular expressions (hereafter known as "regexes") have come a long way since Version 1.0. They're like the eccentric uncle who spends a few years sojourning somewhere exotic and comes back older, wiser, and more worldly. Or perhaps, in Perl's case, more out-of-this-worldly. Perl's regexes aren't what computer science purists would call "regular"&#151;that idea was thrown out the window when backreferences became something you could include in the pattern itself, as shown in this simple dynamic regex:</p>

<blockquote>
<pre>
# find any any doubled words
my $text = "Four score and and seven years ago";
while ($text =~ /\b(\w+)\W+\1\b/g) {
  print "'$1' repeats itself";
  # 'and' repeats itself
}

</pre>
</blockquote>

<p>This regex is self-referential: The backreference <i>\1</i> tells the regex engine to match whatever has been captured into <i>$1</i>. This means the regex doesn't know for certain what it will match until runtime. And this is only the tip of the iceberg.</p>

<p>I'm going to share two pieces of regex arcana with you&#151;features of the Perl regex engine that aren't used often, but offer those who can master them great power.</p>
<h3>Code Evaluation</h3>

<p>The code evaluation assertion <i>(?{ CODE })</i> was introduced in Perl 5.005, but it was rough around the edges. In Perl 5.6, it was fine-tuned and more adjustments were made by 5.8. The code evaluation assertion allows a regex to execute an arbitrary block of Perl code during the course of its pattern matching. This device becomes particularly interesting when one realizes that the backtracking stack is much like a variable scoping stack.</p>
<h3>Delayed Execution</h3>

<p>The delayed execution assertion <i>(??{ CODE })</i> is the heart of dynamic regexes. Using them is like walking across a bridge as you're building it. Their true beauty is revealed when you create a <i>Regexp</i> object (via <i>qr//</i>) with a delayed execution assertion in it. Inside that assertion is the self-same <i>Regexp</i> object. Then you have created a recursive regex, one that is capable of matching nested data structures or acting like a proper grammar.</p>
<h3>Getting Up to Speed</h3>

<p>This article is going to use some regex variables you might not be familiar with, so I introduce them to you in <a name="rt1"></a><a href="0408at1.html">Table 1</a>.</p>

<p>Here is a simple example exhibiting these variables:</p>

<blockquote>
<pre>
"perl" =~ /(.(..).)/;
# $1  = "perl"
# $2  = "er"
# $+  = "er" (in this case, $2)
# $^N = "perl" (in this case, $1)
# @-  = (0, 0, 1)
# @+  = (4, 4, 3)

</pre>
</blockquote>

<p>After a successful pattern match against <i>$str</i>, the pair of values <i>$-[X]</i> and <i>$+[X]</i> hold the offsets in <i>$str</i> of the <i>Xth</i> capture group. <i>$-[0]</i> and <i>$+[0]</i> are the offsets for <i>$&amp;</i>:</p>

<blockquote>
<pre>
substr($str, 0, $-[0]);		# produces $'
substr($str, $-[0], $+[0] - $-[0]);	# produces $&amp;
substr($str, $+[0]);			# produces $'
substr($str, $-[1], $+[1] - $-[1]);	# produces $1
substr($str, $-[2], $+[2] - $-[2]);	# produces $2 (etc.)

</pre>
</blockquote>

<p>You are probably aware that <i>$&amp;</i> and its friends are bad news. They cause reductions in the speed of your programs, so use them only when debugging. If you use them once in your code, you might as well use them as many times as you can in that code, because you've already suffered the hit.</p>
<h3>Using (?{ ... })</h3>

<p>Our first incantation is the code evaluation assertion. It will execute the code inside when it is encountered, and then continue with the regex; they always succeed, unless their contents interrupt the execution of your program.</p>

<p>Let's look at some applications of this assertion.</p>
<h3>Inspecting a Regex's State</h3>

<p>It has been said that "you can't observe the behavior of a system without affecting the system's behavior." Not so, at least in Perl 5.8. The following regex uses code assertions to show how a particular optimization in the engine works:</p>

<blockquote>
<pre>
# the /x modifier allows embedded whitespace
# to help improve readability and clarity
"salad" =~ m{
  .* (?{ print "[$&amp;] " }) a
  .* (?{ print "($&amp;) " }) a
}x;
# output:
# [sal] [s] (sal)

</pre>
</blockquote>

<p>We often see backtracking as a character-at-a-time procedure, but it's really much more efficient than that. In this case, something like <i>.*a</i> makes the engine jump from one "a" to the next when matching or backtracking. The code assertion doesn't get in the way of this, even though it's in-between the <i>.*</i> and <i>a</i>. (Sadly, in Perl 5.6, it did get in the way. Try that code in 5.6 and you'll see it stops the optimization from occurring.)</p>

<p>You have access to the regex variables inside a code assertion; my example above uses <i>$&amp;</i>, although it's general practice to avoid that variable. You can also access <i>$1</i> and other digit variables, <i>$^N</i>, <i>$+</i>, and the arrays <i>@-</i> and <i>@+</i>. One caveat, though, is that you can't access a capture group until it has been completed; that is, the closing parenthesis for <i>$1</i> must be passed before you can see the contents of <i>$1</i> in a code assertion, otherwise you'll be seeing its previous value. This code won't print anything inside the quotes because <i>$1</i> hasn't been closed before it's printed:</p>

<blockquote>
<pre>
"perl" =~ m{
  ( (?: . (?{ print "'$1'\n" }) )+ )
}x;

</pre>
</blockquote>

<p>In order to inspect the digit variables as they're being created, you need to be cunning: Create a variable inside a code evaluation immediately prior to the capture group that stores the current location in the string. Then, use <i>substr()</i> on <i>$_.</i> This is a copy of the string you're matching against inside a code evaluation:</p>

<blockquote>
<pre>
"perl" =~ m{
  (?{ $p = $-[0] })
  ( (?:
    .
    (?{ print substr($_, $p, $+[0] - $p), "\n" })
  )* )
}x;

</pre>
</blockquote>

<p>That <i>$_</i> trick is undocumented as far as I can tell, which means it might not stay that way in the future. (But considering Perl 6 regexes will be radically different, we should have fun while we can.)</p>

<p>In this way, code evaluation assertions are good debugging tools because you can use them without interfering with the execution of the regex and you can get helpful diagnostic information, such as where you are in the string, what you've captured, and so on.</p>
<h3>Capturing Repetitions</h3>

<p>I've often been asked why a regex like <i>/(\w)+/</i> only returns the last word character captured:</p>

<blockquote>
<pre>
"japhy" =~ /(\w)+/ and print "&lt;$1&gt;";  # 'y'

</pre>
</blockquote>



<p>The reason is because the regex is continually storing what <i>\w</i> matches to <i>$1</i>, and each time, it's overwriting the old contents. It won't create <i>$2</i>, <i>$3</i>, and so forth, and it doesn't create a <i>@1</i> array. So the question remains: How can I keep track of repeated capture groups?</p>

<p>The answer works on the principle that the matching and backtracking in a regex is very similar to a variable scoping stack if you use <i>local()</i> on your variables inside code evaluations. <i>local()</i> works differently in a regex; it gives a variable a value that remains until that point is backtracked past. Let's say we wanted to count how many characters we matched before the last "<i>r</i>" in a string. Here's code that doesn't use <i>local()</i>:</p>

<blockquote>
<pre>
# not local()ized
"perl" =~ m{
  (?{ $x = 0 })
  (?: \w (?{ ++$x }) )*
  r
}x and print "x = $x\n";

</pre>
</blockquote>

<p>It prints "4" as the value of <i>$x</i>, even though there are only two. This time, we'll use <i>local()</i> on a temporary variable and assign its value to <i>$x</i> only after the regex has succeeded:</p>

<blockquote>
<pre>
# local()ized
"perl" =~ m{
  (?{ local $_x = 0 })
  (?: \w (?{ local $_x = $_x + 1 }) )*
  r
  (?{ $x = $_x })
}x and print "x = $x\n";

</pre>
</blockquote>

<p>This time the value is reported correctly as "<i>2</i>."</p>

<p>The first code prints "4" because when the backtracking occurs, <i>$x</i>'s value isn't rewound or reverted to its previous one. The second code prints "<i>2</i>" for the opposite reason: Because of the use of <i>local()</i>, when backtracking occurs, the scoped variable <i>$_x</i> reverts to its previous value. Think of your regex as being a recursive function.</p>

<p>Although it requires a bit more coding on your part, this technique can be extended to let you keep track of capturing groups that have quantifiers on them:</p>

<blockquote>
<pre>
"age=22 name=jeff state=NJ" =~ m{
  (?{ local (@_1, @_2) })
  (?:
    ([^=]+) = (\S+) \s*
    (?{ local @_1 = (@_1, $1);
        local @_2 = (@_2, $2); })
  )*
  (?{ (@1 = @_1), (@2 = @_2) })
}x;

</pre>
</blockquote>



<p>The code above produces two arrays, <i>@1</i> and <i>@2</i>, whose contents are "<i>age</i>," "<i>name</i>," "<i>state</i>," and "<i>22</i>," "<i>jeff</i>," "<i>NJ</i>," respectively.</p>

<p>If you're wondering why I didn't just write <i>push(@_1, $1)</i>, it's because I have to make a new locally scoped array, and <i>push()</i> wouldn't do that. You must use <i>local()</i> every time you need a variable to revert to its previous value when it's backtracked past.</p>
<h3>Using <i>$^R</i> to Simplify Things</h3>

<p>There's another regex variable, <i>$^R</i>, which stores the value of the last <i>(?{ ... })</i> assertion. Perl is nice and makes <i>$^R</i> scope properly, so if your regex backtracks, <i>$^R</i> will revert to its previous value. This means we can write a much simpler looking regex:</p>

<blockquote>
<pre>
"perl" =~ m{
  (?{ 0 })	# sets $^R to 0
  (?:
    \w
    (?{ $^R + 1 })	# sets $^R to $^R + 1
  )*
  r
  (?{ $x = $^R })	# sets $x to $^R
}x and print "x = $x\n";

</pre>
</blockquote>

<p>Here, we don't need to worry about localizing our variables because <i>$^R</i> is properly dealt with by Perl. You need to be careful about how you modify it, though. We could not have written <i>(?{ $^R++ })</i> or <i>(?{ ++$^R })</i> in our regex because both of those explicitly assign to <i>$^R</i>. You usually don't want to assign to <i>$^R</i> explicitly; just make the last value in your code assertion be the value you want <i>$^R</i> to have. Perl builds up a stack of the values returned by code evaluations and rolls that stack back when backtracking occurs. Setting <i>$^R</i> explicitly interrupts that stacking procedure.</p>

<p>We can use <i>$^R</i> with our example that creates <i>@1</i> and <i>@2</i> as well. We have to be careful in how our code assertions work:</p>

<blockquote>
<pre>
"age=22 name=jeff state=NJ" =~ m{
  #     @1  @2
  (?{ [ [], [] ] })
  (?:
    ([^=]+) = (\S+) \s*
    (?{ [ 
      [ @{$^R-&gt;[0]}, $1 ],
      [ @{$^R-&gt;[1]}, $2 ],
    ] })
  )*
  (?{
    @1 = @{ shift @{$^R} };
    @2 = @{ shift @{$^R} };
  })
}x;

</pre>
</blockquote>

<p>You cannot rely on what value <i>$^R</i> will have outside of your regex, so you should always get the data from it at the very end of your regex, once you are sure it has succeeded.</p>
<h3>If-Then Assertions</h3>

<p>You can also use code assertions as the condition to an if-then assertion. If you're not familiar with them, they look like <i>(?(cond)true|false)</i>. If the <i>cond</i> part evaluates to a true value, then the true pattern is matched; otherwise, the false pattern is matched. If the selected pattern fails to match, the if-then assertion fails altogether. The <i>cond</i> part can be a look-ahead, a look-behind, a number referring to a capture group, or a code assertion. If it is a code assertion, <i>$^R</i> does not get its value changed.</p>

<p>Here's a regex that matches <i>$_</i> if it is an integer less than 100:</p>

<blockquote>
<pre>
my $less_than_100 = qr{
  ^ ( -?\d+ ) $       # match the entire number
  (?(?{ $1 &gt;= 100 })	# if $1 &gt;= 100...
    (?!)		#   then fail
  )		# otherwise it succeeds
}x;
if ($n =~ $less_than_100) {
  # $n is less than 100
}

</pre>
</blockquote>
<h3>Using <i>(??{ ... })</i></h3>

<p>Our second gimmick is the delayed execution assertion. It acts very much like a code evaluation assertion, but the return value is returned to the regex as something to be matched. Consider our first example, which finds words that are repeated&#151;here's a way to write it using a delayed execution assertion:</p>

<blockquote>
<pre>
while ($text =~ /\b(\w+)\W+(??{ $1 })\b/g) {
  print "'$1' is repeated\n";
}

</pre>
</blockquote>

<p>Because <i>$1</i> only contains word characters, I didn't need to worry about any regex-specific characters being interpolated incorrectly. However, if we change from "words" to "non-whitespace chunks," we'll need to be safer:</p>

<blockquote>
<pre>
while ($text =~ m{
  (?&lt;!\S) (\S+)		# a chunk of non-spaces
  \s+			# some whitespace
  (??{ quotemeta $1 }) (?!\S)	# that chunk again
}xg) {
  print "'$1' is repeated\n";
}

</pre>
</blockquote>

<p>If you're curious why I didn't use <i>\b</i> in this regex, it's because I'm not looking for word boundaries, so I can't count on it working properly.</p>

<p>But that's a very silly use of a very powerful feature. It's not very economically sound to use your car to drive one city block and this assertion is an SUV. So let's go off-roading.</p>
<h3>Self-Constructing Regexes</h3>

<p>One of the first uses I found for this assertion was to match a chunk of unique characters in a string. Take, for example, "the quick brown fox jumped over the lazy dog." If I want to grab the first chunk of unique characters, I use an assertion that builds a character class based on the characters it has already matched:</p>

<blockquote>
<pre>
my $unique = qr{
  .		# match any character
  (??{		# evaluate and match...
    "[^\Q$&amp;\E]"	# a self-modifying char class
  })*		# zero or more times
}sx;
my ($uniq_str) = $string =~ /($unique)/;

</pre>
</blockquote>

<p>It's important to realize that we're not matching the same character class over and over again. The delayed execution assertion is being reexecuted each time. We can see what the character class looks like if we want:</p>

<blockquote>
<pre>
my $unique = qr{
  .
  (??{
    print my $cc = "[^\Q$&amp;\E]";
    $cc;
  })*
}sx;
my ($uniq_str) = $string =~ /($unique)/;

</pre>
</blockquote>

<p>On "the quick brown...," this gives the output:</p>

<blockquote>
<pre>
[^t]
[^th]
[^the]
[^the\ ]
[^the\ q]
[^the\ qu]
[^the\ qui]
[^the\ quic]
[^the\ quick]

</pre>
</blockquote>

<p>It stops when it reaches the second occurrence of a space because the character class fails to match something it hasn't seen before. If we wanted to ignore spaces, we could simply add the ability to match any whitespace characters before we try matching a new character:</p>

<blockquote>
<pre>
qr{
  . (?: \s+ | (??{ "[^\Q$&amp;\E]" }) )*
}xs

</pre>
</blockquote>

<p>This time we would match "the quick brown f."</p>
<h3>Dynamic Quantifiers</h3>

<p>You can use this assertion to make a quantifier based on text you've matched. If you have a string like "2aaaa 6a 3aaa 5 1a," and you want to match a number if it is followed by exactly that many a's, you capture the number and then make use of a delayed execution assertion, which uses that number as its quantifier:</p>

<blockquote>
<pre>
@matches = $data =~ m{
  \b (\d+)
  (??{ "a{$1}" })
  (?! a )
}gx;

</pre>
</blockquote>



<p>You could be more efficient and use <i>"a" x $1</i> instead of <i>"a{$1}"</i>, but the example can be expanded to match a lower and upper limit:</p>

<blockquote>
<pre>
# match X,Y, then match between X and Y a's
$data = "1,3aa  2,5aaaaaaa 2,7aaaa";
@matches = $data =~ m{
  \b (\d+) , (\d+)
  (??{ "a{$1,$2}" })
  (?! a )
}xg;

</pre>
</blockquote>

<p>Of course, you'd have to make sure <i>$1</i> isn't greater than <i>$2</i>, but that's left as an exercise to the reader.</p>

<p>Let's look back to our "is X less than 100?" example. We can rewrite this with <i>(??{ ... })</i> instead:</p>

<blockquote>
<pre>
if ($num =~ m{
  ^ (-?\d+) $
  (??{ $1 &gt;= 100 and '(?!)' })
}x) {
  # $1 is less than 100
}

</pre>
</blockquote>

<p>Here, if <i>$1</i> is equal to or greater than 100, we insert <i>(?!)</i> into the regex, an empty negative look-ahead, which always fails. If <i>$1</i> is less than 100, then our <i>&gt;= </i>operation returns "" as its false value, and the regex succeeds.</p>
<h3>Recursive Regexes</h3>

<p>More often than not, you'll see these assertions used to create recursive regexes, ones that can match arbitrarily deeply nested parentheses, etc. To get this to work, you create a <i>Regexp</i> object and then put that object inside a delayed execution assertion:</p>

<blockquote>
<pre>
# if you're using a lexical, it MUST be
# declared before it is assigned to
my $px;
$px = qr{
  (?:
    (?&gt; [^\\()]+ | \\. )
    |
    \( (??{ $px }) \)
  )*
}xs;

</pre>
</blockquote>

<p>Now we can make sure a string is properly <i>()</i>-balanced:</p>

<blockquote>
<pre>
my $good = 'a + (b / (c - 2)) * (d ^ (e+f))';
my $bad1 = 'a + (b / (c - 2) * (d ^ (e+f))';
my $bad2 = 'a + (b / (c - 2)) * (d) ^ (e+f))';
print "not " if $good !~ /^$px$/; print "ok\n";
print "not " if $bad1 !~ /^$px$/; print "ok\n";
print "not " if $bad2 !~ /^$px$/; print "ok\n";

</pre>
</blockquote>

<p>The first is reported OK, the second and third are reported as not OK (which is what we would expect).</p>

<p>Using this recursive mechanism, we can create a regex that matches a palindrome. A palindrome is a string that reads the same forwards and backwards, like "toot," or "madam, I'm adam." Punctuation and case are ignored.</p>

<blockquote>
<pre>
my $pdrome;
$pdrome = qr{
  (\w) \W*
  (??{ $pdrome })?
  \W* \1
}xi;

</pre>
</blockquote>



<p>This matches "toot," but not "rotor." Can you see why?</p>

<p>There is a boundary case: If the string has an odd number of letters, then the turning point letter won't be repeated. Let's add support for that:</p>

<blockquote>
<pre>
my $pdrome;
$pdrome = qr{
  (\w) \W*
  (??{ $pdrome })?
  \W* \1
  |
  \w
}xi;

</pre>
</blockquote>

<p>Now we can match "Sit on a potato pan, Otis."</p>
<h3>The Future: Perl 6</h3>

<p>So those are the two workhorses of Perl's eccentric regex implementation. In Perl 6 (http://dev.perl.org/perl6/), these features won't disappear. In fact, the code evaluation assertion will probably become increasingly prevalent. Here's a quick taste of how Perl 6 will make these assertions easier to write and simpler to understand.</p>
<h3>Code Evaluation</h3>

<p>In Perl 6, regex metacharacters will change meaning to make things clearer. Code evaluation assertions are now just enclosed by curly braces. Our regex earlier that matches a number that is less than 100 could be written in Perl 6 like this:</p>

<blockquote>
<pre>
/ ^ (\d+) $ { $1 &lt; 100 or fail } /

</pre>
</blockquote>



<p>The first thing you should notice is that, with all that whitespace, there is no <i>/x</i> modifier on my regex. That's because it will be the default. Next, you can see the code evaluation is <i>{ code }</i>, which looks like a closure (because it is). Finally, even though this is merely a code evaluation, it can affect the regex by calling <i>fail</i>.</p>
<h3>Delayed Execution</h3>

<p>But the syntax <i>{ COND or fail }</i> has a shorthand in Perl 6, by way of the new look of the delayed execution assertion: <i>&lt;...&gt;</i>. You'll be able to use <i>&lt;$rx&gt;</i> to get the same effect as <i>(??{ $rx })</i>. A special case of this assertion is <i>&lt;(...)&gt;</i>, which regards its internals as an expression that causes the match to fail at its current point if it evaluates to false:</p>

<blockquote>
<pre>
/ ^ (\d+) $ &lt;( $1 &lt; 100 )&gt; /

</pre>
</blockquote>


<h3>Backtracking Control</h3>

<p>Finally, a note on efficiency. Our regex here is currently less efficient than it should be. After a number like "300" fails, Perl backtracks the <i>\d+</i> to match "30," which fails because "30" is followed by "0," not the end of the string. But Perl still does this needless backtracking. In Perl 5, we would use <i>(?&gt; (\d+) )</i>, the "cut" assertion, which prevents internal backtracking.</p>

<p>In Perl 6, you'll be able to say it more succinctly with a colon (or two, or three). In our case, we'll want two colons, which says that this set of alternatives is all to fail if we have to backtrack past the <i>::</i>. Since our regex has no alternatives in it, it means this match will fail if the <i>::</i> is backtracked past.</p>

<blockquote>
<pre>
/ ^ (\d+) $ :: &lt;( $1 &lt; 100 )&gt; /

</pre>
</blockquote>

<p>We can get even sleeker&#151;if we use <i>&lt;commit&gt;</i> instead of <i>::</i>, the entire regex will fail, not just the match at the current location. This will eliminate our need for anchors:</p>

<blockquote>
<pre>
/ (\d+) &lt;commit&gt; &lt;( $1 &lt; 100 )&gt; /

</pre>
</blockquote>
<h3>Food for Thought</h3>

<p>With your new knowledge, you should try tackling these two tasks. First, construct a regex that determines the longest sequence of unique characters in a string. Now, decipher the goal of this regex. It uses both kinds of code assertions, the if-then assertion, and the <i>$^R</i> variable:</p>

<blockquote>
<pre>
my %data = $str =~ m{
  (?{ [ "", "", "" ] })
  ( [^\s=]+ ) \s* = \s*
  (?(?=["'])
    (?{
      my $c = substr($_, $+[0], 1);
      [ $c, "[^\Q$c\E]*", $c ]
    })
    |
    (?{ [ "", '\S+', "" ] })
  )
  (??{ $^R-&gt;[0] })
  ((??{ $^R-&gt;[1] }))
  (??{ $^R-&gt;[2] })
}xg;

</pre>
</blockquote>

<p>To see its dissection, go to http://japhy.perlmonk.org/regexes/.</p>


<p><b>TPJ</b></p>




</body>
</html>