
<html>
<head>
<title>February, 2004: An Almanac in Perl</title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; The Perl Journal-->

<h1>An Almanac in Perl</h1>
<p><i>The Perl Journal</i> February, 2004</p>
<h2></h2>


<h3>By brian d foy</h3>


<I>brian has been a Perl user since 1994. He is founder of the first Perl Users Group, NY.pm, and Perl Mongers, the Perl advocacy organization. He has been teaching Perl through Stonehenge Consulting for the past five years, and has been a featured speaker at The Perl Conference, Perl University, YAPC, COMDEX, and Builder.com.</I>

<hr>


<p><i>brian is currently on active duty with the United States Army as a military policeman in Iraq. You can e-mail him at comdog@ panix.com, if you don't mind waiting a couple months for a reply.</i></p>





<p>I have been living in the middle of the desert for several months while on active duty with the U.S. Army and, occasionally, we take advantage of our night vision technology. In any conflict, the ability to see at night has been important. Night vision can be limited by weather and climate effects such as cloud cover or fog, but over here it is mostly affected by the phase of the moon. We consider that full or new moons give off 100 percent illumination, a half-moon 50 percent, and so on; and whenever we plan any night operation, we try to schedule it for the lowest illumination possible to make maximum use of our night vision devices. This is nothing new&#151;soldiers in any war realize that the darker it is, the easier it is to sneak around unseen. All of this information is available in almanacs, but I am going to create my own since Perl makes it so easy.</p>

<p>The lunar cycle is 28 days, so the same phase comes at different days of each month. If I marked a known phase on my calendar, I could count the right number of days to the next phase, and through that process, create my own almanac. Indeed, you can buy such books. That way, however, does not let me play with Perl and, eventually, I want to create a program to give me a summary of the day's information.</p>

<p>In <a name="rl1"></a><a href="#l1">Listing 1</a>, lines 3 to 6 pull in the modules I need. The <i>Astro::MoonPhase</i> module does most of the hard work for me, and some of the Time and Date modules handle the rest. I want to make a graph of luminosity as a function of the date. The <i>GD::Graph</i> module takes care of visuals as long as I come up with the data.</p>

<p>Lines 8 and 9 look at the command-line arguments to get the time zone offset and year for which I want to create the graph; otherwise, I assume I am in Greenwich during the current year. On line 12, I do a little trickery to get the first Sunday in the year because I want that to be my first data point. When I label the dates on the graph, I will make a mark for each Sunday. The seventh element that <i>localtime()</i> returns gives me the day of the week, starting at 0 for Sunday, so I keep looping through the days of the year until <i>localtime()</i> gives me a false value, taking into account the time zone difference.</p>

<p>On line 22, I get the latest time in the year for which I want to calculate the luminosity, and I will use that as a limit when I start generating data points. The <i>for() </i>loop on line 25 is the interesting work. I start at the first Sunday time, stored in <i>$now</i>, and go to the end of the year, stored in <i>$then</i>, and I create a data point every three hours. I keep the list of times in <i>@times</i> and the luminosity in <i>@illum</i>. I could have put these in a hash, but I am going to take them right back out to plot them, so I just use named arrays.</p>

<p>On line 35, I create the <i>@data </i>array to pass off to <i>GD::Graph</i>. The <i>GD::Graph </i>modules expect the data points to be in an array of arrays. The first anonymous array element is the horizontal axis value and the rest are the vertical axis values.</p>

<p>On line 37, I create a new graph object by telling <i>GD::Graph </i>that I want a line graph that is 800 pixels wide and 300 pixels long. On line 39, I set the options for the graph. The interesting options include the <i>x_number_format</i>, which I give an anonymous subroutine to translate the time data to an English date, and the <i>x_tick_number,</i> which tells <i>GD::Graph </i>to make 52 x ticks, one for each week (remember that I started at the first Sunday). The rest of the options specify various colors and labels.</p>

<p>Once I specify what I want, on line 58, I tell <i>GD::Graph </i>to make it happen by plotting the data. <i>GD::Graph </i>creates the graph and puts everything where it should be (if I have set my options right&#151;sometimes I have to tweak it a bit). Although <i>GD::Graph </i>created the graph, it did not actually give me an image. On line 61, I tell the module that I want the graph as a PNG image and I save it to a file.</p>

<p>Once I run the program, I open the image to make sure it is what I want, but it usually is not, so I change a few <i>GD::Graph </i>options and try again. You can play with the options yourself to make the image pleasing to you if you do not like my choices. The <i>GD::Graph</i> module can do quite a bit more than I have shown.</p>

<p>I want to know all sorts of other things, too. I want to know the various levels of sunrises and sunsets. The sun's relationship to the horizon defines the different types. The civil twilight is when it becomes too dark to read outside, and that is when we like to start moving around at night.</p>

<p>For my almanac, I want to use Perl's formats, which saves me a lot of work in displaying the data and making it suitable for printing. Formats fell out of favor a while ago, largely because they require package variables, but they are meant for what I want to do&#151;create multiple pages of columnar data. Formats handle pagination, which I would have to do myself if I used <i>printf()</i> or something similar.</p>

<p>On line 11 of <a name="rl2"></a><a href="#l2">Listing 2</a>, I read in my personal configuration settings found in the file .almanacrc in my home directory. The configuration file is very simple. When I move around, I can change the configuration and generate a new almanac:</p>

<blockquote>
<pre>
longitude 38.5
latitude 45.5
time_zone 2

</pre>
</blockquote>

<p>On line 19, I start a <i>for()</i> loop that goes from the current time until one month later (there are 3.15e7 seconds in one year, so that many divided by 12 in one month. Remember that number in case you run out of things to talk about at a party&#151;there are approximately ¹ times 107 seconds in a year). I advance one day for each iteration.</p>

<p>On lines 21 to 25, I get the current date from <i>localtime()</i> and format it with <i>Date::Format </i>to look nice for me. You may want to change the format to suit yourself. I put the formatted date in the global variable <i>$date</i> because formats work with global variables.</p>

<p>In the <i>foreach()</i> loop that starts on line 29, I get the sunrise and sunset times for the various altitudes of the sun using the values from the <i>Astro::Sunrise</i> documentation. I add each pair of values, the sunrise and sunset, to the global array <i>@sunrises.</i> I get the moon illumination from <i>Astro::MoonPhase</i>'s <i>phase()</i> and store that in the global <i>$moon_phase</i>.</p>

<p>Once I have all the variables in place, I <i>write()</i> the format. Perl prints the STDOUT_TOP if I am at the top of the page (and when I get to the next page, it will do it again, so my columns always have headings). The STDOUT format puts the values of the <i>$date</i>, <i>@sunrises</i>, and <i>$moon_phase</i> data in the right places.</p>

<p>On line 35, I get the moon phase data by passing the date-time in <i>$i</i>, adjusted for the time zone, to <i>Astro::MoonPhase</i>'s <i>phase()</i>. The number I get back is a fractional value, and I turn it into a percentage by multiplying by 100. To make sure I end up with an integer, I use <i>sprintf()</i> to format the number as a decimal integer. I do not mind losing precision after the third decimal place&#151;atmospheric effects make that meaningless anyway.</p>

<p>Now, I have all of the information I want to put into my almanac, and all of the values are in package variables. I call <i>write(),</i> which invokes the current format, which is the same name as the current default output filehandle name. Since I have not messed with any of that, both names are still STDOUT. I do not need to pass any arguments to <i>write()</i> because the format already knows which package variables to use.</p>

<p>The output (<a name="re1"></a><a href="0402ce1.html">Example 1</a>) looks kind of dry, but it is perfect for my needs. I can refer quickly to any of the columns to get the information I want. When is the morning going to be light enough that it wakes me up? I look in the first column of the "Upper" set of times. When will I be able to see most of the stars? I look at the second column under the "Astro" heading. In the army, we consider the day over (or the night beginning) at nautical twilight, which is the second column of the "Nautical" column.</p>

<p>Pages and pages of this sort of data may be too long for me to carry around, so I can make a graph, like I did before. All I have to do is put the right values in the <i>@data </i>array I used in my <i>GD::Graph</i> program.</p>

<p>With a couple of Perl modules from CPAN, I was able to quickly churn out as much astrological data as I wanted. If I want to calculate data that does not already have a module, I only need to turn to the instructions in <i>Practical Astronomy With Your Calculator</i>, by Peter Duffett-Smith (Cambridge University Press, 1989; ISBN 0521356997), into Perl programs. Some of the <i>Astro::</i> modules recommend other sources as well. Good luck with your own astrological projects.</p>


<p><b>TPJ</b></p>



	
	
<br>
<br>
<b><a name="l1"></a>Listing 1</b><br>
<pre>1   #!/usr/bin/perl
2   
3   use Astro::MoonPhase;
4   use Date::Format;
5   use GD::Graph::lines;
6   use Time::Local;
7   
8   my $tz_offset = $ARGV[1] || 0;
9   my $year      = $ARGV[0] || ( (localtime)[5] + 1900 );
10  
11  # get the first Sunday in the year
12  my $now = do {
13      for( $day = 1; ; $day++ )
14          {
15          $time = timegm( 0, 0, 0, $day, 0, $year );
16          last unless (localtime($time + $tz_offset))[6];
17          }
18          
19      $time;
20      };
21      
22  my $then = timegm(59, 59, 23, 31, 11, $year);
23  
24  # calculate the moon phase for every three hours
25  for( my $i = $now; $i &lt; $then; $i += 60 * 60 * 3 )
26      {   
27      my @data = phase( $i + $tz_offset );
28                  
29      push @time, $i;
30      push @illum, $data[1];
31      }
32      
33  my @data = ( \@time, \@illum,);
34  
35  my $my_graph = new GD::Graph::lines( 800, 300 );
36  
37  $my_graph-&gt;set(
38      x_label             =&gt; 'Day',
39      y_label             =&gt; 'Moon Illumination',
40      title               =&gt; 'Moon Illumination',
41 .    x_number_format     =&gt; sub { time2str( "%h %e", $_[0] ) },
42      line_width          =&gt; 1,
43      x_label_position    =&gt; 1/2,
44      r_margin            =&gt; 15,
45      x_min_value         =&gt; $time[0],
46      x_max_value         =&gt; $time[-1],
47      x_tick_number       =&gt; 52,
48      transparent         =&gt; 0,
49      x_labels_vertical   =&gt; 1,
50      tick_clr            =&gt; 'gray',
51      border_clr          =&gt; 'dgray',
52      x_long_ticks        =&gt; 1,
53      y_long_ticks        =&gt; 0,
54      border              =&gt; 1,
55      border_clr          =&gt; 'black',
56  );
57  
58  my $gd = $my_graph-&gt;plot(\@data);
59  
60  open IMG, "&gt; moon.png" or die "$!\n";
61  print IMG $gd-&gt;png;
62  close IMG;</pre>
<a href="#rl1">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l2"></a>Listing 2</b><br>
<pre>1   #!/usr/bin/perl
2   use strict;
3   
4   use Astro::MoonPhase;
5   use Astro::Sunrise;
6   use ConfigReader::Simple;
7   use Date::Format;
8   
9   use vars qw( $date @sunrises $moon_phase );
10  
11  my $config = ConfigReader::Simple-&gt;new( ".almanacrc" );
12  
13  my $long = $config-&gt;longitude;
14  my $lat  = $config-&gt;latitude;
15  my $tz   = $config-&gt;time_zone;
16  
17  my $now = time;
18  
19  for( my $i = $now; $i &lt; $now + 3.15e7/12 ; $i += 24 * 60 * 60 )
20      {   
21      my @date = localtime( $i );
22      
23      print "\n" unless $date[6];
24      
25      $date = time2str( "%a %b %d", $i );
26      
27      @sunrises = ();
28      
29      foreach my $altitude ( -0.833, -6, -12, -18 )
30          {
31          push @sunrises, sunrise( @date[5,4,3], 
32              $long, $lat, $tz, $date[8], $altitude );
33          }
34          
35      $moon_phase = sprintf "%3d", 100 * ( phase( $i + $tz ) )[1];
36  
37      write;  
38      }
39      
40  format STDOUT = 
41  @&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;  @&gt;&gt;&gt;&gt; @&gt;&gt;&gt;&gt;  @&gt;&gt;&gt;&gt; @&gt;&gt;&gt;&gt;  @&gt;&gt;&gt;&gt; @&gt;&gt;&gt;&gt;  @&gt;&gt;&gt;&gt; @&gt;&gt;&gt;&gt;  @&gt;&gt;
42  $date,      @sunrises,            $moon_phase
43  .
44  
45  format STDOUT_TOP =
46  Date           Upper        Civil       Nautical      Astro    Illum
47  .</pre>
<a href="#rl2">Back to article</a><br>
	
	
	


</body>
</html>