
<html>
<head>
<title>February, 2004: Scraping Yahoo Groups</title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; The Perl Journal-->

<h1>Scraping Yahoo Groups</h1>
<p><i>The Perl Journal</i> February, 2004</p>
<h2></h2>


<h3>By Simon Cozens</h3>


<I>Simon is a freelance programmer and author, whose titles include Beginning Perl (Wrox Press, 2000) and Extending and Embedding Perl (Manning Publications, 2002). He's the creator of over 30 CPAN modules and a former Parrot pumpking. Simon can be reached at simon@ simon-cozens.org.</I>

<hr>





<p>You can learn a lot about a man from what he writes about. In my previous articles, ostensibly about Perl both here and at perl.com, I've talked about poker, Ruby, church work, Japanese literature, housemates, and linguistics. This month, I'm going to talk about obscure Japanese pop groups. No, really.</p>

<p>The great Pizzicato Five began life in 1984 as a four-piece band made up of Konishi Yasuharu, Takanami Keitaro, Ryo Kamamiya, and Sasaki Mamiko, and went through a series of line-up changes and hit albums. Eventually becoming a duo of Konishi Yasuharu and Nomiya Maki, they split up in March 2001. I got interested in Pizzicato Five (P5) in March 2001. Life is hard sometimes.</p>

<p>The P5 have always been more popular outside Japan than inside it, although "popular" is something to be taken relatively&#151;there's a vibrant English-language discussion group, P5ML, at Yahoo Groups. Unfortunately, coming late into the P5 scene, I missed a lot of the initial mail. I'd love to have a copy of all the old messages in my mailbox, but I don't really want to have to go trolling through the Yahoo Groups web interface to get them.</p>

<p>Maybe Perl can help.</p>
<h3>Grabbing Mails</h3>

<p>The first port of call in cases like this is search.cpan.org, and asking it about "Yahoo Groups" points us directly to the <i>WWW::Yahoo::Groups</i> module. This is a module based on <i>WWW::Mechanize</i>, a tool we've looked at before for scraping web sites.</p>

<p>Once we've read, marked, and inwardly digested the documentation, we can start working on our program to download the mail:</p>

<blockquote>
<pre>
use WWW::Yahoo::Groups;
my $scraper = WWW::Yahoo::Groups-&gt;new();
$scraper-&gt;login($username =&gt; $password);
$scraper-&gt;list("p5ml");

</pre>
</blockquote>

<p>Now we have logged in, all being well, and selected our group. We're ready to see what mail is available to be downloaded. <i>WWW::Yahoo::Groups</i> gives us functions to tell us the first message in the archive and the last one.</p>

<blockquote>
<pre>
my $lwm = $scraper-&gt;first_msg_id();
my $hwm = $scraper-&gt;last_msg_id();

</pre>
</blockquote>

<p>These IDs form a sequence internal to Yahoo Groups, and are not actually related to the message ID specified in the e-mail. We could take a simple-minded approach to grabbing all the messages, and simply walk from the low water mark to the high water mark, downloading and storing each message in turn:</p>

<blockquote>
<pre>
open OUT, "&gt;&gt; mail/p5ml" or die $!;
for ($lwm...$hwm) {
    print OUT $scraper-&gt;fetch_message($_)."\n";
}

</pre>
</blockquote>

<p>However, there are two problems with this idea. The first is that it's dangerous&#151;any mail arriving due to new posts to the P5ML mailing list will be clobbered by our overzealous output. The second is that it is wasteful&#151;we may already have downloaded many of the messages in the archive. Let's take these problems one at a time.</p>
<h3>Storing Mail</h3>

<p>The first thing we want is a safe way of storing mail into a mailbox. We'll assume that we're going to be using the UNIX mbox format for the purposes of this article since, well, that's what I use. There are, as I've mentioned in the past, a large number of mail folders handling libraries on CPAN, but we're going to use the simplest one, <i>Email::LocalDelivery</i>.</p>

<p>Its job is to take an e-mail from the "wire," and store it in some kind of file on disk. It's a very simple module with one method: <i>deliver</i>. It takes the e-mail as a plain string, which is quite handy because that's what <i>WWW::Yahoo::Groups</i> gives us. Hence, we can make our code a lot safer by rewriting it like so:</p>

<blockquote>
<pre>
for ($lwm...$hwm) {
    Email::LocalDelivery-&gt;deliver($scraper-&gt;fetch_message($_),
                                  "~/mail/p5ml");
}

</pre>
</blockquote>

<p>This makes sure we don't overwrite incoming messages, but it doesn't necessarily help us to pull down messages we already have. Of course, for that, we need to know what messages we've already got.</p>
<h3>Reading Folders</h3>

<p>Again, there is a plethora of modules for dealing with mail folders, but the best one, which does nothing other than split a folder into separate mail messages, is Richard Clamp's <i>Email::Folder</i>:</p>

<blockquote>
<pre>
use Email::Folder;
my $folder = Email::Folder-&gt;new("mail/p5ml");

</pre>
</blockquote>

<p>Once we've opened the folder, we can get its messages as individual <i>Email::Simple</i> objects:</p>

<blockquote>
<pre>
my @ids = map { $_-&gt;header("Message-ID") } $folder-&gt;messages;

</pre>
</blockquote>



<p>Now we know what message IDs we have, but we're going to find it much more convenient to have that list as a hash rather than an array. Did that immediately jump out at you? Don't worry if it didn't; just like learning the idioms and constructions of the human language, the hash formulation is just one of those things you sense as you become more fluent in Perl.</p>

<p>We want to know if an incoming message is one that we already hold a copy of. Another way to phrase this is that the message exists in the set of messages in our folder. Once you start thinking about sets and existence in Perl, you should immediately start thinking about hashes because, like sets, they provide a data structure that doesn't necessarily hold an order, but can be used to test easily whether or not something is a member of the set. Perl's <i>exists</i> function gives us a quick and simple test for set membership.</p>

<p>It's a Perl rule of thumb&#151;if you want to know if something is part of a list of values, that list should become a hash and you should use <i>exists</i>.</p>

<p>So, I'd naturally have written the above line as:</p>

<blockquote>
<pre>
my %ids = map { $_-&gt;header("Message-ID") =&gt; 1 } $folder-&gt;messages;

</pre>
</blockquote>



<p>Notice that we don't really care about the value attached to the message ID in the hash is because all we're going to use it for is to test for existence.</p>

<p>Now we can look again at the messages we're downloading via <i>WWW::Yahoo::Groups</i>. If we see a message that's already in the folder, we can assume that we've caught up with the archives.</p>

<p>The only slight nit is that to match the message ID from the downloaded message against our set from the folder, we need to turn that into an <i>Email::Simple</i> object, too. But thankfully, by definition, that's simple!</p>

<blockquote>
<pre>
for ($lwm...$hwm) {
    my $message = $scraper-&gt;fetch_message($_);
    my $id = Email::Simple-&gt;new($message)-&gt;header("Message-ID");
    last if exists $ids{$id}; # Caught up with archive.

    Email::LocalDelivery-&gt;deliver($message, "~/mail/p5ml");
}

</pre>
</blockquote>



<p>Hoorah! Now we should have a mail box full of old Pizzicato Five messages. What could be a better use for Perl?</p>
<h3>Threading Mail</h3>

<p>I suppose, though, that we might want to have a fast way of catching up with these hundreds of old messages. In next month's article, we're going to look in more depth about summarizing mailing-list archives, but for this month, we'll take a quick look at how to organize these messages into threads. We'll again start with <i>Email::Folder</i> and <i>Email::Simple</i> to split off the messages from the folder, and now we want to thread them.</p>

<p>Thankfully, CPAN is there for us again, and <i>Email::Thread</i> does all the work in arranging a collection of <i>Email::Simple</i> objects into threads. It could hardly be much easier. To kick off the threading process, we create a new threader object with all the messages we want organized, and then call the obviously named thread method:</p>

<blockquote>
<pre>
use Email::Folder;
use Email::Thread;

my $threader = Email::Thread-&gt;new(
                Email::Folder-&gt;new("mail/p5ml")-&gt;messages
              );
$threader-&gt;thread;

</pre>
</blockquote>

<p>This goes away and populates a bunch of internal data structures&#151;quite remarkably quickly&#151;and allows us to ask for the set of root messages that form the initial messages in threads:</p>

<blockquote>
<pre>
my @roots = $threader-&gt;root_set;

</pre>
</blockquote>



<p>Each of these root messages returned from <i>root_set</i> is an <i>Email::Thread::Container</i> object; it may have a parent, child, or sibling, each of which is another <i>Email::Thread::Container</i>, and a message, which is the <i>Email::Simple</i> object representing the message. The child is a container that refers to a reply to the current node; the sibling is another reply to the same parent.</p>

<p>Sometimes the message will be empty (if we have missed out some posts in the thread), but the threader can still deduce that a message should be there. Let's begin by looking at the subjects of each thread:</p>

<blockquote>
<pre>
for my $root (@roots) {
    print $root-&gt;message-&gt;header("Subject")."\n";
}

</pre>
</blockquote>

<p>This gives us a good overview of the various topics that come up on the mailing list, but how popular is each one? To count the number of replies to a thread-starter, we'll have to traverse the child and sibling links of the thread.</p>

<p>Walking a tree-like data structure, such as a message thread, should suggest to you a recursive algorithm: Do something for the node, then perform this algorithm on its sibling, then perform it on its child. I was once told two golden rules for dealing with recursive procedures. First, make sure there's a termination condition, then have faith that it'll do the right thing. It usually will. Here's a basic thread walker:</p>

<blockquote>
<pre>
sub walk_thread {
    my $node = shift;
    do_something($node);
    walk_thread($node-&gt;sibling) if $node-&gt;sibling;
    walk_thread($node-&gt;child)   if $node-&gt;child;
}

</pre>
</blockquote>



<p>Will this terminate? Eventually, we'll see a node that has no siblings or no replies. Will it do the right thing? It's easier to see this if you assume a thread with no siblings, just one reply to each message:</p>

<blockquote>
<pre>
sub walk_thread {
    my $node = shift;
    do_something($node);
    walk_thread($node-&gt;child)   if $node-&gt;child;
}

</pre>
</blockquote>

<p>Now we can see that this will cause each child to be visited in turn, and then terminate. Putting the sibling link back is exactly the same but makes the walk two-dimensional.</p>

<p>But how do we go from here to finding out the number of messages in a thread? Imagine an army squad in a field. It's almost in a line, but some people are ahead of others. The commander at the west end of the field wants to know how many soldiers are still alive and responding. He can only the see the sergeant, so he stealthily runs over to him and says, "Find out how many soldiers are east of you, add one for yourself, and tell me the answer." The soldier can see two corporals to his east, so asks both of them "Find out how many soldiers are east of you, add one for yourself, and tell me the answer." The corporals go and do exactly the same thing for the men that they can see, and report back the answer. The sergeant gets two answers back, adds them together, adds one for himself and reports it to the commander, who adds one for himself, and knows how many messages, uh, men he's got.</p>

<p>This works because the men at the east end can see no other men, so they add one for themselves, and report back "one," and the answer bubbles up until you've summed everyone. Here's what that looks like in Perl.</p>

<blockquote>
<pre>
sub count_offspring {
    my $node = shift;
    my $count = 0;
    if ($node-&gt;next)  { $count += count_offspring($node-&gt;next); }
    if ($node-&gt;child) { $count += count_offspring($node-&gt;child); }
    # And one for mynode
    $count++;
    return $count;
}

</pre>
</blockquote>

<p>And now, we can sort the threads by the number of messages they contain:</p>

<blockquote>
<pre>
my @rootset = $threader-&gt;rootset;
my %counts = map { $_ =&gt; count_offspring($_) } @rootset;
for my $node (sort { $counts{$b} &lt;=&gt; $counts{$a} } @rootset) {
    print $node-&gt;message-&gt;header("Subject"),": $counts{$node}\n";
}

</pre>
</blockquote>

<p>When we do this, we find that, unsurprisingly, the most prominent thread is the one about the band splitting up...</p>
<h3>Providing Links</h3>

<p>Now let's change the subject a tiny bit. Getting back to these Yahoo Groups, you'll remember I said there was no correlation between a Yahoo Group message ID and a message's Message-ID header. Wouldn't it be nice, then, if we could insert a header into each message giving the URL where the message came from?</p>

<p>We can do this because we know that <i>WWW::Yahoo::Groups</i> uses the <i>WWW::Mechanize</i> robot to do its fetching, that <i>WWW::Mechanize </i>allows us to get at the <i>HTTP::Response</i> object for the last fetch, and that <i>HTTP::Response</i> has a base method. And thankfully, we can get at the underlying <i>WWW::Mechanize</i> object by calling <i>agent</i>. So our modified scraper code looks like this:</p>

<blockquote>
<pre>
for ($lwm...$hwm) {
    my $message = $scraper-&gt;fetch_message($_);
    my $uri = $scraper-&gt;agent-&gt;res-&gt;base;

</pre>
</blockquote>

<p>Next, we need to modify the headers of this e-mail. Since we're parsing the mail into <i>Email::Simple</i> anyway to get the Message ID, we can add an X-Yahoo-URL header in there as well:</p>

<blockquote>
<pre>
    my $simple = Email::Simple-&gt;new($message);
    my $id = $simple-&gt;header("Message-ID");
    $simple-&gt;header_set("X-Yahoo-URL", $uri);

    last if exists $ids{$id}; # Caught up with archive.

    Email::LocalDelivery-&gt;deliver($simple-&gt;as_string, "~/mail/p5ml");
}

</pre>
</blockquote>

<p>There's only one slight problem with this&#151;the URLs you get back are really horrifically long in some cases. Thankfully, there's a nice solution we can slot in here&#151;another CPAN module called <i>WWW::Shorten</i>. This uses the various URL-shortening services out there, like the well-known MakeAShorterLink&#151;but many others, too&#151;to produce a more palatable URL. Let's support the home side, and use <i>Ask Bjorn Hansen</i>'s <i>metamark</i>. (<i>Ask</i> runs most of the perl.org services.)</p>

<blockquote>
<pre>
use WWW::Shorten 'metamark';
for ($lwm...$hwm) {
    my $message = $scraper-&gt;fetch_message($_);
    my $uri = makeashorterlink($scraper-&gt;agent-&gt;res-&gt;base);
    my $simple = Email::Simple-&gt;new($message);
    my $id = $simple-&gt;header("Message-ID");
    $simple-&gt;header_set("X-Yahoo-URL", $uri);

    last if exists $ids{$id}; # Caught up with archive.

    Email::LocalDelivery-&gt;deliver($simple-&gt;as_string, "~/mail/p5ml");
}

</pre>
</blockquote>

<p>And there we are! Job done!</p>
<h3>Dedication</h3>

<p>Some of you may have cottoned on to something strange about this article: As it progressed from a relatively plausible (for me, at least) premise, it got more and more contrived. My apologies for that, but there is a good reason for it.</p>

<p>This month, I wanted to particularly showcase a special set of modules and a special module author. Three of the modules we discussed in this article, <i>WWW::Yahoo::Groups</i>, <i>Email::Thread</i>, and <i>WWW::Shorten</i> were all written by the same man, Iain Truskett.</p>

<p>Iain not only wrote these modules, but many others, too. He also contributed fixes and advice to a large number of other people's modules, to the Perl 5 core documentation, and to the DateTime project. Additionally, he ran the Perl books site at http:// books.perl.org/, and was a regular contributor to many Perl mailing lists, newsgroups, and IRC channels, always known for his patience and calmness at all times.</p>

<p>He was a tireless benefactor to the Perl community, even though he largely remained humbly behind the scenes. We lost a good man when Iain passed away on New Year's Eve, 2003. This article is respectfully dedicated to him.</p>


<p><b>TPJ</b></p>




</body>
</html>