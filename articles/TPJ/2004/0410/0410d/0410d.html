
<html>
<head>
<title>October, 2004: Musical Archaeology With Perl</title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; The Perl Journal-->

<h1>Musical Archaeology With Perl</h1>
<p><i>The Perl Journal</i> October, 2004</p>
<h2></h2>


<h3>By Simon Cozens</h3>


<I>Simon is a freelance programmer and author, whose titles include Beginning Perl (Wrox Press, 2000) and Extending and Embedding Perl (Manning Publications, 2002). He's the creator of over 30 CPAN modules and a former Parrot pumpking. Simon can be reached at simon@ simon-cozens.org.</I>

<hr>





<p>I've recently moved house, and one of the joys of moving house is that you occasionally turn up things you'd forgotten existed. One of the most bittersweet things I turned up was a backup CD of my computer from about 1998. The sense of morbid curiosity was overpowering, and after wading through the pictures of ex-girlfriends, university-era essays, and curious Windows applications, I found a directory that was itself a backup of another computer. From 1994.</p>

<p>At the time I was in high school, and was a very profilic music composer. Looking at the titles of the song files and remembering the songs I wrote at the time, I realized that one or two of them weren't all that bad, and I'd quite like to listen to them again. This is when I remembered that I didn't use real computers in 1994. The files came from an Atari ST and were written by a program called Notator SL.</p>

<p>Searching the Web, the only two ways I could find to use these files again were either to find an Atari ST already running Notator, or buy a copy of Notator's grandchild, Logic (formerly from eMagic, now owned by Apple). Logic is expensive, and the chances of finding anyone in my neighborhood with a 10-year-old music computer set-up were pretty slim.</p>

<p>When something like this happens, it takes me over. I have to find a solution. Besides, it's more fun than packing. There was only one thing for it: I was going to have to decode the file format myself.</p>
<h3>Laying the Foundations</h3>

<p>Since I had a lot of these .SON files, the first step was to take a couple of them, and discover how they differed. Thankfully, I knew that I had a few files that were very similar in nonessential regards&#151;the drum mappings were the same, the track names and parameters were the same, and only the actual notes played were different. So I sat down with a hex editor and each of the files open in its own window, and wrote out what I saw (see <a name="rf1"></a><a href="0410df1.html">Figure 1</a>).</p>

<p>For the time being, all I cared about was the data on each track. With the track name appearing in plain text, 8 bytes padded with spaces, I knew where the tracks began. Unfortunately, I couldn't work out where a given the track ended. I could see that "PIANO"   was a track name and started a new track, but I couldn't make a rule for that to explain it a computer.</p>

<p>So I came at things from a different angle. I knew that the data was going to be something like MIDI data&#151;a series of <i>n</i>-byte messages. So the first thing I needed to do was work out the value of <i>n</i>. We can do this by spotting patterns. I wrote something quick to dump the output in rows of <i>n</i> bytes:</p>

<blockquote>
<pre>
open IN, shift or die $!;
seek IN, 0x5ae0, 0;
my $track;
read(IN, $track, 8) and print "Track name: '$track'\n";
my $n = 3;
my $data;
while (read(IN, $data, $n)) {
    my @bytes = map ord, split //, $data;
    print join " ", map { sprintf "%x", $_ } @bytes;
    print "\n";
}

</pre>
</blockquote>

<p>We loop over the file, reading in <i>$n</i> bytes at a time into <i>$data</i>. Then these are split into their individual bytes&#151;<i>split //</i> splits every single character into a separate array element. We convert them to their ASCII code, turn that into hex, and then dump them out.</p>

<p>I started with <i>$n</i> = 3 because a lot of MIDI commands are 3 bytes, and that seemed like a good guess. I then ran it on the shortest file I could find, and got this:</p>

<blockquote>
<pre>
Track name: '*Undo*  '
0 0 9c
22 20 59
58 0 80
21 0 0
2 23 2d
90 21 0
81 0 39
90 21 0
81 0 3d
90 21 0
81 0 40
90 21 0

</pre>
</blockquote>

<p>Not bad. We have a few rows that I guessed were to do with setting up, then we settle into a pattern of 90 21 0 and then 81 0 3x. These could be something to do with notes, I guess. But the way that they alternate like that, maybe the two rows are part of the same event. We set <i>$n = 6</i>, and we see:</p>

<blockquote>
<pre>
Track name: '*Undo*  '
0 0 9c 22 20 59
58 0 80 21 0 0
2 23 2d 90 21 0
81 0 39 90 21 0
81 0 3d 90 21 0
81 0 40 90 21 0
81 0 2d 90 23 ff
1 0 39 90 23 ff
1 0 3d 90 23 ff
1 0 40 90 23 ff
1 0 31 90 24 0

</pre>
</blockquote>

<p>Oh, now this is a bit more interesting. We notice that the last 2 bytes form a number that is monotonically increasing. What could monotonically increase over events in a track? Maybe that's the time that the event occurs. The fourth column is almost always 90; maybe that signifies that this is a note.</p>

<p>There's a way to check&#151;we run it on a file with a drum part as the first track, and, given that I vaguely remember what the song sounds like, I can work out the interval between drum beats. That will tell us how to convert between 21 0 and some measure-beat-tick value (or bar-beat-tick, since I'm from the UK and we use different terminology over here). Here's a bit of the dump of a drum track:</p>

<blockquote>
<pre>
01 00 24 90 36 00
81 00 2e 90 36 00
81 00 36 90 36 00
81 00 24 90 36 5f
01 00 2e 90 36 5f
01 00 36 90 36 5f
01 00 36 90 36 60
81 00 36 90 36 bf
01 00 26 90 36 c0
81 00 2e 90 36 c0
81 00 36 90 36 c0

</pre>
</blockquote>

<p>Now we only see two values for the first row: 81 and 01. We'll assume that these are somehow paired, so we'll only look at the 81 events for now. We'll also only concentrate on unique values of the last two columns, and we get a sequence like this:</p>

<blockquote>
<pre>
36 00 / 36 5f / 36 bf / 36 c0 / 37 1f / 37 7f 
37 80 / 37 df / 38 3f / 38 40 / 38 9f / 38 ff
39 00 / 39 5f / 39 bf / 39 c0 / 3a 1f / 3a 7f 
3a 80 ...

</pre>
</blockquote>

<p>I happen to know that there's a tambourine hit every quaver (that's English for an eighth-note) in this drum track, and there's an increment of 0x5f (96) between every note. That's when it all came back to me&#151;Notator uses 768 ticks in a measure. An eighth of 768 is 96. So if we take these 2 bytes as representing a 2-byte integer, we can convert them to musical time, like so:</p>

<blockquote>
<pre>
ub tick2time {
    my ($hi, $lo) = @_;
    my $ticks = (256*$hi) + $lo;
    my $bar = int($ticks / 768);
    $ticks %= 768;
    my $beat = int($ticks / 192);
    $ticks %= 192;
    "$bar/$beat/$ticks";
}

</pre>
</blockquote>

<p>Now we can improve our dumping tool a little:</p>

<blockquote>
<pre>
while (read(IN, $data, $n)) {
    my @bytes = map ord, split //, $data;
    print join " ", map { sprintf "%02x", $_ } @bytes[0..3];
    print " [".tick2time(@bytes[4..5])."]";
    print "\n";
}

</pre>
</blockquote>

<p>This gives us the beginning of a drums track, like so:</p>

<blockquote>
<pre>
00 00 9c 22 [10/2/152]
18 00 80 a1 [0/0/0]
02 23 2e 90 [12/3/96]
81 00 2e 90 [12/3/191]
01 00 24 90 [13/0/0]
81 00 2e 90 [13/0/0]
81 00 31 90 [13/0/0]
81 00 36 90 [13/0/0]
81 00 24 90 [13/0/95]
01 00 2e 90 [13/0/95]
01 00 31 90 [13/0/95]

</pre>
</blockquote>

<p>From this we can tell that the first two lines really aren't related, and that the song appears to start 10 measures in. We'll ignore the latter detail for now, since it's just cosmetic&#151;we can shift all the tracks back to the start in a more sophisticated sequencer&#151;and concentrate on those two lines of set-up data. The beginning of the third line starts to look a little suspect too: Every other line starts 81 00 or 01 00.</p>

<p>Perhaps what's actually going on is that the data starts at 2e 90, and we have not 12 but 14 bytes of setup. Then we have one number that fluctuates a bit; 90 for a note, the time, then some other numbers.</p>

<p>This requires some major changes to our dumper script:</p>

<blockquote>
<pre>
read(IN, $track, 8) and print "Track name: '$track'\n";
my $setup;
read(IN, $setup, 14); 
while (read(IN, $data, $n)) {
    my @bytes = map ord, split //, $data;
    print join " ", map { sprintf "%02x", $_ } @bytes[0..1];
    print " [".tick2time(@bytes[2..3])."] ";
    print join " ", map { sprintf "%02x", $_ } @bytes[4..5];
    print "\n";
}

</pre>
</blockquote>



<p>And we now see:</p>

<blockquote>
<pre>
Track name: 'Drums   '
2e 90 [12/3/96] 81 00
2e 90 [12/3/191] 01 00
24 90 [13/0/0] 81 00
2e 90 [13/0/0] 81 00
31 90 [13/0/0] 81 00
36 90 [13/0/0] 81 00
24 90 [13/0/95] 01 00
2e 90 [13/0/95] 01 00
31 90 [13/0/95] 01 00
36 90 [13/0/95] 01 00

</pre>
</blockquote>

<p>Ah, now this is looking promising. But now what? This is the curse of reverse engineering&#151;you solve one piece of the problem, but then you're back to square one for the other pieces until inspiration strikes again.</p>
<h3>MIDI Inspiration</h3>

<p>Inspiration struck again while I was reading Sean Burke's MIDI-Perl documentation. I figured I needed to translate these files into MIDI files to do anything with them, so I took a look at how to produce MIDI files. I learned two things there&#151;first, MIDI files don't encode note length, but have paired "note on" and "note off" events. Maybe that's our paired 81 and 01 events.</p>

<p>Second, the MIDI Perl module exports a number of hashes, including <i>%number2note</i> and <i>%notenum2percussion</i>, which turn a MIDI file's representation of a pitch or percussion instrument name into an English representation. Maybe even if Notator didn't exactly store its events in MIDI file format, it at least used the same representation for pitch. So we take the first column of our dump, the one that bobbles about a bit, and feed it through one of these hashes:</p>

<blockquote>
<pre>
my @bytes = map ord, split //, $data;
print $track =~ /drum|percuss/i ?
    $MIDI::notenum2percussion{$bytes[0]} :
    $MIDI::number2note{$bytes[0]};
print join " ", map { sprintf "%02x", $_ } $bytes[1];
print " [".tick2time(@bytes[2..3])."] ";
print join " ", map { sprintf "%02x", $_ } @bytes[4..5];
print "\n";

</pre>
</blockquote>

<p>This turns our drum track into:</p>

<blockquote>
<pre>
Bass Drum 1 90 [17/2/0] 81 00
Open Hi-Hat 90 [17/2/0] 81 00
Tambourine 90 [17/2/0] 81 00
Bass Drum 1 90 [17/2/95] 01 00
Open Hi-Hat 90 [17/2/95] 01 00
Tambourine 90 [17/2/95] 01 00
Bass Drum 1 90 [17/2/96] 81 00
Tambourine 90 [17/2/96] 81 00
Bass Drum 1 90 [17/2/191] 01 00
Tambourine 90 [17/2/191] 01 00

</pre>
</blockquote>

<p>This looks good enough, and we can check to see if an ordinary music track is more or less in key:</p>

<blockquote>
<pre>
Track name: 'Vocals  '
E5 90 [10/3/144] 81 00
E5 90 [10/3/191] 01 00
Gs5 90 [11/0/0] 81 00
Gs5 90 [11/0/47] 01 00
Gs5 90 [11/0/48] 81 00
Gs5 90 [11/0/191] 01 00
Fs5 90 [11/1/0] 81 00
Fs5 90 [11/1/95] 01 00
E5 90 [11/1/96] 81 00
B5 90 [11/1/144] 81 00
E5 90 [11/1/191] 01 00
B5 90 [11/2/47] 01 00

</pre>
</blockquote>



<p>This is consistent with a song in E major. Finally, I noticed that for some songs, as well as 81 and 01, there were note events with other values for this column; I guessed that this related to the velocity of the note, another MIDI concept. Velocity represents conceptually how hard the note is struck; it's a bit like volume, but can also change the timbre of the tone. For now, though, we'll take numbers more than 0x80 to mean note on with full velocity, and less than 0x80 to mean note off.</p>

<p>We have everything we need to convert a single track into MIDI format&#151;except we still don't know how a track ends. Our notes say that it ends with some number of 0 bytes, but we don't know how many. It was back to the hex editor.</p>

<p>After comparing files and tracks in the hex editor once again, I came up with an idea&#151;what if the track name wasn't the first bit of data in the track? What if there was some other setup data before the name of each track? That section starting just before the tracks that I'd labeled as "DATA" might be part of the track header. This would mean that the stray null bytes I'd been seeing were not the end of the track, but the beginning.</p>

<p>This cracked it&#151;I found that each track began with either the 4 bytes 7f ff ff ff or 00 0f ff ff. Then there was a 24-byte header, followed by the track name, and the 14 other header bytes I had determined earlier. Now I could write the MIDI file translator.</p>
<h3>The Translator</h3>

<p>First, I decided to read the data in with the Perl slurp operator and regular expressions, instead of the more cumbersome <i>read</i>. This allowed me to use <i>split</i> to split up the tracks on the boundaries that I'd just discovered. I also decided to get the data together first, then pass over it, converting it to MIDI tracks. Here's the part that reads in the tracks:</p>

<blockquote>
<pre>
my ($input, $output) = @ARGV;
open IN, $input or die $!;

seek IN, 0x5ac8, 0;
local $/;
my $boundary = qr/(?:\x7f\xff\xff\xff)|(?:\x00\x0f\xff\xff)/
my @lines = split /$boundary/, scalar &lt;IN&gt;;
my @tracks;
for my $track (@lines) {
    my $stuff = {};
    $stuff-&gt;{header} = substr($track, 0, 24,"");
    $track =~ s/^(.{8})//; $stuff-&gt;{title} = $1;
    $stuff-&gt;{data} = $track;
    push @tracks, $stuff;
}

</pre>
</blockquote>

<p>Now we need to convert our Notator events to MIDI events. There are a couple of things we have to know about MIDI events first, however. First, although we have "absolute" times, in terms of measures, beats and ticks, MIDI files actually deal in terms of delta time&#151;that is, an event is placed a number of ticks after the previous event. This means we need a counter to keep track of where we're up to in the track.</p>

<p>Next, whereas Notator deals in terms of tracks and events on a track, a MIDI file has both tracks and channels. We need to assign a channel to each track, and then spit out that channel number with each of the track's events. Finally, MIDI events are binary data, but the MIDI-Perl distribution makes it relatively easy to construct them by allowing us to specify them as an array of arrays. For instance, the first event we want to spit out is:</p>

<blockquote>
<pre>
[ track_name =&gt; 0 =&gt; $track-&gt;{title} ]

</pre>
</blockquote>



<p>This puts the track name at time-position zero in the track. Note that events look like this:</p>

<blockquote>
<pre>
[ note_on =&gt; $time, $track-&gt;{channel}, $note, $velocity ]

</pre>
</blockquote>



<p>We want to build up an array of these events, and then turn them into a <i>MIDI::Track</i> object. Here's how we do it.</p>

<blockquote>
<pre>
my $channel = 0;
my @midi;
for my $track (@tracks) {
   $track-&gt;{data} =~ s/.{14}//; # Rest of header
   next if length($track-&gt;{data}) == 0;
   print $track-&gt;{title}. "\n";

   $track-&gt;{counter} = 0;
   $track-&gt;{channel} = $channel++;
   $track-&gt;{events} = [
    [track_name =&gt; 0 =&gt; $track-&gt;{title} ],
   ];
   my $size = 6;
   while (my $event = substr($track-&gt;{data}, 0, $size, "")) {
       push @{$track-&gt;{events}}, data2event($track, $event);
   }
   my $midi_track = MIDI::Track-&gt;new;
   $midi_track-&gt;events(@{$track-&gt;{events}});
   push @midi, $midi_track;
}

</pre>
</blockquote>

<p>Notice that we're treating <i>$track</i> a little like an object, which stores all its own data inside the hash reference; it knows where we are in the song (<i>$track-&gt;{counter}</i>), the track's channel, the MIDI events, and so on. The only mystery is <i>data2event</i>, which turns the 6 bytes of data into an array reference representing a MIDI event:</p>

<blockquote>
<pre>
sub data2event { 
    my $track = shift;
    my $line = shift;
    my ($note, $status, $pos1, $pos2, $vel, $arg3) = 
        map ord, split//, $line;
    if ($status == 0x90) {
        $status = "note_on";
        $vel = $vel - 0x80;
        if ($vel &lt;0) { $status = "note_off" }
        $vel = 127;
        my $pos = $pos1*256 +$pos2;
        my $delta = $pos - $track-&gt;{counter};
        $track-&gt;{counter} = $pos;
        return [ $status, $delta, $track-&gt;{channel}, $note, $vel ]
    } 
    warn "Skipping over unknown event $status ($note, $vel, $arg3)
           at position ".tick2time($pos1,$pos2);
    return;
}

</pre>
</blockquote>

<p>Notice that at present, we don't know what <i>$arg3</i> is for. However, we do now know that if we see any events that aren't 0x90, then we get a warning. We'll come back to this in a moment. To finish off, we now have an array of <i>MIDI::Track</i> objects.</p>

<p>Turning these into a MIDI file is now easy:</p>

<blockquote>
<pre>
$song = MIDI::Opus-&gt;new(
    { 'format' =&gt; 1, 'ticks' =&gt; 192, 'tracks' =&gt; \@midi } 
);

warn "Writing on $output";
$song-&gt;write_to_file($output);

</pre>
</blockquote>

<p>This creates a MIDI format 1 file, with a rather arbitrary tempo, and fills it with our tracks.</p>

<p>I ran it on one of my old compositions, and out popped a working MIDI file. After a few minutes, fiddling around with instrumentation on a rather more modern sequencer, my 10-year-old music file was playing in all its glory.</p>
<h3>Later Improvements</h3>

<p>Well, most of its glory. I got quite a few of those warnings telling me it had skipped over some events. Thankfully, it was now much, much easier to work out what those events should be.</p>

<p>So when the bass guitar was supposed to slide up and down but instead we got a load of warnings about an unknown event 224, we can guess that 224 means "pitch wheel." I used the original dumper and grepped for events with a code of 224, and found that the velocity moved around, centering on 128. MIDI files, on the other hand, encode pitch wheel changes not on a scale of 0 to 255 but a scale of -8192 to 8192, so I needed to do a bit of scaling. It was then easy enough to drop another stanza into <i>data2event</i>.</p>

<blockquote>
<pre>
elsif ($status == 224) {
    my $pitch = ($vel -129)*(8192/128);
    my $pos = $pos1*256 +$pos2;
    my $delta = $pos - $track-&gt;{counter};
    $track-&gt;{counter} = $pos;
    return [ "pitch_wheel_change", $delta, $track-&gt;{channel}, $pitch ]
}

</pre>
</blockquote>

<p>Now there was one final problem. Songs longer than 85 measures were getting truncated, and I was getting lots of warnings about event 145. I thought about this, and realized that the 2-byte position counter could only go up to 65535 ticks, and 65535 ticks was just over 85 measures. Then we flip over from a note being event 144 to being 145&#151;it seems that Notator used some bits in the "event type" byte to extend the position counter. This is pretty hateful, but I suppose it's better than restricting everyone to short songs. The conversion program had to change, like so:</p>

<blockquote>
<pre>
my ($note, $status, $pos1, $pos2, $vel, $arg3) = map ord, split//, $line;
if ($status =~ /(14[45])/) {
    $pos1 += 256 *($1-144); 

</pre>
</blockquote>

<p>This still doesn't cover everything that can happen inside a Notator file, but at least it gets the notes out, and it's enough for me to play around with those old songs again. If, by some strange chance, you have a bunch of Notator songs and a sense of nostalgia, you can get my converter from http://simon-cozens.org/ programmer/releases/son2midi.pl. But beware&#151;next time you get stuck in the early nineties, Perl might not be able to drag you back...</p>






<p><b>TPJ</b></p>




</body>
</html>