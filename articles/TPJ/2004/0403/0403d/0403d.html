
<html>
<head>
<title>March, 2004: Bayesian Analysis For RSS Reading</title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; The Perl Journal-->

<h1>Bayesian Analysis For RSS Reading</h1>
<p><i>The Perl Journal</i> March, 2004</p>
<h2></h2>


<h3>By Simon Cozens</h3>


<I>Simon is a freelance programmer and author, whose titles include Beginning Perl (Wrox Press, 2000) and Extending and Embedding Perl (Manning Publications, 2002). He's the creator of over 30 CPAN modules and a former Parrot pumpking. Simon can be reached at simon@ simon-cozens.org.</I>

<hr>





<p>I am very good at distracting myself from getting things done. One of my best displacement activities used to be flicking through various web news sites: the BBC news, Slashdot, use.perl.org, linuxtoday.com, and that's before we get started on the comics.</p>

<p>Occasionally, I have spurts of enthusiasm and try to work out ways to curb my distractions in order to free up time for more interesting forms of procrastination. Instead of having to cycle through all these pages to see if there was any new news or a new edition of the comic, wouldn't it be a lot more efficient if these news sites told me when there was something new for me to read? Then I could spend the equivalent time that I'd save playing chess or something...</p>

<p>Thankfully, other people have obviously had the same question, as one of the big trends in online publishing over the past two or three years has been the surge in syndication of web news sites via Remote Site Syndication(RSS) and RDF. RSS is an XML format that gives the headlines and some of the content of stories on a site in a machine-readable way. This can be read by an RSS aggregator like NetNewsWire (http://ranchero.com/ netnewswire/), which periodically grabs the RSS feeds of all the sites you visit often. By parsing the feeds, it can work out if there are any articles you haven't seen yet and give you a summary of the new news.</p>

<p>This was a good start, but it wasn't enough. So, we'll first have a look at how we can manipulate RSS in Perl, and then we can find out how I managed to cut down my procrastination time even more!</p>
<h3><i>XML::RSS</h3>

<p>The first handy tool to deal with RSS is the obviously named </i>XML::RSS module. It can be used both to create new RSS files and to parse existing feeds and turn them into Perl data structures. It's this latter use that I'll discuss here.</p>

<p>For instance, to get a list of the current news stories on the Perl news site use.perl.org, we start by downloading its RSS feed:</p>

<blockquote>
<pre>
use LWP::Simple;
my $xml = get("http://use.perl.org/perl-news-short.rdf");

</pre>
</blockquote>

<p>Then we turn this into an <i>XML::RSS</i> object:</p>

<blockquote>
<pre>
use XML::RSS;
my $rss = XML::RSS-&gt;new;
$rss-&gt;parse($xml);

</pre>
</blockquote>

<p>And now we have a load of information about the feed. For instance, the title and link:</p>

<blockquote>
<pre>
print $rss-&gt;{title}, ": ", $rss-&gt;{link}, "\n";

</pre>
</blockquote>



<p>An RSS feed contains a list of items, which we can retrieve. Each item returns a hash reference:</p>

<blockquote>
<pre>
for my $item (@{$rss-&gt;{items}}) {
    print $item-&gt;{title}, ": ", $item-&gt;{link}, "\n";
}

</pre>
</blockquote>

<p>And sometimes, the content providers put a short description or the first few paragraphs of the news item in the description tag of an item:</p>

<blockquote>
<pre>
for my $item (@{$rss-&gt;{items}}) {
    print $item-&gt;{title}, ": ", $item-&gt;{link}, "\n";
    print $item-&gt;{description}, "\n\n";
}

</pre>
</blockquote>

<p>However, in order to create a nice news aggregator, we still have to do a lot of work ourselves: downloading the feeds, parsing them, working out which articles have already been seen, and so on. This isn't the Perl way. Hasn't someone else done this for us?</p>
<h3>Timesink</h3>

<p>Regular readers will know that I find it hard to get through a column without mentioning at least one of the two most interesting modules in the Perl world for me: <i>Class::DBI</i> and the Template Toolkit; well, this time, I get to mention both.</p>

<p>Richard Clamp also got the RSS bug around the same time as me, and rather than be dependent on an application running on one particular machine, he created a web-based aggregator that could keep track of what he had and hadn't read. His project, Timesink, uses <i>XML::RSS</i> to download and parse the feeds, <i>Class::DBI</i> to store the stories in a database, and the Template Toolkit to display the rendered stories to the user. It supports multiple users subscribed to a variety of different feeds, and is remarkably small and well-engineered.</p>

<p>On the other hand, Timesink is still very much in development and isn't very well documented yet, but I managed to get it set up and running on one of my computers. It provided me with the platform I needed to get the next stage of my procrastination optimization under way.</p>

<p>You see, the problem is that with the advent of RSS, it's very easy for me to subscribe to a large number of news sources, many of which will only be of marginal interest to me but occasionally throw up something worthwhile. Instead of having to trudge through a small number of web pages in search of new news, I now have to trudge through a large number of news feeds in search of interesting news.</p>

<p>I first got the computer to decide for me what was new; my thought was that the obvious way to proceed was to get it to decide what's interesting. My dream aggregator would read my news for me, and only present me with the news I want to read.</p>
<h3>Based on Bayes</h3>

<p>How can a computer tell me what's interesting? By watching what I tell it is interesting, working out the features of an "interesting" article, and then weighing up whether a given news article would be best categorized as "interesting" or "boring." This turns the question into a machine learning problem, and machine learning has been worked on by far greater minds than mine.</p>

<p>There are a number of text categorization modules on CPAN, many of them better than the one I ended up using. A technique called Bayesian analysis has been in vogue for text categorization in the past year or two, particularly for spam filtering, but to be honest, it's not really that good. There's a module called <i>AI::Categorizer</i> that implements a whole bunch of better approaches.</p>

<p>On the other hand, one of the advantages of Bayesian analysis being in vogue is that it has been implemented a lot, and this means that it's likely that one of its implementations has a very simple interface. <i>Algorithm::NaiveBayes</i> was the module I picked in the end, and it does indeed have an easy-to-use interface.</p>

<p>The analyzer in <i>Algorithm::NaiveBayes</i> takes a number of "attributes" and corresponding "weights," and associates them with a set of "labels." Typically, for a text document, the attributes are the words of the document, and the weight is the number of times each word occurs. The labels are the categories under which we want to file the document.</p>

<p>For instance, suppose we have the news article:</p>

<blockquote>
<pre>
The YAPC::Taipei website is now open and calls for registration.
Welcome to Taipei and join the party with us!

</pre>
</blockquote>

<p>We would split that up into words, count the occurrence of the relevant words, and produce a hash like this:</p>

<blockquote>
<pre>
my $news = { 
  now =&gt; 1, taipei =&gt; 2, join =&gt; 1, party =&gt; 1, us =&gt; 1,
  registration =&gt; 1, website =&gt; 1, welcome =&gt; 1, open =&gt; 1,
  calls =&gt; 1, yapc =&gt; 1 
};

</pre>
</blockquote>

<p>Then, we can add this article to our categorizer's set of known documents:</p>

<blockquote>
<pre>
$categoriser-&gt;add_instance( attributes =&gt; $news, 
                            labels     =&gt; [qw( perl conference 
                                               interesting )] );

</pre>
</blockquote>

<p>When we've added a load of documents, the categorizer has a good idea of what kind of words make up Perl stories, what kind of words suggest that the document is about current affairs, what kind of stories I would categorize as "interesting," and so on. We then ask it to do some sums, and can find out what it thinks about a new document:</p>

<blockquote>
<pre>
$categoriser-&gt;train;
my $probs = $categoriser-&gt;predict( $new_news );

</pre>
</blockquote>

<p>This returns a hash mapping each category to the probability that the document fits into that category. So, once we have our categorizer trained with a few hand-categorized documents, we can say:</p>

<blockquote>
<pre>
if ( $categoriser-&gt;predict( $new_news )-&gt;{interesting} &gt; 0.5 ) {
    # Probably interesting 
}

</pre>
</blockquote>

<p>Hooray!</p>

<p>Of course, you may be asking how we turned those documents into hashes of words anyway, and what I meant when I said we only look at the "relevant" words.</p>

<p>Well, turning a string into a hash isn't too difficult:</p>

<blockquote>
<pre>
my %hash;
$hash{$_}++ for split /\s+/, $string;

</pre>
</blockquote>

<p>This doesn't do too badly, but it does have some problems. It copes badly with punctuation, for starters, it passes on things that are not words, like numbers, and it treats differently cased words ("the" versus "The") separately. We can try refining it a little:</p>

<blockquote>
<pre>
$hash{$_}++ for map lc, split /\W+/, $string;

</pre>
</blockquote>

<p>Or, we could try a fundamentally better approach; the <i>Lingua::EN::Splitter</i> module was designed for precisely this kind of task and knows to normalize case, be careful of punctuation, and so on. We can ask its <i>words</i> function to return just the words in a string and leave the implementation details to someone else:</p>

<blockquote>
<pre>
$hash++ for @{words($string)};

</pre>
</blockquote>



<p>That's neater; however, it still has two problems. First, there are a lot of words in English text that are pretty much redundant. They don't carry any semantic content, they're just there to move the story along, as it were&#151;words like "as," "it," and "were." These words, called "stopwords," don't help us know whether or not something is about Perl or about Java, whether a text is interesting or not, so we should get rid of them.</p>

<p>Of course, we don't want to collect a list of such stopwords ourselves. That's a job for the author of the <i>Lingua::EN::StopWords</i> module, which exports the <i>%StopWords</i> hash. This allows us to grep out the stopwords from our list, like so:</p>

<blockquote>
<pre>
$hash++ for grep {!$StopWords{$_}} @{words($string)};

</pre>
</blockquote>



<p>The second problem is that, for an <i>XML::RSS</i> item, we should give more weight to words in the title of a news item than in the description because the title is supposed to be a concise summary of what the piece is about. Hence, we come up with two functions like this:</p>

<blockquote>
<pre>
sub invert_item {
    my $item = shift;
    my %hash;
    invert_string($item-&gt;{title}, 2, \%hash);
    invert_string($item-&gt;{description}, 1, \%hash);
    return \%hash;
}

sub invert_string {
    my ($string, $weight, $hash) = @_;
    $hash-&gt;{$_} += $weight for
        grep { !$StopWords{$_} }
        @{words($string)};
}

</pre>
</blockquote>

<p>This gives title words twice as much weight as the words in the body and returns a hash we can feed to the categorizer. We're only interested in two categories, "interesting" and "boring," so that makes it easier for us.</p>

<p>Now, we have enough parts of the equation to put together our automatic newsreading tool: A way to mark a document as interesting or boring, and a way to predict whether a new document is likely to be interesting.</p>
<h3>Automatic Newsreading</h3>

<p>How do we fit this into Timesink? The first modification is to provide the user the interface for categorizing a story manually. User interface stuff lives in the templates, so we'll begin by editing the templates.</p>

<p>After the description of the story, we add two links that the user can use to categorize the story, and we add a display showing the percentage of "interest," using a method we'll fill in soon:</p>

<blockquote>
<pre>
     [% IF item.published %]
        &lt;small&gt;Published: [% date.format(item.published) %]&lt;/small&gt;
     [% END %]
 +    [% IF user %]
 +    &lt;p align="right"&gt;
 +        [ &lt;a href="?feed=[%feed.id %]&amp;item=[% item.id %]&amp;class=interesting"&gt; 
 +          Interesting? &lt;/a&gt; ]
 +        [ &lt;a href="?feed=[%feed.id %]&amp;item=[% item.id %]&amp;class=boring"&gt; 
 +          Boring? &lt;/a&gt; ]
 +
 +        ( [% user.interest(item) * 100  | format("%2.1d") %]%)
 +    &lt;/p&gt;
 +    [% END %]

</pre>
</blockquote>

<p>Second, if these links are clicked, we need to do something&#151;hand off the ID of the story and the ID of the user to a routine that adds a new document to an <i>Algorithm::NaiveBayes</i> categorizer. Thankfully, the <i>Template::Plugin::CGI</i> helper makes it really easy for us to get the data back out of the query string:</p>

<blockquote>
<pre>
     IF user;
         feeds = user.feeds;
 +       IF cgi.param('class');
 +          SET dumm = user.mark_item(cgi.param('item'), cgi.param('class'));
 +       END;
     ELSE;

</pre>
</blockquote>

<p>Remember that the class is going to be interesting or boring depending on which link the user clicked. Finally, from the template point of view, we need to change the order in which the stories are displayed so that the most interesting stories float to the top:</p>

<blockquote>
<pre>
      &lt;h2&gt;&lt;a href="./"&gt;Timesink:&lt;/a&gt; &lt;a href="[% feed.link %]" target="_new"&gt;[% feed.name %]&lt;/a&gt;&lt;/h2&gt;
 -[% FOREACH item = feed.items.nsort('seq') %]
 +    [% 
 +    IF user &amp;&amp; feed;
 +        SET items = user.sort_interest(feed.items);
 +    ELSE;
 +        SET items = feed.items.nsort('seq');
 +    END;
 +    FOREACH item = items %]

</pre>
</blockquote>

<p>Now we have a few methods that we need to write: the <i>mark_item</i> method, which calls <i>add_element</i> given a user and a feed ID; <i>interest</i>, which trains the categorizer and computes the probability that an article is interesting; and the <i>sort_interest</i> function, which sorts the articles (unseen articles by interest, then seen articles by interest).</p>

<p>These methods have to be on the <i>user</i> object (<i>Timesink::DBI::Subscriber</i>) because what's considered interesting depends on who's reading the page. However, we immediately have a problem.</p>

<p>Where do we acquire the <i>Algorithm::NaiveBayes</i> categorizer that we're going to use? This somehow has to be persistent because we want the user to be able to go away and come back to the web site tomorrow and still have the system know what makes an interesting article, and it needs to work on a per-user basis.</p>

<p>We also need to save away the categorizer after we add a new document to the corpus, and restore it again when we need to make a decision about a new piece of news. The <i>Algorithm::NaiveBayes</i> object is very simple, and we can use the core <i>Storable</i> module to serialize and reload it:</p>

<blockquote>
<pre>
my %bayes_cache;
sub _bayes_file { $config-&gt;bayes_toks."/".shift-&gt;id.".nb"; }

sub get_bayes {
    my ($self) = @_;
    my $user = $self-&gt;id;
    return $bayes_cache{$user} if $bayes_cache{$user};

    my $nb;
    eval { $nb = retrieve($self-&gt;_bayes_file) } if -e $self-&gt;_bayes_file;
    $nb ||= $bayes_cache{$user} = Algorithm::NaiveBayes-&gt;new(purge =&gt; 0);
}

</pre>
</blockquote>

<p><i>_bayes_file</i> turns the user's ID into a file name and <i>get_bayes</i> uses <i>Storable::retrieve</i> to get the object from that file, unless we already have an up-to-date analyzer hanging around in the <i>%bayes_cache</i> hash.</p>

<p>Similarly, when we're finished with the object, we use <i>Storable::store</i> to put the analyzer back to file:</p>

<blockquote>
<pre>
sub done_with_bayes {
    my $self = shift;
    my $nb = shift;
    store($nb, $self-&gt;_bayes_file);
}

</pre>
</blockquote>

<p>Now, filling in the missing methods becomes nice and simple. For instance, marking an item as interesting or uninteresting is:</p>

<blockquote>
<pre>
sub mark_item {
    my ($self, $item_id, $class) = @_;
    return unless my $item = Timesink::DBI::Item-&gt;retrieve($item_id);
    my $nb = $self-&gt;get_bayes;
    $nb-&gt;add_instance( attributes =&gt; $self-&gt;invert_item($item),
                       label      =&gt; $class );
    $self-&gt;done_with_bayes($nb);
}

</pre>
</blockquote>

<p>While interest is a thin wrapper around the routine we saw earlier:</p>

<blockquote>
<pre>
sub interest {
    my ($self, $item) = @_;
    eval { $self-&gt;get_bayes-&gt;predict(
            attributes =&gt; $self-&gt;invert_item($item)
           )-&gt; {interesting}; 
         };
}

</pre>
</blockquote>

<p>Finally, to sort the items efficiently, we use the "Orcish Maneuver" to cache the things we're looking up:</p>

<blockquote>
<pre>
sub sort_interest {
    my ($self, $items) = @_;
    my @items = @$items;
    my $nb = $self-&gt;get_bayes;
    $nb-&gt;train;
    my (%seen, %interest);
    my @sorted = sort {
        ($seen{$a} ||= $self-&gt;is_seen($a)) &lt;=&gt; 
            ($seen{$b} ||= $self-&gt;is_seen($b))
                           ||
        ($interest{$b} ||= $self-&gt;interest($b)) &lt;=&gt; 
            ($interest{$a} ||= $self-&gt;interest($a))
    } @items;
    $self-&gt;done_with_bayes($nb);
    return \@sorted;
}

</pre>
</blockquote>



<p>(Note: The "Orcish Maneuver" is a term coined by Joseph Hall and Randal Schwartz in their book <i>Effective Perl Programming</i> [Addison-Wesley, 1997, ISBN 0201419750] and is so named as a pun on the phrase "or cache.")</p>

<p>The complicated <i>sort</i> block will first find articles that haven't been seen before, then will order articles by interest from highest to lowest.</p>

<p>As you click to signify that you find a piece interesting, it floats to the top while less interesting articles sink to the bottom. Over time, my automatic newsreader learns what I like to read and gets better at suggesting stories for me, saving me even more time and allowing me to find new and interesting ways to avoid getting on with writing <i>The Perl Journal </i>articles...</p>



<p><i>[Ed: We'd like to point out that since Simon's procrastination has now become highly efficient, we'll be asking him for his column one week early next month.]</p>




<p><b>TPJ</i></b></p>




</body>
</html>