
<html>
<head>
<title>June, 2004: Making the Most of Exporter and use</title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; The Perl Journal-->

<h1>Making the Most of Exporter and use</h1>
<p><i>The Perl Journal</i> June, 2004</p>
<h2></h2>


<h3>By Randal L. Schwartz</h3>


<I>Randal is a coauthor of Programming Perl, Learning Perl, Learning Perl for Win32 Systems and Effective Perl Programming, as well as a founding board member of the Perl Mongers (perl.org). Randal can be reached at merlyn@stonehenge.com.</I>

<hr>





<p>Unless this is your first day of programming in Perl, I think it would be safe to say that you've used <i>use </i>at least once, and probably with some module that imports subroutines into your current package namespace. Let's take a look at precisely how that works.</p>

<p>As an example, let's look at the <i>LWP::Simple</i> module. By default, having:</p>

<blockquote>
<pre>
use LWP::Simple;

</pre>
</blockquote>



<p>at the top of my program means I can safely invoke <i>get</i>, <i>head</i>, <i>getprint</i>, <i>getstore</i>, and <i>mirror</i>, as well as a series of <i>HTTP::Status</i> constants, treating them as if I had written the program directly. I can narrow down that namespace pollution with a direct specification of things to import:</p>

<blockquote>
<pre>
use LWP::Simple qw(mirror RC_OK RC_NOT_MODIFIED);

</pre>
</blockquote>



<p>And now I get only the <i>mirror</i> routine and its two likely return statuses.</p>

<p>These invocations act as if I had included a <i>BEGIN</i> block wrapped around a <i>require</i> operation followed by a method call. For example, that latter invocation looks like this:</p>

<blockquote>
<pre>
BEGIN {
  require LWP::Simple;
  LWP::Simple-&gt;import(qw(mirror RC_OK RC_NOT_MODIFIED));
}

</pre>
</blockquote>

<p>Notice first that because it's a <i>BEGIN</i> block, we're talking about an operation that happens at compile time, not deferred until runtime. As the package name of <i>LWP::Simple</i> is fed to <i>require</i>, it is converted into an appropriate directory/subdirectory path for the architecture running the program, with a .pm extension automatically tacked on, as if we had said:</p>

<blockquote>
<pre>
require "LWP/Simple.pm";

</pre>
</blockquote>



<p>on a UNIX architecture machine.</p>

<p>Because we're using <i>require</i>, the <i>@INC</i> path is automatically consulted to locate the file. By default, this includes the system directories where Perl was installed and the current directory. We can alter the <i>@INC</i> path permanently by recompiling Perl, or temporarily through a combination of setting the <i>PERL5LIB</i> environment variable, adding a <i>-I</i> command-line switch, or preceding this use with either a <i>use lib</i> setting or something else that alters <i>@INC</i> at compile time.</p>

<p>Also, because we're using require, the .pm file must have a <i>true </i>value as the last expression evaluated. This is typically provided with a simple:</p>

<blockquote>
<pre>
1;

</pre>
</blockquote>



<p>at the end of the file; although, if you were being perverse, you could change that to:</p>

<blockquote>
<pre>
"this line intentionally left true";

</pre>
</blockquote>



<p>or something else as long as it was <i>true</i>.</p>

<p>Up to this point, we simply have a <i>require</i> operator that runs at compile-time instead of runtime, with a consistent name-management scheme to find the file. But the final step of <i>use</i> is the implicit method call, and this is where we get our subroutine name imports.</p>

<p>A typical module brought in with <i>use</i> will define subroutines in the package namespace appropriate to that module; for example, <i>LWP::Simple</i> defines <i>LWP::Simple::get, LWP::Simple::mirror</i>, and so on. The method call to import within <i>LWP::Simple</i>'s class causes these names to be aliased to their same name within the invoker's namespace (typically <i>main</i>). This is the result of <i>LWP::Simple</i> inheriting from <i>Exporter</i>, a core Perl module that provides a relatively flexible and smart import method to handle the aliasing.</p>

<p>This means that <i>LWP::Simple</i>, like most modules, begins with something like:</p>

<blockquote>
<pre>
package LWP::Simple;
use base qw(Exporter);

</pre>
</blockquote>

<p>or equivalently, but backward compatible for fairly old Perl versions:</p>

<blockquote>
<pre>
package LWP::Simple;
use vars qw/@ISA/;
require Exporter;
@ISA = qw(Exporter);

</pre>
</blockquote>



<p>But now we've just moved the problem around a bit. The author of <i>LWP::Simple</i> doesn't have to write <i>import</i>, but <i>Exporter::import</i> still needs to know what to do. The <i>Exporter::import</i> routine looks at the <i>@EXPORT </i>and <i>@EXPORT_OK</i> variables in the current package to determine the permitted exports.</p>

<p>For example, <i>LWP::Simple</i> starts with something like:</p>

<blockquote>
<pre>
use vars qw(@EXPORT @EXPORT_OK);
@EXPORT = qw(get head getprint getstore mirror);
@EXPORT_OK = qw($ua);

</pre>
</blockquote>

<p>This means that, by default, the five subroutines are imported into the caller's namespace. However, any specific list of imports must come from all six names (everything in both <i>@EXPORT</i> and <i>@EXPORT_OK</i>). In truth, the exports of <i>HTTP::Status</i> are also added to <i>@EXPORT</i>, but let's ignore them for a second.</p>

<p>So, if I ask for:</p>

<blockquote>
<pre>
use LWP::Simple;

</pre>
</blockquote>



<p>I get the default list (everything in <i>@EXPORT</i>). If I ask for just <i>get</i> and <i>mirror</i> explictly:</p>

<blockquote>
<pre>
use LWP::Simple qw(get mirror);

</pre>
</blockquote>



<p>then that's all I get. If I ask for something not in the list:</p>

<blockquote>
<pre>
use LWP::Simple qw(head tail);

</pre>
</blockquote>



<p>then <i>Exporter::import</i> aborts because <i>tail</i> is neither <i>@EXPORT</i> nor <i>@EXPORT_OK</i>.</p>

<p>We can completely skip the invocation of import with an empty set of parenthesis:</p>

<blockquote>
<pre>
use LWP::Simple ();

</pre>
</blockquote>



<p>The <i>$ua</i> member of <i>@EXPORT_OK</i> here is actually a package variable, initially defined as <i>$LWP::Simple::ua</i>, but available for import into my namespace. As a rule of thumb, it's generally better to export behavior (subroutines or objects) rather than data (variables) because once your data format is "out there," it's hard to change it for future releases.</p>

<p>What if we want "all the default, plus this one" or "all the default, except for that one"? It turns out that <i>Exporter::import</i> is actually fairly flexible. We can use:</p>

<blockquote>
<pre>
use LWP::Simple qw(:DEFAULT $ua);

</pre>
</blockquote>



<p>to mean "all the default and <i>$ua.</i>" Similarly, we can use:</p>

<blockquote>
<pre>
use LWP::Simple qw(:DEFAULT !getprint !getstore);

</pre>
</blockquote>



<p>to mean "all the default without these two listed names." Additionally, we can use regular expressions, such as:</p>

<blockquote>
<pre>
use LWP::Simple qw(:DEFAULT !/^get/);

</pre>
</blockquote>



<p>to mean "all the default without any name beginning with get." An expression that begins with <i>!</i> appearing at the beginning of the list implies a <i>:DEFAULT</i> ahead of it, so we can write that last one more simply as:</p>

<blockquote>
<pre>
use LWP::Simple qw(!/^get/);

</pre>
</blockquote>



<p>We could even ask for:</p>

<blockquote>
<pre>
use LWP::Simple qw(/^/);

</pre>
</blockquote>



<p>to mean "everything that can possibly be exported" (because the regular expression of <i>/^/ </i>matches all possible strings).</p>

<p>We can define additional shortcut keywords such as <i>:DEFAULT</i> if subsets of our export list are frequently desired. For example, if the <i>get</i> routines are frequently selected or deselected as a group, we can group them as the <i>:GET</i> group like so:</p>

<blockquote>
<pre>
package LWP::Simple;
use base qw(Exporter);

use vars qw(@EXPORT @EXPORT_OK %EXPORT_TAGS);
@EXPORT = qw(get head getprint getstore mirror);
@EXPORT_OK = qw($ua);
%EXPORT_TAGS = ("GET" =&gt; [qw(getprint getstore)]);

</pre>
</blockquote>

<p>And now we can include them or exclude them easily:</p>

<blockquote>
<pre>
use LWP::Simple qw(:GET);

</pre>
</blockquote>



<p>which brings in only <i>getprint</i> and <i>getstore</i>, or:</p>

<blockquote>
<pre>
use LWP::Simple qw(!:GET);

</pre>
</blockquote>



<p>which gives us the default list, minus <i>getprint</i> and <i>getstore</i>, or even:</p>

<blockquote>
<pre>
use LWP::Simple qw(/^/ !:GET);

</pre>
</blockquote>



<p>which is all possible exports, except for <i>getprint</i> and <i>getstore</i>.</p>

<p>If <i>/^/</i> is a bit mystical to explain to your users, you can help them along with an <i>:ALL</i> tag:</p>

<blockquote>
<pre>
%EXPORT_TAGS = (
  "ALL" =&gt; [@EXPORT, @EXPORT_OK],
  "GET" =&gt; [qw(getprint getstore)]
);

</pre>
</blockquote>

<p>which permits us to say:</p>

<blockquote>
<pre>
use LWP::Simple qw(:ALL);

</pre>
</blockquote>



<p>and get all possible exports and:</p>

<blockquote>
<pre>
use LWP::Simple qw(:ALL !:GET);

</pre>
</blockquote>



<p>to get everything except the <i>getprint</i> and <i>getstore</i> routines.</p>

<p>You can also go the other way, defining your symbols primarily in <i>%EXPORT_TAGS</i>, and then adding them to the <i>@EXPORT</i> and <i>@EXPORT_OK</i> variables:</p>

<blockquote>
<pre>
package My::Package;
use base qw(Exporter);
use vars qw(%EXPORT_TAGS);
%EXPORT_TAGS = (
  "CORE" =&gt; [qw(core_enable core_disable)],
  "FIRE" =&gt; [qw(halt_and_catch_fire)],
  "VARS" =&gt; [qw($core_heat $core_fire)],
);
Exporter::export_tags('CORE', 'FIRE'); # default list
Exporter::export_ok_tags('VARS'); # optional list

</pre>
</blockquote>

<p>Another feature of <i>Exporter</i> and <i>use</i> is the ability to check version numbers. If we include a version number before the (optional) import list, the <i>$VERSION</i> variable of the package will be checked to ensure that it is not smaller than the requested version. For example, if <i>My::Package</i> is defined as:</p>

<blockquote>
<pre>
package My::Package;
use base qw(Exporter);
use vars qw($VERSION @EXPORT @EXPORT_OK);
$VERSION = 1.02;
@EXPORT = ...;
@EXPORT_OK = ...;

</pre>
</blockquote>

<p>and I try to invoke it with:</p>

<blockquote>
<pre>
use My::Package 1.05;

</pre>
</blockquote>



<p>I will get a complaint that the version number is not sufficient. This feature is provided by the <i>Exporter::require_version</i> method, and compares the numbers as floating-point values. If you want a different sort of comparison, you should write your own.</p>

<p>Because <i>Exporter</i> exports a half-dozen routines into the current namespace, you might want to narrow it down to just <i>import</i>:</p>

<blockquote>
<pre>
package My::Package;
use Exporter qw(import);

</pre>
</blockquote>

<p>and now we don't have to worry about other name collisions. However, we just broke version checking, as described above, so be careful with this.</p>

<p>So far, we've seen most of the standard things you can do with <i>Exporter::import</i>. But we can also write our own <i>import</i> routine to do nonstandard tasks. For example, the <i>use lib</i> module takes the import list and adds it to <i>@INC</i>. The code is roughly something like:</p>

<blockquote>
<pre>
package lib;
sub import {
  unshift @INC, @_;
}

</pre>
</blockquote>



<p>which simply prepends the arguments passed to <i>lib::import</i> to the <i>@INC</i> path. (The actual <i>lib</i> module also deals with architecture-specific path additions.)</p>

<p>Another example of a very complex <i>import</i> routine is the core CGI module, which supports a version of colon-prefixed keywords as well as some other flags to trigger various actions.</p>

<p>You can, of course, create your own import routines as well:</p>

<blockquote>
<pre>
package My::Package;

sub import {
  my $filename = shift;
  open MY_LOG, "&gt;&gt;$filename" or die;
}

</pre>
</blockquote>

<p>and now use this as:</p>

<blockquote>
<pre>
use My::Package qw(/my/log/file);

</pre>
</blockquote>



<p>The indicated filename will be opened as the (package based) filehandle for logging.</p>

<p>Well, I think this about wraps it up for now. Hopefully, you've seen a few new ways to use your import list. Until next time, enjoy!</p>


<p><b></b></p>
<p><b></b></p>
<p><b>TPJ</b></p>




</body>
</html>