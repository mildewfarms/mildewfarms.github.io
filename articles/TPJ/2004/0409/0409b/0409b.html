
<html>
<head>
<title>September, 2004: Scoping: Letting Perl  Do the Work for You</title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; The Perl Journal-->

<h1>Scoping: Letting Perl  Do the Work for You</h1>
<p><i>The Perl Journal</i> September, 2004</p>
<h2></h2>


<h3>By David Oswald</h3>


<I>Dave may be reached via e-mail at daoswald@adelphia.net or on http://www.perlmonks.org/ as davido.</I>

<hr>





<p>In 1982, my parents took a comedian's advice and bought a Texas Instruments TI-99/4a computer. It doesn't matter two decades later what computer I grew up with, so much as where it led. I embraced BASIC, not because it was spectacular, but because it was what was immediately available. And there was a lot of support for it out there back then. Armed with articles from <i>Compute!</i> and a few books, I began my adventure. Despite the primitive and limited nature of early BASIC implementations, I learned a thought process and developed a passion.</p>

<p>Those of you who remember early BASICs probably have fond memories of developing little gee-whiz programs that presented simple games and solved trivial problems. But despite its popularity, BASIC lacked many of the refinements that the previous 20 years of computer science had developed, refinements that, in the 20 years since, have become de facto standards. Subroutines in primitive BASICs consisted of segments of code accessed via <i>gosub</i>. There were no such things as user-defined functions, nor were there user subroutines with parameter lists. All variables were global, all code was on the same playing field, and as you may recall, that was a recipe for confusion as programs grew in size. It didn't help that it was common for us novice programmers to use such descriptive variable names as <i>x</i> and <i>a</i>. After a few lines of code, it became difficult to remember what different symbols stood for, and even more difficult to remember which symbols were already in use. This situation was mitigated by the fact that computers of the day usually were a little short on memory; a constraint that inhibited the creation of disastrously long BASIC programs. Languages such as Pascal overcame many of BASIC's shortcomings, but were considered beyond the reach of the hobbyists who took to BASIC.</p>

<p>Perl has had the benefit of growing up the brainchild of some very bright minds from our generation of computer science. But Perl isn't too proud to borrow an idea or two from the bright minds behind other programming languages, as well. It comes as no surprise then, that Perl implements many of the ideas of modularization and scoping found in languages like Pascal, C, Modula-II, and C++, ideas reinforced in my computer-science courses. Of course, Perl is equally likely to ignore some of the rules these languages established. But we're going to focus on a feature that Perl has (in my opinion) truly excelled at: scoping.</p>
<h3>Scope</h3>

<p>Perl provides a rich set of tools to control scope that can be broken into three categories: package scope, dynamic scope, and lexical scope. First, let's talk about packages. Packages house namespaces implemented as glorified hashes. It is a common idiom for there to exist a 1:1 relationship between packages and files, but this is only a convention, and it's entirely possible to find packages that span files and files that contain multiple packages. None of that really matters: A package is a namespace and that's all we really need to remember.</p>

<p>A variable that lives in a package's namespace is called a "package global." Often the word "package" is left off, and we talk about "global variables." We almost always mean package globals. Even the main portion of a Perl script exists within a package: package <i>main</i>. Perl isn't particularly strict about who can look at a package's globals; other packages can inspect each others' package globals, and these variables can even be exported into other packages, including package <i>main</i>. But the general idea is that package globals are scoped to their package. Because of all the tricks we can play with package globals, they're incredibly useful in implementing Perl's modules. But this article is about letting scoping do the work for you. To this end, I want to shift the focus away from package globals. Their overuse also tends to lead to the sort of code we tried to leave behind 20 years ago. So let's move on to discuss the primary subject: lexicals.</p>

<p>I mentioned earlier that Perl provides three primary types of scope: package (the global symbol tables); dynamic (a means of manipulating the global symbol table); and lexical. Lexical scope is built and controlled by blocks. Blocks can exist in the form of files, packages, bare <i>{ ... }</i> blocks, loops, conditionals, <i>eval</i> blocks, code blocks (as in <i>map { ... } @array</i> or <i>sort { $b &lt;= $a } @array</i>), and subroutines. Where package globals are visible to other packages via the use of fully qualified names (i.e., <i>$MyPackage::varname</i>), lexically scoped variables are not visible to the world outside of their scope. But Perl is a little smarter about how this works than a language such as C. "Auto" variables in C should never have a pointer to them passed outside of their scope. But with Perl, it's perfectly OK to pass references to a lexically scoped variable to the world outside of that variable's scope. More on this in a minute.</p>

<p>Lexical variables are declared using <i>my()</i>. They can be initialized at time of declaration. I'm going to assume that this is enough information on how to declare a lexical. If not, have a look at <i>perldoc -f my</i> and <i>perldoc perlsub</i> for more information. It's a good read.</p>
<h3>Reference Counting</h3>

<p>Perl, unlike C, handles garbage collection by itself with the reference-counting technique. When a lexical variable is declared, its reference count increments to one. When it falls from scope, the reference count is decremented. If it drops to zero, the variable is garbage collected. This is a bit of an oversimplification, but sufficient for our discussion. Now, what happens if in addition to the named variable, a reference to that variable also exists, perhaps at a broader scope? See the following example:</p>

<blockquote>
<pre>
my $ref;
{		# Create a block-bound lexical scope.
    my $var = 10;
    print $var, "\n";
    $ref = \$var;	# Create a reference to $var, with a variable
		# that is declared at broader scope.
}		# Close the block-bound lexical scope.
print $var, "\n";	# Nothing prints. $var is out of scope.
print $$ref, "\n";	# Dereference $ref, and thus, print '10'.

</pre>
</blockquote>

<p>You can see from this example that though <i>$var</i> has passed out of scope, <i>$ref</i>, which holds a reference to <i>$var</i>, is keeping the contents of <i>$var</i> alive. <i>$var</i> is inaccessible by name, but by reference its value is still available. If <i>$ref</i> also passed out of scope, the reference count would be decremented again, and reaching zero would result in garbage collection.</p>
<h3>Maintainable Style</h3>

<p>One of the primary advantages of lexical scoping is that it promotes a maintainable programming style. If a programmer keeps the variable scope narrow, it becomes less important to worry about whether <i>$idx</i> is being used elsewhere in a script, so long as any preexisting use isn't needed within the current lexical scope. For example:</p>

<blockquote>
<pre>
use strict;
use warnings;
my $var = 10;
{
    my $var = 20;
    print $var, "\n";
}
print $var, "\n";

</pre>
</blockquote>

<p>The output will be 20, and then 10. This is because within the narrower lexical block, we've declared and defined a new <i>$var</i>, which means that whatever we do to or with <i>$var</i>, in that lexical block, the <i>$var</i> existing at a broader scope is unaffected. A declaration at narrower scope masks variables of the same name and type that exist at broader scopes. That means that each lexical scope can, if needed, become a new private namespace. <i>my()</i> is aptly named; just think of a lexical scope being the person talking: "My <i>$var</i> equals 10" (as opposed to some other scope's <i>$var</i>).</p>

<p>Lexical scoping obviously may be nested. Narrower scopes will have access to all the variables declared at broader scopes, so long as those variables haven't been masked by a declaration at the narrower scope. But completely separate scopes that aren't nested won't have such access. For example:</p>

<blockquote>
<pre>
{
    my $this = 10;
}
{
    print $this, "\n";
}

</pre>
</blockquote>

<p>Here we have two separate lexical blocks. They're not nested, thus <i>$this</i> is unavailable to be printed.</p>
<h3>Destructors</h3>

<p>When a lexical variable passes out of scope and its reference count drops to zero, it is garbage collected or destroyed. Normally, this has the simple effect that the memory consumed by the value of the variable is relinquished back to Perl, and the name that accessed it becomes inaccessible. But sometimes there are other side effects. Lexical scoping can be put to work taking advantage of those side effects for your benefit.</p>

<p>One example of the destructor doing more than just reclaiming memory is with the <i>open</i> command. If the filehandle being opened is a scalar with undefined value, it becomes a lexical filehandle. What happens when a lexical filehandle falls out of scope? The file gets closed implicitly.</p>

<blockquote>
<pre>
my $filename = 'somefile.txt';
my $linecount = 0;
{
    open my $fh, '&lt;', $filename or die $!;
    while( my $line = &lt;$fh&gt; ) {
        print $line;
        $linecount++;
    }
}
print $linecount, "\n";

</pre>
</blockquote>

<p>This is a complete snippet (though contrived, and not really all that useful). By complete I mean that the filehandle held in <i>$fh</i> gets closed as soon as its enclosing lexical block falls out of scope. This leads to a common Perl file-slurping idiom that relies on lexical scoping to close the file being read:</p>

<blockquote>
<pre>
my @lines = do{ open my $fh, '&lt;', $filename or die $!;
                &lt;$fh&gt;;
              };

</pre>
</blockquote>

<p>Now a file has been opened, its contents slurped into <i>@lines</i>, and its handle implicitly closed as the <i>do{...}</i> block finishes. The one caveat is that if you open a file for output and let the filehandle close implicitly via the magic of lexical scoping, you won't be able to perform the <i>or die $!;</i> error checking on the <i>close()</i> function.</p>

<p>Destructors also apply to object-oriented programming. If you define a <i>DESTROY()</i> method as part of your object, whatever code exists in that method will be executed as the object's entity reference falls out of scope. For example:</p>

<blockquote>
<pre>
package MyPack;
sub new {
    my $class = shift;
    bless \my $self, $class;
}
sub DESTROY {
    print "Goodbye.\n";
}
1;

package main;
{
    my $obj = MyPack-&gt;new();
    print "The object was created.\n";
    print "Now we're going to let it fall out of scope.\n";
}
print "See, it was just destroyed.\n";

</pre>
</blockquote>



<p>As you see, when the object's last reference falls out of scope, it is destroyed, and the <i>DESTROY()</i> method is invoked prior to the final garbage collection. This is useful anytime you have cleanup that needs to take place when an object disappears from existence.</p>

<p><i>DESTROY()</i> also applies to tied entities. That means that if you use <i>tie</i> to tie a scalar to a class, you can define the <i>DESTROY()</i> method to carry out some task when the tied scalar falls out of scope.</p>
<h3>Closures</h3>

<p>No discussion of lexical scoping would be complete without mention of closures. This topic took me a little time to pick up, but it's really not all that complicated.</p>

<p>A closure is a situation where a lexical scope has closed and a reference to a sub defined within that scope is passed to the outside world. That closure sub still has access to the lexical variables that existed within the scope that just ended. Confusing? Look at this:</p>

<blockquote>
<pre>
my $subref;
{
    my $value = 100;
    $subref = sub { return ++$value; }
}
print $subref-&gt;(), "\n";
print $subref-&gt;(), "\n";

</pre>
</blockquote>

<p>Now is it a little clearer? <i>$value</i> is inaccessible directly from outside its narrowly defined scope. Yet the reference to the sub created in that scope exists at a broader scope. The sub it refers to has full access to whatever variables existed in the scope in which it was defined. Thus, any time you call up that sub referred to by <i>$subref</i> to do its duty, it is able to act upon <i>$value</i>.</p>
<h3>Accessors and Setters</h3>

<p>In the object-oriented world, an accessor is an object method that accesses (or returns) data internal to the object. A setter is an object method that sets data internal to the object. Here's an object-oriented example:</p>

<blockquote>
<pre>
package MyPack;

sub new {
    bless {}, shift;
}

sub setter {
    my( $self, $val ) = @_;
    $self-&gt;{VALUE} = $val;
}

sub accessor {
    my $self = shift;
    return $self-&gt;{VALUE};
}
1;

package main;

my $obj = MyPack-&gt;new();
$obj-&gt;setter("Hello world\n");
my $phrase = $obj-&gt;accessor();
print $phrase;

</pre>
</blockquote>



<p>Though the topic of setters and accessors isn't strictly a discussion about lexical scoping, I provided that example as a means of introducing the fact that setters and accessors may also apply to closures:</p>

<blockquote>
<pre>
my $setref;
my $getref;
{
    my $value;
    $setref = sub { $value = shift; };
    $getref = sub { return $value;  };
}
$setref-&gt;( 100 );
my $closure_val = $getref-&gt;();
print $closure_val, "\n";

</pre>
</blockquote>
<h3>Putting the Pieces Together</h3>

<p>Lexical scoping can and should be used to constrain a variable to the narrowest useful scope. This practice will aid in writing maintainable code; code where a minor change here won't ripple into a major disaster somewhere else, and code where it is easy (or at least possible) for someone to come along after the fact and understand what use a particular variable has.</p>

<p>I also hope to have illustrated that lexical scoping can be used to perform complex tasks through the use of destructors and closures. I encourage you to proceed from here to Perl's POD. In particular, perlsub and perlref will assist you in gaining a firmer grasp on what lexical scoping is all about. Finally, I hope that its use will help you to get more out of Perl.</p>






<p><b>TPJ</b></p>




</body>
</html>