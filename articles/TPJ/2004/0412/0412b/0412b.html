
<html>
<head>
<title>December, 2004: Metamodeling with  Perl and AMPL</title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; The Perl Journal-->

<h1>Metamodeling with  Perl and AMPL</h1>
<p><i>The Perl Journal</i> December, 2004</p>
<h2></h2>


<h3>By Christian Hicks and Dessislava Pachamanova</h3>


<I>Christian is president and cofounder of Elysium Digital LLC. Dessislava is an assistant professor of operations research at Babson College, MA. They can be contacted at cbhicks@elys.com and dpachamanova@babson.edu, respectively.</I>

<hr>





<p>In the 1980s, a significant conceptual breakthrough was accomplished in computational optimization with the introduction of universal optimization languages, such as the Algebraic, or Applied, Mathematical Programming Language (AMPL) and the General Algebraic Mathematical System (GAMS). AMPL and GAMS do not solve optimization problems. Instead, they provide the environment for users to describe their models in a standardized way, and call on separate solvers to solve the problem. The results are returned to human modelers via the same software.  </p>

<p>Originally developed at AT&amp;T Bell Labs, AMPL is a modeling language for optimization problems. With AMPL, users describe an optimization problem in the AMPL language, after which the AMPL software processes the problem and passes it to a separate optimization solver. AMPL supports numerous commercial and free solvers. For a full listing of available solvers, see http://www.ampl.com/. The Student Edition of AMPL (which limits the number of variables and constraints in the model) is included with <i>AMPL: A Modeling Language for Mathematical Programming</i>, Second Edition, by Robert Fourer, David M. Gay, and Brian W. Kernighan (Duxbury Press/Brooks/Cole Publishing, 2002; ISBN 0534388094). Windows, UNIX (including Linux), and Mac OS X versions of the Student Edition and compatible solvers are also available for free download from http://www.ampl.com/. The full version of AMPL can be purchased from ILOG at http://www.ilog.com/. ILOG also sells an AMPL-compatible version of CPLEX, which is one of the most powerful solvers for linear, mixed-integer, and quadratic optimization. Windows and UNIX (including Linux) binaries of AMPL and CPLEX are available. AMPL is also one of the modeling languages supported by the NEOS Server for Optimization (http://www-neos.mcs.anl.gov/neos/). The NEOS server lets users submit optimization problems via the Internet, solves them for free on a NEOS computer, and returns the results to the submitter.</p>

<p>A simple optimization example from the book <i>AMPL: A Modeling Language for Mathematical Programming</i> is as follows: A steel mill processes unfinished steel slabs into steel bands and steel coils. It can produce 200 tons of bands per hour and 140 tons of coils per hour, but cannot sell more than 6000 tons of bands per week or 4000 tons of coils per week. When sold, bands profit $25.00 per ton, and coils profit $30.00 per ton. How many tons of bands and how many tons of coils should be produced to maximize profit?  </p>

<p>To solve this problem, you create the two input files for AMPL, like <a name="re1"></a><a href="0412be1.html">Examples 1(a) and 1(b)</a>. Of course, this is a simple problem, and AMPL can handle problems far more complex. But real-world optimization models can have infinite variety. Frequently, they require multiple refinements of the model and data, and therefore, multiple calls to optimization and data-processing software. Some optimization formulations involve solving sequences of subproblems. Other models require, in addition to optimization, the ability to analyze data through statistical techniques (to run regressions, for example), the ability to simulate scenarios, the ability to pass results from one stage of the computation as inputs for the next stages of the problem, and so on. We define such complex models as "optimization metamodels."</p>

<p>We ran into the problem of solving optimization metamodels while creating sophisticated financial models that required simulation, statistical analysis, and multistage optimization. As we sought to devise a good system for handling optimization metamodels, we focused on leveraging AMPL because of its modern design and its support of a wide range of solvers. </p>
<h3>Little Languages and Optimization Systems</h3>

<p>In a 1995 talk at Princeton University, Brian Kernighan, one of AMPL's creators, described AMPL as a prime example of a "little language." The term "little languages" was introduced for the first time by Jon Bentley (1986), who was at Bell Labs at the same time AMPL was designed. Bentley considered three different approaches to solving the problem of generating certain images:</p>

<p> </p>

<ul>
  <li>Interactive systems (such as PhotoShop) made drawing simple images easy, but did not allow for programmatic generation of complex pictures. </li>
  <li>Subroutine libraries worked well for generating images from big programs, but were awkward for small tasks. </li>
  <li>Little languages provided an attractive option, since they could be powerful enough to generate complex images in an automated way, but limited enough that users could master the syntax for the language in a reasonable amount of time.</li>
</ul>



<p>Bentley's reasoning applied perfectly to AMPL, which was designed to allow for the easy handling of both simple and complex optimization problems. We used Bentley's reasoning (as well as our own) in creating a system for optimization metamodeling.</p>

<p>Interactive systems were unsuitable because of the complexity of optimization metamodels. The goal of an integrated system was not to help users create simple models&#151;this problem had already been solved by other systems. We placed a premium on supporting complexity and automation.</p>

<p>The little languages approach adopted by AMPL had certainly produced an effective optimization tool, and could be extended to support optimization metamodeling. This would require adding new functionality to the little language for each task that the metamodeler might need; for example, loading data, running simulations, storing and processing results in fully flexible data structures, launching external programs to process intermediate results. In fact, some such capabilities have been added recently: Today, AMPL provides not only an environment for optimization formulations but also tools for data manipulation and database access. For example, AMPL's table declaration lets you define explicit connections between sets, parameters, variables, and expressions in AMPL, and relational database tables maintained by other software, such as Microsoft Access and Excel. In addition, recently introduced command script features let you specify the number of times a model should be rerun, change values of parameters, and specify output format.</p>

<p>The problem with this continuous expansion of AMPL's capabilities is that a metamodel might need a great variety of functionality, to the point that the little language might not be so little anymore. If AMPL were to encompass the entire functionality of C/C++, Perl, or Java, it would be difficult to learn, to say nothing of how difficult it would be to create.</p>

<p>Alternatively, you could allow a little language access to outside resources by providing a mechanism for the invocation of outside code. You can imagine an AMPL program that made calls to other code written in C/C++, Perl, or Java. This option provoked the question: If one language is to be used to call code written in a second language, is it not preferable for the calling language to be the broader, general-purpose language, and the called code to be written in the special-purpose language? The two options are illustrated in <a name="rf1"></a><a href="0412bf1.html">Figures 1(a) and 1(b)</a>. Again, Bentley's paper shed light on the subject, in that he agrees that performance and logic suggest that flow-control elements, such as loops, are better implemented in general-purpose languages like Pascal, rather than in little languages. From a system design standpoint, we believe that the system illustrated in <a href="0412bf1.html">Figure 1(b)</a> is the better option.</p>

<p>This analysis pushed us toward using a subroutine library packaged up in an API. Users can download the library, then call the functions contained in it from within his program. Inputs and outputs to an individual optimization model are variables within the main program, and can therefore be manipulated directly using the full facilities of the programming language. </p>

<p>An existing metamodeling system that we analyzed in this context was Matlab, a programming language that "provides core mathematics and advanced graphical tools for data analysis, visualization, and algorithm and application development" (http:// www.Mathworks.com/). One of the available toolboxes for Matlab is the Optimization Toolbox, which enables general and large-scale optimization, including linear programming, quadratic programming, nonlinear least-squares, and nonlinear equations. Accordingly, Matlab could be viewed as an example of a language that provides access to optimization functionality via a subroutine library.</p>

<p>However, the evolution of Matlab showed us that it is more like a little language that has been expanded to include additional functionality, instead of an efficient general-purpose programming language with subroutine library access to special functionality. This shows in the performance of the Matlab language, especially in its slow handling of loops. Since we wanted to take advantage of good performance and flexibility in our master language, we shied away from Matlab's solution.</p>

<p>Several candidates emerged for the programming language of our library:</p>



<ul>
  <li>Microsoft Visual Basic is easy to learn, allows for the fast creation of attractive user interfaces for programs, and has mechanisms to interact with Excel and Access. However, it has inconsistent syntax, making programming frustrating at times. Programs written in Visual Basic run slowly, and in our experience, they run unreliably, letting the same program behave differently when run repeatedly under the same conditions. This is unacceptable for many programming situations. Additionally, Visual Basic is nonportable, limiting users to Windows, and it is licensed, requiring a license for each programmer.</li>
  <li>C/C++ was an automatic candidate, given how widely the language is used for software development. A C/C++ compiler is available on almost every platform. Programs written in C/C++ tend to run fast (unless written poorly), and there are many existing subroutine libraries available to provide programmers with additional resources. The downside of C/C++ is that programming in the language is difficult and error prone, due to the way low-level memory management and addressing are left in the hands of the programmer. Also, while there are compilers on many platforms, code usually requires some customization for each platform.</li>
  <li>Java was a serious contender, with many strengths. The programming language is rigorously designed for large, object-oriented projects. Programs written in Java tend to be resistant to memory errors, thanks to an effective memory-management model that includes garbage collection. There are many existing Java subroutine libraries available to provide the programmer with additional resources. Finally, portability was a key goal in Java's design, and as a result, Java programs tend to run across several platforms. Java's downside relates to one of its upsides: The language's strict reliance on the object-oriented model makes learning the language difficult and writing small programs cumbersome.</li>
  <li>Perl has very strong built-in data parsing and reformatting functionality. Programs written in Perl are portable, running on many platforms, including all major UNIX variants and Windows versions. The language allows for easy procedural programming for smaller tasks, as well as object-oriented programming for larger projects. Also, there is a wide range of add-ons already available for Perl. This was important because it told us that programmers would have many resources available to them, and that creating such an add-on ourselves was likely to be easier and well documented. On the downside, for very large projects, Perl's object-oriented model is less rigorous than that of Java. Also, graphical user interfaces are harder to create in Perl than in Visual Basic or in some C/C++ toolkits.</li>
</ul>



<p>After some consideration, Perl emerged as the winner. Visual Basic's design weaknesses and per-programmer licensing excluded it easily. C/C++ is too cumbersome. Java was a tempting option, but the ease with which researchers can throw together Perl programs made Perl our choice.</p>

<p>We implemented a Perl-AMPL library and packaged it as a module that is to be distributed via the Comprehensive Perl Archive Network (CPAN; http://www.cpan.org/). CPAN, a large collection of software and documentation that add extra functionality to Perl, is complemented by the Perl Automatic Upload Server (PAUSE). This server lets creators of Perl modules upload to CPAN, where their modules become available to Perl users for free. Because PAUSE makes it possible for Perl programmers around the world to contribute to CPAN, the archive contains thousands of Perl modules on a broad range of subjects: accessing different kinds of databases, communicating via different network protocols, interoperating with different data formats, and so on.</p>

<p>We need to mention here that there are some commercial products that fit the main requirements of the metamodeling system-design framework we have outlined in this article. For example, ILOG's OPL Studio (http://www.ilog.com/products/oplstudio/) includes the ability to build optimization models that are then accessed from a subroutine library using Visual Basic, Java, or C/C++. The main program can generate data for the model dynamically, and the program can trigger the solving of the model and the generated data repeatedly. This indicates that OPL Studio includes the kind of functionality that we endorse for optimization metamodeling; namely, allowing the use of a general-purpose programming language that can make optimization calls to a subroutine library. However, we believe that the existence of CPAN is an important reason for selecting Perl as the general-purpose programming language of the system. By creating a framework that lets a large number of programmers contribute code that they perceive as helpful, CPAN has created the open-source framework by which a product (in this case, Perl) can outgrow the imagination of its own original creators.</p>
<h3>The PerlAmpl Module</h3>

<p>The <i>PerlAmpl</i> module was designed as an object-oriented Perl class. The AMPL object is an abstraction of an AMPL problem. The member functions let you set the problem's model, data, and options, after which member functions can be used to trigger the solver and access the results.</p>

<p>To use the API, include the module in your program with use <i>Math::Ampl;</i>, then initialize the library:</p>

<blockquote>
<pre>
Math::Ampl::Initialize(inAmplDir, inAmplBin, inTempDir, [inPreserveFiles]);

</pre>
</blockquote>

<p>The argument <i>inAmplDir</i> is the directory containing the AMPL binary, <i>inAmplBin</i> is the name of the binary in that directory, <i>inTempDir</i> is a place to store temporary files, and <i>inPreserveFiles</i> is an optional argument (if 1, temp files are not deleted).</p>

<p>You next construct an instance:</p>

<blockquote>
<pre>
$problem = new Math::Ampl;

</pre>
</blockquote>



<p>Then, by using <i>$problem-&gt;Function(Arguments)</i> syntax, you can use the following instance member functions:</p>



<ul>
  <li><i>Input_Mod_Append(inModText)</i> to append <i>inModText</i> to the <i>mod</i> data, which will be passed to AMPL as the model.</li>
  <li><i>Input_Mod_Clear</i> to clear the <i>mod</i> data.</li>
  <li><i>Input_Dat_Add_Set_Item(inSet, inDimension1, ......)</i> adds a set item to the <i>dat</i> data. <i>inSet</i> is the name of the set. <i>inDimension1</i> is the first dimension of the item, and so on.</li>
  <li><i>Input_Dat_Add_Param_Item(inParam, inDimension1, ......, inValue)</i> to add a <i>param</i> item to the <i>dat</i> data. <i>inParam</i> is the name of the <i>param</i>. <i>inDimension1</i> is the first dimension of the item, and so on. <i>inValue</i> is the value.</li>
  <li><i>Input_Dat_Clear</i> to clear the <i>dat</i> data.</li>
  <li><i>Input_Display_Add(inDisplayItem)</i> to add an item to be retrieved from AMPL using <i>Display</i>. After solving, the values of all added items can be retrieved in Perl using <i>Output_Display_Get_Text </i>or <i>Output_Display_Get_Value</i>.</li>
  <li><i>Input_Display_Clear</i> to clear the list of <i>Display</i> items to retrieve.</li>
  <li><i>Input_Option_Add(inOption, inValue)</i> to add an option to be set during the <i>Solve</i> command, right before AMPL actually runs the solver. <i>inOption</i> is the option to set, <i>inValue</i> is the value.</li>
  <li><i>Input_Option_Clear</i> to clear the options to set during <i>Solve</i>.</li>
  <li><i>Solve</i> to run AMPL on the problem. It returns -1 if an error occurred, which prevented the solver from being called, and 0 if the solver was called but could not generate a solution. It returns 1 if the solver found a solution.</li>
  <li><i>Solve_Best_Solver(inMinOrMax, inObjective, inSolver1, ...)</i> to run <i>Solve</i> for each <i>inSolver</i>, keeping track of the best results. <i>inMinOrMax</i> must be either "min" or "max." The function uses <i>inMinOrMax</i> to decide if one result is better than the next. After trying all the solvers, <i>Solve_Best_Solver</i> reruns the best solver to fill the <i>Display</i> variables with the best values (unless the best solver was already the last one, in which case, it does not need to be rerun). This function has been particularly useful for solving difficult nonlinear problems where the solution found may vary from solver to solver.</li>
  <li><i>Output_Display_Get_Value(inParamOrVar, inDimension1, ...)</i> to get the retrieved value of a variable or parameter. The <i>inParamOrVar</i> must have been added using <i>Input_Display_Add</i> before calling <i>Solve</i>.</li>
  <li><i>Output_Display_Get_Text(inParamOrVar)</i> to get the complete text block returned by AMPL for <i>inParamOrVar</i>. The <i>inParamOrVar</i> must have been added using <i>Input_Display_Add</i> before calling <i>Solve</i>.</li>
  </ul>

<p>The <i>PerlAmpl</i> module addresses many of the metamodeling issues mentioned here. The Perl environment facilitates the efficient handling of data and intermediate results, allows leveraging a wide variety of modules to maximize code reuse and minimize product development time, and makes formatting end results easy. We illustrate the system's capabilities with several examples.</p>
<h3>Example: Handling Input</h3>

<p>While the World Wide Web contains an enormous amount of data, it is frequently difficult to download them in the appropriate format. Perl modules, such as <i>Finance::Quote</i>, can be used directly to download stock price data from web sites, such as Yahoo Finance. Perl modules also exist for accessing spreadsheets and databases. For example, <i>DBD::Excel</i> lets you access the data within an Excel file from a database interface, while <i>Spreadsheet::TieExcel</i> lets you tie an array within a Perl program to an Excel spreadsheet.</p>

<p>When a module is not available from CPAN, Perl's strong regular expression engine makes parsing unformatted (or inconveniently formatted data) an easy task. <a name="rf2"></a><a href="0412bf2.html">Figure 2</a> and <a name="rl1"></a><a href="#l1">Listing 1</a> present an example use of <i>PerlAmpl</i> parsing Treasury yield curve data that was then used in a computational study of credit risky bonds. The subroutine <i>read_file</i> reads in data on Treasuries downloaded from http://www.bloomberg.com/ (see <a name="rf2"></a><a href="0412bf2.html">Figure 2</a>) and stores the yields and maturities of the 3-month, 6-month, and 1-year bills in Perl lists <i>@gYieldList</i> and <i>@gPeriodList</i>, respectively (it ignores the other information). </p>
<h3>Example: Handling Intermediate  Results and Formatting Output </h3>

<p>We now show an example from finance&#151;computing and plotting the mean-variance efficient frontier of a portfolio of three stocks&#151;to illustrate how you could use the <i>PerlAmpl</i> module to run optimization problems multiple times, perform statistical analysis of data, plot graphs, and format output. To generate the efficient frontier, we need to solve the optimization problem in <a name="re2"></a><a href="0412be2.html">Example 2</a>, where <i>n</i> is the number of stocks in the portfolio, <i>xi</i> are decision variables corresponding to the allocation in each of the <i>n</i> stocks, <i>rtarget</i> is the target portfolio return, and <i>E[ri]</i> and <i>sij</i> are the expected values and the covariances of asset returns <i>i, j, i=1,...,n, j=1,...,n</i>, respectively. <i>sij</i> equals the variance of return <i>i</i>.</p>

<p>Lists of expected returns, target returns, and a covariance matrix are passed to the optimization problem formulation using the <i>PerlAmpl</i> module. The forecasts for expected returns could be generated in Perl, for example, by running regressions or using time series techniques. A number of statistics Perl modules, such as <i>Statistics::Regression</i>, available free from http://www.cpan.org/, can help with the statistical analysis of data. Also, Perl modules, such as <i>Math::Matlab</i>, provide additional capabilities by allowing for calling outside statistical software and collecting the results.</p>

<p>The portfolio optimization problem is nonlinear, so the FSQP solver is called from within AMPL. The results are stored in a hash in Perl. JGraph (a free graphing software project under Linux; http://sourceforge.net/projects/jgraph/) plots the results dynamically. The advantage of this type of organization is that the whole metamodel can be run efficiently from beginning to end while keeping the same output format. Output formats that are frequently convenient, for example, are Latex table, Excel, or HTML. <a name="rl2"></a><a href="#l2">Listing 2</a> contains a subroutine for printing the optimal standard deviation for each level of target portfolio return directly in a Latex table format.</p>

<p>The output of this program is a file efficientFrontier.jgr and this table:</p>

<blockquote>
<pre>
Expected Return &amp; Standard Deviation \\ 
0.080 &amp; 0.105 \\ 
0.085 &amp; 0.105 \\ 
0.090 &amp; 0.105 \\ 
0.095 &amp; 0.107 \\ 
0.100 &amp; 0.118 \\ 
0.105 &amp; 0.136 \\ 
0.110 &amp; 0.160 \\ 
0.115 &amp; 0.186 \\ 
0.120 &amp; 0.214 \\ 

</pre>
</blockquote>

<p>The efficientFrontier.jgr file contains the text in <a name="re3"></a><a href="0412be3.html">Example 3</a>. Using the command <i>jgraph efficientFrontier.jgr &gt; efficientFrontier.ps</i>, the file can be converted to a picture (<a name="rf3"></a><a href="0412bf3.html">Figure 3</a>), which can then be used, for example, in a Latex file. The conversion of the Jgraph file to a PostScript file can be automated by including it directly in the Perl program.</p>
<h3>Example: Handling Intermediate Results  And Formatting Output</h3>

<p>We successfully used the PerlAmpl module in computational comparisons of the efficiency of multiperiod versus single-period portfolio optimization techniques. The module can be similarly used in other optimization metamodels that involve simulations at multiple stages.</p>

<p>Consider an example of a multiperiod optimization problem: Given a portfolio of stocks and information about their future expected values at multiple times in the future, solve two different portfolio optimization problem formulations to determine optimal portfolio allocations for each time period. Then run simulations to test which of the two allocations results in a better final period portfolio return. To compare the two optimization formulations, you would need to compare cumulative returns at the end of the time horizon. At every point in time, you need to keep track of the portfolio value, rebalance the portfolio, simulate returns for one period ahead, and recompute the value of the portfolio. </p>

<p>AMPL contains several built-in random generator functions, such as <i>Beta(a,b)</i>, <i>Cauchy()</i>, <i>Exponential()</i>, <i>Normal(mean, standard deviation)</i>, <i>Poisson(parameter)</i>, and <i>Uniform(lower limit, upper limit)</i>. All of the previous random distributions can also be generated in Perl by using, for example, the <i>Math::Cephes</i> Perl module, which contains over 150 mathematical functions. In some contexts, such as the multiperiod portfolio optimization example we mention, it is significantly more convenient to run the simulations within the main Perl program because of the need to process intermediate optimization results. Moreover, Perl offers a wider variety of random generator functions. <a name="rl3"></a><a href="#l3">Listing 3</a> is Perl code for a simplified simulation example in which the asset returns in a portfolio are drawn from a multivariate normal distribution with prespecified expected values vector and covariance matrix. Currently, there is no provision for generating correlated random variables from within AMPL. We use the Perl library random_multivariate_normal.pl (http://www.cpan.org/).</p>
<h3>Conclusion </h3>

<p>While the PerlAmpl module is effective and useful in its current state of development, possible enhancements include parallelizing the execution of solvers to take advantage of multiprocessors, as well as networking the PerlAmpl module to allow client computers to solve optimization metamodels by leveraging software on server computers.</p>


<p><b></b></p>
<p><b></b></p>
<p><b>TPJ</b></p>



	
	
<br>
<br>
<b><a name="l1"></a>Listing 1 </b><br>
<pre>%kMonthHash = ("Jan" =&gt; 1, "Feb" =&gt; 2, "Mar" =&gt; 3, "Apr" =&gt; 4,  "May" =&gt; 5,
               "Jun" =&gt; 6, "Jul" =&gt; 7, "Aug" =&gt; 8, "Sep" =&gt; 9, "Oct" =&gt; 10, 
               "Nov" =&gt; 11, "Dec" =&gt; 12);
%kConvertHash = ("3month" =&gt; 90, "6month" =&gt; 180, "1year" =&gt; 360, 
               "2year" =&gt; 720, "5year" =&gt; 1800, "10year" =&gt; 3600, 
               "30year" =&gt; 10800);
sub read_file
{
    my($inFile) = @_;
    my($state, $temp, $period, $yield);
    open(INPUT, $inFile) || die("Cannot open $inFile");
    $state = 0;
    while (&lt;INPUT&gt;)
    {
        if ($state == 0)
        {
            #if (/U.S. Treasury yield curve/)
            if (/\*U.S. Treasuries\*/)
            {
                $state = 1;
            }
        }
        elsif ($state == 1)
        {
            #READ IN CURRENT DATE
            # Sun, 4 Jul 1999, 11:32am EDT
            if (/[A-Z][a-z][a-z],\s*([0-9]+)\s*([A-Z][a-z][a-z])\s*([0-9]+),/)
            {
              $gDay = $1;
              #assign a numerical value to the month according to %kMonthHash 
              $gMonth = $kMonthHash{$2};
              $gYear = $3;
              $state = 2;
            }
            else
            {
              die("cannot parse date");
            }
        }
        elsif ($state == 2)
        {
            #if (/Bills/)
            if (/Prc Chg/)
            {
                $state = 3;
            }
        }
        elsif (($state == 3) || ($state == 5))
        {
            if ((/Notes/) || (/\[U.S. Treasury Yield Curve\]/))
            {
                $state++;
            }
            elsif (/^(\S+)\s+[^(]+\(([^)]+)\)/) #READ IN RELEVANT DATA
            {
                $period = $kConvertHash{$1};
                push(@gPeriodList, $period);
                $yield = $2;
                push(@gYieldList, $yield);
            }
            elsif (/\s*[+-][0-9+-]+\s*$/)
            {
                # do nothing
            }
            else
            {
                die("cannot parse data: $_");
            }
        }
        elsif ($state == 4)
        {
            #if (/Bonds\s+Coupon\s+Mat\s+Date/)
            if (/Prc Chg/)
            {
                $state = 5;
            }
        }
    }
    close(INPUT);
}</pre>
<a href="#rl1">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l2"></a>Listing 2</b><br>
<pre>use Math::Ampl;
use strict;

#SPECIFICATION OF INPUT DATA:
my($NumStocks) = 3;
#vector of expected returns
my(@ExpectedReturnsList) = (0.08, 0.09, 0.12);
#vector of standard deviations for each stock
my(@StdDeviationsList) = (0.15, 0.20, 0.22);
#vector of target portfolio returns 
my(@TargetReturnsList) = (0.08, 0.085, 0.09, 0.095, 0.10, 0.105, 
                                              0.11, 0.115, 0.12);
#RESULTS STORAGE:
#hash table to store optimal portfolio standard deviation results after 
#solving all optimization problems
my(%OptimalStdDeviationsHash) = (); 
#hash table to store optimal portfolio holdings after solving the 
#portfolio optimization problem for each value of TargetReturn
my(%OptimalHoldingsHash) = (); 

#OUTPUT FILES:
my($kOutputFile) = "efficientFrontier.out"; #to store Latex table 
my($kFrontierGraphFile) = "efficientFrontier.jgr"; #to store graph

#DECLARATION OF AMPL PROBLEM INSTANCE:
my($gProblem);

#optimization model problem to be solved: minimize portfolio variance 
#subject to constraints on portfolio expected return

#optimization model file to be pased to AMPL using PerlAmpl

sub setup_ampl_mod
{
    my($mod);
    $mod =&lt;&lt;EODATA;
    
    param NumStocks;
    param ExpectedReturns{1..NumStocks};
    param StdDeviations{1..NumStocks};
    param TargetReturn;
    
    var holdings{1..NumStocks};

    minimize portfolio_variance:
    sum{i in 1..NumStocks}
             (StdDeviations[i]*StdDeviations[i]*holdings[i]*holdings[i]);

    subject to portfolio_target_return:
    sum{i in 1..NumStocks} (ExpectedReturns[i]*holdings[i]) &gt;= TargetReturn;

    subject to portfolio_total:
    sum{i in 1..NumStocks}(holdings[i]) = 1;
EODATA
        $gProblem-&gt;Input_Mod_Append($mod);
}
#optimization data file to be passed to AMPL using PerlAmpl
sub setup_ampl_dat
{
    #target portfolio return passed for this instance of the problem
    my($inTargetReturn) = @_; 
    my($iStock);

    $gProblem-&gt;Input_Dat_Add_Param_Item("NumStocks", $NumStocks);
    $gProblem-&gt;Input_Dat_Add_Param_Item("TargetReturn", $inTargetReturn);
    for($iStock = 1; $iStock &lt;= $NumStocks; $iStock++)
    {
        $gProblem-&gt;Input_Dat_Add_Param_Item("ExpectedReturns", 
                                    $iStock,$ExpectedReturnsList[$iStock-1]);
        $gProblem-&gt;Input_Dat_Add_Param_Item("StdDeviations", 
                                     $iStock,$StdDeviationsList[$iStock-1]);
    }
}
#request for AMPL to keep track of variables of interest

sub setup_ampl_display
{
    $gProblem-&gt;Input_Display_Clear;
    $gProblem-&gt;Input_Display_Add("solve_result");
    $gProblem-&gt;Input_Display_Add("portfolio_variance");
    $gProblem-&gt;Input_Display_Add("holdings");
}
#script for running the problem in AMPL and obtaining the results
sub solve_problem
{
    #target portfolio return passed for this instance of the problem
    my($inTargetReturn) = @_;
    my($solved);

    $gProblem-&gt;Input_Mod_Clear; 
    $gProblem-&gt;Input_Dat_Clear;
    setup_ampl_dat($inTargetReturn);
    setup_ampl_mod();
    setup_ampl_display();
    $solved = $gProblem-&gt;Solve;
    return $solved;
}
#PRINT GRAPH OF EFFICIENT FRONTIER DYNAMICALLY
#the output is a .jgr file which can then be converted to a .ps file

sub print_graph
{
    my($graph);
    my($targetReturn); #portfolio target return, read from list
    my($portfolioVariance); #optimal result from optimization problem
    my($portfolioStdDeviation); #to be computed from portfolio variance
    my(@CurrentHoldings); #obtained from AMPL output  
    my($iStock); #counter

    open(GRAPH, "&gt;$kFrontierGraphFile") || 
        die ("Cannot open file \"$kFrontierGraphFile\" for graph: $!");
    $graph = "newcurve marktype none linetype solid linethickness 1";
    $graph .= " label : Efficient Frontier\n"; 
    $graph .= "\tpts ";
    foreach $targetReturn (@TargetReturnsList)
    {
        solve_problem($targetReturn);
        $portfolioVariance = 
            $gProblem-&gt; Output_Display_Get_Value("portfolio_variance");
        $portfolioStdDeviation = sqrt($portfolioVariance);
        $graph .= " $portfolioStdDeviation $targetReturn ";
#store optimal standard deviations if necessary
        $OptimalStdDeviationsHash{"$targetReturn"} = $portfolioStdDeviation;
#if necessary, store also the optimal holdings for each value of TargetReturn
        for($iStock = 1; $iStock &lt;= $NumStocks; $iStock++)
        {
            $CurrentHoldings[$iStock-1] =
                $gProblem-&gt;Output_Display_Get_Value("holdings",$iStock);
            $OptimalHoldingsHash{"$targetReturn,$iStock"} = 
                $CurrentHoldings[$iStock-1];
        }
    }
    print GRAPH "newgraph\n xaxis\n label : Standard Deviation\n";
    print GRAPH "yaxis \n ";
    print GRAPH "label : Expected Portfolio Return\n";
    print GRAPH "$graph\n\n";
    print GRAPH "legend defaults\n x 0.17 y 0.10\n";
    close GRAPH;
}
#PRINT A TABLE WITH RESULTS IN LATEX TABLE FORMAT
sub print_table
{
    my($portfolioStdDeviation);
    my($targetReturn);

    open(OUTPUT, "&gt;$kOutputFile") || 
        die ("Cannot open file \"$kOutputFile\" with results: $!"); 
    printf OUTPUT "%s %s ", "Expected Return", "&amp;";
    printf OUTPUT "%s %s \n", "Standard Deviation", "\\\\";

    foreach $targetReturn (@TargetReturnsList)
    {
        $portfolioStdDeviation = $OptimalStdDeviationsHash{$targetReturn};
        printf OUTPUT "%2.3f %s ", $targetReturn, "&amp;";
        printf OUTPUT "%2.3f %s \n",  $portfolioStdDeviation, "\\\\";   
    }
    close OUTPUT;
}
#MAIN
#initialization
Math::Ampl::Initialize($kAmplDir, $kAmplBin, $kTempDir, 1);
$gProblem =  new Math::Ampl;

print_graph();
print_table();</pre>
<a href="#rl2">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l3"></a>Listing 3</b><br>
<pre>#Generates one path, and returns a List of Lists indexed by [time
#period, asset number]. The entries equal single-period returns for
#each stock. Single-period returns are multivariate normal random variables.

sub create_scenario
{
    #pass number of stocks in portfolio and number of time periods ahead
    my($inNumStocks, $inNumPeriods) = @_; 

    my(@SimulatedReturnsList); #single time period simulated returns
    my(@ScenarioLoL); #list of lists of asset returns for each time period
    my($iT); #time period counter 

    for ($iT = 0; $iT &lt; $inNumPeriods; $iT++)
    {
        @SimulatedReturnsList = 
                          random_multivariate_normal($inNumStocks,
                               @ExpectedReturnsList, @CovarianceMatrixList);
        #add simulated returns for time period iT to scenario path
        @ScenarioLoL[$iT] = [ @SimulatedReturnsList ];
    }
    return (@ScenarioLoL);
}</pre>
<a href="#rl3">Back to article</a><br>
	
	
	


</body>
</html>