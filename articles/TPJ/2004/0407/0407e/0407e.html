
<html>
<head>
<title>July, 2004: Cleaning Up a Symlink Mess</title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; The Perl Journal-->

<h1>Cleaning Up a Symlink Mess</h1>
<p><i>The Perl Journal</i> July, 2004</p>
<h2></h2>


<h3>By Randal L. Schwartz</h3>


<I>Randal is a coauthor of Programming Perl, Learning Perl, Learning Perl for Win32 Systems and Effective Perl Programming, as well as a founding board member of the Perl Mongers (perl.org). Randal can be reached at merlyn@stonehenge.com.</I>

<hr>





<p>The box that hosts http://www.stonehenge.com/ also takes care of http://www.geekcruises.com/, the company web site for my buddy, "Captain" Neil. As such, I have a dual role: I'm not only a frequent Geek Cruise attendee&#151;I'm also the webmaster!</p>

<p>Recently, I noticed that Neil had moved a few pages around on his site to reorganize some of the information on past cruises. As quite a few links have been announced and bookmarked to the old location for a given page, he didn't want to break those. So he näively placed a symbolic link from the old location to the new location. This means that a reference to the old location such as:</p>

<blockquote>
<pre>
http://www.geekcruises.com/cruises/2003/perlwhirl3.html

</pre>
</blockquote>



<p>would be the same as:</p>

<blockquote>
<pre>
http://www.geekcruises.com/past_cruises/perlwhirl3.html

</pre>
</blockquote>



<p>because he had moved the page as follows:</p>

<blockquote>
<pre>
$ cd /data/web/geekcruises # the DocumentRoot for his server
$ cd cruises/2003
$ mv perlwhirl3.html ../past_cruises
$ ln -s ../past_cruises/perlwhirl3.html .

</pre>
</blockquote>

<p>Now, at first glance, this appears to work. When either page is referenced, the same material is delivered by the server. However, there's no way for anyone outside my server to know that these two pages are absolutely identical. This means that any cache (including browser caches, outward border caches at large organizations, or even our own reverse proxy cache) would now have two copies of the same material, having fetched the material needlessly twice.</p>

<p>Worse, some of the relative URLs are now somewhat broken. In the original location, getting back up to the index page requires ../../index.html, but in the new location, it was merely ../index.html. It was for this reason that I actually noticed the symlinks in the first place, because a badly constructed web crawler was sucking down multiple copies of the web site, thinking that each index.html at the top level was different as well.</p>

<p>The correct way to move such a page that might have been bookmarked or indexed is to have Apache issue an <i>http redirect</i> when the old URL is referenced. For example, in the configuration file for the Geek Cruises website, we can add:</p>

<blockquote>
<pre>
Redirect /cruises/2003/perlwhirl3.html http://www.geekcruises.com/past_cruises/perlwhirl3.html

</pre>
</blockquote>



<p>With this line in the configuration, a browser requesting the old URL will be asked to fetch the new URL instead. This redirect (also called an <i>external redirect</i>) is sufficient to ensure that caches will cache only one version (at the new URL) and indexers such as Google will invalidate the old URL over time.</p>

<p>Now, Neil doesn't have direct access to the web-server configuration master file, but he can add .htaccess files in the various affected directories. That particular command can be placed directly into the cruises/2003 subdirectory, and it would have the same result.</p>

<p>When I saw a few dozen of these symlinks all over the document tree for Neil's server, I explained this to him and then said it'd actually be a small matter of programming to automatically replace all of those symlinks with updated .htaccess files. When all I heard was silence at the other end of the connection, I recognized that I'd need to write the program myself, since I'd now claimed it could be done. And that program is in <a name="rl1"></a><a href="#l1">Listing 1</a>.</p>

<p>Lines 1 through 3 start nearly every program I write, enabling warnings for development, compiler restrictions (forbidding undeclared variables, symbolic references, and barewords), and turning off the pesky output buffering.</p>

<p>Lines 5 through 9 define my configuration parameters. The <i>$URL</i> is needed because an external redirect has to include the hostname and there's no easy way to get at that from inside the .htaccess file. The <i>$USER</i> and <i>$GROUP</i> are the values for the newly created or updated .htaccess file; I'm running this as root so I have to set it correctly for Neil to be able to edit later. And <i>$MODE</i> gives the new permissions for a new .htaccess file.</p>

<p>Line 11 pulls in the <i>abs_path</i> routine from the core module <i>Cwd</i>. Lines 13 and 14 use my <i>File::Finder</i> module (found in CPAN) to easily get a list of directories below the document root.</p>

<p>Lines 17 to 60 iterate over each of those directories, which can be considered completely separately. Line 18 finds all the symbolic links within that directory using a simple <i>grep</i> over the result of a <i>glob</i>. Note that I'm presuming UNIX file syntax here, but that's safe because I know my server box is not likely to ever be anything but UNIX. Had I wanted this a bit more portable, I'd use <i>File::Spec</i> to construct the path.</p>

<p>Line 20 sets up the list of <i>@deletes</i>. These are the candidate symbolic links that are being replaced with .htaccess redirects, and can be deleted once the updated .htaccess file is in place. Line 21 computes the name of the .htaccess file for this particular directory.</p>

<p>Lines 23 to 45 process each symbolic link that was found in the directory separately. First, the target of the symbolic link is read in lines 24 and 25. If the <i>$path</i> is not defined, it's either not a symbolic link or something went horribly wrong, and we ignore it.</p>

<p>Next, lines 26 and 27 ignore absolute symbolic links. I'm not sure why this code is in there, but it seemed to be the safest thing to do, since I only wanted to fix relative symbolic links. I've learned over the years that when you have root power, and you're mucking around with stuff and deleting and replacing a lot of files, it's safest to try to ignore everything that doesn't precisely fit your desired goal.</p>

<p>Line 28 uses <i>abs_path</i> to compute the resulting absolute path of the symbolic link target. Line 29 is left over from debugging, where I wanted to see if my calculations were correct for all of the existing links.</p>

<p>Lines 30 and 31 strip off the document root path from the source of the symbolic link. I need to do this to ensure that my <i>Redirect</i> command is framed in terms of URLs and not UNIX pathnames. The <i>\Q</i> quotes any metacharacters in the pathname. Again, this is a safe thing to do, even though I know there are no metacharacters in the particular paths I've configured at the top. Always be very conservative with root power.</p>

<p>Line 32 takes the matched tail part of the symbolic link source and builds the source URL for the <i>Redirect</i>. I'm presuming the <i>$1</i> here has been properly set from the previous match and there's no possible way I'm using the value from a stale match.</p>

<p>Lines 33 through 35 repeat the stripping and building for the destination path, although I now have to create a full scheme-based URL for the path. Without the http prefix, Apache would have treated this operation as an <i>internal redirect</i>, with all the same problems as a simple symlink, because no indication would be sent to the client that something had moved.</p>

<p>Lines 36 to 42 create the <i>NEW</i> handle to which the new .htaccess file is written. This happens only once per directory because, after the first time, the <i>@deletes</i> array contains some previous entry. The existing .htaccess file (if any) is also copied to the beginning of the new .htaccess file. Note that we're using the <i>OLD</i> filehandle in a list context, so it gets slurped in as a list of lines, then immediately dumped to the new filehandle.</p>

<p>Line 43 writes the proper <i>Redirect</i> command to the new .htaccess file. Line 44 marks the symbolic link as one to be deleted once the .htaccess file is in place. Lines 46 to 59 are executed once per directory but only if some symbolic link was found that was eligible for removal.</p>

<p>First, line 47 closes the output file handle to ensure that the data is completely flushed. Then, lines 48 through 51 set the permissions and ownership on the new file to be defined by the configuration parameters at the top of the program. Lines 52 to 53 try to rename the existing .htaccess file out of the way to end in .OLD. Again, with Great Power comes Great Responsibility, including the mandate to have a Great Undo when one makes a Great Mistake. So, after each run of this program, I can verify that I have not completely mangled the .htaccess files, and then delete the .OLD files manually. And carefully.</p>

<p>Lines 54 and 55 move the newly created .htaccess file into place. Note that, because I've created the completed file in a separate location and then renamed it atomically into place, there's no chance that the live Apache process will read a partially written .htaccess file. This is a very important principle when dealing with live production activity. Finally, lines 56 to 58 delete the existing useless symbolic links one at a time. And that's all there is!</p>

<p>Oddly enough, as I was writing this article, I discovered yet another symlink in the tree. So I ran the code once again and, sure enough, it got replaced with the right redirect. Good thing I've kept this code around. Looks like it's time to send Neil another refresher message, or maybe just disable symbolic links in his tree. Until next time, enjoy!</p>


<p><b></b></p>
<p><b></b></p>
<p><b>TPJ</b></p>



	
	
<br>
<br>
<b><a name="l1"></a>Listing 1</b><br>
<pre>=1=     #!/usr/bin/perl -w
=2=     use strict;
=3=     $|++;
=4=     
=5=     my $URL = "http://www.geekcruises.com";
=6=     my $DIR = "/data/web/geekcruises";
=7=     my $USER = 2100;
=8=     my $GROUP = 2100;
=9=     my $MODE = 0644;
=10=    
=11=    use Cwd qw(abs_path);
=12=    
=13=    use File::Finder;
=14=    my @dirs = File::Finder-&gt;type('d')-&gt;in($DIR);
=15=    
=16=    # print "$_\n" for @dirs;
=17=    for my $dir (@dirs) {
=18=      my @symlinks = grep -l, glob "$dir/*";
=19=      # print "$dir: @symlinks\n";
=20=      my @deletes;
=21=      my $htaccess = "$dir/.htaccess";
=22=    
=23=      for my $symlink (@symlinks) {
=24=        defined(my $path = readlink($symlink)) or
=25=          warn("Cannot read $symlink: $!"), next;
=26=        $path =~ m{^/} and
=27=          warn("skipping absolute $path for $symlink\n"), next;
=28=        my $abs_path = abs_path("$dir/$path");
=29=        # print "$symlink -&gt; $path =&gt; $abs_path\n";
=30=        $symlink =~ m{^\Q$DIR\E/(.*)}s or
=31=          warn("$symlink doesn't begin with $DIR"), next;
=32=        my $original_url = "/$1";
=33=        $abs_path =~ m{^\Q$DIR\E/(.*)}s or
=34=          warn("$abs_path doesn't begin with $DIR"), next;
=35=        my $redirect_url = "$URL/$1";
=36=        unless (@deletes) {
=37=          ## print "in $dir...\n";
=38=          open NEW, "&gt;$htaccess.NEW" or die;
=39=          if (open OLD, $htaccess) {
=40=            print NEW &lt;OLD&gt;;
=41=          }
=42=        }
=43=        print NEW "Redirect $original_url $redirect_url\n";
=44=        push @deletes, $symlink;
=45=      }
=46=      if (@deletes) {
=47=        close NEW;
=48=        chown $USER, $GROUP, "$htaccess.NEW"
=49=          or die "Cannot chown $htaccess.NEW: $!";
=50=        chmod $MODE, "$htaccess.NEW"
=51=          or die "Cannot chmod $htaccess.NEW: $!";
=52=        ! -e $htaccess or rename $htaccess, "$htaccess.OLD"
=53=          or die "Cannot mv $htaccess $htaccess.OLD: $!";
=54=        rename "$htaccess.NEW", $htaccess
=55=          or die "Cannot mv $htaccess.NEW $htaccess: $!";
=56=        for (@deletes) {
=57=          unlink $_ or warn "Cannot unlink $_: $!";
=58=        }
=59=      }
=60=    }</pre>
<a href="#rl1">Back to article</a><br>
	
	
	


</body>
</html>