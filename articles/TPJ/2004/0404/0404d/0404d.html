
<html>
<head>
<title>April, 2004: </title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; The Perl Journal-->

<h1>Cooking with Maypole, Part 1</h1>
<p><i>The Perl Journal</i> April, 2004</p>
<h2>By Simon Cozens</h2>



<I>Simon is a freelance programmer and author, whose titles include Beginning Perl (Wrox Press, 2000) and Extending and Embedding Perl (Manning Publications, 2002). He's the creator of over 30 CPAN modules and a former Parrot pumpking. Simon can be reached at simon@ simon-cozens.org.</I>

<hr>





<p>We've all done it. We need to whip up a web application based on a database or some other structured data source; we drag out our favorite templating library, our favorite database abstraction layer, and write a bunch of code to glue the two together, maybe using something like <i>CGI::Application</i> to handle some of the front-end work. We extend the application in particular ways as the specifications change, but at least we have something working.</p>

<p>Then, the next time we need to create a database-backed web application, we drag out the same tools and do it again. And again. Wouldn't it be nice if there were a decent, extensible web application framework like Java's Struts? Well, there are one or two: OpenInteract and OpenFrame, to start with. But in this article, and its follow-up next month, I'd like to talk about a new one: Maypole.</p>

<p>Maypole started when I couldn't find any tools to "magically" put a web front end on a database without the need for lots of code or customization. Since then, it's become generalized to support extensions in all directions: adding functionality to the application itself using different templating modules and database abstractions. However, it's retained its focus of allowing you to get as much done as possible without writing very much code.</p>

<p>To introduce Maypole and some other friends, we'll look at an application I described a few months ago&#151;the house-sharing database. I mentioned then that I planned to extend the application to track what food was in the cupboards. We're going to do that now, and our eventual application will also collect a database of recipes and have the system suggest the best recipe to use up the food before it goes off.</p>
<h3>Tracking Provisions</h3>

<p>We'll start, as usual, by developing our database schema, beginning with the recipes. The recipes will come in as XML, as we'll see in a few moments, so the table looks like this:</p>

<blockquote>
<pre>
CREATE TABLE recipe (
    id int not null auto_increment primary key,
    name varchar(255),
    xml text
);

</pre>
</blockquote>

<p>Now, each recipe will have multiple ingredients, so we need a many-to-many table linking ingredients to recipes:</p>

<blockquote>
<pre>
CREATE TABLE ingredient (
    id int not null auto_increment primary key,
    recipe int,
    food int,
    quantity varchar(255)
);

</pre>
</blockquote>

<p>Then, we're going to need a table for the names of food. One of the things you notice about recipes is that they're not always consistent about their use of the names of ingredients. "Lamb mince" and "minced lamb" are the same thing, so we'll use a column in the table to identify the "canonical" name of a food to help us when we're importing recipes. We'll call this the normalized name, and so our schema looks like this:</p>

<blockquote>
<pre>
CREATE TABLE food (
    id int not null auto_increment primary key,
    name varchar(255),
    normalized varchar(255)
);

</pre>
</blockquote>

<p>The recipes have categories as well as ingredients, so here's another many-to-many linking table:</p>

<blockquote>
<pre>
CREATE TABLE category (
    id int not null auto_increment primary key,
    name varchar(255)
);

CREATE TABLE categorization (
    id int not null auto_increment primary key,
    recipe int,
    category int
);

</pre>
</blockquote>

<p>And finally, there's a table for the food in the cupboards. I called my database "larder," so "larder.contents" is a good name for this table:</p>

<blockquote>
<pre>
CREATE TABLE contents (
    id int not null auto_increment primary key,
    food int,
    quantity varchar(255),
    use_by date
);

</pre>
</blockquote>



<p>Now I could use <i>Class::DBI</i> to set up the database, create classes for each of the tables, load them up with the column names, declare the relationships between them, and start writing my templating controller code. Or I could just say (see <a name="rl1"></a><a href="#l1">Listing 1</a>):</p>

<blockquote>
<pre>
package Larder;
use base 'Apache::MVC';
Larder-&gt;setup("dbd:mysql:larder");

</pre>
</blockquote>

<p><i>Apache::MVC</i> is a subclass of Maypole that is specifically suited to creating Apache <i>mod_perl</i> handlers, and it takes care of pretty much everything.</p>

<p>We still need to declare our relationships between the tables, but we'll do this in several parts: First, we'll get a system going where we can add and edit the contents of our larder, then we'll look at displaying recipes as well; finally, in next month's article, we'll look at how to link the recipes with their ingredients and also the ingredients with what's in the larder.</p>

<p>To relate the food types in the contents table to the food table, we use a <i>has_a</i> relationship:</p>

<blockquote>
<pre>
Larder::Contents-&gt;has_a( food =&gt; "Larder::Food" );

</pre>
</blockquote>


<h3>Deploying the Web Site</h3>

<p>Believe it or not, we're almost done with setting up a simple interface to the database. All we need to do is tell the system where it's going to live, so that it can construct URLs to itself, and we're done:</p>

<blockquote>
<pre>
package Larder;
use base 'Apache::MVC';
Larder-&gt;setup("dbi:mysql:larder");
Larder-&gt;config-&gt;{uri_base} = "http://localhost/larder/";
Larder::Contents-&gt;has_a( food =&gt; "Larder::Food" );
1;

</pre>
</blockquote>

<p>Well, almost done. That's as much Perl as we need to write for the moment. The rest are HTML templates but, thankfully, Maypole includes a useful bunch of those, too. We copy the factory templates directory inside our web root:</p>

<blockquote>
<pre>
% cp -r templates/factory /var/www/larder/

</pre>
</blockquote>



<p>We probably want a CSS file in there also; the sample one provided with Maypole works well. And then set up the Apache configuration to use the <i>Larder</i> Perl module:</p>

<blockquote>
<pre>
&lt;Location /larder&gt;
    SetHandler perl-script
    PerlHandler Larder
&lt;/Location&gt;

</pre>
</blockquote>



<p>That's all.</p>

<p>Restart Apache, go to the site, and you should be able to view, search, and browse the database. This works equally well for any database, and it's a great way to look around an established database that has a lot of relationships defined. But, of course, our larder database doesn't have any data in it at the moment.</p>
<h3>Editing and Untainting</h3>

<p>To allow us to add and edit records, we need to tell Maypole more about the kinds of data we're expecting. To do this, Maypole makes use of <i>Class::DBI::Untaint</i>, an application of <i>CGI::Untaint</i>.</p>

<p><i>CGI::Untaint</i> is a mechanism for testing to make sure that incoming form data conforms to various properties. For instance, given a <i>CGI::Untaint</i> object that encapsulates some POST parameters, we can extract an integer like so:</p>

<blockquote>
<pre>
my $h = CGI::Untaint-&gt;new(CGI-&gt;Vars);
$h-&gt;extract(-as_integer =&gt; "food");

</pre>
</blockquote>

<p>This checks that the <i>food</i> parameter is an integer and returns it if it is; if not, <i>$h-&gt;error</i> will be set to an appropriate error message. Other tests by which you can extract your data are <i>as_hex</i> and <i>as_printable</i>, which tests for a valid hex number and an ordinary printable string, respectively; there are other handlers available on CPAN, and you can make your own, as documented in <i>CGI::Untaint</i>.</p>

<p>To tell the Maypole handler what handler to use for each of your columns, you need to use the <i>untaint_columns</i> methods in the classes representing your tables. We have a class representing food in the cupboard, <i>Larder::Contents</i>, which has a string column quantity and a date column <i>use_by</i>, so we declare these:</p>

<blockquote>
<pre>
Larder::Contents-&gt;untaint_columns(
    printable =&gt; [ "quantity" ],
    date      =&gt; [ "use_by" ]
);

</pre>
</blockquote>

<p>This will allow us to edit these columns, but there's one column we've forgotten&#151;the <i>food</i> column will have to contain an integer that refers to a primary key in the food table. Even though this will be displayed as a drop-down list, we need to ensure that the number passed in to the edit process is an integer:</p>

<blockquote>
<pre>
Larder::Contents-&gt;untaint_columns(
    printable =&gt; [ "quantity" ],
    date      =&gt; [ "use_by" ],
    integer   =&gt; [ "food" ]
);

</pre>
</blockquote>

<p>Now we can add and edit rows in the contents table.</p>
<h3>Tomatoes and Triggers</h3>

<p>The food table itself is slightly different. We have three columns, <i>id</i>, <i>name</i>, and <i>normalized</i>. The only one we need to edit is <i>name</i>, as the others should be hidden. Maypole automatically hides the <i>id</i> column, but we can specify the columns to be displayed by overriding the <i>display_columns</i> method in the <i>Larder::Food</i> class:</p>

<blockquote>
<pre>
package Larder::Food;
sub display_columns { "name" }

</pre>
</blockquote>

<p>We also need to make sure that the <i>normalized</i> column gets set every time there's an update to <i>name</i>. We'll use a very simple canonicalization subroutine for demonstration purposes, but a proper foodstuff canonicalization routine needs to know about specialist cooking terms; for example, that chopped tomatoes and diced tomatoes are both tomatoes. Our routine is a common one for doing fuzzy searches: It ditches vowels and punctuation characters, and compresses whitespace and repeated letters.</p>

<blockquote>
<pre>
sub normalize {
    my $word = lc shift;
    $word =~ s/[aeiou]//g;
    $word =~ s/[^\s\w]//g;
    $word =~ tr/a-z /a-z /s;
    return $word;
}

</pre>
</blockquote>

<p>To arrange this subroutine to set the value of <i>normalized</i>, we use a feature of <i>Class::DBI</i> called "triggers." These are subroutine references fired off by particular events; the <i>Class::Trigger</i> class helps you to define these. For instance, every time a record is created: <i>Class::DBI</i>-based classes will call their <i>after_create</i> trigger. We can use this trigger to make sure that the <i>normalized</i> field is set to the right thing:</p>

<blockquote>
<pre>
Larder::Food-&gt;add_trigger( after_create =&gt; sub {
    my $self = shift;
    $self-&gt;normalized(normalize($self-&gt;name));
} );

</pre>
</blockquote>

<p>Similarly, when we update the name from the web interface, we want to ensure that the new name is correctly normalized, if the normalized version is now different:</p>

<blockquote>
<pre>
Larder::Food-&gt;add_trigger( after_update =&gt; sub {
    my $self = shift;
    my $old = $self-&gt;normalized;
    my $new = normalize($self-&gt;name);
    if ($old ne $new) {
        $self-&gt;normalized($new);
    }
} );

</pre>
</blockquote>

<p>Finally, we set our <i>untaint_columns</i> so we can update the name of a foodstuff:</p>

<blockquote>
<pre>
Larder::Food-&gt;untaint_columns( printable =&gt; [ "name" ] );

</pre>
</blockquote>



<p>and now we have two tables we can display, edit, and add to. With a few bits of data in it, the site looks like <a name="rf1"></a><a href="0404df1.html">Figure 1</a>.</p>
<h3>RecipeML</h3>

<p>The next stage of this is to get the recipes into the system. The state of machine-readable recipes is somewhat lamentable at the moment, but I did find a large archive of recipes in RecipeML, an application of XML. A RecipeML recipe looks like <a name="re1"></a><a href="0404de1.html">Example 1</a>.</p>

<p>We want to extract the categories and the ingredients, then dump the data into the database. First, we fire up our old friend <i>XML::Simple</i> to parse the XML into a Perl data structure:</p>

<blockquote>
<pre>
use XML::Simple;
use File::Slurp;
for my $recipe (&lt;xml/*&gt;) {
    my $xml = read_file($recipe);
    my $structure = XMLin($xml)-&gt;{recipe};
}

</pre>
</blockquote>

<p><i>XML::Simple</i> has some quirky features, and one of these is that if there's an <i>&lt;ingredients&gt;</i> tag with several <i>&lt;ing&gt;</i> tags inside it, it will present these as an array, as one might expect; unfortunately, if there's only one ingredient, it presents it as a hash. Naturally, this causes problems when we come to dereference it. To get around this, we force <i>XML::Simple</i> to present everything as arrays, regardless of the number of subelements. This has the unfortunate side effect of making the rest of the code ugly, but at least it's consistently ugly:</p>

<blockquote>
<pre>
my $structure = XMLin($xml, ForceArray =&gt; 1)-&gt;{recipe}-&gt;[0];
my $name = $structure-&gt;{head}-&gt;[0]-&gt;{title}-&gt;[0];
my @ingredients = @{$structure-&gt;{ingredients}[0]{ing}};
my @cats = @{$structure-&gt;{head}[0]{categories}[0]{cat}};

</pre>
</blockquote>

<p>We have all the data we need.</p>

<p>For the time being, we'll only worry about the name and the XML; the ingredients and categories require slightly trickier many-to-many relationships, so we'll deal with those in next month's column.</p>

<p>To create the database rows, we will load up the same <i>Larder</i> module that we've been using as an Apache handler, since this does the hard work of setting up the <i>Class::DBI</i> classes for us.</p>

<blockquote>
<pre>
use Larder;
use XML::Simple;
use File::Slurp;
for my $recipe (&lt;xml/*&gt;) {
    my $xml = read_file($recipe);
    my $structure = XMLin($xml, ForceArray =&gt; 1)-&gt;{recipe}-&gt;[0];
    my $name = $structure-&gt;{head}-&gt;[0]-&gt;{title}-&gt;[0];
    my @ingredients = @{$structure-&gt;{ingredients}[0]{ing}};
    my @cats = @{$structure-&gt;{head}[0]{categories}[0]{cat}};
    Larder::Recipe-&gt;find_or_create({
        name =&gt; $name,
        xml =&gt; $xml
    });
}

</pre>
</blockquote>



<p>Now we have a load of recipes in our system and we can list them by visiting the URL /larder/recipe/list. However, we have one problem. Since Maypole tries to display all columns in a table by default, it shows the recipe XML alongside the name. We don't want it to display the XML by default, and we need to work out a way to turn that XML into HTML.</p>

<p>The first stage in this is to use the <i>display_columns</i> method as before to have it only display the name. We also need to put the recipe back into the list of allowable tables:</p>

<blockquote>
<pre>
package Larder::Recipe;
sub display_columns { "name" }

</pre>
</blockquote>

<p>Our final version of <i>Recipe.pm</i> will be shown next month. The next stage, actually displaying the XML as HTML, requires a bit of trickery.</p>
<h3>Cooking the Source</h3>

<p>We only want the recipe to be displayed in full when we go to the view page for an individual recipe; we can do this by writing our own custom view template. Maypole searches for templates in three directories to allow flexibility in overloading the default templates. When you go to a URL such as /recipe/view/10, it looks for the view template in the recipe directory; if there isn't one specific to the recipe table, it looks in the custom directory to allow you to specify site-wide defaults; finally, it checks the factory directory, which contains the generic templates that come with Maypole.</p>

<p>So we want to write our own custom template specifically for recipes, which should live in recipe/view. However, we still need to get the RecipeML into HTML somehow. What we'll use to do that is called XSL.</p>

<p>XSL is a language, itself expressed in terms of XML, for turning one XML document into another. For instance, here's the fragment of an XSL stylesheet for transforming a recipe:</p>

<blockquote>
<pre>
&lt;xsl:template match="recipe"&gt;
&lt;html&gt;
&lt;head&gt;&lt;title&gt;&lt;xsl:value-of select="head/title"/&gt;&lt;/title&gt;
&lt;/head&gt;
&lt;body&gt;
    &lt;xsl:apply-templates /&gt;
&lt;/body&gt;
&lt;/html&gt;
&lt;/xsl:template&gt;

</pre>
</blockquote>



<p>Most of the tags here are supposed to be output verbatim, apart from the tags with the namespace <i>xsl:</i>, which are magic and refer to the transformation process itself. In these cases, the first line says, "if you see a recipe tag, spit out this chunk of XML." The third line outputs ordinary HTML head and title tags, then looks for the first title element inside a head tag in the source recipe XML, and outputs the value inside those tags. The <i>apply-templates</i> in the middle means "keep working through the source document and apply any other fragments for tags that you find."</p>

<p>To apply the XSL stylesheet to a recipe, we need to use an XSL processor. One of the best in the business (or at least, in the Perl business) comes from the GNOME project and is wrapped in the <i>XML::LibXSLT</i> module. We can take an <i>XML::LibXML</i> parser and parse a document:</p>

<blockquote>
<pre>
use XML::LibXML;
my $parser = XML::LibXML-&gt;new();
my $source = $parser-&gt;parse_file("recipe.xml");

</pre>
</blockquote>

<p>And then parse a stylesheet because that's just plain XML, as well:</p>

<blockquote>
<pre>
my $xsl = $parser-&gt;parse_file("recipe.xsl");

</pre>
</blockquote>



<p>And now we use <i>XML::LibXSLT</i> to turn that into a stylesheet object:</p>

<blockquote>
<pre>
use XML::LibXSLT;
my $stylesheet_parser = XML::LibXSLT-&gt;new();
my $stylesheet = $stylesheet_parser-&gt;parse_stylesheet($xsl);

</pre>
</blockquote>

<p>Now that stylesheet object can be used to transform the original recipe:</p>

<blockquote>
<pre>
my $results = $stylesheet-&gt;transform($source);
print $stylesheet-&gt;output_string($results);

</pre>
</blockquote>

<p>This should output some nice HTML for our recipe. Now we need to know how to get that nice HTML into the context of our web application.</p>
<h3>Simmer and Serve</h3>

<p>Since we'll be processing our Maypole templates using the Template Toolkit, the most natural way to do this is as a Template plugin. We're going to write our own plugin using the <i>XML::LibXSLT</i> module to transform the RecipeXML into HTML. We'll start by envisaging the syntax we want out of it, which will look something like this:</p>

<blockquote>
<pre>
[% USE transform = XSLT("recipe.xsl") %]

[% recipe.xml | $transform %]

</pre>
</blockquote>

<p>We can inherit from the basic filter class <i>Template::Plugin::Filter</i> and plan to override the two methods <i>init</i> and <i>filter</i> with methods that set up our XSLT parser and apply the stylesheet to the filtered text, respectively.</p>

<p>The <i>init</i> method needs to load the stylesheet, and may as well parse it and turn it into an <i>XML::LibXSLT::Stylesheet</i> object there and then. First, we get the name of the stylesheet we want to use; this will be provided in the <i>_ARGS</i> slot of the filter object:</p>

<blockquote>
<pre>
sub init {
    my $self = shift;
    my $file = $self-&gt;{ _ARGS }-&gt;[0]
       or return $self-&gt;error('No filename specified!');

</pre>
</blockquote>



<p>Next, we load up the parsers and try parsing the stylesheet's XML:</p>

<blockquote>
<pre>
$self-&gt;{ parser } = XML::LibXML-&gt;new();
$self-&gt;{ XSLT } = XML::LibXSLT-&gt;new();
my $xml;
eval {
    $xml = $self-&gt;{ parser }-&gt;parse_file($file);
};
return $self-&gt;error("Stylesheet parsing error: $@") if $@;
return $self-&gt;error("Stylesheet parsing failed") unless $xml;

</pre>
</blockquote>

<p>If that works, then we can try feeding the stylesheet to <i>XML::LibXSLT</i>:</p>

<blockquote>
<pre>
eval {
    $self-&gt;{ stylesheet } =
        $self-&gt;{ XSLT }-&gt;parse_stylesheet( $xml );
};
return $self-&gt;error("Stylesheet not valid XSL: $@") if $@;
return $self-&gt;error("Stylesheet parsing failed") 
    unless $self-&gt;{stylesheet};
return $self;

</pre>
</blockquote>

<p>This handles what happens when the <i>USE</i> call is made; the filter method is called when the returned object is used as a filter. This gets handed some text and needs to parse it:</p>

<blockquote>
<pre>
my ($self, $text) = @_;
my $xml;
eval { $xml = $self-&gt;{ parser }-&gt;parse_string($text); };
return $self-&gt;error("XML parsing error: $@") if $@;
return $self-&gt;error("XML parsing failed") unless $xml;

</pre>
</blockquote>

<p>And then it needs to apply the stylesheet to it:</p>

<blockquote>
<pre>
return $self-&gt;{ stylesheet }-&gt;output_string(
    $self-&gt;{ stylesheet }-&gt;transform( $xml )
);

</pre>
</blockquote>

<p>That's essentially the core of the <i>Template::Plugin::XSLT</i> module, which I wrote precisely in order to display these recipes. Now we can write our recipe/view template, based on the generic one in factory/view:</p>

<blockquote>
<pre>
[% INCLUDE header %]
&lt;h2&gt; [% recipe.title %] &lt;/h2&gt;
[% INCLUDE navbar;
USE transform = XSLT("recipe.xsl");
recipe.xml | $transform %]

</pre>
</blockquote>

<p>We can now view a recipe using an XSL template. <a name="rf2"></a><a href="0404df2.html">Figure 2</a> is one I prepared earlier.</p>
<h3>Serving Suggestion</h3>

<p>We've looked at a wide variety of things in this article: using XSLT to transform XML into HTML; writing Template Toolkit filter plugins; using <i>Class::DBI::FromCGI</i> to restrict the possible input for form fields; and, of course, using Maypole, a new web application framework.</p>

<p>While we've not gone into much depth about how Maypole does its stuff&#151;we'll look at that next month&#151;I hope I've given you the flavor (ho, ho) of how easy it is to construct web applications in Maypole. We've put together an interface to a larder inventory system, together with an XSL-based recipe display in around 40 lines of Perl code.</p>

<p>Next month, we'll look at linking recipes with their ingredients and searching for optimal recipes to use up fading food! Until then, happy cooking!</p>


<p><b></b></p>
<p><b></b></p>
<p><b>TPJ</b></p>



	
	
<br>
<br>
<b><a name="l1"></a>Listing 1</b><br>
<pre>package Larder;
use strict;
use base 'Apache::MVC';
Larder-&gt;setup("dbi:mysql:larder");
Larder-&gt;config-&gt;{display_tables} = [qw[food contents recipe]];
Larder-&gt;config-&gt;{uri_base} = "http://localhost/larder/";
Larder::Contents-&gt;has_a( food =&gt; "Larder::Food" );
Larder::Contents-&gt;untaint_columns(
    printable =&gt; [ "quantity" ],
    date      =&gt; [ "use_by" ],
    integer   =&gt; [ "food" ]
);

package Larder::Food;
sub display_columns { "name" }
Larder::Food-&gt;add_trigger( after_update =&gt; sub {
    my $self = shift;
    my $old = $self-&gt;normalized;
    my $new = normalize($self-&gt;name);
    if ($old ne $new) {
        $self-&gt;normalized($new);
    }
} );
Larder::Food-&gt;add_trigger( after_create =&gt; sub {
    my $self = shift;
    $self-&gt;normalized(normalize($self-&gt;name));
} );

Larder::Food-&gt;untaint_columns( printable =&gt; [ "name" ] );
sub normalize {
    my $word = lc shift;
    $word =~ s/[aeiou]//g;
    $word =~ s/[^\s\w]//g;
    $word =~ tr/a-z /a-z /s;
    return $word;
}

package Larder::Recipe;
sub display_columns { "name" }

1;</pre>
<a href="#rl1">Back to article</a><br>
	
	
	


</body>
</html>