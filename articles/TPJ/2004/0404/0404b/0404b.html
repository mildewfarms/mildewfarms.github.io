
<html>
<head>
<title>April, 2004: Reformatting Text Using Pattern Matching</title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; The Perl Journal-->

<h1>Reformatting Text Using Pattern Matching</h1>
<p><i>The Perl Journal</i> April, 2004</p>
<h2></h2>


<h3>By Julius C. Duque</h3>


<I>Julius is a freelance network consultant in the Philippines. He can be contacted at jcduque@lycos.com.</I>

<hr>





<p>Whenever I write documents, I spend a considerable amount of time just reformatting lines of text so that they fit within a 70-character-wide column. I do this because I don't like reading long lines that wrap across the screen.</p>

<p>Because adjusting lines of text by hand is time consuming, I decided to put my Perl skills to good use by automating this cumbersome task. Being very good at text manipulation, Perl is the ideal tool for this job. Sure, there's already the <i>fmt</i> command, but it's available only on UNIX/Linux systems. Besides, it can only output left-justified lines.</p>

<p>The full source code of my Perl script, called <i>pretty</i>, can be found in <a name="rl1"></a><a href="#l1">Listing 1</a>. Examples 1 through 6 (<a name="re1"></a><a href="0404be1.html">1</a>, <a name="re2"></a><a href="0404be2.html">2</a>, <a name="re3"></a><a href="0404be3.html">3</a>, <a name="re4"></a><a href="0404be4.html">4</a>, <a name="re5"></a><a href="0404be5.html">5</a>, <a name="re6"></a><a href="0404be6.html">6</a>) show us all possible output formats when the input file, <i>gettysburg.txt</i>, which contains Lincoln's Gettysburg Address, is reformatted. Both the source for <i>pretty</i> and <i>gettysburg.txt</i> are available for download at http://www .tpj.com/source/.</p>

<p>Note: When using <i>pretty</i>, it expects unformatted paragraphs to be separated by at least two consecutive newlines.</p>
<h3>Anatomy of <i>pretty</i></h3>

<p>Lines 9-12 of <i>pretty</i> declare the switches available to the user. The <i>--width</i> switch, which takes a mandatory integer value (<i>=i</i>), is used to control the line width of output lines. The decision to make <i>--width</i> take a mandatory value is just a matter of  preference.</p>

<p>The options <i>--help</i>, <i>--left</i>, <i>--right</i>, <i>--centered</i>, <i>--both</i>, and <i>--newline</i> don't take any value; they're activated if explicitly specified on the command line.</p>

<p><i>--left</i> and <i>--indent</i> are the only options that have default values. Lines are printed left-justified, regardless of whether <i>--left</i> (or its short form <i>-l</i>) is specified, unless overridden by <i>--right</i>, <i>--centered,</i> or <i>--both</i>. The switch <i>--indent</i> (or <i>-i</i>), whose integer argument specifies the amount of indention at the start of a paragraph, defaults to a value of 0 (no indention) if it is not explicitly specified (line 15).</p>

<p><i>--centered</i> (or <i>-c</i>) places a line of text of equal spacing from the left and right margin. <i>--right</i> (or its short form <i>-r</i>) outputs lines that are right-justified, while <i>--both</i> (or <i>-b</i>) produces both left-justified and right-justified lines. To put empty lines between paragraphs, specify <i>--newlines</i> (or <i>-n</i>).</p>

<p>To aid the user, the function <i>syntax()</i> is called if <i>--help</i> is specified or if <i>--width</i> is omitted (line 14).</p>
<h3>Parsing a Paragraph</h3>

<p>Normally, Perl reads a chunk of data one line at a time&#151;a "line" being a string of characters terminated by a newline (<i>\n</i>). Since we want to reformat paragraphs that span multiple lines, we need to change the meaning of "line." We tell Perl to parse paragraphs instead of single lines. Now, the meaning of a "line" becomes "a string of characters delimited by two or more consecutive newlines." This is, essentially, the meaning of a "paragraph."</p>

<p>Fortunately, Perl offers a special variable that can be set to change the meaning of a "line." That variable is <i>$/</i>, the input record separator. You may set it to a multicharacter string to match a multicharacter delimiter. In our problem of reformatting paragraphs, be warned, though, that the choice of a new value for <i>$/</i> can be tricky. For instance, the obvious delimiter, <i>\n\n</i>, is wrong. If Perl sees three consecutive newlines, for example, Perl will assume that the third newline belongs to the next paragraph. Meanwhile, Perl will swallow the whole input, from the first up to the last character, if <i>$/</i> is set to <i>undef</i>. The correct approach is to set <i>$/</i> to an empty string, <i>""</i>. This tells Perl to treat two or more consecutive newlines as a single newline; see line 17.</p>
<h3>The Basic Idea</h3>

<p>Lines 19-23 illustrate the core idea of <i>pretty</i>. On line 19, the use of the loop <i>while (&lt;&gt;)</i> enables <i>pretty</i> to act as a filter. With this loop, you can use the script like this:</p>

<blockquote>
<pre>
  cat file1.txt file2.txt file3.txt | ./pretty --width=64 --right 

</pre>
</blockquote>



<p>as well as like this:</p>

<blockquote>
<pre>
  ./pretty --width=64 --right file1.txt file2.txt file3.txt 

</pre>
</blockquote>



<p>where <i>file1.txt</i>, <i>file2.txt</i>, and <i>file3.txt</i> are input files.</p>

<p>The paragraph read by Perl on line 19 is implicitly loaded into another special variable, <i>$_</i>. On line 20, the split function implicitly acts on <i>$_</i> and strips off all whitespaces (spaces, tabs, and newlines), storing only chunks of nonwhitespaces into an array, <i>@linein</i>. On Line 21, function <i>printpar()</i> takes <i>@linein</i> as argument and prints out the formatted paragraph. Last, on line 22, a newline is printed to separate two consecutive paragraphs if <i>--newline</i> is specified.</p>
<h3>Printing a Line</h3>

<p>The function <i>printpar()</i> performs the actual work of reformatting paragraphs (starting on line 25). When this function is called on line 21, the argument, <i>@linein</i>, is passed on to another variable, <i>@par</i> (line 27). From now on, the paragraph read by Perl will be manipulated through this new variable. The variable <i>$firstline</i> (on lines 28, 31, and 36-39) is relevant only if the option <i>--newline</i> is specified. See the section "Indentation" for more details.</p>

<p>The logic of <i>printpar()</i> is as follows:</p>



<ol>
  <li>We make use of a temporary line buffer that is, at most, as long as the line width <i>($width</i>) specified by the user. We call this buffer <i>$buffer</i>. The unit  of length is 1 character.</li>
  <li>2	We maintain a running total of characters read so far. Store this running total to variable <i>$charcount</i>. Initially, this is set to 0. <i>$charcount</i> must not exceed <i>$width</i>.</li>
  <li>Extract an element (a nonwhitespace chunk) from <i>@par</i> one at a time and take note of the element's length. Insert the element into <i>$buffer</i>. Increment <i>$charcount</i> by an amount equal to the extracted element's length. Then, insert a single space into <i>$buffer</i> to serve as a word separator. Increment <i>$charcount</i> by 1 to account for this single space. In doing so, realize that the last inserted element in <i>$buffer</i> is always a single space.</li>
  <li>Repeat Step 3 until <i>$charcount</i> either exceeds or equals <i>$width</i>. Note that <i>$charcount</i> may exceed or equal <i>$width</i> by the insertion of an extracted element into <i>$buffer</i>, even before the  mandatory word separator is added to <i>$buffer</i>.</li>
  <li>If Step 3 terminates because <i>$charcount</i> either exceeds or equals <i>$width</i>, discard the last single space inserted in <i>$buffer</i>. Decrement <i>$charcount</i> by 1. Jump to Step 7 if the new value of <i>$charcount</i> becomes less than or equal to <i>$width</i>; otherwise, proceed to Step 6.</li>
  <li>If, after decrementing by 1 (in Step 5), <i>$charcount</i> still exceeds the <i>$width</i>, requeue the last, extracted nonwhitespace element back into <i>@par</i> and update <i>$charcount</i> by subtracting from it the length of the excess element. Note that there is a word separator inserted into <i>$buffer</i> just before the returned element was inserted into <i>$buffer</i>. Delete this extra space also and decrement <i>$charcount</i> by 1.</li>
  <li>Transfer the contents of <i>$buffer</i> to another variable, <i>$lineout</i>, and print  it.</li>
  <li>Repeat Steps 1-7 until there are no more elements in <i>@par</i>.</li>
</ol>



<p>Lines 30-67, except those lines that contain the variable <i>$firstline</i>, show us how to the implement the 8 steps above. Lines containing <i>$firstline</i> are used only when <i>--newline</i> is switched on. Let's ignore these lines for the moment; we'll get to that when we discuss indentation later.</p>

<p>On line 32, we use two temporary variables: <i>$buffer</i> (to hold the line to be printed) and <i>$word</i> (to hold the extracted element from <i>@par</i>). On line 33, <i>$wordlen</i> holds the length of the element just extracted, and <i>$charcount</i> is the number of characters in <i>$buffer</i> so far. On line 34, we use a temporary variable, <i>$linewidth</i>, to hold the value of <i>$width</i>, the line width specified by the user on the command line. We're doing this because we don't want to alter <i>$width</i> directly.</p>

<p>On lines 30 and 41, there is a constant reference to scalar <i>@par</i>. This is necessary because the function scalar returns the number of elements in an array. Recall that we always remove elements from the array <i>@par</i>. We must make sure, then, that it still has elements to be extracted; we stop extracting if it is already empty, even if <i>$charcount</i> has not yet exceeded the line width.</p>

<p>When <i>$buffer</i> is ready to be printed out, we transfer its contents to another variable, <i>$lineout</i>, for final printing (lines 60 and 102).</p>

<p>In case the line width is too short to accommodate even a single word, I have provided an easy way out (lines 46-50). The solution is to use a wider line width.</p>
<h3>Indention</h3>

<p><i>pretty</i> does not produce indented paragraphs by default. But you can alter this behavior by specifying the <i>--indent</i> (or <i>-i</i>) switch, with an integer as argument. This integer tells <i>pretty</i> to pad that many spaces at the start of a line. Note, however, that indention takes effect only on the first line of each paragraph.</p>

<p>Line 12 declares <i>--indent</i> as a switch that takes an optional integer value (<i>:i</i>). Line 15 initializes <i>--indent</i> with a value of 0, in case it is not specified.</p>

<p>To help <i>pretty</i> distinguish the first line of a paragraph from the rest of the lines, I make use of a flag, the <i>$firstline</i> variable, on line 28. Before <i>pretty</i> starts scanning for input lines (on line 30), <i>$firstline</i> is initially set to 0, corresponding to a <i>false</i> value. When scanning begins, <i>$firstline</i> is incremented by 1 (its value now becomes <i>true</i>), signifying that <i>pretty</i> has just found the first line of the paragraph (line 31).</p>

<p>On lines 36-39, we check the value of <i>$firstline</i>. If it is equal to 1, then we know that we are currently dealing with the first line of the paragraph.</p>

<p>On line 37, <i>$linewidth</i> is decreased by an amount equal to the integer specified as an argument to <i>--indent</i>. On line 38, we print out that many leading spaces to serve as indention. We then fill up the remaining portion of the first line with texts, whose accumulated lengths must be less than or equal to the value of the updated <i>$linewidth</i>.</p>

<p>On the next iteration, <i>$firstline</i> is no longer equal to 1. Hence, the value of <i>$linewidth</i> on line 34 remains unchanged. This also means that initial padding of spaces will no longer occur. <a name="re5"></a><a href="0404be5.html">Examples 5</a> and <a name="re6"></a><a href="0404be6.html">6</a> present sample outputs using indention.</p>
<h3>Left-Justified Output</h3>

<p>Undoubtedly, this is the simplest paragraph format to implement: just print <i>$lineout</i> as is (line 102).</p>
<h3>Right-Justified Output</h3>

<p>For a right-justified format, we must know how many spaces to pad the left portion of <i>$lineout</i>. This amount of space is simply the difference between the specified line width and the length of <i>$lineout</i>. On line 69, we use the variable <i>$spaces_to_fill</i> to hold this value. On line 75, we print this many leading spaces before printing <i>$lineout</i>.</p>
<h3>Centered Output</h3>

<p>The centered format is very similar to the right-justified format, only this time, we divide the value of <i>$spaces_to_fill</i> by 2. If the result has a fractional part, take only the integer part (line 72), and print this many leading spaces (line 73).</p>
<h3>Left- and Right-Justified Output</h3>

<p>This is the hardest part to implement. The trick is to take <i>$lineout</i> and modify it by adding extra spaces between nonwhitespaces. But how do we distribute the spaces evenly within the line? I'll illustrate my own solution using examples.</p>

<p>Suppose that we want the line width to be 39 characters long, and the string to be printed is:</p>

<blockquote>
<pre>
fathers&#8226;brought&#8226;forth&#8226;on&#8226;this

</pre>
</blockquote>



<p>where &#8226; represents a single space.</p>

<p>The line above is 29 characters long (including 4 embedded spaces), 10 spaces short of being both left- and right-justified. My solution consists of two steps:</p>



<ol>
  <li>Scanning from the left, look for the first occurrence of a single space, and replace it with a double space.</li>
</ol>

<blockquote>
<pre>
fathers&#8226;&#8226;brought&#8226;forth&#8226;on&#8226;this

</pre>
</blockquote>



<ol>
  <li>Reverse the string.</li>
</ol>

<blockquote>
<pre>
siht&#8226;no&#8226;htrof&#8226;thguorb&#8226;&#8226;srehtaf

</pre>
</blockquote>



<p>Consider this as one round.</p>

<p>Since the new string is not yet 39 characters long, do another round:</p>



<ol>
  <li>Scanning from the left, look for the first occurrence of a single space, and replace it with a double space.</li>
</ol>

<blockquote>
<pre>
siht&#8226;&#8226;no&#8226;htrof&#8226;thguorb&#8226;&#8226;srehtaf

<ol>
  <li>Reverse the string.</li>
</ol>
fathers&#8226;&#8226;brought&#8226;forth&#8226;on&#8226;&#8226;this

</pre>
</blockquote>



<p>Notice that we have now evenly distributed two "filler" single spaces, one on the left and the other on the right. We repeat the two steps over and over until the string becomes 39 characters long.</p>

<p>We are now ready to formulate our strategy:</p>



<ol>
  <li>Scanning from the left, look for the first occurrence of a single space and replace it with a double space. Reverse the string.</li>
  <li>Repeat Step 1 until the required line width is reached.</li>
</ol>



<p>If we ran out of single spaces to replace, rephrase Step 1 as "Scanning from the left, look for the first occurrence of a double space, and replace it with a triple space. Reverse the string." Repeat the two steps.</p>

<p>If we ran out of double spaces to replace, rephrase Step 1 as "Scanning from the left, look for the first occurrence of a triple space, and replace it with a quadruple space. Reverse the string." Repeat Steps 1 and 2, and so on. Get the picture?</p>

<p>Lines 83-92 show us how to implement the strategy. The actual work of replacing spaces is found on lines 85-86. On line 81, we make use of a counter, <i>$reps</i>, that tracks the kind of spaces (single, double, triple, and so on) to look for. Initially, <i>$reps</i> is set to 1, meaning we begin our search for single spaces.</p>

<p>If the line width is not yet reached (line 83), replace the first single space found with a double space (lines 85-86). If you fail to see the single space in there, here's a closer view of lines 85-86 (a stands for a single space):</p>

<blockquote>
<pre>
if ($tempbuf =~ /(\S+&#8226;{$reps})(\S+)/) { 
    $tempbuf =~ s/(\S+&#8226;{$reps})(\S+)/$1&#8226;$2/; 

</pre>
</blockquote>

<p>When <i>$reps</i> is 1, lines 85-86 become:</p>

<blockquote>
<pre>
if ($tempbuf =~ /(\S+&#8226;{1})(\S+)/) { 
    $tempbuf =~ s/(\S+&#8226;{1})(\S+)/$1&#8226;$2/; 

</pre>
</blockquote>

<p>When searching for double spaces (<i>$reps</i> = 2), the two lines are equivalent to:</p>

<blockquote>
<pre>
if ($tempbuf =~ /(\S+&#8226;{2})(\S+)/) { 
    $tempbuf =~ s/(\S+&#8226;{2})(\S+)/$1&#8226;$2/; 

</pre>
</blockquote>

<p>or, to put it in another way:</p>

<blockquote>
<pre>
if ($tempbuf =~ /(\S+&#8226;&#8226;)(\S+)/) { 
    $tempbuf =~ s/(\S+&#8226;&#8226;)(\S+)/$1&#8226;$2/; 

</pre>
</blockquote>

<p>When given the pattern \S+&#8226;{n}, Perl will look for the presence of exactly <i>n</i> consecutive spaces preceded by one or more nonwhitespaces. And if the pattern is enclosed in parentheses, Perl will remember the substring that matches this pattern. The remembered substrings can be accessed via the special variables, <i>$1</i>, <i>$2</i>, <i>$3</i>, etc.</p>

<p>As an example, testing the string fathers&#8226;brought&#8226;forth for the pattern (\S+&#8226;{1})(\S+), we find that the substring fathersmatches the first subpattern, (\S+&#8226;{1}); thus, fathersgets assigned to <i>$1</i>. Likewise, the substring brought matches the second subpattern, (\S+), and gets assigned to <i>$2</i>. And so, the original string, fathers&#8226;brought&#8226;forth, now becomes fathers&#8226;&#8226;brought&#8226;forth.</p>

<p>What makes this implementation so challenging is that it is not readily apparent that we need to include the nonwhitespaces in the search. Naively searching for whitespaces only, Perl will only replace the first whitespace it sees and not the spaces in the middle of the line. The result is that only the whitespaces at the beginning and end of the line get replaced. It's important to realize that the nonwhitespaces serve as reference points in the substitution.</p>

<p>Let's now turn our attention to line 84. I've only included this line to make <i>pretty</i> as idiot-proof as possible. Line 84 handles a special case. Here's the scenario: Suppose we want a line to be 12 characters wide, and we have the string, democratic&#8226;institutions. We can see immediately that only the word, <i>democratic</i>, can fit on the line buffer. With only one word, it's impossible to make the line both left- and right-justified.</p>

<p>Line 85 will repeatedly search for at least one whitespace between two nonwhitespaces. Finding only one nonwhitespace on the line, line 85 will fail, and two things will happen:</p>



<ol>
  <li><i>$tempbuf</i> will never be updated (line 86). As a consequence, there will be an infinite loop on line 83.</li>
  <li>Since the <i>if</i> condition on line 85 fails, the alternative  else condition increments <i>$reps</i> continuously (line 90).</li>
</ol>



<p>Soon enough, Perl will complain and will generate error messages similar to the following:</p>

<blockquote>
<pre>
 Quantifier in {,} bigger than 32766 in regex; marked by &lt;-- HERE 
in m/(\S+ { &lt;-- HERE 32767})(\S+)/ (#1) 
   (F) There is currently a limit to the size of the min and max 
values of the {min,max} construct. The &lt;-- HERE shows in the 
regular expression about where the problem was discovered. See 
perlre. 

</pre>
</blockquote>

<p>The infinite loop on line 83 was averted because <i>$reps</i> exceeded the maximum value of the <i>{min,max}</i> regular expression construct.</p>

<p>The fix, therefore, is to check if <i>$tempbuf</i> has any embedded spaces. If there is a space in it, then we are sure that there are at least two words in <i>$tempbuf</i>. Break out of the <i>while</i> loop as soon as possible if no space is found&#151;this means that <i>$tempbuf</i> contains only one word. In doing so, we print the one-word line as is, making that line left-justified.</p>

<p>Finally, recall our previous example:</p>

<blockquote>
<pre>
fathers&#8226;brought&#8226;forth&#8226;on&#8226;this

</pre>
</blockquote>



<p>After the first substitution, the string is <i>reverse</i>d, and becomes</p>

<blockquote>
<pre>
siht&#8226;no&#8226;htrof&#8226;thguorb&#8226;&#8226;srehtaf

</pre>
</blockquote>



<p>We see here that, for an odd number of substitutions, the last call to reverse is unnecessary. So, we employ another variable, <i>$replacements_made</i>, to keep track of how many spaces have been inserted. <i>$replacements_made</i>, initially set to 0 (line 78), is incremented every time a substitution is made (line 87). So, on lines 95-98, we check <i>$replacements_made</i> to know whether to undo the last reverse. If <i>$replacements_made</i> is odd, we reverse the string one more time (line 98); otherwise, we leave the string alone (line 96).</p>
<h3>Sample Outputs</h3>

<p>Examples 1-6 show some example output using <i>pretty</i>. Any of the lines at the top of these examples will produce the lines below them. Line width is set to 64 characters. Indentions are set to four single spaces. The input file is <i>gettysburg.txt</i> (available with the source code for this article at http://www.tpj.com/source/).</p>


<p><b>TPJ</b></p>



	
	
<br>
<br>
<b><a name="l1"></a>Listing 1</b><br>
<pre>1  #!/usr/local/bin/perl 
2  use diagnostics; 
3  use strict; 
4  use warnings; 
5  use Getopt::Long; 
6   
7  my ($width, $help, $left, $centered, $right, $both); 
8  my ($indent, $newline); 
9  GetOptions("width=i" =&gt; \$width, "help" =&gt; \$help, 
10    "left" =&gt; \$left, "centered" =&gt; \$centered, 
11    "right" =&gt; \$right, "both" =&gt; \$both, 
12    "indent:i" =&gt; \$indent, "newline" =&gt; \$newline); 
13   
14  syntax() if ($help or !$width); 
15  $indent = 0 if (!$indent); 
16   
17  local $/ = ""; 
18   
19  while (&lt;&gt;) { 
20    my @linein = split; 
21    printpar(@linein); 
22    print "\n" if ($newline); 
23  } 
24   
25  sub printpar 
26  { 
27    my (@par) = @_; 
28    my $firstline = 0; 
29   
30    while (scalar @par) { 
31      $firstline++; 
32      my ($buffer, $word); 
33      my ($charcount, $wordlen) = (0, 0); 
34      my $linewidth = $width; 
35   
36      if ($firstline == 1) { 
37        $linewidth -= $indent; 
38        print " " x $indent; 
39      } 
40   
41      while (($charcount &lt; $linewidth) and (scalar @par)) { 
42        $word = shift @par; 
43        $buffer .= $word; 
44        $wordlen = length($word); 
45   
46        if ($wordlen &gt; $linewidth) { 
47          print "\nERROR: The word \"$word\""; 
48          print " ($wordlen chars) cannot be accommodated\n"; 
49          exit 1; 
50        } 
51   
52        $charcount += $wordlen; 
53        $buffer .= " "; 
54        $charcount++; 
55      } 
56   
57      chop $buffer; 
58      $charcount--; 
59   
60      my $lineout = $buffer; 
61   
62      if ($charcount &gt; $linewidth) { 
63        unshift(@par, $word); 
64        $charcount -= $wordlen; 
65        $charcount--; 
66        $lineout = substr $buffer, 0, $charcount; 
67      } 
68   
69      my $spaces_to_fill = $linewidth - $charcount; 
70   
71      if ($centered) { 
72        my $leftfill = int($spaces_to_fill/2); 
73        print " " x $leftfill; 
74      } elsif ($right) { 
75        print " " x $spaces_to_fill; 
76      } elsif ($both) { 
77        my $tempbuf = $lineout; 
78        my $replacements_made = 0; 
79   
80        if (scalar @par) { 
81          my $reps = 1; 
82   
83          while (length($tempbuf) &lt; $linewidth) { 
84            last if ($tempbuf !~ /\s/); 
85            if ($tempbuf =~ /(\S+ {$reps})(\S+)/) { 
86              $tempbuf =~ s/(\S+ {$reps})(\S+)/$1 $2/; 
87              $replacements_made++; 
88              $tempbuf = reverse $tempbuf; 
89            } else { 
90              $reps++; 
91            } 
92          }  # while 
93        } 
94   
95        if ($replacements_made % 2 == 0) { 
96          $lineout = $tempbuf; 
97        } else { 
98          $lineout = reverse $tempbuf; 
99        } 
100      } 
101   
102      print "$lineout\n"; 
103    } 
104  } 
105   
106  sub syntax 
107  { 
108    print "Options:\n"; 
109    print "--width=n (or -w=n or -w n)   Line width is n chars "; 
110    print "long\n"; 
111    print "--left (or -l)                Left-justified"; 
112    print " (default)\n"; 
113    print "--right (or -r)               Right-justified\n"; 
114    print "--centered (or -c)            Centered\n"; 
115    print "--both (or -b)                Both left- and\n"; 
116    print "                                right-justified\n"; 
117    print "--indent=n (or -i=n or -i n)  Leave n spaces for "; 
118    print "initial\n"; 
119    print "                                indention (defaults "; 
120    print "to 0)\n"; 
121    print "--newline (or -n)             Output an empty line \n"; 
122    print "                                between "; 
123    print "paragraphs\n"; 
124    exit 0; 
125  }</pre>
<a href="#rl1">Back to article</a><br>
	
	
	


</body>
</html>