
<html>
<head>
<title>January, 2005: Inversion of Control  In Perl</title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; The Perl Journal-->

<h1>Inversion of Control  In Perl</h1>
<p><i>The Perl Journal</i> January, 2005</p>
<h2></h2>


<h3>By Stevan Little</h3>


<I>Stevan is the Senior Developer at Infinity Interactive, a small NYC consultancy specializing in building LAMP applications for a number of corporate clients. Stevan can be contacted at stevan@ iinteractive.com.</I>

<hr>





<p>Inversion of Control (IoC) is the very simple idea of releasing control of some part of your application over to some other part of your application or an outside framework.</p>

<p>IoC is a common paradigm in GUI frameworks, whereby you give up control of your application flow to the framework and install your code at callback hooks within the framework. For example, take a very simple command-line interface: The application asks a question, the user responds, the application processes the answer and asks another question, and so on until it is done. Now consider the GUI approach for the same application: The application displays a screen and goes into an event loop, users actions are processed with event handlers and callback functions. The GUI framework has inverted the control of the application flow and relieved your code of having to deal with it.</p>

<p>If you hang around enough Java programmers, chances are you have heard the phrase "inversion of control" thrown around recently. IoC is also sometimes referred to as "dependency injection" or the "dependency injection principle," and many people confuse the two. IoC and dependency injection are not the same, and in fact, the concepts behind dependency injection are actually just an example of IoC principles in action (in particular, as they relate to your application's dependency relationships). IoC is also sometimes referred to as the "hollywood principle" because of the "don't call us, we'll call you" approach of things like callback functions and event handlers.</p>

<p>Despite its current overhyped buzzword status, IoC is nothing new. It is a time-tested concept that most programmers use and encounter on a daily basis. And while the ideas behind the current dependency injection hype have been around for several years, they are now reaching a level of maturity where their widespread use is becoming practical.</p>

<p>Howard Lewis Ship, the creator of the HiveMind IoC Framework, once referred to dependency injection as being the inverse of garbage collection. With garbage collection, you hand over the details of the destruction of your objects to the garbage collector. With dependency injection, you are handing over control of object creation, which also includes the satisfaction of your dependency relationships. In his opinion, just as many programmers today would never want to go back to manual memory management&#151;soon programmers will forget the days when they had to manage the creation of their own objects.</p>

<p>This is just the tip of the IoC iceberg, though. If you expand the idea of managed object creation, it becomes clear that it can be used for much more than just creating objects and handling dependency relationships. It becomes much simpler to do a number of things that would otherwise complicate your design; for instance, wrapping your objects in AOP-like method call tracers/proxies, silently substituting mock objects for real ones during testing, or exerting transparent and fine-grained control over the lifecycle of your objects. But all this is heavily in the abstract, so let's get down to something more concrete.</p>
<h3>IOC in Action</h3>

<p>I recently released an IoC framework to CPAN called <i>IOC</i>, which is available at http://search.cpan.org/~stevan/IOC/. It is still a work in progress, and its current focus is on object creation and managing dependency relationships. Future directions will include more work with object proxies and AOP-style technologies. The following is a discussion of how you can use <i>IOC</i> in its current form.</p>
<h3>Containers</h3>

<p>The central part of just about any IoC framework is the container. A container's responsibilities are roughly to dispense objects, to handle the resolution of said object's dependency relationships, and to manage the lifecycles of the objects.</p>

<p>Let's start with dispensing objects. First, we must create a container for our objects to live in and give it a name:</p>

<blockquote>
<pre>
my $c = IOC::Container-&gt;new('MyApplication');

</pre>
</blockquote>



<p>Next, we need to add a component to that container and give it a name. In the <i>IOC</i> framework, we use an <i>IOC::Service</i> to wrap and manage the lifecycle of our components:</p>

<blockquote>
<pre>
$c-&gt;register(IOC::Service-&gt;new('logger' 
        =&gt; sub { My::Logger-&gt;new() }));

</pre>
</blockquote>

<p>Now, if we want an instance of our logger component, we simply ask the container for it:</p>

<blockquote>
<pre>
my $logger = $c-&gt;get('logger');

</pre>
</blockquote>



<p>Pretty simple.</p>
<h3>Dependency Management</h3>

<p>Dependency management is also quite simple and is easily shown with an example. But first, let's create another component for our container&#151;a database connection:</p>

<blockquote>
<pre>
$c-&gt;register(IOC::Service-&gt;new(
  'db_conn' =&gt; sub { 
    DBI-&gt;connect('dbi:mysql:test', '', '') 
  }
));

</pre>
</blockquote>

<p>Now, let's add an authenticator to our container. The authenticator requires both a database connection and a logger instance in its constructor:</p>

<blockquote>
<pre>
$c-&gt;register(IOC::Service-&gt;new(
  'authenticator' =&gt; sub {
    my $c = shift;
    My::Authenticator-&gt;new(
      $c-&gt;get('db_conn'), $c-&gt;get('logger')
    );
  }
));

</pre>
</blockquote>

<p>As you can see, the first argument to our service subroutine is actually our container instance. Through this, we can then resolve the authenticator's dependency relationships.</p>
<h3>Lifecycle Management</h3>

<p>The default lifecycle for <i>IOC::Service</i> components is that of a Singleton, which means each time we ask for, say, the logger, we will get the same instance back. There is also another option for lifecycle management that we call "prototype." It is worth noting that this is not the same as prototype-based OO and should not be confused with that. Here is an example of how we would use the prototype lifecycle:</p>

<blockquote>
<pre>
$c-&gt;register(IOC::Service::Prototype-&gt;new(
  'db_conn' =&gt; sub { 
    DBI-&gt;connect('dbi:mysql:test', '', '')
  }
));

</pre>
</blockquote>

<p>Now, each time we request a new database connection from our container, we will get a new one. Being able to change between the different lifecycles by simply changing the service wrapper will come in handy as your application grows. Extending this idea, it is possible to see how you could create your own custom service objects to manage your specific lifecycle needs, such as a pool of database connections.</p>
<h3>Unit Testing with Mock Objects</h3>

<p>We have now decoupled the creation of our authenticator from the creation of both our logger instance and our database connection. We have already seen that the respective lifecycles of each component are irrelevant to their sibling components and can be varied as needed. And since nothing is referred to directly, only by name through the container, we are free to change the details and/or implementation of our components and to do something like substitute mock components.</p>

<p>In the unit tests for our authenticator, we might want to use the mock DBI driver <i>DBD::Mock</i>. To do this, we would only have to change the details of our <i>db_conn</i> service and none of our other components would be the wiser:</p>

<blockquote>
<pre>
$c-&gt;register(IOC::Service-&gt;new(
  'db_conn' =&gt; sub { 
    DBI-&gt;connect('dbi:Mock:mysql', '', '')
  }
));

</pre>
</blockquote>

<p>We could also substitute our own mock logger&#151;one that would check the expected log statements without needing to parse a log file. In abstracting away the details of the creation of an object, IoC makes the use of mock objects a much simpler process than it might otherwise be.</p>
<h3>Debugging with Proxies</h3>

<p>As we have seen with lifecycle management and switching in mock objects in our unit tests, we can do a number of things to a component behind the scenes and not have it affect our other components. The <i>IOC</i> framework also offers another means of doing this through its <i>IOC::Proxy</i> module.</p>

<p><i>IOC::Proxy</i> implements a (mostly) transparent proxy package around a particular instance of a component. Through this proxy, we can actually capture each method call and do pretty much anything we like. The most obvious usage is for logging method calls for debugging purposes. Here is an example of a basic debugger proxy:</p>

<blockquote>
<pre>
my $p = IOC::Proxy-&gt;new({
  on_wrap =&gt; sub {
    my ($p, $object_being_wrapped, 
             $proxy_package) = @_;
    warn("&gt;&gt;&gt; wrapping $object_being_wrapped
                       with $proxy_package\n");
  }  
  on_method_call =&gt; sub {
    my ($p, $method_name, $full_method_name,
                         $method_args) = @_;
    warn("&gt;&gt;&gt; $method_name called with [" . 
             (join ", " =&gt; @$method_args) . 
        "] dispatching to $full_method_name\n");
  }
});

</pre>
</blockquote>

<p>In order for this to be useful, we need to install the proxy on a component. There are two ways of accomplishing this. Either we can associate our proxy with a component at registration time using the <i>registerWithProxy</i> method, like so:</p>

<blockquote>
<pre>
$c-&gt;registerWithProxy(IOC::Service-&gt;new(
  'authenticator' =&gt; sub { ... }, $p
);

</pre>
</blockquote>

<p>or we can add the proxy to a component by name, using the <i>addProxy</i> method:</p>

<blockquote>
<pre>
$c-&gt;addProxy('authenticator', $p);

</pre>
</blockquote>



<p>The result is that when you ask for our authenticator component, you will actually be given back a proxy object, which is almost indistinguishable from the real component. The proxy object actually does quite a lot to cover its tracks and will respond as expected to <i>isa</i> and <i>can</i> (including <i>UNIVERSAL::isa</i> and <i>UNIVERSAL::can</i>) and will not leave any information to be found in the output of caller. It will even handle overloaded operators and AUTOLOAD correctly. The only place in which the proxy object is easily detectable is when it is passed to <i>ref</i>.</p>

<p>This is just the base for the proxying possibilities. There are plans in the works for creating more specialized proxy objects and these will likely show up in future releases of <i>IOC</i>.</p>
<h3>IOC in Detail</h3>

<p>I have shown how to use <i>IOC</i> to manage components lifecycles, their dependencies, and even how the <i>IOC</i> framework itself can be used to help unit testing and debugging. All of this can be done with a fairly high degree of decoupling and only a small configuration overhead. For the most part, these represent the most common and basic usage of the <i>IOC</i> framework, but as your application grows, there are other, deeper parts of the IOC framework that might come in handy.</p>
<h3>Service Creation/Injection Styles</h3>

<p>So far, I have shown the default way of creating a service or component, using an anonymous subroutine. But this is not the only way to go about this. Those who have encountered IoC in the Java world may be familiar with the idea that there are three "types" of IoC/dependency injections: constructor injection, setter injection, and interface injection. In <i>IOC</i>, we support both constructor and setter injection. However, I decided that interface injection was not only too complex, but highly java-specific, and the concept did not adapt itself well to Perl. The three injections styles <i>IOC</i> supports are as follows.</p>



<p><b>Block injection</b>. This is <i>IOC</i>'s default style of injection. It is not one of the official three types (mostly because it's not possible in Java), although it can be found in a few Ruby IoC frameworks, and "Block injection" is my own term for it. It could be viewed as a compound injection style, in that it can be used to mix constructor and setter injection. Take this example:</p>

<blockquote>
<pre>
my $s = IOC::Service-&gt;new('app' =&gt; sub {
  my $c = shift;
  my $app = My::Application-&gt;new($c-&gt;get('authenticator'));
  $app-&gt;setLogger($c-&gt;get('logger'));
  $app-&gt;setDatabaseConnection($c-&gt;get('db_conn'));
  return $app;
});

</pre>
</blockquote>

<p>The <i>My::Application</i> object requires an authenticator in its constructor, and then a logger instance and a database connection are added through setter methods. This allows for a great deal of flexibility with component creation and especially tends to work best when retrofitting an application with <i>IOC</i>.</p>

<p><b>Constructor injection.</b> With constructor injection, the container calls the object's constructor and feeds it the required arguments. This promotes what is called a "good citizen" object, or an object that is completely initialized upon construction. This is the style used and popularized by the Pico Container Java Framework. <i>IOC</i> supports this style through the <i>IOC::Service::ConstructorInjection</i> and <i>IOC::Service::Prototype::ConstructorInjection</i> modules. Here are some examples of how constructor injection is done in <i>IOC</i>:</p>

<blockquote>
<pre>
my $s = IOC::Service::ConstructorInjection-&gt;new(
  'db_conn' =&gt; (
     'DBI', 'connect', [ 'dbi:mysql', 
                     'user', '****' ]
  )
);

</pre>
</blockquote>

<p>Dependencies are managed somewhat differently with constructor injection than they are with the default block injection. Here is an example of how that looks:</p>

<blockquote>
<pre>
my $s = IOC::Service::ConstructorInjection-&gt;new(
  'authenticator' =&gt; (
    'My::Authenticator', 'new', [
IOC::Service::ConstructorInjection
              -&gt;ComponentParameter('db_conn'),
IOC::Service::ConstructorInjection
              -&gt;ComponentParameter('logger'),
    ];
  )
);

</pre>
</blockquote>

<p>As you can see, the class method <i>ComponentParameter</i> is used as a placeholder for any dependencies that need to be resolved at component creation time. There are advantages to this style, one of which is that it promotes the "good citizen" object pattern. However, it is easy to see how a constructor could get very messy as the number of dependencies are increased.</p>

<p><b>Setter Injection.</b> <i>IOC</i> also supports setter injection. The idea behind setter injection is that for each component dependency, a corresponding <i>setter</i> method is created. This style has been popularized by the Spring Java Framework. <i>IOC</i> supports this style with its <i>IOC::Service::SetterInjection</i> and <i>IOC::Service::Prototype::SetterInjection</i> modules. Here is an example of how setter injection is done with <i>IOC</i>:</p>

<blockquote>
<pre>
my $s = IOC::Service::SetterInjection-&gt;new(
  'app' =&gt; sub {
    'My::Application', 'new'	=&gt; [
     { setAuthenticator	=&gt; 'authenticator' },
     { setLogger		=&gt; 'logger'        },
     { setDatabaseConnection	=&gt; 'db_conn'       },
    ]
  }
);

</pre>
</blockquote>

<p>As you can see, for each <i>setter</i> method, we provide a component name to be passed to it. This style has its advantages, but one obvious problem is the public nature of the setters.</p>
<h3>Hierarchal Containers</h3>

<p>So far, I have shown basic containers that only have a single level of components. As your application grows larger, it may become useful to have a more hierarchal approach to your containers. <i>IOC::Container</i> supports this behavior through its many subcontainer methods. <a name="rl1"></a><a href="#l1">Listing 1</a> is an example of how we might rearrange the previous examples using subcontainers.</p>

<p>We have introduced the <i>IOC::Container</i> method <i>find</i> in this example. This method can be used to find a component that is stored outside of the current components container and it supports a basic path-like syntax for climbing your container hierarchy.</p>
<h3>Global Container Registry</h3>

<p><i>IOC</i> provides a global container registry through the module <i>IOC::Registry</i>. <i>IOC::Registry</i> is a Singleton and can be used to store and then access all of your containers and services; it also includes the ability to search for services and containers within your hierarchy. You would likely initialize your registry Singleton in the same configuration file where you created all your containers. So utilizing the code from <a name="rl1"></a><a href="#l1">Listing 1</a>, your registry might look like this:</p>

<blockquote>
<pre>
my $r = IOC::Registry-&gt;new();
$r-&gt;registerContainer($app_c);

</pre>
</blockquote>

<p>Now, from anywhere else in your code, you could do something like this:</p>

<blockquote>
<pre>
# get the singleton instance
my $r = IOC::Registry-&gt;instance(); 

# and now try to find a particular service
my $s = $r-&gt;searchForService('laundry') 
  || die "Could not find the laundry service";

# or for a particular container
my $s = $r-&gt;searchForService('database') 
  || die "Could not find the database container";

</pre>
</blockquote>

<p>You can also address services and containers by paths, much like the <i>find</i> method of the <i>IOC::Container</i> object. That would look like this:</p>

<blockquote>
<pre>
my $db_conn = $r-&gt;locateService('/database/connection');

</pre>
</blockquote>



<p>The registry object can serve as a central point for all your object dispensing needs, and reduce a sometimes complex collection of Singletons and object factories.</p>
<h3>The Future of <i>IOC</i></h3>

<p><i>IOC</i> is still very much a work in progress. Its current version as of this writing is 0.11, but no doubt that will change as I am taking the "release early/release often" approach to this module. Here are some directions <i>IOC</i> may take:</p>



<ul>
  <li><b>Handling cyclical dependencies.</b> Currently <i>IOC</i> does not handle cyclical dependencies. It can catch the cyclical dependency and will throw an exception when it is found. I am currently looking for a way to overcome this problem and hope to have it resolved soon.</li>
  <li><b>XML or YAML configuration.</b> I have been experimenting with both XML- and YAML-based configuration files, which could be used to automagically create containers.</li>
  <li><b>Dependency visualization.</b> As an application grows, it can become more and more difficult to visualize all the interdependencies within it. I can see a real use for having the ability for <i>IOC</i> to visualize those dependencies for you using some kind of graph drawing tool like <i>GraphViz</i>.</li>
  <li><b>More proxies.</b> The current <i>IOC::Proxy</i> object is just the beginning of what can be done using this AOP-like approach. I have a number of ideas for all sorts of funky proxy objects.</li>
</ul>


<p><b></b></p>
<p><b></b></p>
<p><b>TPJ</b></p>



	
	
<br>
<br>
<b><a name="l1"></a>Listing 1</b><br>
<pre>my $app_c = IOC::Container-&gt;new('app');

my $db_c = IOC::Container-&gt;new('database');
$db_c-&gt;register(IOC::Service-&gt;new('dsn' =&gt; sub { 'dbi:mysql:test' }));
$db_c-&gt;register(IOC::Service-&gt;new('username' =&gt; sub { 'user' }));  
$db_c-&gt;register(IOC::Service-&gt;new('password' =&gt; sub { '****' }));   
$db_c-&gt;register(IOC::Service-&gt;new('connection' =&gt; sub {
              my $c = shift;
              return DBI-&gt;connect(
                          $c-&gt;get('dsn'), 
                          $c-&gt;get('username'), 
                          $c-&gt;get('password'));
              }));

$app_c-&gt;addSubContainer($db_c);

my $log_c = IOC::Container-&gt;new('logging');
$log_c-&gt;register(IOC::Service-&gt;new('log_file', sub {
  '/var/log/app.log' 
}));
$log_c-&gt;register(IOC::Service-&gt;new('logger', sub { 
              My::Logger-&gt;new((shift)-&gt;get('log_file')) 
              }));  

$app_c-&gt;addSubContainer($log_c);

my $sec_c = IOC::Container-&gt;new('security');
$sec_c-&gt;register(IOC::Service-&gt;new('authenticator' =&gt; sub {
              my $c = shift;
              My::Authenticator-&gt;new(
                  $c-&gt;find('../database/connection'), 
                  $c-&gt;find('../logging/logger')
                  );
              }));

$app_c-&gt;addSubContainer($sec_c);

$app_c-&gt;register(IOC::Service-&gt;new('app' =&gt; sub {
              my $c = shift;
              my $app = My::Application-&gt;new($c-&gt;find('/security/authenticator'));
              $app-&gt;setLogger($c-&gt;find('/logging/logger'));
              $app-&gt;setDatabaseConnection($c-&gt;find('/database/connection'));
              return $app;
              }));</pre>
<a href="#rl1">Back to article</a><br>
	
	
	


</body>
</html>