
<html>
<head>
<title>March, 2005: Monitoring Network Traffic Revisited</title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; The Perl Journal-->

<h1>Monitoring Network Traffic Revisited</h1>
<p><i>The Perl Journal</i> March, 2005</p>
<h2></h2>


<h3>By Paul Barry</h3>


<I>Paul lectures at the Institute of Technology, Carlow, in Ireland, and can be reached at paul.barry@itcarlow.ie. His web site is http:// glasnost.itcarlow.ie/~barryp/.</I>

<hr>





<p>In the July 2004 issue of <i>TPJ</i>, Robert Casey wrote an excellent article on using the <i>Net::Pcap</i> module to capture and process network traffic. Robert assumed that the reader was familiar with the inner workings of libpcap, the C library to which <i>Net::Pcap</i> provides a Perl interface. The API provided is very C-like, as <i>Net::Pcap</i> was purposely designed to match libpcap's C API call-for-call and, as a result, is very complex. Although this gives the Perl programmer total control over what's going on, the requirement to work at the "C level" can often make things harder than they need to be. And trust me, learning the inner workings of libpcap&#151;documented in the pcap(3) and <i>Net::Pcap</i> man pages&#151;is not for the faint hearted.</p>
<h3>Is There Something Easier than <i>Net::Pcap</i>?</h3>

<p>With this in mind, Tim Potter, the author of <i>Net::Pcap</i>, produced the companion module <i>Net::PcapUtils</i> to encapsulate most of the <i>Net::Pcap</i> functionality, concentrating on providing a set of default initialization values. Only three functions make up the entire <i>Net::PcapUtils</i> API, as opposed to the more than 15 provided by <i>Net::Pcap</i>. In this article, I'll rewrite Robert's code to use <i>Net::PcapUtils</i> instead of <i>Net::Pcap</i>. In doing so, I'll use just two of the former module's functions. What's gained is a program that is smaller and, one hopes, easier to understand and maintain. What's lost is the ability to minutely customize the initialization of the packet-capturing environment. By taking advantage of <i>Net::PcapUtils</i> default initialization values, we can concentrate on processing the captured packets as opposed to worrying about specific initialization details.</p>
<h3>Rewriting Robert's Analyzer with <i>Net::PcapUtils</i></h3>

<p>Rather than use the <i>Net::Pcap</i> module at the top of the program, my code (in <a name="rl1"></a><a href="#l1">Listing 1</a>) brings in <i>Net::PcapUtils</i> instead, together with the other required modules:</p>

<blockquote>
<pre>
use Net::PcapUtils;
use NetPacket::Ethernet;
use NetPacket::IP;
use NetPacket::TCP;

</pre>
</blockquote>

<p>As with Robert's code, the <i>NetPacket::*</i> modules&#151;also by Tim Potter&#151;are used to decode chunks of network traffic as they are captured. After setting a Boolean constant, I define two packet-capturing constant values:</p>

<blockquote>
<pre>
use constant CAPTURE_FILTER =&gt; 
  '(dst 127.0.0.1) &amp;&amp; (tcp[13] &amp; 2 != 0)'; 
use constant CAPTURE_DEVICE =&gt; 'eth0';

</pre>
</blockquote>

<p>The first, CAPTURE_FILTER, holds the filter string to use when deciding when to process a captured packet. This string is identical to that used by Robert and it filters on TCP segments that have the SYN flag set. Be sure to change the 127.0.0.1 address to the IP address of the interface you want to capture traffic on. The CAPTURE_DEVICE constant identifies the network card to use when capturing and is set to <i>eth0</i>. Robert's code was able to work this value out for itself, but as <i>eth0</i> is the default network device on most machines, it's quicker and easier to hard-code this value into the program. If you later want to capture on <i>eth1</i> instead, simply change this constant. In fact, we need not specify <i>eth0</i> at all, as <i>Net::PcapUtils</i> uses it by default. However, I find the extra effort required to define the constant to be worthwhile. Changing the filter is neater now, too, as constant values are easily identified at the top of the source code.</p>

<p>The just-defined constant values are used on the very next piece of code, which calls the <i>Net::PcapUtils</i> <i>open</i> function to prepare the network card to capture traffic:</p>

<blockquote>
<pre>
my $pkt_descriptor = Net::PcapUtils::open( 
                      FILTER =&gt; CAPTURE_FILTER,
                      DEV    =&gt; CAPTURE_DEVICE 
                     );

</pre>
</blockquote>

<p>The <i>open</i> function does a number of things for us. The most useful is that it automatically puts the identified network card into promiscuous mode, which saves us the trouble. If the call to <i>open</i> succeeds, a reference to a valid "packet descriptor" is returned to our code. If the call fails, the reference is undefined. My code checks the status of the packet descriptor and exits with an appropriate message if something goes wrong. Typically, the code in this <i>if </i>statement fires if an attempt is made to put the network card in promiscuous mode while running as a regular user on Linux/UNIX systems. Only root can put the network card into promiscuous mode:</p>

<blockquote>
<pre>
if ( !ref( $pkt_descriptor ) )
{
  print "Net::PcapUtils::open returned:
                     $pkt_descriptor\n";
  exit;
}

</pre>
</blockquote>

<p>With a valid packet descriptor created, my code enters an infinite loop, which captures and processes packets:</p>

<blockquote>
<pre>
while( TRUE )
{
  my ( $packet, %header )
     = Net::PcapUtils::next( $pkt_descriptor );
  syn_packets( $packet );
}

</pre>
</blockquote>

<p>The <i>next </i>function from the <i>Net::PcapUtils</i> module blocks, waiting for a packet to arrive on the network interface associated with the packet descriptor. When one does, <i>next</i> returns the entire packet (as a scalar), as well as its header information (as a hash). The entire packet is sent to the <i>syn_packets</i> subroutine for processing, which differs only slightly from that provided by Robert (in how it processes its parameters).</p>

<p>In my view, my program is more Perlish than that written by Robert, even though both programs do roughly the same thing. I hope it is easier to understand, maintain, and extend.</p>
<h3>Building Another Analyzer </h3>

<p>Writing filter specifications, as defined in the CAPTURE_FILTER constant in <a name="rl1"></a><a href="#l1">Listing 1</a> (and borrowed from Robert's code), requires a little bit of research and learning. Fortunately, the <i>tcpdump(8)</i> man page has all the details. To continue to keep things simple, here's a straightforward filter specification that captures all TCP traffic on port 8080:</p>

<blockquote>
<pre>
( tcp port 8080 )

</pre>
</blockquote>



<p>In <a name="rl2"></a><a href="#l2">Listing 2</a>, this filter is used to create a simple analyzer that captures and displays any and all traffic that matches the specification. Perhaps you've implemented a custom web server or application that communicates on port 8080, and you need to debug (or postprocess) the traffic generated. As this version of the analyzer isn't interested in displaying the <i>to</i> and <i>from</i> IP address information (unlike Robert's code), we can dispense with all of the decoding when the captured packet is processed. Instead, we simply strip away any header information associated with lower level protocols&#151;IP and Ethernet, in this case&#151;and decode what's left as a chunk as TCP traffic. Here's how to do this:</p>

<blockquote>
<pre>
sub process_packet {
  my $packet = shift;
  my $tcp = NetPacket::TCP-&gt;decode( 
       ip_strip ( eth_strip ( $packet ) ) 
     );
  print $tcp-&gt;{ data };
}

</pre>
</blockquote>

<p>Note how, in this program, what was called <i>syn_packet</i> has been changed to the more generic <i>process_packet</i>. The <i>eth_strip</i> and <i>ip_strip</i> functions come from their respective <i>NetPacket</i> modules. These are imported into the code using the predefined export tags as follows:</p>

<blockquote>
<pre>
use NetPacket::Ethernet qw( :strip );
use NetPacket::IP qw( :strip );  

</pre>
</blockquote>

<p>When <i>next</i> returns a captured packet, it returns only the first 100 bytes captured, which is a <i>Net::PcapUtils</i> default. This is enough packet data if all you are interested in is the header data; for instance, IP addresses, protocol ports, or Ethernet types. However, if what you are interested in is the actual data sent&#151;as well as the protocol-generated data&#151;then you need to tell <i>Net::PcapUtils</i> to return more of the captured packet. In <a name="rl2"></a><a href="#l2">Listing 2</a>, I define another constant, CAPTURE_AMOUNT, and set it to the value of 1500, which is the largest chunk of traffic that an Ethernet network interface can transmit at any one time:</p>

<blockquote>
<pre>
use constant CAPTURE_AMOUNT =&gt; 1500;

</pre>
</blockquote>



<p>This constant is then used as part of the call to <i>open</i> and sets the value for the SNAPLEN parameter:</p>

<blockquote>
<pre>
my $pkt_descriptor = Net::PcapUtils::open( 
                  FILTER	=&gt; CAPTURE_FILTER,
                  DEV	=&gt; CAPTURE_DEVICE,
                  SNAPLEN	=&gt; CAPTURE_AMOUNT
                );

</pre>
</blockquote>

<p>When the program in <a name="rl2"></a><a href="#l2">Listing 2</a> is executed (as root, you'll recall), all of the output generated by the <i>print</i> statement can be piped to a file for later perusal at your leisure. The program in <a name="rl2"></a><a href="#l2">Listing 2</a> can be made even more generic by adding some simple command-line processing to set the values for the constants using the standard modules <i>Getopt::Std</i>, <i>Getopt::Long</i>, or good ol' <i>shift</i>. Doing so is left as an exercise for the keen reader.</p>
<h3>Learning More</h3>

<p>For more on <i>Net::PcapUtils</i> and packet capturing, refer to Chapter 2 of my first book <i>Programming the Network with Perl</i>; and if you want to use this technology to decode traffic for a protocol not covered by the <i>NetPacket::* </i>modules (such as DNS), see issue 0.6 of <i>The Perl Review</i>. And be sure to check out Robert's original article&#151;it's a great read.</p>
<h3>References</h3>

<p>The tcpdump(8) man page.</p>

<p>Casey, Robert. "Monitoring Network Traffic with <i>Net::Pcap</i>," <i>TPJ</i>, July 2004.</p>

<p>Barry, Paul. "Who's Doing What? Analyzing Ethernet LAN Traffic," <i>The Perl Review</i>, Volume 0, Issue 6, November 2002.</p>

<p>Barry, Paul. <i>Programming the Network with Perl</i>, Wiley, 2002, ISBN 0471486701.</p>


<p><b>TPJ</b></p>



	
	
<br>
<br>
<b><a name="l1"></a>Listing 1</b><br>
<pre>#! /usr/bin/perl -w

use strict;

# Use the correct set of modules.
use Net::PcapUtils;
use NetPacket::Ethernet;
use NetPacket::IP;
use NetPacket::TCP;

use constant TRUE =&gt; 1;

use constant CAPTURE_FILTER =&gt; '(dst 127.0.0.1) &amp;&amp; (tcp[13] &amp; 2 != 0)'; 
use constant CAPTURE_DEVICE =&gt; 'eth0';

# Open the network card for capturing.
my $pkt_descriptor = Net::PcapUtils::open( 
                          FILTER =&gt; CAPTURE_FILTER,
                          DEV    =&gt; CAPTURE_DEVICE 
                  );

# Check that the card "opened" OK.
if ( !ref( $pkt_descriptor ) )
{
  print "Net::PcapUtils::open returned: $pkt_descriptor\n";
  exit;
}

while( TRUE )    # i.e., forever, or until "killed" ... 
{
  # Capture a packet from the network card.
  my ( $packet, %header ) = Net::PcapUtils::next( $pkt_descriptor );

  # Process the captured packet.
  syn_packets( $packet );
}

sub syn_packets {
  my $packet = shift;

  # Strip Ethernet encapsulation of captured packet.
  my $ether_data = NetPacket::Ethernet::strip($packet);

  # Decode contents of TCP/IP packet contained within 
  # captured Ethernet packet.
  my $ip = NetPacket::IP-&gt;decode($ether_data);
  my $tcp = NetPacket::TCP-&gt;decode($ip-&gt;{'data'});

  # Print all out where its coming from and where its 
  # going to!
  print $ip-&gt;{'src_ip'}, ":", $tcp-&gt;{'src_port'}, " -&gt; ",
          $ip-&gt;{'dest_ip'}, ":", $tcp-&gt;{'dest_port'}, "\n";
}</pre>
<a href="#rl1">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l2"></a>Listing 2</b><br>
<pre>#! /usr/bin/perl -w

use Net::PcapUtils;
use NetPacket::Ethernet qw( :strip );
use NetPacket::IP qw( :strip );
use NetPacket::TCP;
use strict;

use constant TRUE =&gt; 1;

use constant CAPTURE_FILTER =&gt; '(tcp port 8080)'; 
use constant CAPTURE_DEVICE =&gt; 'eth0';
use constant CAPTURE_AMOUNT =&gt; 1500;

my $pkt_descriptor = Net::PcapUtils::open( 
                          FILTER  =&gt; CAPTURE_FILTER,
                          DEV     =&gt; CAPTURE_DEVICE,
                          SNAPLEN =&gt; CAPTURE_AMOUNT
                  );

if ( !ref( $pkt_descriptor ) )
{
  print "Net::PcapUtils::open returned: 
                      $pkt_descriptor\n";
  exit;
}

while( TRUE )
{
  my ( $packet, %header ) = 
            Net::PcapUtils::next( $pkt_descriptor );

  process_packet( $packet );
}

sub process_packet {
  my $packet = shift;

  my $tcp = NetPacket::TCP-&gt;decode( 
       ip_strip ( eth_strip ( $packet ) ) 
     );

  print $tcp-&gt;{ data };
}</pre>
<a href="#rl2">Back to article</a><br>
	
	
	


</body>
</html>