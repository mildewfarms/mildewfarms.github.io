
<html>
<head>
<title>March, 2005: Creating Self-Contained Perl Executables, Part I</title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; The Perl Journal-->

<h1>Creating Self-Contained Perl Executables, Part I</h1>
<p><i>The Perl Journal</i> March, 2005</p>
<h2></h2>


<h3>By Julius C. Duque</h3>


<I>Julius is a systems administrator for Ayala Systems Technology. He can be contacted at jcduque@lycos.com.</I>

<hr>





<p>Being a Perl programmer, one of my biggest frustrations with using Windows is the absence of a Perl interpreter. On many occasions, I had to install ActivePerl (http://www .activestate.com/) just so I could use my Perl scripts. A number of times, I have asked myself, "Wouldn't it be nice to have a Perl script compiler to convert my scripts into self-contained executables so that I won't have to worry about a Perl interpreter?" Fortunately for me, I discovered <i>LibZip</i>.</p>

<p>This month, I'll discuss <i>LibZip</i>. Next month, I'll follow up with a discussion of PAR, the Perl Archive Toolkit, which is another way to create Perl executables. Obviously, you'll need a working C compiler to convert your Perl scripts into self-contained executables, as discussed here.</p>
<h3><i>LibZip</i> </h3>

<p><i>LibZip</i> is the brainchild of GracilianoMonteiroPassos and is available from http://search.cpan.org/CPAN/authors/id/G/GM/ GMPASSOS. In Graciliano's own words, the goal of <i>LibZip</i> is to "create very low weight executables" out of Perl scripts. To achieve this, <i>LibZip</i> bundles all modules needed by a Perl script into one big file, called lib.zip, and creates a native code equivalent of the script. When the native code is run, it uncompresses lib.zip into a temporary folder and uses the files in this folder, thereby eliminating the need for a Perl interpreter. The downside, of course, is that the native code won't run without lib.zip.</p>
<h3>Using <i>LibZip</i> </h3>

<p><i>LibZip</i> needs three other modules before it can be useful: <i>Pod::Stripper</i>, <i>Compress::Zlib</i>, and <i>Archive::Zip</i>, all available from CPAN. Assuming you have successfully installed <i>LibZip</i>, using it is a piece of cake. To use <i>LibZip</i>, follow these simple steps. Let's convert dos2unix.pl (<a name="rl1"></a><a href="#l1">Listing 1</a>) into a Windows native executable, dos2unix.exe:</p>



<ol>
  <li>Scan all modules that dos2unix.pl needs, including dependencies:</li>


<blockquote>
<pre>
perl -MLibZip::Scan dos2unix.pl
</pre>
</blockquote>

    <li>	This will build libzip.modules. <i>LibZip</i> will use this file to create the EXE file in the next step.</li>
  <li>Build the library, lib.zip, and executable, dos2unix.exe:</li>
</ol>
<blockquote>
<pre>
libzip -o dos2unix.pl -lzw -striplib
</pre>
</blockquote>




<p>The <i>-o</i> option tells the batch file, libzip, to create an EXE file. Note also that the name of the Perl script must immediately follow the option <i>-o</i>. The <i>-lzw</i> option tells libzip to use LZW compression. An interesting benefit of compression, albeit a trivial one, is that you can now obfuscate your code with it! -<i>striplib</i> tells libzip to remove PODs from lib.zip.</p>

<p>That's it! One more thing, though. After the last step, <i>LibZip</i> will import  perl56.dll and  perl58.dll from your Perl interpreter's installation folder into the folder where you created lib.zip and dos2unix.exe. So, go ahead, run your shiny new executable! Rename your Perl installation folder to something else and see if dos2unix.exe will run without a Perl interpreter.</p>
<h3>A Word Of Warning</h3>

<p>I usually use diagnostics in my scripts to make debugging easier. But, for some reason, I can't get a functioning executable (both in Windows and Linux) if I use diagnostics. Not using diagnostics solves the problem.</p>
<h3>Compiling <i>Perl/Tk</i> Scripts</h3>

<p>Compiling console Perl scripts are straightforward, but compiling <i>Perl/Tk</i> scripts is a bit problematic. If you compile perl-tk.pl (<a name="rl2"></a><a href="#l2">Listing 2</a>), for example:</p>

<blockquote>
<pre>
perl -MLibZip::Scan perl-tk.pl

libzip -o perl-tk.pl -lzw -striplib

</pre>
</blockquote>

<p>the generated executable won't run. Apparently, <i>Tk</i> performs some magic that <i>LibZip</i> fails to see. Specifically, utf8_heavy.pl and the whole unicore directory were not included in libzip.modules during the scan. So when  lib.zip was finally built, some files were missing. The fix, therefore, is to edit libzip.modules manually and add utf8_heavy.pl and unicore. Recompiling perl-tk.pl should now be a breeze.</p>

<p>You may also use the <i>-gui</i> option, aside from <i>-lzw</i> and <i>-striplib</i>,  to create a nonconsole executable.</p>

<blockquote>
<pre>
libzip -o perl-tk.pl -lzw -striplib -gui

</pre>
</blockquote>



<p>You are encouraged to use <i>-gui</i> if you're compiling a <i>Perl/Tk</i> script.</p>
<h3>UPX </h3>

<p><i>LibZip</i> can also use what is known as UPX, short for "Ultimate Packer for executables" to compress lib.zip and executables even more. UPX's inventors, Markus F.X.J. Oberhumer and L&aacute;szl&oacute; Moln&aacute;r, claim that UPX is a "free, portable, extendable, high-performance executable packer for several different executable formats. It achieves an excellent compression ratio and offers very fast decompression." http://upx.sourceforge.net/ is UPX's official web site where you'll find versions of UPX for different platforms.</p>

<p>Using UPX within <i>LibZip</i> is also painless:</p>

<blockquote>
<pre>
libzip -o perl-tk.pl -lzw -striplib -gui -upx best  				       -upxlib best

</pre>
</blockquote>



<p>Option <i>-upx</i> will compress the executable, while <i>-upxlib</i> will compress lib.zip. The <i>best</i> argument to <i>-upx</i> and <i>-upxlib</i> tells libzip to call UPX using the best possible compression.</p>

<p>Because UPX is also a standalone program, you may also use it independently from <i>LibZip</i>. For example, you may type Step 2 as:</p>

<blockquote>
<pre>
libzip -o perl-tk.pl -lzw -striplib -gui

</pre>
</blockquote>



<p>and then follow it up with:</p>

<blockquote>
<pre>
upx &#151;best perl-tk.exe perl56.dll perl58.dll

</pre>
</blockquote>
<h3>perlcc&#151;Perl's Own Compiler </h3>

<p>How about Perl's own compiler, perlcc? Based from my personal experience, I have yet to produce a working executable that is compiled with perlcc. Indeed, the Perl documentation says that perlcc's output is not guaranteed to work. The man page further says "the whole codegen suite (perlcc included) should be considered very experimental. Use for production purposes is strongly discouraged." Aside from that, perlcc takes several minutes to compile even a simple script.</p>
<h3>Compiling Scripts for Linux </h3>

<p>You may ask, "Can LibZip also create executables for Linux?" The answer is a resounding "Yes." The steps are the same, but without perl56.dll and perl58.dll being generated (for the obvious reason).</p>
<h3><i>PAR</i>&#151;Another Executable Maker </h3>

<p>What if you dislike having to bundle your executables with a separate library and prefer, instead, to have just a single executable? The <i>LibZip</i> man page mentions a similar tool, <i>PAR</i>, or the Perl Archive Toolkit, from Autrijus Tang. I'll discuss <i>PAR</i> next month.</p>


<p><b></b></p>
<p><b>TPJ</b></p>



	
	
<br>
<br>
<b><a name="l1"></a>Listing 1</b><br>
<pre>#!/usr/local/bin/perl

# Julius C. Duque

use strict;
use warnings;
use Getopt::Long;

my ($format, $help) = ();

GetOptions(
    "format=s"  =&gt; \$format,   # =s -&gt; takes mandatory string argument
    "help"      =&gt; \$help      # optional switch
);

if (!$format or $help) {
    if ($^O eq "MSWin32") {
        $0 =~ s/.*\\//g;    # Windows
    } else {
        $0 =~ s/.*\///g;
    }

    print "Usage: $0 --format=unix file1 [file2] [file3] ...\n";
    print "       $0 -f=unix file1 [file2] [file3] ...\n";
    print "\n";
    print "       $0 --format=dos file1 [file2] [file3] ...\n";
    print "       $0 -f=dos file1 [file2] [file3] ...\n";
    print "\nOriginal file(s) will be overwritten\n";
    exit 1;
}

foreach my $infile (@ARGV) {
    print "Converting $infile to $format format...\n";
    open INFILE, $infile;
    open OUTFILE, "&gt;temp.$infile";
    binmode INFILE;
    binmode OUTFILE;
    while (&lt;INFILE&gt;) {
        $_ =~ s/\015\012/\012/g if ($format =~ /u/);
        $_ =~ s/\012/\015\012/g if ($format =~ /d/);
        print OUTFILE;
    }

    close INFILE;
    close OUTFILE;
    rename "temp.$infile", "$infile";
}</pre>
<a href="#rl1">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l2"></a>Listing 2</b><br>
<pre>#!/usr/local/bin/perl

use strict;
use warnings;
use Tk;
use Tk::Balloon;

my $INDENT_DEF = 0;
my $LWIDTH_DEF = 70;
my $VERSION = "1.4.3";
my $TITLE = "Perl/Tk Example $VERSION";
my $AUTHOR = "Julius C. Duque";
my $indent = $INDENT_DEF;
my $newline = 1;
my $hyphenate = 1;
my $width = $LWIDTH_DEF;
my ($BOTH, $LEFT, $RIGHT, $CENTERED) = (1, 2, 3, 4);
my $format_choice = $BOTH;
my ($infile, $outfile) = ();

my $mw = new MainWindow();
drawButtons();
Tk::MainLoop();

sub processfile
{
  open INFILE, $infile;
  open OUTFILE, "&gt; $outfile";
  while (&lt;INFILE&gt;) {
    print OUTFILE;
  }

  close INFILE;
  close OUTFILE;

  printMessage("info", "OK", "File was successfully saved.");
}

sub drawButtons
{
  $mw-&gt;title($TITLE);

  # Status bar widget
  my $status = $mw-&gt;Label(-width =&gt; 70, -relief =&gt; "sunken",
    -anchor =&gt; "w")-&gt;pack(-side =&gt; "bottom", -padx =&gt; 1, -pady =&gt; 1,
    -fill =&gt; "x");

  # Create balloon widget
  my $b = $mw-&gt;Balloon(-statusbar =&gt; $status);

  # Create menu bar frame
  my $menubar = $mw-&gt;Frame(-borderwidth =&gt; 4, -relief =&gt; "ridge")-&gt;
    pack(-side =&gt; "top", -fill =&gt; "x"); 

  # Create Open File button
  my $openfilebutton = $menubar-&gt;
    Button(-text =&gt; "Open File", -relief =&gt; "raised", -width =&gt; 10,
      -command =&gt; [\&amp;fileDialog, $mw, "open"])-&gt;pack(-side =&gt; "left");

  $b-&gt;attach($openfilebutton, -msg =&gt; "Open a file");

  # Create Save File button
  my $savefilebutton = $menubar-&gt;
    Button(-text =&gt; "Save To File", -relief =&gt; "raised", -width =&gt; 10,
      -command =&gt; sub {
          if (defined $infile and $infile ne "") {
            fileDialog($mw, "save");
          } else {
              printMessage("warning", "OK",
                "You must open a file first");
          }
        })-&gt;pack(-side =&gt; "left");

  $b-&gt;attach($savefilebutton,
    -msg =&gt; "Proceed with saving a file");

  # Create About button
  my $aboutbutton = $menubar-&gt;Button(-text =&gt; "About",
    -relief =&gt; "raised", -width =&gt; 10,
    -command =&gt; [\&amp;printMessage, "info", "OK",
      "A Perl/Tk script created by $AUTHOR"])-&gt;pack(-side =&gt; "left");

  $b-&gt;attach($aboutbutton,
    -msg =&gt; "$TITLE created by $AUTHOR");

  # Create Quit button
  my $quitbutton = $menubar-&gt;Button(-text =&gt; "Dismiss",
    -relief =&gt; "raised", -width =&gt; 10, -command =&gt; sub { exit })-&gt;
    pack(-side =&gt; "right");

  $b-&gt;attach($quitbutton, -msg =&gt; "Quit this Perl/Tk script");

  my $both = $mw-&gt;Radiobutton(-variable =&gt; \$format_choice,
    -value =&gt; $BOTH, -text =&gt; "Radio Button 1")-&gt;
    pack(-side =&gt; "top", -anchor =&gt; "w");

  $b-&gt;attach($both, -msg =&gt; "Radio Button 1");

  my $left = $mw-&gt;Radiobutton(-variable =&gt; \$format_choice,
    -value =&gt; $LEFT, -text =&gt; "Radio Button 2")-&gt;pack(-side =&gt; "top",
    -anchor =&gt; "w");

  $b-&gt;attach($left, -msg =&gt; "Radio Button 2");

  my $right = $mw-&gt;Radiobutton(-variable =&gt; \$format_choice,
    -value =&gt; $RIGHT, -text =&gt; "Radio Button 3")-&gt;pack(-side =&gt; "top",
    -anchor =&gt; "w");

  $b-&gt;attach($right, -msg =&gt; "Radio Button 3");

  my $centered = $mw-&gt;Radiobutton(-variable =&gt; \$format_choice,
    -value =&gt; $CENTERED, -text =&gt; "Radio Button 4")-&gt;pack(-side =&gt; "top",
    -anchor =&gt; "w");

  $b-&gt;attach($centered, -msg =&gt; "Radio Button 4");

  $both-&gt;select;   # Set default to $both

  my $chknewline = $mw-&gt;Checkbutton(-variable =&gt; \$newline,
    -text =&gt; "Check Box 1")-&gt;
    pack(-side =&gt; "top", -anchor =&gt; "w");

  $b-&gt;attach($chknewline,
    -msg =&gt; "Check Box 2");

  $chknewline-&gt;select;   # Set default to $newline

  my $chkhyphen = $mw-&gt;Checkbutton(-variable =&gt; \$hyphenate,
    -text =&gt; "Check Box 2")-&gt;pack(-side =&gt; "top", -anchor =&gt; "w");

  $b-&gt;attach($chkhyphen, -msg =&gt; "Check Button 2");

  $chknewline-&gt;select;   # Set default to $hyphenate

  my $f = $mw-&gt;Frame-&gt;pack(-side =&gt; "left");

  my $l = $f-&gt;Label(-text =&gt; "Indention: ", -justify =&gt; "left");

  $b-&gt;attach($l, -msg =&gt; "Blah blah blah...");

  Tk::grid($l, -row =&gt; 0, -column =&gt; 0);

  my $tindent = $f-&gt;Entry(-width =&gt; 2, -textvariable =&gt; \$indent,
    -justify =&gt; "right");

  $b-&gt;attach($tindent,
    -msg =&gt; "Number of spaces at the start of every paragraph");

  Tk::grid($tindent, -row =&gt; 0, -column =&gt; 1);

  $l = $f-&gt;Label(-text =&gt; "characters (default: $INDENT_DEF) ",
    -justify =&gt; "left");

  $b-&gt;attach($l,
    -msg =&gt; "Number of spaces at the beginning of each paragraph");

  Tk::grid($l, -row =&gt; 0, -column =&gt; 2);

  $l = $f-&gt;Label(-text =&gt; "Line width: ", -justify =&gt; "left");
  Tk::grid($l, -row =&gt; 1, -column =&gt; 0);
  $b-&gt;attach($l, -msg =&gt; "Maximum length of every line");

  my $tlwidth = $f-&gt;Entry(-width =&gt; 2, -textvariable =&gt; \$width,
    -justify =&gt; "right");

  $b-&gt;attach($tlwidth, -msg =&gt; "Maximum length of every line");
  Tk::grid($tlwidth, -row =&gt; 1, -column =&gt; 1);

  $l = $f-&gt;Label(-text =&gt; "characters (default: $LWIDTH_DEF)",
    -justify =&gt; "left");

  $b-&gt;attach($l, -msg =&gt; "Maximum length of every line");
  Tk::grid($l, -row =&gt; 1, -column =&gt; 2);
}

sub printMessage
{
  my ($icon, $type, $outputmsg) = @_;
  my $msg = $mw-&gt;messageBox(-icon =&gt; $icon, -type =&gt; $type,
  -title =&gt; $TITLE, -message =&gt; $outputmsg);
}

sub fileDialog {
  my ($w, $operation) = @_;
  my @types = (["Text files", [qw/.txt .doc/]],
    ["Text files", "", "TEXT"],
    ["All files", "*"]
  );

  if ($operation eq "open") {
    $infile = $w-&gt;getOpenFile(-filetypes =&gt; \@types);
  }

  if ($operation eq "save") {
      $outfile = $w-&gt;getSaveFile(-filetypes =&gt; \@types,
        -initialfile =&gt; "Untitled",
        -defaultextension =&gt; ".txt");

    processfile() if (defined $outfile and $outfile ne "");
  }
}</pre>
<a href="#rl2">Back to article</a><br>
	
	
	


</body>
</html>