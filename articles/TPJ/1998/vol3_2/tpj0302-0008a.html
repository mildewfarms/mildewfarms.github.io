<html>
<head>
  <title>Listing 1: T-Bond.pl: Controlling OLE objects - The Perl Journal, Summer 1998</title>
  <meta name="generator" content=
  "HTML Tidy for Linux/x86 (vers 12 April 2005), see www.w3.org">
  <meta name="vscategory" content="Perl">
  <meta name="vsisbn" content="">
  <meta name="vstitle" content="Win32::OLE - Listing 1">
  <meta name="vsauthor" content="Jan Dubois">
  <meta name="searchdescription" content="">
  <meta name="vspublisher" content="Earthweb">
  <meta name="vsimprint" content="The Perl Journal">
  <meta name="vspubdate" content="Summer 1998">
  <!-- update listing #, listing title, and issue  -->

</head>

<body bgcolor="#FFFFFF" text="#000000" link="#DD0000" vlink=
"#DD0000" alink="#DD0000" leftmargin="0" topmargin="0" marginwidth=
"0" marginheight="0">
  <!-- update listing #, listing title, and issue throughout the table -->

  <table cellspacing="0" cellpadding="5" width="100%" border="0">
    <tr align="left" valign="top">
      <td bgcolor="#CCCC99">
        <table width="100%">
          <tr>
            <td valign="top" bgcolor="#CCCC99" nowrap><font face=
            "Arial,Helvetica" size="+1">Listing 1.</font><br>
            <font size="-1" face="Arial,Helvetica" color=
            "#663300"><b>T-Bond.pl: Controlling OLE
            objects</b></font><br>
            <font size="-1" face="Arial,Helvetica" color=
            "#663300"><i>Jan Dubois</i></font></td>

            <td valign="top" bgcolor="#CCCC99"><font size="-1"
            face="Arial,Helvetica" color="#663300">Win32::OLE<br>
            The Perl Journal, Summer 1998</font></td>
          </tr>
        </table>
      </td>
    </tr>

    <tr>
      <td>
        <table width="100%">
          <tr>
            <td width="5%" bgcolor="#FFFFFF">&nbsp;</td>

            <td bgcolor="#FFFFFF">
              <font color="#000000">
              <!-- insert code listing or table here -->
              <b>T-Bond.pl: Controlling OLE objects</b></font>
              <pre>
<font color="#000000">use strict;
$| = 1;

my $Contract = 'us9h';
use LWP::Simple;
my $URL = 'http://www.cbot.com/mplex/quotes/tsfut';
my $text = get("$URL/tsf$Contract.htm");

my ($Day,$Time,$hhmm,$Open,$High,$Low,$Close,@Bars);
foreach (split "\n", $text) {
    # 03/12/1998 US 98Mar 12116 15:28:34 Open
    my ($Date,$Price,$Hour,$Min,$Sec,$Ind) =
        m|^\s*(\d+/\d+/\d+) # " 03/12/1998"
           \s+US\s+\S+\s+(\d+) # " US 98Mar 12116"
           \s+(\d+):(\d+):(\d+) # " 12:42:40"
           \s*(.*)$|x; # " Ask"
    next unless defined $Date;
    $Day = $Date;
        
    # Convert from fractional to decimal format
    $Price = int($Price/100) + ($Price%100)/32;
    # Round up time to next multiple of 15 minutes
    my $NewTime = int(($Sec+$Min*60+$Hour*3600)/900+1)*900;
    unless (defined $Time &amp;&amp; $NewTime == $Time) {
        push @Bars, [$hhmm, $Open, $High, $Low, $Close]
                                    if defined $Time;
        $Open = $High = $Low = $Close = undef;
        $Time = $NewTime;
        my $Hour = int($Time/3600);
        $hhmm = sprintf "%02d:%02d", $Hour, $Time/60-$Hour*60;
    }
    # Update 15 minute bar values
    $Close = $Price;
    $Open = $Price unless defined $Open;
    $High = $Price unless defined $High &amp;&amp; $High &gt; $Price;
    $Low = $Price unless defined $Low &amp;&amp; $Low &lt; $Price;
}

die "No data found" unless defined $Time;
push @Bars, [$hhmm, $Open, $High, $Low, $Close];

<b># Start Excel and create new workbook with a single sheet</b>
use Win32::OLE qw(in valof with);
use Win32::OLE::Const 'Microsoft Excel';
use Win32::OLE::NLS qw(:DEFAULT :LANG :SUBLANG);

my $lgid = MAKELANGID(LANG_ENGLISH, SUBLANG_DEFAULT);
$Win32::OLE::LCID = MAKELCID($lgid);
$Win32::OLE::Warn = 3;

my $Excel = Win32::OLE-&gt;new('Excel.Application', 'Quit');
$Excel-&gt;{SheetsInNewWorkbook} = 1;
my $Book = $Excel-&gt;Workbooks-&gt;Add;
my $Sheet = $Book-&gt;Worksheets(1);
$Sheet-&gt;{Name} = 'Candle';

# Insert column titles
my $Range = $Sheet-&gt;Range("A1:E1");
$Range-&gt;{Value} = [qw(Time Open High Low Close)];
$Range-&gt;Font-&gt;{Bold} = 1;

$Sheet-&gt;Columns("A:A")-&gt;{NumberFormat} = "h:mm";
# Open/High/Low/Close to be displayed in 32nds
$Sheet-&gt;Columns("B:E")-&gt;{NumberFormat} = "# ?/32";

# Add 15 minute data to spreadsheet
$Range = $Sheet-&gt;Range(sprintf "A2:E%d", 2+$#Bars);
$Range-&gt;{Value} = \@Bars;

# Create candle stick chart as new object on worksheet
$Sheet-&gt;Range("A:E")-&gt;Select;
my $Chart = $Book-&gt;Charts-&gt;Add;
$Chart-&gt;{ChartType} = xlStockOHLC;
$Chart-&gt;Location(xlLocationAsObject, $Sheet-&gt;{Name});
$Chart = $Excel-&gt;ActiveChart;

# Add title and remove legend
with($Chart, HasLegend =&gt; 0, HasTitle =&gt; 1);
$Chart-&gt;ChartTitle-&gt;Characters-&gt;{Text} = "US T-Bond";

# Setup daily statistics
$Open  = $Bars[0][1];
$High  = $Sheet-&gt;Evaluate("MAX(C:C)");
$Low   = $Sheet-&gt;Evaluate("MIN(D:D)");
$Close = $Bars[$#Bars][4];

# Change ticks from decimals to fractions
with($Chart-&gt;Axes(xlValue),
    HasMajorGridlines =&gt; 1,
    HasMinorGridlines =&gt; 1,
    MajorUnit =&gt; 1/8,
    MinorUnit =&gt; 1/16,
    MinimumScale =&gt; int($Low*16)/16,
    MaximumScale =&gt; int($High*16+1)/16,
);

# Fat candles with only 5% gaps
$Chart-&gt;ChartGroups(1)-&gt;{GapWidth} = 5;

sub RGB { $_[0] | ($_[1] &lt;&lt; 8) | ($_[2] &lt;&lt; 16) }

# White background with a solid border
$Chart-&gt;PlotArea-&gt;Border-&gt;{LineStyle} = xlContinuous;
$Chart-&gt;PlotArea-&gt;Border-&gt;{Color} = RGB(0,0,0);
$Chart-&gt;PlotArea-&gt;Interior-&gt;{Color} = RGB(255,255,255);

# Add 1 hour moving average of the Close series
my $MovAvg = $Chart-&gt;SeriesCollection(4)-&gt;Trendlines
         -&gt;Add({Type =&gt; xlMovingAvg, Period =&gt; 4});
$MovAvg-&gt;Border-&gt;{Color} = RGB(255,0,0);

# Save workbook to file
my $Filename = 'i:\tmp\tpj\data.xls';
unlink $Filename if -f $Filename;
$Book-&gt;SaveAs($Filename);
$Book-&gt;Close;

<b># Start ActiveX Data Objects and update the database</b>

use Win32::OLE::Const 'Microsoft ActiveX Data Objects';

my $Connection = Win32::OLE-&gt;new('ADODB.Connection');
my $Recordset = Win32::OLE-&gt;new('ADODB.Recordset');
$Connection-&gt;Open('T-Bonds');

{     # Open a recordset for table of this contract
    local $Win32::OLE::Warn = 0;
   $Recordset-&gt;Open($Contract, $Connection, adOpenKeyset,
         adLockOptimistic, adCmdTable);
}
# Create table and index if it doesn't exist yet
if (Win32::OLE-&gt;LastError) {
    $Connection-&gt;Execute(&lt;&lt;"SQL");
        CREATE TABLE $Contract
(
           Day DATETIME,
  Open DOUBLE, High DOUBLE, Low DOUBLE, Close DOUBLE
)
SQL
    $Connection-&gt;Execute(&lt;&lt;"SQL");
        CREATE INDEX $Contract
        ON $Contract (Day) WITH PRIMARY
SQL
    $Recordset-&gt;Open($Contract, $Connection, adOpenKeyset,
        adLockOptimistic, adCmdTable);
}

# Add new record to table
use Win32::OLE::Variant;
$Win32::OLE::Variant::LCID = $Win32::OLE::LCID;

my $Fields = [qw(Day Open High Low Close)];
my $Values = [Variant(VT_DATE, $Day),
      $Open, $High, $Low, $Close];
{
   local $Win32::OLE::Warn = 0;
   $Recordset-&gt;AddNew($Fields, $Values);
}

# Replace existing record
if (Win32::OLE-&gt;LastError) {
    $Recordset-&gt;CancelUpdate;
    $Recordset-&gt;Close;
    $Recordset-&gt;Open(&lt;&lt;"SQL", $Connection, adOpenDynamic);
        SELECT * FROM $Contract WHERE Day = #$Day#
SQL
    $Recordset-&gt;Update($Fields, $Values);
}

$Recordset-&gt;Close;
$Connection-&gt;Close;

<b># Send mail to the customer using Lotus Notes</b>
sub EMBED_ATTACHMENT {1454;}

my $Notes = Win32::OLE-&gt;new('Notes.NotesSession');
my $Database = $Notes-&gt;GetDatabase('', '');
$Database-&gt;OpenMail;
my $Document = $Database-&gt;CreateDocument;

$Document-&gt;{Form} = 'Memo';
$Document-&gt;{SendTo} = ['Jon Orwant &lt;orwant@media.mit.edu&gt;',
                          'Jan Dubois &lt;jan.dubois@ibm.net&gt;'];
$Document-&gt;{Subject} = "US T-Bonds Chart for $Day";

my $Body = $Document-&gt;CreateRichtextItem('Body');
$Body-&gt;AppendText(&lt;&lt;"EOT");
I\'ve attached the latest US T-Bond data and chart for $Day.
The daily statistics were:

\tOpen\t$Open
\tHigh\t$High
\tLow\t$Low
\tClose\t$Close

Kind regards,
Mary

EOT
$Body-&gt;EmbedObject(EMBED_ATTACHMENT, '', $Filename);
$Document-&gt;Send(0);
</font>
</pre><!--  end code listing  -->
            </td>
          </tr>
        </table>
      </td>
    </tr>
  </table>
</body>
</html>
