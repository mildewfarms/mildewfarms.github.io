<html>
<head>
  <title>Listing 2: How fast is OO? - The Perl Journal, Spring 1999</title>
  <meta name="generator" content=
  "HTML Tidy for Linux/x86 (vers 12 April 2005), see www.w3.org">
  <meta name="vscategory" content="Perl">
  <meta name="vsisbn" content="">
  <meta name="vstitle" content=
  "The rezrov Infocom Game Interpreter - Listing 2">
  <meta name="vsauthor" content="Michael Edmonson">
  <meta name="searchdescription" content="">
  <meta name="vsimprint" content="The Perl Journal">
  <meta name="vspublisher" content="Earthweb">
  <meta name="vspubdate" content="Spring 1999">

</head>

<body bgcolor="#FFFFFF" text="#000000" link="#DD0000" vlink=
"#DD0000" alink="#DD0000" leftmargin="0" topmargin="0" marginwidth=
"0" marginheight="0">
  <table cellspacing="0" cellpadding="5" width="100%" height="100%"
  border="0">
    <tr align="left" valign="top">
      <td bgcolor="#CCCC99">
        <table width="98%">
          <tr>
            <td valign="top" bgcolor="#CCCC99" nowrap><font face=
            "Arial,Helvetica" size="+1">Listing 2.</font><br>
            <font size="-1" face="Arial,Helvetica" color=
            "#663300"><b>How fast is OO?</b></font><br>
            <font size="-1" face="Arial,Helvetica" color=
            "#663300"><i>Michael Edmonson</i></font></td>

            <td valign="top" bgcolor="#CCCC99"><font size="-1"
            face="Arial,Helvetica" color="#663300">"The rezrov
            Infocom Game Interpreter"<br>
            The Perl Journal, Spring 1999</font></td>
          </tr>
        </table>
      </td>
    </tr>

    <tr>
      <td>
        <table width="98%">
          <tr>
            <td width="5%" bgcolor="#FFFFFF">&nbsp;</td>

            <td bgcolor="#FFFFFF">
              <pre>
<font color="#000000">#!/usr/bin/perl -w
#
# Performance comparison for lots of calls to a small sub:
# OO of varying degrees, static functions, and inlined.
#
use strict;
use Benchmark;

$Bogus::DATA = "";

my $COUNT = shift || 100000;

printf "timing %d iterations; initializing...\n", $COUNT;
for (my $i=0; $i &lt; $COUNT; $i++) {
     vec($Bogus::DATA, $i, 8) = 42;
}

package Bogus;

sub new {
    my $self = bless {}, shift;
    $self-&gt;{"data_ref"} = \$Bogus::DATA;
    return $self;
}
sub data_ref {
    return $_[0]-&gt;{"data_ref"};
}
sub get_byte_superverbose_instance_data {
    my ($self, $where) = @_;
    return vec(${$self-&gt;data_ref()}, $where, 8);
}
sub get_byte_verbose_instance_data {
    my ($self, $where) = @_;
    return vec(${$self-&gt;{"data_ref"}}, $where, 8);
}
sub get_byte_terse_instance_data {
    return vec(${$_[0]-&gt;{"data_ref"}}, $_[1], 8);
}
sub get_byte_terse_static {
    return vec($Bogus::DATA, $_[1], 8);
}
sub get_byte_verbose_static {
    my ($self, $where) = @_;
    return vec($Bogus::DATA, $where, 8);
}

package main;

sub get_byte_main_terse_static {
    return vec($Bogus::DATA, $_[0], 8);
}
sub get_byte_main_verbose_static {
    my ($where) = @_;
    return vec($Bogus::DATA, $where, 8);
}

$main::bogus = $main::bogus = new Bogus(); # shut up -w

timethis($COUNT,
  '$x = $main::bogus-&gt;get_byte_superverbose_instance_data(0)',
  'superverbose instance OO');
timethis($COUNT,
     '$x = $main::bogus-&gt;get_byte_verbose_instance_data(0)', 
     'verbose instance OO');
timethis($COUNT,
     '$x = $main::bogus-&gt;get_byte_terse_instance_data(0)',
     'terse instance OO');
timethis($COUNT,
    '$x = $main::bogus-&gt;get_byte_verbose_static(0)',
    'verbose static OO');
timethis($COUNT,
    '$x = $main::bogus-&gt;get_byte_terse_static(0)',
    'terse static OO');
timethis($COUNT,
    '$x = main::get_byte_main_verbose_static(0)',
    'verbose static call');
timethis($COUNT,
    '$x = main::get_byte_main_terse_static(0)',
    'terse static call');
timethis($COUNT,
    '$x = vec($Bogus::DATA, 0, 8)',
    'fully inlined');</font>
</pre>
            </td>
          </tr>
        </table>
      </td>
    </tr>
  </table>
</body>
</html>
