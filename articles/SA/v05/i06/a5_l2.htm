<!DOCTYPE HTML PUBLIC "-//IETF//DTD HTML 4.0//EN">


<HTML>
<HEAD>
<TITLE><title>v05, i06: Listing 2: Reflection sign-on script</title></TITLE>
<LINK REL=StyleSheet HREF="../../resource/css/sacdrom.css" TYPE="text/css" TITLE="CSS1">
</HEAD>

<body bgcolor="#ffffff" text="#000000" link="#990000" alink="#669999" vlink="#333366" topmargin=0 leftmargin=0>

<! -- Begin MASTER TABLE -- >
<!center><table width=600 cellpadding=0 cellspacing=0 border=0 bgcolor="#ffffff">

<tr>

<table cellpadding=5 cellspacing=0 border=0>
	<tr>

		<td><span class=navbarLink><a href="a5.htm">Article</a></span></td>
		<td><span class=navbarLink><a href="a5_f1.htm">Figure 1</a></span></td>
		<td><span class=navbarLink><a href="a5_l1.htm">Listing 1</a></span></td>
		<td><span class=navbarLink>Listing 2</span></td>
		<td><span class=navbarLink><a href="a5_l3.htm">Listing 3</a></span></td>
	</tr>
	<tr>
		<td><span class=navbarLink><a href="a5_s1.htm">Sidebar 1</a></span></td>
		<td><span class=navbarLink><a href="a5_s2.htm">Sidebar 2</a></span></td>
		<td><a href="../../../../source/SA/1996/jun96.tar"><b class=codeListing>jun96.tar</b></a></td>

	</tr>
</table>


</tr>
<tr>
<! -- Begin Content ------ >
<td valign=top width=440 bgcolor="#ffffff">
<table width=100% cellpadding=15 cellspacing=0 border=0>
<tr><td valign=top>
<H1>Listing 2: Reflection sign-on script</H1><p>
<PRE>
;==========================================================
; HP3000.RCL - Version 1.1 - (c) 1996 by Steve Johnson. -
;   Roncis, Inc. - 909-594-0013
; HP3000 logon script for Reflection for DOS+
; Script uses Novell LAN Workplace for DOS TCP/IP stack
;   (TCPIP.EXE)
; Documentation - Script for scan the NET.CFG file to obtain
;   the workstation's IP address.  If an IP address is
;   found,it is used as the &quot;Username&quot; on the HP3000 Telnet
;   Server Session. The file HP3000.ADR is expected to
;   contain the IP address(es) of the HP3000 host.  Each
;   line of this file should contain an IP address.  The
;   script reads a line from this file and attempts to
;   establish a session with one of the HP3000 Telnet
;   Servers.  If a session is not available, another line
;   is read from the address file, and another session is
;   attempted.
;==========================================================
; Variables - V1 - General message processing
;             V2 - Address file found flag
;             V4 - IP address variable
;             V6 - Host input processing variable
;             V7 - Connected to Telnet Server flag
;==========================================================

let V7 = &quot;NO&quot;

set quiet-command yes
set disconnect-on-exit yes

;===Find Workstation IP Address ===
;---Make sure there is a net.cfg file
if not exist(&quot;c:\\nwclient\\net.cfg&quot;)
let V1 = &quot;NetCfg_Not_Fnd&quot;
goto continue_1
endif
open c:\nwclient\net.cfg input as 2
:read_net_cfg
;---Scan Netware Config file for 'IP_ADDRESS' line
if not eof(2)
read 2 V1
;---Remove excess spaces and convert to uppercase
let V1 = PACK(V1)
let V1 = UPPER(V1)
if mid(V1,2,11) = &quot;IP_ADDRESS&quot;
close 2
;---Strip off statement identifier
let V1 = mid(V1,13,99)
;---Strip off End-of-line characters
let V1 = mid(V1,1,length(V1)-2)
else
;---Didn't find it so get another line
goto read_net_cfg
endif
else
;---Didn't find and IP address, so use a dummy value
close 2
let V1 = &quot;Unknown&quot;
endif

:continue_1
display &quot;Workstation IP Address = &quot; &amp; V1 &amp; &quot;^m^j&quot;
;===End of Workstation IP Address Routine===

if not exist(&quot;HP3000.ADR&quot;)
display &quot;^m^j^j&quot;
display &quot;Your workstation does not have a copy ^m^j&quot;
display &quot;of HP3000 address file, HP3000.ADR.^m^j&quot;
display &quot;Please call Technical Support to obtain a^m^j&quot;
display &quot;copy of this file.  In the meantime, if^m^j&quot;
display &quot;you know the address for the HP3000^m^j&quot;
display &quot; please enter it now.  Otherwise, press ^m^j&quot;
display &quot;&lt;ALT&gt;&lt;X&gt; to exit.^m^j^j&quot;
display &quot;   IP Address&gt; &quot;
accept 0:0:30 V4
if not found
exit
endif
let V2 = &quot;NO&quot;
goto try_address
else
let V2 = &quot;YES&quot;
endif

open HP3000.ADR input as 1

:read_address
if V2 = &quot;NO&quot;
goto no_sessions
endif

if eof(1)
close 1
goto no_sessions
endif

;---Read line from Address File, format=xxx.xxx.xxx.xxx
;---where the xxx.xxx.xxx.xxx is a valid IP address.
read 1 V4
if length(V4) &lt; 5
goto read_address
endif

;---Send connect command with IP address
:try_address
transmit &quot;connect $4^m&quot;

;---Look for password request
wait 0:0:3 for &quot;password&gt; &quot;
;---If not found, disconnect and try again
if not found
display &quot;Session Timeout&quot;
disconnect
transmit &quot;^m&quot;
wait 0:0:5 for &quot;CON-MGR&gt; &quot;
if found
goto read_address
else
goto not_avail
endif
endif

;---Send stock password
transmit &quot;HP3000^m&quot;

:readhost
;---See how the server responds
readhost 10 V6 until &quot; &quot;
;---If nothing is received in 10 seconds,
;--- kill the session and try again
if not found
display &quot;Session Timeout&quot;
disconnect
transmit &quot;^m&quot;
wait 0:0:20 for &quot;CON-MGR&gt; &quot;
goto read_address
else
let V7 = &quot;YES&quot;
;display &quot;****&quot; &amp; V6 &amp; &quot;****^m^j&quot;
;wait 0:0:5
;---Server is requesting workstation ID,
;--- so send IP address found above
if mid(V6,5,12) = &quot;Username&quot;
transmit &quot;$1 ^m&quot;
readhost 2 V6 until &quot; &quot;
;---Loop around to grab next line from server
goto readhost
endif
;---Server is ready to work, goto the
;--- &quot;Connected&quot; Banner display
if mid(V6,5,14) = &quot;Connecting&quot;
goto connected
endif
;---Problem: probably out of sessions,
;--- try again with another server
if mid(V6,5,10) = &quot;%Error&quot;
disconnect
wait 0:0:20 for &quot;CON-MGR&gt; &quot;
goto read_address
endif
;---Catch the unanticipated response, but give
;--- them the session if available
mclear
display &quot;Host response not found^m^j&quot;
display &quot;Please call Tech Support^m^j&quot;
display &quot;***&quot; &amp; V6 &amp; &quot;***^m^j^j^j&quot;
transmit &quot;^m&quot;
stop
endif

:no_sessions
if V7 = &quot;NO&quot;
goto not_avail
endif
mclear
display &quot;All ports on the HP3000 are in use.^m^j&quot;
display &quot;Please try again at a later time.^m^j&quot;
display '^j    To exit press &lt;ALT&gt;&lt;X&gt;^m^j^j'
wait 0:0:30
exit
stop

:not_avail
display &quot;^m^j^j&quot;
display &quot;Unable to attach to the HP3000 due to a^m^j&quot;
display &quot;network problem.  Please call tech support^m^j&quot;
display &quot;for assistance, or try again at a later time.^j&quot;
display '^j         To exit press &lt;ALT&gt;&lt;X&gt;^m^j^j'
wait 0:0:30
exit
stop

:connected
wait 0:0:3 for &quot;~&gt;.&quot;
mclear
display &quot;      Attached to HP3000.^m^j&quot;
display '^jTo sign-off, type &quot;BYE&quot; at the colon prompt,^m^j'
display &quot; then press &lt;ALT&gt;&lt;X&gt;^m^j^j'
transmit &quot;^m&quot;
if V2 = &quot;YES&quot;
close 1
endif
stop
</pre><p>
<p>


<P>
<br>&nbsp;<br>
</table></center></td>
<! -- End Content ------ >

</table><!/center>
<! -- End MASTER TABLE -- >

</body></html>
