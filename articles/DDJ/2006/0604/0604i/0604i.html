
<html>
<head>
<title>April, 2006: Tight Code</title>
</head>

<body BGCOLOR="#ffffff" LINK="#0000ff" VLINK="#330066" ALINK="#ff0000" TEXT="#000000">
<!--Copyright &#169; Dr. Dobb's Journal-->

<p><i>Dr. Dobb's Journal</i>, April 2006</p>
<h1>Tight Code</h1>
<h2></h2>


<h3>By Ed Nisley</h3>


<I>Ed's an EE and author in Poughkeepsie, NY. Contact him at ed.nisley@ieee.org with "Dr Dobbs" in the subject to avoid spam filters.</I>

<hr>




Geeks sometimes have trouble finding appropriate gifts for their loved ones, and alas, I'm no exception. This past Christmas, I decided what Mary <i>really</i> needed was some assistance toward a long-standing goal&#151;organizing our science fiction collection.

<p>So I built her an ISBN bar-code scanner. Perfect gift, huh?</p>

<p>Even if your sweetie doesn't need one, the project serves as a good introduction to the world of small-scale programming. The mission statement seems straightforward: Read a bar-code wand, blink a few LEDs, and send a dozen ASCII characters corresponding to a valid bar code through a serial port.</p>

<p>Sounds easy enough, right?</p>

<p>Your resources include an 8-bit, sub-1-MIPS processor, 64 bytes of RAM for data, and 1 KB of Flash ROM for the program. Oh, by the way, the hardware doesn't include a serial port.</p>

<p>Here's a look at programming on the low end, down where one person can actually comprehend a complete project. If you've been working on enterprise-class jobs, you're in for a treat!</p>
<h3>The Hardware</h3>

<p><a name="rf1"></a><a href="0604if1.html">Figure 1</a> shows the entire project, tucked neatly into a 5-inch box. The chip behind the LEDs on the left, an Atmel AT89C1051 microcontroller, does all the heavy lifting. A MAX232 clone behind the 9-pin serial connector converts logic-level signals to proper serial-port voltages. The front panel, a transparent sheet of CNC-milled plastic, lacks even rudimentary labels because you just turn the thing on and start scanning.</p>

<p>The HP bar-code scanner wand was a surplus find that, as with many such treasures, lacks a part number. The HBCS-A000 datasheets seemed close enough, even if the connector pinout didn't match. I took a hint from Procrustes: I chopped off the impossible-to-match connector and replaced it with a 3.5-mm stereo headphone plug.</p>

<p>Four nickel-metal-hydride AA cells provide over a week of normal use. The microcontroller monitors the battery voltage and lights a red LED when it's recharge time. If this were a commercial project, you'd expect a power jack and a battery charger but, being a geek, I find nothing wrong with just popping the top and swapping the cells.</p>

<p>The schematic in <a name="rf2"></a><a href="0604if2.html">Figure 2</a> shows the microcontroller and I/O circuitry, which are the most interesting bits of the project. I omitted the battery, MAX232, and some other odds and ends.</p>

<p>The Atmel AT89C1051 single-chip microcontroller descends from the Intel 8051 series, which was introduced in 1980 and remains alive and well today. Although the 8051 architecture can best be described as weird, it accurately fits the low end of the embedded-systems market and has hundreds of variants from a dozen manufacturers.</p>

<p>Even if you've never pondered a schematic before, you can tell there isn't much involved in the "computer" part of this project. A single-chip microcontroller means exactly that: One chip provides all the logic functions.</p>

<p>How much can you fit into 1024 bytes of instruction space?</p>
<h3>Timing Is Everything</h3>

<p>The oval silver can between the 89C1051 and the batteries, labeled Q1 in the schematic, is the quartz crystal setting the CPU's clock frequency to exactly 11.0592 MHz. Why not a nice, round 12.0000 MHz?</p>

<p>A current high-end CPU multiplies its external clock by a large factor to produce the internal clock that controls its circuitry. For example, a 3-GHz Pentium multiplies an external 200-MHz clock by 15 to get a 333 ps instruction time. Not coincidentally, it also confines the microwave frequencies to a single chip where distributing them is merely very difficult.</p>

<p>In contrast, the 8051 <i>divides</i> the external clock by a factor of 12. A single-cycle instruction thus executes in 1.08507 ms, a cool factor of 3255 slower than the Pentium. The original architecture dates back to the era when decoding simple instructions required a multistage pipeline with access to glacially slow memories. Even though contemporary variants have single-clock-cycle execution, there's still a demand for backward compatibility.</p>

<p>The original 8051 included a hardware serial port, with the bit rate equal to fractions of 1/32 of the instruction rate:</p>

<img src="0604iq1.gif" width="299" height="72">
<p>A 9600 baud port sends a bit every 104.167 ms, which makes <i>n</i>=3. Any other crystal frequency wouldn't divide down to the proper serial port rate, so the serial I/O bit rate determines the clock crystal frequency. As we'll see, sending serial bits in software still requires that odd crystal.</p>

<p>You can get 11.0592-MHz crystals from nearly any crystal supplier, which should tell you something about the 8051 architecture's market share.</p>

<p>Instruction-processing times on high-end microprocessors depend on so many factors that you can no longer even estimate the time required to execute a given chunk of code. In pleasant contrast to all that, <a name="rl1"></a><a href="#l1">Listing One</a> is a simple 1-msec delay routine consisting of a few loop instructions.</p>

<p>The 8051 has 255 unique instructions, each of which executes in either one or two instruction cycles: 1.08 or 2.16 microseconds. You can write a routine, tally the instruction cycle times, and know exactly how long the CPU will spend working on it!</p>
<h3>Timing the Bars</h3>

<p>Large-system application programs deal  with I/O in terms of high-level API functions, where packets and streams of tidy data magically funnel in and out of system memory. The notion of a user program directly controlling an I/O port pretty much died with Windows 98, as did any link between program execution and real-time I/O.</p>

<p>Microcontroller programs have an intimate relationship with the external world, because such things as intelligent peripheral controllers don't exist. Indeed, microcontrollers tend to <i>be</i> those controllers: Processing raw data and passing it up the line to more complex programs that prefer not to handle the details. That's exactly what my bar-code scanner does by converting raw timing data from the HP wand into ASCII characters.</p>

<p>The top trace of the oscilloscope image in <a name="rf3"></a><a href="0604if3.html">Figure 3</a> shows the wand's output for a typical bar code. The voltage is high (about 5 volts) when the wand sees a black bar. Although the bars are nominally multiples of a unit width, the pulse width for each bar depends on both the actual printed width and how fast the wand moves across the book cover.</p>

<p><a name="rl2"></a><a href="#l2">Listing Two</a> shows what happens at each edge. Oddly, the 8051 lacks a one-bit exclusive-OR instruction, so detecting and acting on an input change requires four instructions.</p>

<p>The 16-bit hardware timer counts instruction cycles when control bit TR0 is turned on by a SETB instruction. Because 8051 has an 8-bit datapath, you must read and process the high (TH0) and low (TL0) bytes in two distinct operations. Rookies often forget that reading the whole timer isn't an atomic operation, an error that causes bizarre intermittent failures.</p>

<p>The code turns the <i>BusyOut</i> bit off to indicate it's waiting for a bar edge and turns the bit on when it's finished saving the time. <i>BusyOut</i> is thus high when the firmware is doing something useful with the bar times, as shown in the bottom trace of <a name="rf2"></a><a href="0604if2.html">Figure 2</a>. You can tell that the firmware spends nearly all its time waiting for the next edge!</p>

<p>With a raw bar pulse width in hand, the next step is to figure out what it means. This requires a trip into the land of algorithms rather than brute I/O.</p>
<h3>Bare-Bone Algorithms</h3>

<p>ISBN bar codes start (on the left) with three guard bars that form the high-low-high pattern on the left of <a name="rf3"></a><a href="0604if3.html">Figure 3</a>. The next four bars (low, high, low, high) encode the first ISBN digit. Now, in theory, bar widths are 1, 2, 3, or 4 times a nominal unit bar, but you can see that the vagaries of printing and manual scanning produce a variety of widths, even across seven bars!</p>

<p>The widths of the four bars in each ISBN digit add up to 7 unit bar widths, so that you can compute a nominal unit width for each digit and decode its bars relative to that width. The algorithm seems straightforward: Add up the widths for each digit, divide by 7 to get the nominal width, then divide each bar's width by that value.</p>

<p>Unfortunately, the 8051 <i>DIV</i> instruction divides the 8-bit B register by the 8-bit ACC. Dividing a 16-bit quantity by an 8-bit quantity thus requires the usual subtract-and-shift long division you forgot shortly after that Comp Sci 201 final. Oh, you must implement the division using just 8-bit rotates through the carry flag.</p>

<p>Not only is that a bulky routine, it's also time consuming. The good news is that the 8051 can multiply B times ACC to get a 16-bit result, so I used a little fixed-point math-fu: Multiply the high byte of the total time by 256/7.</p>

<p>Homework: Show why the 16-bit product is actually a pretty good approximation for typical ISBN pulse widths. Hint: It's okay to limit the minimum useful bar width by the calculation time.</p>

<p>With the nominal bar width in hand, the code can normalize the digit's four bar widths, a process that seems to require 16-by-16 divisions. Once again, there's an easier way: each bar will be too short, one of four possible widths, or too long. Given six possible quotients, division is too big a hammer.</p>

<p><a name="rl3"></a><a href="#l3">Listing Three</a> shows the first part of that routine. It begins by subtracting half the nominal width, so that a set Carry flag indicates a runt pulse. The rest of the code, which I don't have room for here, performs four successive subtractions of the nominal width to peel out the valid bars. At the end, a cleared Carry flag means the pulse was too long.</p>

<p>I actually did use a 16-bit subtraction subroutine for the rest of the operations. Because the 8051 has a painfully limited variety of pointers, the routine always uses the same four registers. In this context, "reusable code" means you must arrange the caller's context to match the subroutine's expectations, not the other way around.</p>

<p>The code then searches a table of bar width patterns to determine which digit the four bars represent. The 8051 has only one counted-loop construct, the <i>DJNZ</i> instruction, which decrements a register and branches if it's not zero.</p>

<p>Homework: Convert a possibly invalid bar pattern into a single ASCII digit, given an increment-only table pointer and a decrement-only counter. Hint 1: Pack the four width codes into a byte. Hint 2: Your first table layout will be wrong.</p>

<p>The rest is easy: After processing all the digits on the fly, simply squirt them through a single output bit in ordinary 9600 baud, 8-N-1 asynchronous serial format. Because the AT89C1051 doesn't have a hardware serial port, your code must ensure each bit hits the output pin at precisely the right moment. But that should be easy by now, right?</p>

<p>Speaking of easy, error handling at this level is almost trivial. When the code detects any error, whether a too-short bar or an invalid digit, it simply sounds a beep (software timing, again), blinks a red LED, and branches to a routine that simulates a power-on reset. As with most deeply embedded applications, there isn't a whole lot the user could do in any case, so the error handling must be entirely automatic.</p>
<h3>Reentry Checklist</h3>

<p>Despite what you may think, the 8051 isn't the worst microcontroller out there. In my opinion, the Microchip PIC series represents an almost perfect triumph of marketing over engineering. Programming at this level presents a whole new set of challenges, along with a definite thrill at seeing your new gadget do something useful. Better than watching phosphor dots, anyway.</p>

<p>The whole ISBN bar-code reader occupies about 500 bytes of code and uses 30 bytes of RAM. Obviously it doesn't have nearly enough features!</p>

<p>It turns out most of our SF collection predates ISBNs, let alone those newfangled bar-code thingies, but Mary's count just hit 1 kilobook anyway.</p>

<p>The Tellico collection manager, found at http://www.periapsis.org/tellico/, or included with your Linux distro, fetches book information by ISBN/author/title and handles database functions. It can export its database in Palm-DB format for my PDA, so we know what we <i>don't</i> need when we're deep in the used-book stacks.</p>

<p>Find bar-code wand datasheets at http://www.datasheetcatalog.com/. The Official ISBN Bar Code Doc is at http://www.isbn-international.org/ and a Linux kernel-based bar-code reader is at http://www.linuxjournal.com/article/4545/. More on Procrustes at http://www.mythweb.com/ teachers/why/basics/procrustes.html.</p>

<p>A ZIP file containing the full schematic, PCB layout, source code, and HEX file is available electronically; see "Resource Center," page 5. Even though it's not commercial-quality stuff, if it helps scratch your itch, you're welcome to it!</p>


<p><b>DDJ</b></p>



	
	
<br>
<br>
<b><a name="l1"></a>Listing One</b><br>
<pre>Wait1ms:
           PUSH   ACC          ; 2 cycles
           MOV    A,#200       ; 1 cycle
           NOP                 ; 1 cycle

W1_1:      DJNZ   ACC,W1_1     ; 400 cycles
W1_2:      DJNZ   ACC,W1_2     ; 512 cycles

           POP    ACC          ; 2 cycles
           RET                 ; 2 + 2 for CALL</pre>
<a href="#rl1">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l2"></a>Listing Two </b><br>
<pre>WaitBar:
           CLR     BusyOut     ; show we're at a new bar

           SETB    BarBitIn    ; ensure it's still an input
           MOV     C,BarBitIn  ; save current state
           MOV     F0,C 

WB_Stall:
           CPL    SpinOut      ; show we're spinning
           JB     TF0,WB_Fail  ; reset on timeout (64k ms)
           MOV    C,F0         ; XOR old &amp; new state
           JNB    BarBitIn,WB_0
           CPL    C            ; change =&gt; 1
WB_0:      JNC    WB_Stall     ; 0 =&gt; no change

           CLR    TR0          ; stop Timer0
           MOV    R3,TH0       ; save bar time
           MOV    R2,TL0
MOV   TH0,#HIGH 10  ; fudge for stop/start overhead
           MOV   TL0,#LOW 10
           SETB  TR0           ; start Timer0

           CLR   SpinOut       ; down until next spin
           SETB  BusyOut

           RET

WB_Fail:   JMP   CrashReset    ; restart on timeout</pre>
<a href="#rl2">Back to article</a><br>
	
	
	
<br>
<br>
<b><a name="l3"></a>Listing Three </b><br>
<pre>           MOV   A,R7
           CLR   C
           RRC   A
           MOV   B,ACC         ; save high byte
           MOV   A,R6
           RRC   A             ; ACC = low byte

           XCH   A,R2          ; compute low byte
           CLR   C
           SUBB  A,R2
           XCH   A,R2          ; R2 gets low result

           MOV   A,B           ; compute high byte
           XCH   A,R3
           SUBB  A,R3
           XCH   A,R3          ; R3 get high result

          JC     FW_fail       ; C means &lt; 0, error</pre>
<a href="#rl3">Back to article</a><br>
	
	
	


</body>
</html>